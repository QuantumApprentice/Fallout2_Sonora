#include "..\headers\define.h"
//---------------------

#define NAME                            SCRIPT_SCPost
#define TOWN_REP_VAR                    GVAR_TOWN_REP_PUERTO // привязка к репутации (-1, если не нужно)

#define NUM_TEAM                        TEAM_BROTHERS_STEEL   // группа, к которой принадлежит персонаж (-1 если нужно оставить Team, заданный в редакторе)
#define NUM_AI                          AI_BOS_PALADIN        // AI персонажа (-1 если нужно оставить AI, заданный в редакторе)
#define KILL_SLAVA                      SLAVA_KILL_GOOD       // слава за убийство этого персонажа (SLAVA_KILL_GOOD, SLAVA_KILL_BAD, SLAVA_KILL_NEITRAL, SLAVA_KILL_CHILD)
#define KILL_TOWN_REP                   REP_KILL_CRITICAL     // репутация за убийство этого персонажа (-100 для враждебности всего поселения)
#define KILL_FRAC_REP                   FRAC_BOS              // принадлежность определенной фракции (-1, если не нужно)

#define FLAG_STEAL                      120 // Нижний порог навыка для кражи (для торговцев 200, для животных 300)
#define FLAG_ALCOHOL                    1  // Примет ли алкоголь от ГГ: 0 - принимает, 1 - не принимает
#define FLAG_FOOD                       1  // Примет ли пищу от ГГ: 0 - принимает, 1 - не принимает

//---------------------

procedure start;
procedure map_enter_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;
procedure pickup_p_proc;
procedure combat_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;

procedure Node998;
procedure Node999;
procedure NodeStartDialog;
procedure NodeFstDialog;
procedure NodeFstDialog2;
procedure NodeFstDialog3;
procedure NodeFstDialog4;
procedure NodeFstDialog5;
procedure NodeFstDialog101;
procedure NodeFstDialog102;
procedure NodeFstDialog103;
procedure NodeFstDialog104;
procedure NodeFstDialog105;
procedure NodeFstDialog200;
procedure NodeFstDialog201;
procedure NodeFstDialog202;
procedure NodeFstDialog203;
procedure NodeFstDialog204;
procedure NodeFstDialog205;
procedure NodeSndDialog;
procedure NodeSndDialog2;
procedure NodeSndDialog3;

//---------------------

//#define LVAR_Flag_A                     (6) // отвечает за ...

//---------------------



procedure start begin
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
       set_start_home
   end
end

procedure look_at_p_proc begin
   lookProcMsg
end

procedure description_p_proc begin
   dsrptProcMsg
end

procedure timed_event_p_proc begin
   // Таймер за номером 1 рарезервирован под перемещение персонажа по локации
   // Таймер за номером 2 рарезервирован под всплывающие реплики
   if (fixed_param == 1) then begin
       timer_move_to_home
   end
   if (fixed_param == 2) then begin
       //timer_float_reply(110,119)
   end
end

procedure critter_p_proc begin
   set_critter_options(1,0) //условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ
   if self_enemy_dude then begin
   BarklyAttakDude
   end
end

procedure use_skill_on_p_proc begin
   skillUseSteal
   skillUseFAid
end

procedure pickup_p_proc begin
   pickupProcCritter
end

procedure use_obj_on_p_proc begin
   UsedItemToCrit_Proc
end

procedure combat_p_proc begin
   effectAttak(0,0,2,2,2) //отравление min, облучение min, травма ног %, травма рук %, травма глаз %
end

procedure damage_p_proc begin
   if (source_obj == dude_obj) then begin
   BarklyAttakDude
   end
   set_damage_dude
end

procedure destroy_p_proc begin
   set_global_var(GVAR_PUERTO_STATUS_POST,-1);
   if (source_obj == dude_obj) then begin
   set_global_var(GVAR_PUERTO_DOPUSK,-1);
   end
   kill_critter_opt
   kill_critter_BoS_item
end

procedure talk_p_proc begin
   if (global_var(GVAR_PUERTO_BOS_DESTROY_TIMER) != 0) then begin
   floater(dude_obj,102,COLOR_MSG_GREY);
   floater(self_obj,103,COLOR_MSG_NORMAL);
   end
   else if (global_var(GVAR_PUERTO_DOPUSK) < 0) then begin
   floater(dude_obj,104,COLOR_MSG_RED);
   end
   else begin
   setReaction(0) // Отношение к ГГ (1 - персонаж положительный, -1 отрицательный, 0 нейтральный)
   dialog_proc(1,105,NodeStartDialog,HEAD_BOSS_POST,BACKGROUND_WASTELND)
   end
end

procedure Node998 begin
   set_global_var(GVAR_PUERTO_DOPUSK,-1);
   set_enemy_dude;
end

procedure Node999 begin
end

procedure NodeStartDialog begin
   if (global_var(GVAR_TITUL_BOS) > 0) then begin
   call NodeSndDialog2;
   end
   else if (dude_iq<=3) then begin
   call NodeFstDialog4;
   end
   else if ((global_var(GVAR_ADDICT_NUKA_COLA) > 0) or (global_var(GVAR_ADDICT_BUFF_OUT) > 0) or (global_var(GVAR_ADDICT_MENTATS) > 0) or (global_var(GVAR_ADDICT_PSYCHO) > 0)) then begin
   call NodeFstDialog3;
   end
   else if (global_var(GVAR_PUERTO_DOPUSK) == 0) then begin
   call NodeFstDialog;
   end
   else begin
   call NodeSndDialog;
   end
end

//--- ПЕРВЫЙ ДИАЛОГ

procedure NodeFstDialog begin
   Reply(200)

   NAnswer(220,3,NodeFstDialog101)
   NAnswer(223,3,NodeFstDialog102)
   NAnswer(226,3,NodeFstDialog103)
   GAnswer(230,3,NodeFstDialog104)

   if (global_var(GVAR_PUERTO_DOPUSK) == 0) then begin
   NAnswer(240,1,NodeFstDialog200)
   end

   if (bitDialog(bit_2) == 0) then begin
   BAnswer(202,1,NodeFstDialog2)
   end else begin
   NAnswer(201,1,Node999)
   end
end

procedure NodeFstDialog2 begin
   bitDialogExp(bit_2,0)
   Reply(203)
   NAnswer(204,1,Node999)
end

procedure NodeFstDialog3 begin
   Reply(205)
   NAnswer(206,1,Node999)
end

procedure NodeFstDialog4 begin
   Reply(200)
   NAnswer(207,1,NodeFstDialog5)
end

procedure NodeFstDialog5 begin
   Reply(208)
   NAnswer(209,1,Node999)
end

procedure NodeFstDialog101 begin
   Reply(221)
   NAnswer(225,1,NodeFstDialog102)
   NAnswer(229,1,NodeFstDialog103)
   NAnswer(222,1,Node999)
end

procedure NodeFstDialog102 begin
   Reply(224)
   NAnswer(228,1,Node999)
end

procedure NodeFstDialog103 begin
   Reply(227)
   NAnswer(228,1,Node999)
end

procedure NodeFstDialog104 begin
   Reply(231)
   NAnswer(233,3,NodeFstDialog105)
   NAnswer(232,1,Node999)
end

procedure NodeFstDialog105 begin
   Reply(234)
   NAnswer(235,1,Node999)
end

procedure NodeFstDialog200 begin
   Reply(241)

   if ((getQuest(GVAR_PHENIX_QST_OWRS_HOLO) == qStart) or (item_amnt(dude_obj,PID_HOLODSK_ULISS) > 0)) then begin
   GAnswer(251,1,NodeFstDialog202)
   end
   if (global_var(GVAR_TWOSUN_STATUS_BOSS) > 0) then begin
   GAnswer(250,1,NodeFstDialog202)
   end

   if (getQuest(GVAR_PUERTO_QST_INFERNO) >= qEnd) then begin
   GAnswer(252,1,NodeFstDialog202)
   end
   else if (getQuest(GVAR_PUERTO_QST_BANDITS_PIP) >= qEnd) then begin
   GAnswer(252,1,NodeFstDialog202)
   end
   else if (global_var(GVAR_PUERTO_SKLAD_DOPUSK) > 0) then begin
   GAnswer(252,1,NodeFstDialog202)
   end

   if (gvar_bit(GVAR_DIALOGS_FLAGS_BIT, bit_20) != 0) then begin
   GAnswer(253,1,NodeFstDialog202)
   end
   else if (getQuest(GVAR_PUERTO_QST_HERMOSILLO) >= qEnd) then begin
   GAnswer(253,1,NodeFstDialog202)
   end

   NAnswerSkill(254,3,NodeFstDialog202,SKILL_BARTER,110,255,NodeFstDialog204)

   NAnswer(242,1,NodeFstDialog201)
end

procedure NodeFstDialog201 begin
   Reply(243)
   NAnswer(244,1,Node999)
   BAnswer(245,1,Node999)
end

procedure NodeFstDialog202 begin
   set_global_var(GVAR_PUERTO_DOPUSK,1);
   bitDialogExp(bit_1,250)
   Reply(246)
   NAnswer(247,1,NodeFstDialog203)
end

procedure NodeFstDialog203 begin
   Reply(248)
   NAnswer(249,1,Node999)
   BAnswer(258,1,NodeFstDialog205)
end

procedure NodeFstDialog204 begin
   Reply(256)
   NAnswer(257,1,Node999)
end

procedure NodeFstDialog205 begin
   setBonusReaction(-35)
   set_REP(-1)
   call Node999;
end

//--- ПОВТОРНЫЙ ДИАЛОГ

procedure NodeSndDialog begin
   if (armor_pid_dude_var == PID_ARMOR_CHEST_SHIELD) then begin
   Reply(214)
   end
   else begin
   Reply(210)
   end
   NAnswer(211,1,Node999)
end

procedure NodeSndDialog2 begin
   zReply(212)
   if (global_var(GVAR_PUERTO_DOPUSK) == 0) then begin
   NAnswer(215,1,NodeSndDialog3)
   end
   NAnswer(213,1,Node999)
end

procedure NodeSndDialog3 begin
   set_global_var(GVAR_PUERTO_DOPUSK,1);
   zReply(216)
   NAnswer(217,1,Node999)
end
