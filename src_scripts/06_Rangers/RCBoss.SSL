#include "..\headers\define.h"
//---------------------

#define NAME                            SCRIPT_RCBoss
#define TOWN_REP_VAR                    GVAR_TOWN_REP_RANGERS // привязка к репутации (-1, если не нужно)

#define NUM_TEAM                        TEAM_RANGERS          // группа, к которой принадлежит персонаж (-1 если нужно оставить Team, заданный в редакторе)
#define NUM_AI                          -1                    // AI персонажа (-1 если нужно оставить AI, заданный в редакторе)
#define KILL_SLAVA                      (SLAVA_KILL_GOOD*20)  // слава за убийство этого персонажа (SLAVA_KILL_GOOD, SLAVA_KILL_BAD, SLAVA_KILL_NEITRAL, SLAVA_KILL_CHILD)
#define KILL_TOWN_REP                   REP_KILL_CRITICAL     // репутация за убийство этого персонажа (-100 для враждебности всего поселения)
#define KILL_FRAC_REP                   FRAC_RANGER           // принадлежность определенной фракции (-1, если не нужно)

#define FLAG_STEAL                      100 // Нижний порог навыка для кражи (для торговцев 200, для животных 300)
#define FLAG_ALCOHOL                    1  // Примет ли алкоголь от ГГ: 0 - принимает, 1 - не принимает
#define FLAG_FOOD                       1  // Примет ли пищу от ГГ: 0 - принимает, 1 - не принимает

#define TILE_GEN_CONCILIUM              21308 // точка размещения генерала во время совещания

//---------------------

procedure start;
procedure map_enter_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;
procedure pickup_p_proc;
procedure combat_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;

procedure Node998;
procedure Node999;
procedure NodeStopDialog;
procedure NodeStopDialog100;
procedure NodeStopDialog101;
procedure NodeStopDialog102;
procedure NodeStopDialog103;
procedure NodeStopDialog104;
procedure NodeStopDialog105;
procedure NodeStopDialog106;
procedure NodeStopDialog107;
procedure NodeStopDialog108;
procedure NodeStartDialog;
procedure NodeFstDialog;
procedure NodeFstDialog2;
procedure NodeFstDialog3;
procedure NodeFstDialog4;
procedure NodeSndDialog;

procedure NodeMainAnswer;
procedure NodeAnswer;
procedure NodeAnswer101;
procedure NodeAnswer102;
procedure NodeAnswer201;
procedure NodeAnswer202;
procedure NodeAnswer203;
procedure NodeAnswer204;
procedure NodeAnswer205;
procedure NodeAnswer206;
procedure NodeAnswer207;
procedure NodeAnswer208;
procedure NodeAnswer209;
procedure NodeDudeRanger;
procedure NodeDudeRangerStat;
procedure NodeDudeRanger1;
procedure NodeDudeRanger2;
procedure NodeDudeRanger3;
procedure NodeDudeRanger4;
procedure NodeDudeRanger5;
procedure NodeDudeRanger6;
procedure NodeDudeRanger7;
procedure NodeDudeRanger8;
procedure NodeDudeRanger9;
procedure NodeDudeRanger10;
procedure NodeDudeRanger11;
procedure NodeDudeRanger12;
procedure NodeDudeRanger13;
procedure NodeDudeRanger14;
procedure NodeDudeRanger15;
procedure NodeDudeRanger16;
procedure NodeDudeRanger17;
procedure NodeDudeRanger18;
procedure NodeDudeRanger19;
procedure NodeFlagstaffRanger;
procedure NodeFlagstaffRanger2;
procedure NodeFlagstaffRanger3;
procedure NodeFlagstaffRanger4;
procedure NodeFlagstaffRanger5;
procedure NodeJkcls;
procedure NodeJkcls2;
procedure NodeJkcls3;
procedure NodeJkcls4;
procedure NodeJkcls5;
procedure NodeJkcls600;
/*
procedure NodeJkcls601;
procedure NodeJkcls602;
procedure NodeJkcls603;
procedure NodeJkcls604;
*/
procedure NodeJkcls700;
procedure NodeJkcls701;
procedure NodeJkcls702;
procedure NodeVilla100;
procedure NodeVilla101;
procedure NodeVilla102;
procedure NodeVilla103;
procedure NodeVilla104;
procedure NodeVilla105;
procedure NodeVilla106;
procedure NodeVilla107;
procedure NodeVilla200;
procedure NodeVilla201;
procedure NodeVilla201b;
procedure NodeVilla201c;
procedure NodeVilla202;
procedure NodeVilla203;
procedure NodeVilla204;
procedure NodeVilla300;

procedure NodeBezh;
procedure NodeBezh2;
procedure NodeBezh3;
procedure NodeAcv;
procedure NodeAcv2;
procedure NodeHolo;
procedure NodeHolo2;
procedure NodeHolo3;
procedure NodeSBrahmin;
procedure NodeSBrahmin100;
procedure NodeSBrahmin101;
procedure NodeSBrahmin102;
procedure NodeSBrahmin200;
procedure NodeSBrahmin201;
procedure NodeSBrahmin202;
procedure NodeSBrahmin203;
procedure NodeSBrahmin204;
procedure NodeSBrahmin205;
procedure NodeSBrahmin206;
procedure NodeSBrahmin207;
procedure NodeSBrahmin208;
procedure NodeSBrahmin209;
procedure NodeSBrahmin210;
procedure NodePhoenix200;
procedure NodePhoenix201;
procedure NodePhoenix202;
procedure NodePhoenix202b;
procedure NodePhoenix203;
procedure NodePhoenix204;
procedure NodePhoenix205;
procedure NodePhoenix206;
procedure NodePhoenix207;

procedure NodeConcilium;
procedure NodeConcilium101;
procedure NodeConcilium102;
procedure NodeConcilium103;
procedure NodeConcilium104;
procedure NodeConcilium105;
procedure NodeConcilium106;
procedure NodeConcilium107;
procedure NodeConcilium108;
procedure NodeConcilium109;
procedure NodeConcilium110;
procedure NodeConcilium200;
procedure NodeConcilium200b;
procedure NodeConcilium201;
procedure NodeConcilium202;
procedure NodeConcilium203;
procedure NodeConcilium204;
procedure NodeConcilium205;
procedure NodeConcilium206;
procedure NodeConcilium207;
procedure NodeConcilium208;
procedure NodeConcilium209;
procedure NodeConcilium210;
procedure NodeConcilium211;
procedure NodeConcilium212;
procedure NodeConcilium213;

//---------------------

#define LVAR_RangerStar_Point                   (6) // очки для зачисления в ряды рейнджеров
#define LVAR_TITUL_ADD                          (7) // фиксирует факт присвоения титула нашему ГГ

import variable box_stop_use;

variable titulPenalty;

//---------------------



procedure start begin
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
       set_start_home
   end
end

procedure look_at_p_proc begin
   lookProcMsg
end

procedure description_p_proc begin
   dsrptProcMsg
end

procedure timed_event_p_proc begin
   // Таймер за номером 1 рарезервирован под перемещение персонажа по локации
   // Таймер за номером 2 рарезервирован под всплывающие реплики
   if (fixed_param == 1) then begin
       if (map_var(MVAR_RNGRS_Councilium) == 1) then begin
       timer_move_to_tile(0,TILE_GEN_CONCILIUM,0)
       end
       else begin
       timer_move_to_home
       end
   end
   if (fixed_param == 2) then begin
       //timer_float_reply(110,119)

       /*
       if (map_var(MVAR_RNGRS_Councilium) == 1) then begin
       timer_float_reply(109,109)
       end
       else if (map_var(MVAR_RNGRS_JclTalk) == 1) then begin
       timer_float_reply(109,109)
       end
       else begin
       timer_float_reply(110,119)
       end
       */
   end
end

procedure critter_p_proc begin
   set_using_stop // привязка хозяина и объекта (удалить, если не нужно)
   set_critter_options(1,0) //условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ
end

procedure use_skill_on_p_proc begin
   skillUseSteal
   skillUseFAid
end

procedure pickup_p_proc begin
   pickupProcCritter
end

procedure use_obj_on_p_proc begin
   UsedItemToCrit_Proc
end

procedure combat_p_proc begin
   effectAttak(0,0,2,2,2) //отравление min, облучение min, травма ног %, травма рук %, травма глаз %
end

procedure damage_p_proc begin
   set_damage_dude
end

procedure destroy_p_proc begin
   //! обнуление квестов также дублировать в RSDorDum
   set_global_var(GVAR_STATUS_RANGERS_GENERAL,-1);
   set_gvar_bit_off(GVAR_RANGERS_RECOMMENDATION, bit_5); // сброс приказа рейнджеру в Флагпуле
   if (getQuest(GVAR_RANGERS_QST_SBRAHMIN) == qStart) then begin
   setQuest(GVAR_RANGERS_QST_SBRAHMIN,qStop);
   end
   if (getQuest(GVAR_RANGERS_QST_PHOENIX) == qStart) then begin
   setQuest(GVAR_RANGERS_QST_PHOENIX,qStop);
   end
   if (getQuest(GVAR_RANGERS_QST_PUERTO) == qStart) then begin
   setQuest(GVAR_RANGERS_QST_PUERTO,qStop);
   end
   kill_critter_opt
   kill_critter_dude_reply(103,0)
end

procedure talk_p_proc begin
   setReaction(1) // Отношение к ГГ (1 - персонаж положительный, -1 отрицательный, 0 нейтральный)

   fracReaction(GVAR_TITUL_ARIZONA_RANGER,50) // Поправка к отношению за принадлежность к фракциям
   fracReaction(GVAR_TITUL_BOS,-50) // Поправка к отношению за принадлежность к фракциям
   fracReaction(GVAR_TITUL_BOS_TRAITOR,20) // Поправка к отношению за принадлежность к фракциям
   fracReaction(GVAR_TITUL_PHENIX_CULT,-40) // Поправка к отношению за принадлежность к фракциям
   fracReaction(GVAR_TITUL_WARRIOR_ROAD,-20) // Поправка к отношению за принадлежность к фракциям
   fracReaction(GVAR_TITUL_DUDE_IS_SLAVE,20) // Поправка к отношению за принадлежность к фракциям

   if (map_var(MVAR_RNGRS_JclTalk) == 1) then begin
   floater(self_obj,104,COLOR_MSG_NEAR_WHITE);
   end
   else if (global_var(GVAR_GAME_END) != 0) then begin
   floater(self_obj,105,COLOR_MSG_GREEN);
   end
   else if (getQuest(GVAR_RANGERS_QST_PUERTO) >= qEnd) then begin
   floater(self_obj,105,COLOR_MSG_GREEN);
   end
   else begin
   dialog_proc(1,102,NodeStartDialog,HEAD_RANGER_GENERAL,BACKGROUND_WASTELND)
   //dialog_proc(1,102,NodeStartDialog,-1,-1)
   end
end

procedure Node998 begin
   setNodeCombat;
end

procedure Node999 begin
   bitDialogExp(bit_1,100)
end

procedure NodeStartDialog begin
   if (map_var(MVAR_RNGRS_Councilium) == 1) then begin
   call NodeConcilium;
   end
   else if ((global_var(GVAR_TITUL_BOS) > 0) or (global_var(GVAR_TITUL_PHENIX_CULT) > 0)) then begin
      if (bitDialog(bit_15) == 0) then begin
         if (local_var(LVAR_TITUL_ADD) > 0) then begin
         call NodeStopDialog101;
         end
         else begin
         call NodeStopDialog104;
         end
      end
      else begin
         call NodeStopDialog100;
      end
   end
   else if ((bitDialog(bit_5) == 0) and (global_var(GVAR_TITUL_REBIRTH) != 0)) then begin
   call NodeStopDialog;
   end
   else if (bitDialog(bit_1) == 0) then begin
   call NodeFstDialog;
   end
   else begin
   call NodeSndDialog;
   end
end

//--- СТОП-ДИАЛОГ

procedure NodeStopDialog begin
   bitDialogExp(bit_5,20)
   Reply(212)
   NAnswer(213,1,NodeStartDialog)
   BAnswer(214,1,NodeStartDialog)
end

procedure NodeStopDialog100 begin
   Reply(550)
   NAnswer(551,1,Node999)
   BAnswer(552,1,Node999)
end

procedure NodeStopDialog101 begin
   Reply(553)
   NAnswer(554,1,NodeStopDialog102)
end

procedure NodeStopDialog102 begin
   if (global_var(GVAR_TITUL_BOS) > 0) then begin
   Reply(555)
   end
   else if (global_var(GVAR_TITUL_PHENIX_CULT) > 0) then begin
   Reply(556)
   end
   else begin
   Reply(557)
   end
   NAnswer(558,1,NodeStopDialog103)
end

procedure NodeStopDialog103 begin
   bitDialogExp(bit_15,0)
   Reply(559)
   NAnswer(560,1,Node999)
   BAnswer(561,1,Node999)
end

procedure NodeStopDialog104 begin
   bitDialogExp(bit_15,0)
   if (global_var(GVAR_TITUL_BOS) > 0) then begin
   Reply(576)
   NAnswer(577,1,NodeStopDialog107)
   NAnswer(580,1,NodeStopDialog108)
   end
   else if (global_var(GVAR_TITUL_PHENIX_CULT) > 0) then begin
   Reply(570)
   NAnswer(571,1,NodeStopDialog105)
   end
   else begin
   Reply(562)
   NAnswer(563,1,Node999)
   end
end

procedure NodeStopDialog105 begin
   Reply(572)
   NAnswer(573,1,NodeStopDialog106)
end

procedure NodeStopDialog106 begin
   Reply(574)
   NAnswer(575,1,Node999)
end

procedure NodeStopDialog107 begin
   Reply(578)
   NAnswer(579,1,Node999)
end

procedure NodeStopDialog108 begin
   Reply(581)
   NAnswer(582,1,Node999)
end

//--- ПЕРВЫЙ ДИАЛОГ

procedure NodeFstDialog begin
   Reply(200)
   NAnswer(201,1,NodeFstDialog2)
end

procedure NodeFstDialog2 begin
   Reply(202)
   call NodeMainAnswer;
   BAnswer(203,1,NodeFstDialog3)
end

procedure NodeFstDialog3 begin
   Reply(204)
   call NodeMainAnswer;
   BAnswer(205,1,NodeFstDialog4)
end

procedure NodeFstDialog4 begin
   setBonusReaction(-25)
   set_REP(-3)
   call Node999;
end

//--- ПОВТОРНЫЙ ДИАЛОГ

procedure NodeSndDialog begin
   if (global_var(GVAR_TITUL_ARIZONA_RANGER) > 0) then begin
   Reply(215)
   end
   else begin
   Reply(210)
   end

   //!!!! ЧИТ
   /*
   setQuest(GVAR_PHENIX_CULT_DESTROY,qEnd);
   setQuest(GVAR_PUERTO_BOS_DESTROY,qEnd);
   setQuest(GVAR_RANGERS_QST_PHOENIX,qStart);
   */
   //!!!

   //--- Разведка в Пуэрто
   if (getQuest(GVAR_RANGERS_QST_PUERTO) == qStart) then begin
      NAnswer(650,1,NodeConcilium200)
   end

   call NodeMainAnswer;

   //--- Отчет по СанБрамин
   if (getQuest(GVAR_RANGERS_QST_SBRAHMIN) == qStart) then begin
   NAnswer(470,1,NodeSBrahmin200)
   end

   //--- Отчет по Фениксу
   if (getQuest(GVAR_RANGERS_QST_PHOENIX) == qStart) then begin
   NAnswer(500,1,NodePhoenix200)
   end

   //--- Голозапись Советника
   if (getQuest(GVAR_PHENIX_QST_OWRS_HOLO) == qStart) then begin
      if (item_amnt(dude_obj,PID_HOLODSK_ULISS) > 0) then begin
      NAnswer(450,1,NodeHolo)
      end
   end

   //--- Шакалы
   if ((town_known(AREA_JACKALS) == MARK_STATE_VISITED) and (global_var(GVAR_JACKALS_RANGER_CONTRAKT) == 0)) then begin
      NAnswer(300,1,NodeJkcls)
   end

   //--- Рейнджер в Флагпуле
   if (global_var(GVAR_STATUS_FLAGSTAFF_RANGER) == 0) then begin
      if (gvar_bit(GVAR_DIALOGS_FLAGS_BIT, bit_1) != 0) then begin
      if (gvar_bit(GVAR_DIALOGS_FLAGS_BIT, bit_5) == 0) then begin
      NAnswer(230,1,NodeFlagstaffRanger)
      end
      end
   end

   //--- Беженцы у дамбы
   if (getQuest(GVAR_RANGERS_QST_FAMILY) == qZero) then begin
      if (gvar_bit(GVAR_RANGERS_BIT_FAMILY, bit_4) != 0) then begin
      if (bitDialog(bit_4) == 0) then begin
      NAnswer(220,1,NodeBezh)
      end
      end
   end

   //--- Преступления в акведуке
   if (global_var(GVAR_RANGERS_AQUEDUCT_BODIES) != 0) then begin
      if (global_var(GVAR_STATUS_RANGERS_GART) == 0) then begin
      NAnswer(340,1,NodeAcv)
      end
   end

   NAnswer(211,1,Node999)
end

procedure NodeMainAnswer begin
   //--- Союз рейнджеров и Виллы
   if (getQuest(GVAR_RANGERS_TO_VILLA_HELP) >= qEnd) then begin
   if (getQuest(GVAR_MAIN_QUEST_FLAGSTAFF) < qEnd) then begin
   NAnswer(279,1,NodeVilla203)
   end
   end

   if (getQuest(GVAR_RANGERS_TO_VILLA_HELP) == qZero) then begin
       if (bitDialog(bit_3) == 0) then begin
       GAnswer(250,1,NodeVilla100)
       end
   end
   else if (getQuest(GVAR_RANGERS_TO_VILLA_HELP) == qStart) then begin
       NAnswer(280,1,NodeVilla200)
   end
   else if (getQuest(GVAR_VILLA_ALLIANCE_RANGER) < qEnd) then begin
       if (getQuest(GVAR_VILLA_ALLIANCE_PIP) == qStart) then begin
       GAnswer(294,1,NodeVilla300)
       end
   end

   //--- Титул рейнджера
   if (global_var(GVAR_TITUL_ARIZONA_RANGER) == 0) then begin
       NAnswer(350,1,NodeDudeRanger)
   end
   //--- Типовые вопросы
   if (bitDialog(bit_1) != 0) then begin
      NAnswer(400,1,NodeAnswer)
   end
end

//--- РАССПРОСЫ

/*
   Старый текст:

   Я хочу знать больше о рейнджерах.
      Мне интересна история.

   if (bitDialog(bit_10) != 0) then begin
      Говоря про порядок, который несут рейнджеры, что именно вы имели ввиду?
         Закон, единый для всех и основанный на общих ценностях. Таких как право на жизнь и свободу. Мы не лезем в устои чужих племен и не указываем им как жить. Но мы заставляем людей принимать некоторые правила. В Старом Мире этим занималась полиция, теперь это наша привилегия.
            Далее развивается метафора электростанции.

	   Мы, рейнджеры, считаем, что никто не в праве указывать другому, как жить. Если ваше общество намерено отказаться от технологий и жить натуральным хозяйством - дело ваше.
	      Но ответь мне - что важнее для твоих людей? Сохранить свой уклад и продолжать замкнутое существование в отрыве от мира? Или измениться, когда ветер подует в другую сторону?
			   Мир меняется. Пока это слабо ощутимо. Но изменения неизбежны.
   end
*/

procedure NodeAnswer begin
   Reply(401)
   NAnswer(410,1,NodeAnswer101)
   NAnswer(420,1,NodeAnswer201)
   NAnswer(424,1,NodeAnswer203)
   if (bitDialog(bit_10) != 0) then begin
   NAnswer(432,1,NodeAnswer207)
   end
   NAnswer(428,1,NodeAnswer205)
   NAnswer(402,1,NodeSndDialog)
end

procedure NodeAnswer101 begin
   Reply(411)
   NAnswer(412,3,NodeAnswer102)
   NAnswer(403,1,NodeAnswer)
end

procedure NodeAnswer102 begin
   bitDialogExp(bit_8,10)
   Reply(413)
   NAnswer(403,1,NodeAnswer)
end

procedure NodeAnswer201 begin
   Reply(421)
   NAnswer(422,4,NodeAnswer202)
   NAnswer(403,1,NodeAnswer)
end

procedure NodeAnswer202 begin
   bitDialogExp(bit_9,10)
   Reply(423)
   NAnswer(403,1,NodeAnswer)
end

procedure NodeAnswer203 begin
   Reply(425)
   NAnswer(426,1,NodeAnswer204)
end

procedure NodeAnswer204 begin
   bitDialogExp(bit_10,10)
   Reply(427)
   NAnswer(403,1,NodeAnswer)
end

procedure NodeAnswer205 begin
   Reply(429)
   NAnswer(430,3,NodeAnswer206)
   NAnswer(403,1,NodeAnswer)
end

procedure NodeAnswer206 begin
   bitDialogExp(bit_11,10)
   Reply(431)
   NAnswer(403,1,NodeAnswer)
end

procedure NodeAnswer207 begin
   Reply(433)
   BAnswer(434,5,NodeAnswer208)
   NAnswer(403,1,NodeAnswer)
end

procedure NodeAnswer208 begin
   bitDialogExp(bit_12,50)
   Reply(435)
   NAnswer(436,7,NodeAnswer209)
   NAnswer(403,1,NodeAnswer)
end

procedure NodeAnswer209 begin
   bitDialogExp(bit_13,35)
   Reply(437)
   NAnswer(403,1,NodeAnswer)
end

//--- ТИТУЛ РЕЙНДЖЕРА

procedure NodeDudeRanger begin
   titulPenalty := 0;
   Reply(351)
   call NodeDudeRangerStat;
   NAnswer(352,1,NodeDudeRanger2)
end

procedure NodeDudeRangerStat begin
   //NAnswer(369,1,NodeDudeRanger17) // ЧИТ !!!

   if (bitDialog(bit_20) == 0) then begin
      if ((global_var(GVAR_JACKALS_RANGER_CONTRAKT) < 0) or (global_var(GVAR_JACKALS_DEAD) >= LIMIT_JACKALS_DEAD)) then begin
      zNAnswer(360,1,NodeDudeRanger4)
      end
      if (global_var(GVAR_JACKALS_RANGER_CONTRAKT) == 1) then begin
      NAnswer(361,1,NodeDudeRanger4)
      end
   end
   if (bitDialog(bit_21) == 0) then begin
      if (getQuest(GVAR_FLAGSTAFF_MERPSWDR) >= qEnd) then begin
      NAnswer(362,1,NodeDudeRanger5)
      end
      else if (global_var(GVAR_STATUS_FLAGSTAFF_FABER) < 0) then begin
      NAnswer(362,1,NodeDudeRanger5)
      end
      else if (getQuest(GVAR_FLAGSTAFF_SLAVES_RUN) >= qEnd) then begin
      zNAnswer(363,1,NodeDudeRanger5)
      end
   end
   if (bitDialog(bit_22) == 0) then begin
      if (getQuest(GVAR_RANGERS_QST_SBRAHMIN) >= qEnd) then begin
      if (getQuest(GVAR_BRAHMIN_QST_GERONIMO) >= 20) then begin
      NAnswer(364,1,NodeDudeRanger6)
      end
      end
   end

   if (bitDialog(bit_23) == 0) then begin
      if (getQuest(GVAR_PHENIX_CULT_DESTROY) >= qEnd) then begin
      zNAnswer(365,1,NodeDudeRanger7)
      end
   end
   if (bitDialog(bit_24) == 0) then begin
      if (getQuest(GVAR_PUERTO_BOS_DESTROY) >= qEnd) then begin
      zNAnswer(366,1,NodeDudeRanger8)
      end
   end
   if (bitDialog(bit_25) == 0) then begin
      if (global_var(GVAR_TWOSUN_STATUS_ZAX)>0) then begin
      NAnswer(367,1,NodeDudeRanger9)
      end
   end
end

procedure NodeDudeRanger1 begin
   if (local_var(LVAR_RangerStar_Point) >= 10) then begin
   Reply(370)
   NAnswer(371,1,NodeDudeRanger10)
   end
   else begin
   Reply(359)
   call NodeDudeRangerStat;
   NAnswer(352,1,NodeDudeRanger2)
   end
end

procedure NodeDudeRanger2 begin
   Reply(353)
   BAnswer(356,1,NodeDudeRanger3)
   NAnswer(355,1,NodeSndDialog)
   NAnswer(354,1,Node999)
end

procedure NodeDudeRanger3 begin
   Reply(357)
   NAnswer(355,1,NodeSndDialog)
   NAnswer(358,1,Node999)
end

procedure NodeDudeRanger4 begin
   bitDialogExp(bit_20,0)
   inc_local_var_amt(LVAR_RangerStar_Point,3);
   call NodeDudeRanger1;
end

procedure NodeDudeRanger5 begin
   bitDialogExp(bit_21,0)
   inc_local_var_amt(LVAR_RangerStar_Point,3);
   call NodeDudeRanger1;
end

procedure NodeDudeRanger6 begin
   bitDialogExp(bit_22,0)
   inc_local_var_amt(LVAR_RangerStar_Point,5);
   call NodeDudeRanger1;
end

procedure NodeDudeRanger7 begin
   bitDialogExp(bit_23,0)
   inc_local_var_amt(LVAR_RangerStar_Point,10);
   call NodeDudeRanger1;
end

procedure NodeDudeRanger8 begin
   bitDialogExp(bit_24,0)
   inc_local_var_amt(LVAR_RangerStar_Point,15);
   call NodeDudeRanger1;
end

procedure NodeDudeRanger9 begin
   bitDialogExp(bit_25,0)
   inc_local_var_amt(LVAR_RangerStar_Point,4);
   call NodeDudeRanger1;
end

procedure NodeDudeRanger10 begin
   call NodeDudeRanger11;
end

procedure NodeDudeRanger11 begin
   if (get_SLAVA <= 0) then begin
   titulPenalty += 2;
   Reply(372)
   NAnswer(373,1,NodeDudeRanger12)
   BAnswer(374,1,NodeDudeRanger12)
   end
   else begin
   call NodeDudeRanger12;
   end
end

procedure NodeDudeRanger12 begin
   if (get_REP <= 0) then begin
   titulPenalty += 3;
   Reply(375)
   NAnswer(376,1,NodeDudeRanger13)
   BAnswer(377,1,NodeDudeRanger13)
   end
   else begin
   call NodeDudeRanger13;
   end
end

procedure NodeDudeRanger13 begin
   if (global_var(GVAR_TITUL_WARRIOR_ROAD) > 0) then begin
   titulPenalty += 3;
   Reply(378)
   NAnswer(379,1,NodeDudeRanger14)
   BAnswer(380,1,NodeDudeRanger14)
   end
   else begin
   call NodeDudeRanger14;
   end
end

procedure NodeDudeRanger14 begin
   if (global_var(GVAR_TITUL_PHENIX_CULT) > 0) then begin
   titulPenalty += 6;
   Reply(381)
   NAnswer(382,1,NodeDudeRanger15)
   BAnswer(383,1,NodeDudeRanger15)
   end
   else begin
   call NodeDudeRanger15;
   end
end

procedure NodeDudeRanger15 begin
   if (global_var(GVAR_TITUL_BOS) > 0) then begin
   titulPenalty += 6;
   Reply(384)
   NAnswer(385,1,NodeDudeRanger16)
   BAnswer(386,1,NodeDudeRanger16)
   end
   else begin
   call NodeDudeRanger16;
   end
end

procedure NodeDudeRanger16 begin
   if (local_var(LVAR_RangerStar_Point) > titulPenalty) then begin
   Reply(390)
   NAnswer(391,1,NodeDudeRanger17)
   BAnswer(392,1,NodeDudeRanger17)
   end
   else begin
   Reply(387)
   NAnswer(388,1,Node999)
   BAnswer(389,1,Node999)
   end
end

procedure NodeDudeRanger17 begin
   zReply(393)
   NAnswer(394,1,NodeDudeRanger19)
end

procedure NodeDudeRanger18 begin
   set_global_var(GVAR_TITUL_ARIZONA_RANGER,1);
   set_local_var(LVAR_TITUL_ADD,1);
   exp_add(1000);
end

procedure NodeDudeRanger19 begin
   call NodeDudeRanger18;
   Reply(395)
   NAnswer(396,1,Node999)
end

//--- РЕЙНДЖЕР В ФЛАГПУЛЕ

procedure NodeFlagstaffRanger begin
   Reply(231)
   NAnswer(232,1,NodeFlagstaffRanger2)
   BAnswer(239,1,NodeFlagstaffRanger3)
end

procedure NodeFlagstaffRanger2 begin
   Reply(233)
   NAnswer(234,1,NodeFlagstaffRanger3)
end

procedure NodeFlagstaffRanger3 begin
   Reply(235)
   NAnswer(238,1,NodeFlagstaffRanger4)
   NAnswer(236,1,Node999)
   BAnswer(237,1,Node999)
end

procedure NodeFlagstaffRanger4 begin
   Reply(240)
   NAnswer(241,1,NodeFlagstaffRanger5)
end

procedure NodeFlagstaffRanger5 begin
   set_gvar_bit_on(GVAR_RANGERS_RECOMMENDATION, bit_5);
   Reply(242)
   NAnswer(243,1,Node999)
   NAnswer(244,1,Node999)
end

//--- ШАКАЛЫ

procedure NodeJkcls begin
   Reply(301)
   NAnswer(302,1,NodeJkcls2)
end

procedure NodeJkcls2 begin
   Reply(303)
   if (global_var(GVAR_JACKALS_STATUS_RANGER) == 2) then begin
   NAnswer(304,1,NodeJkcls3)
   end
   else begin
   NAnswer(307,1,NodeJkcls4)
   end
end

procedure NodeJkcls3 begin
   Reply(305)
   NAnswer(306,1,NodeJkcls4)
end

procedure NodeJkcls4 begin
   Reply(308)
   NAnswer(309,1,NodeJkcls5)
   NAnswer(310,1,Node999)
end

procedure NodeJkcls5 begin
   //set_global_var(GVAR_JACKALS_DEAD,LIMIT_JACKALS_DEAD);
   //set_global_var(GVAR_JACKALS_RANGER_CONTRAKT,-1);
   set_global_var(GVAR_JACKALS_RANGER_CONTRAKT,-2);
   if (getQuest(GVAR_JACKALS_MERC_ATAK) == qStart) then begin
   setQuest(GVAR_JACKALS_MERC_ATAK,-1);
   end
   bitDialogExp(bit_2,500)
   Reply(311)
   NAnswer(312,1,NodeJkcls600)
   NAnswer(325,1,NodeJkcls600)
   if (getQuest(GVAR_JACKALS_UNION_CITY) == qStart) then begin
   NAnswer(330,1,NodeJkcls700)
   end
end

procedure NodeJkcls600 begin
   Reply(326)
   zNAnswer(327,1,Node999)
end

   /*
   Примечание:
      Изначально предполагалось, что к рейнжерам можно приобщиться для физического истребления Шакалов.
      Но такая операция уже имеется в Гараже, где она гораздо выгоднее по балансу и сюжету.
      Рейнджеры же добавят лишних скриптовых заморочек, к тому же управляться с 3-4 напарниками в тесных норах Шакалов будет весьма неудобно.


procedure NodeJkcls600 begin
   zReply(313)
   NAnswer(314,1,NodeJkcls601)
   NAnswer(315,1,NodeJkcls602)
   NAnswer(316,1,NodeJkcls603)
end

procedure NodeJkcls601 begin
   set_global_var(GVAR_JACKALS_RANGER_CONTRAKT,-1);
   zReply(317)
   NAnswer(318,1,NodeJkcls604)
   NAnswer(319,1,Node999)
end

procedure NodeJkcls602 begin
   set_global_var(GVAR_JACKALS_RANGER_CONTRAKT,-2);
   Reply(321)
   NAnswer(322,1,NodeJkcls601)
   NAnswer(323,1,Node999)
end

procedure NodeJkcls603 begin
   zReply(324)
   NAnswer(314,1,NodeJkcls601)
   NAnswer(315,1,NodeJkcls602)
end

procedure NodeJkcls604 begin
   Reply(320)
   NAnswer(319,1,Node999)
end
*/

procedure NodeJkcls700 begin
   Reply(331)
   NAnswer(312,1,NodeJkcls600)

   if (global_var(GVAR_TOWN_REP_JACKALS) <= -30) then begin
   NAnswer(337,1,NodeJkcls600)
   end
   else begin
   NAnswer(332,1,NodeJkcls701)
   end
end

procedure NodeJkcls701 begin
   Reply(333)
   NAnswer(334,4,NodeJkcls702)
   NAnswer(312,1,NodeJkcls600)
end

procedure NodeJkcls702 begin
   set_global_var(GVAR_JACKALS_RANGER_CONTRAKT,1);
   Reply(335)
   zNAnswer(336,1,Node999)
end

//--- СОЮЗ ВИЛЛЫ И РЕЙНДЖЕРОВ

procedure NodeVilla100 begin
   Reply(251)
   NAnswer(252,1,NodeVilla101)
end

procedure NodeVilla101 begin
   Reply(253)
   NAnswer(256,1,NodeVilla102)
   GAnswer(254,1,NodeVilla104)
   BAnswer(255,1,Node999)
end

procedure NodeVilla102 begin
   Reply(257)
   NAnswer(258,1,NodeVilla103)
end

procedure NodeVilla103 begin
   Reply(259)
   GAnswer(254,1,NodeVilla104)
   BAnswer(255,1,Node999)
end

procedure NodeVilla104 begin
   Reply(260)
   if (getQuest(GVAR_VILLA_ALLIANCE_PIP) >= qStart) then begin
   NAnswer(261,1,NodeVilla105)
   end
   else if (getQuest(GVAR_MAIN_QUEST_FLAGSTAFF) == qStart) then begin
   NAnswer(262,1,NodeVilla105)
   end
   else if (getQuest(GVAR_MAIN_QUEST_FLAGSTAFF) == qZero) then begin
   NAnswer(263,1,NodeVilla105)
   end
   NAnswer(264,1,NodeVilla105)
end

procedure NodeVilla105 begin
   Reply(270)
   NAnswer(271,1,NodeVilla106)
   BAnswer(272,1,NodeVilla106)
end

procedure NodeVilla106 begin
   Reply(273)
   NAnswer(274,1,NodeVilla107)
end

procedure NodeVilla107 begin
   bitDialogExp(bit_3,100)
   Reply(275)
   NAnswer(276,1,Node999)
end

procedure NodeVilla200 begin
   Reply(281)
   NAnswer(282,1,NodeVilla201)
   NAnswer(283,1,NodeVilla201)
end

procedure NodeVilla201 begin
   Reply(284)
   NAnswer(290,4,NodeVilla201b)
   NAnswer(285,1,NodeVilla202)
end

procedure NodeVilla201b begin
   Reply(291)
   zNAnswer(292,1,NodeVilla201c)
end

procedure NodeVilla201c begin
   bitDialogExp(bit_14,50)
   Reply(293)
   NAnswer(285,1,NodeVilla202)
end

procedure NodeVilla202 begin
   setQuest(GVAR_RANGERS_TO_VILLA_HELP,qEnd);
   exp_add(1000);
   Reply(286)
   if (getQuest(GVAR_MAIN_QUEST_FLAGSTAFF) < qEnd) then begin
   NAnswer(288,1,NodeVilla203)
   end
   NAnswer(287,1,NodeVilla204)
end

procedure NodeVilla300 begin
   Reply(297)
   NAnswer(298,1,Node999)
   /*
   if (getQuest(GVAR_RANGERS_QST_PUERTO) >= qEnd) then begin
   setQuest(GVAR_VILLA_ALLIANCE_RANGER,qEnd);
   setQuest(GVAR_VILLA_ALLIANCE_BOS,qStop);
   Reply(295)
   NAnswer(296,1,Node999)
   end
   else begin
   Reply(297)
   NAnswer(298,1,Node999)
   end
   */
end

procedure NodeVilla203 begin
   if (global_var(GVAR_VILLA_STATUS_MADRE) == 0) then begin
   set_global_var(GVAR_VILLA_STATUS_MADRE,1);
   end
   if (getQuest(GVAR_MAIN_QUEST_VILLA) < qEnd) then begin
   setQuest(GVAR_MAIN_QUEST_VILLA,qEnd);
   end
   if (getQuest(GVAR_MAIN_QUEST_FLAGSTAFF) < qEnd) then begin
   setQuest(GVAR_MAIN_QUEST_FLAGSTAFF,qEnd);
   end
   if (getQuest(GVAR_FLAGSTAFF_SLAVES_ZAGON) < qEnd) then begin
   setQuest(GVAR_FLAGSTAFF_SLAVES_ZAGON,qEnd);
   end

   Reply(289)
   NAnswer(287,1,NodeVilla204)
end

procedure NodeVilla204 begin
   if (getQuest(GVAR_RANGERS_QST_SBRAHMIN) > qZero) then begin
   call Node999;
   end
   else begin
   call NodeSBrahmin;
   end
end

//--- СЕМЬЯ ПРЕСТУПНИКА

procedure NodeBezh begin
   set_gvar_bit_on(GVAR_RANGERS_BIT_FAMILY, bit_5);
   bitDialogExp(bit_4,25)
   Reply(221)
   NAnswer(222,1,NodeBezh2)
end

procedure NodeBezh2 begin
   Reply(223)
   NAnswer(224,1,NodeBezh3)
   NAnswer(225,1,NodeBezh3)
end

procedure NodeBezh3 begin
   Reply(226)
   NAnswer(227,1,Node999)
   NAnswer(228,1,Node999)
end

procedure NodeAcv begin
   Reply(341)
   NAnswer(342,1,NodeAcv2)
end

procedure NodeAcv2 begin
   set_global_var(GVAR_STATUS_RANGERS_GART,-2);
   Reply(343)
   NAnswer(344,1,Node999)
end

//--- ДИСК УЛИССА ГРАНТА

procedure NodeHolo begin
   bitDialogExp(bit_6,0)
   item_remove(dude_obj,PID_HOLODSK_ULISS,1)
   setQuest(GVAR_PHENIX_QST_OWRS_HOLO,qStop);
   setBonusReaction(20)
   set_REP(2)
   exp_add(500);

   Reply(451)
   NAnswer(452,1,NodeHolo2)
end

procedure NodeHolo2 begin
   if (getQuest(GVAR_PUERTO_BOS_DESTROY) >= qEnd) then begin
   Reply(453)
   NAnswer(454,1,Node999)
   end
   else begin
   Reply(455)
   NAnswer(456,1,NodeHolo3)
   NAnswer(457,1,NodeHolo3)
   end
end

procedure NodeHolo3 begin
   Reply(458)
   NAnswer(459,1,Node999)
end

//--- КВЕСТ В САН-БРАМИН

procedure NodeSBrahmin begin
   setQuest(GVAR_RANGERS_QST_SBRAHMIN,qStart);
   Reply(460)
   NAnswer(461,1,NodeSBrahmin100)
end

procedure NodeSBrahmin100 begin
   Reply(462)
   BAnswer(463,1,NodeSBrahmin101)
   NAnswer(465,1,NodeSBrahmin102)
end

procedure NodeSBrahmin101 begin
   Reply(464)
   NAnswer(465,1,NodeSBrahmin102)
end

procedure NodeSBrahmin102 begin
   Reply(466)
   NAnswer(467,1,Node999)
end

procedure NodeSBrahmin200 begin
   Reply(469)

   //NAnswer(474,1,NodeSBrahmin204) //!!! ЧИТ

   if (getQuest(GVAR_BRAHMIN_QST_RANGER) != qZero) then begin
    if (getQuest(GVAR_BRAHMIN_QST_RANGER) != qStart) then begin
     if (getQuest(GVAR_BRAHMIN_QST_COMMANDER) != qZero) then begin
      if (getQuest(GVAR_BRAHMIN_QST_COMMANDER) != qStart) then begin

      if (getQuest(GVAR_BRAHMIN_QST_GERONIMO) == qZero) then begin
      NAnswer(438,1,Node999)
      end
      else if (getQuest(GVAR_BRAHMIN_QST_GERONIMO) == qStart) then begin
      NAnswer(438,1,Node999)
      end
      else if (global_var(GVAR_BRAHMIN_STATUS_RANGER) < -1) then begin
      BAnswer(487,3,NodeSBrahmin204)
      end
      else if (global_var(GVAR_BRAHMIN_STATUS_RANGER) == -1) then begin
      NAnswer(490,3,NodeSBrahmin205)
      end
      else if (global_var(GVAR_BRAHMIN_STATUS_RANGER) > 0) then begin
      GAnswer(493,3,NodeSBrahmin206)
      end
      else if ((global_var(GVAR_BRAHMIN_STATUS_RANGER) >= 0) and (getQuest(GVAR_BRAHMIN_QST_RANGER) >= qEnd)) then begin
      GAnswer(493,3,NodeSBrahmin206)
      end

      end
     end
    end
   end

   NAnswer(480,3,NodeSBrahmin202)
   NAnswer(471,1,NodeSBrahmin201)
end

procedure NodeSBrahmin201 begin
   Reply(472)
   NAnswer(473,1,Node999)
end

procedure NodeSBrahmin202 begin
   Reply(481)
   NAnswer(482,1,NodeSBrahmin203)
end

procedure NodeSBrahmin203 begin
   Reply(483)
   //zNAnswer(484,1,NodeSndDialog)
   NAnswer(485,1,Node999)
   NAnswer(486,1,Node999)
end

procedure NodeSBrahmin204 begin
   Reply(488)
   NAnswer(489,1,NodeSBrahmin207)
end

procedure NodeSBrahmin205 begin
   Reply(491)
   NAnswer(492,1,NodeSBrahmin207)
end

procedure NodeSBrahmin206 begin
   Reply(494)
   NAnswer(495,1,NodeSBrahmin207)
end

procedure NodeSBrahmin207 begin
   Reply(496)
   NAnswer(497,1,NodeSBrahmin208)
end

procedure NodeSBrahmin208 begin
   PLAY_FADE
   set_REP(5)

   if (getQuest(GVAR_BRAHMIN_QST_COMMANDER) < qZero) then begin
      if (getQuest(GVAR_BRAHMIN_QST_GERONIMO) >= 20) then begin
      set_SLAVA(25) // учесть, что слава также повышается у Джеронимо в конце его квеста
      set_REP(10)
      Reply(498)
      NAnswer(499,1,NodeSBrahmin209)
      end
      else begin
      Reply(475)
      NAnswer(476,1,NodeSBrahmin209)
      end
   end
   else begin
      call NodeSBrahmin209;
      //Reply(475)
      //NAnswer(476,1,NodeSBrahmin209)
   end
end

procedure NodeSBrahmin209 begin
   Reply(477)
   NAnswer(478,1,NodeSBrahmin210)
end

procedure NodeSBrahmin210 begin
   setQuest(GVAR_RANGERS_QST_SBRAHMIN,qEnd);
   setQuest(GVAR_RANGERS_QST_PHOENIX,qStart);
   exp_add(1500);

   Reply(479)
   NAnswer(468,1,Node999)
end


//--- КВЕСТ В ФЕНИКСЕ

procedure NodePhoenix200 begin
   Reply(501)
   if (getQuest(GVAR_PHENIX_CULT_DESTROY) >= qEnd) then begin
   GAnswer(511,3,NodePhoenix203)
   end
   else if (global_var(GVAR_PHENIX_STATUS_CULT_AARON) < 0) then begin
   GAnswer(518,3,NodePhoenix205)
   end
   NAnswer(503,3,NodePhoenix201)
   NAnswer(506,3,NodePhoenix202)
   NAnswer(502,1,NodeSndDialog)
end

procedure NodePhoenix201 begin
   Reply(504)
   NAnswer(510,3,NodePhoenix202)
   NAnswer(505,1,NodeSndDialog)
end

procedure NodePhoenix202 begin
   Reply(507)
   NAnswer(508,1,NodePhoenix202b)
end

procedure NodePhoenix202b begin
   Reply(523)
   NAnswer(509,3,NodePhoenix201)
   NAnswer(524,1,NodeSndDialog)
end

procedure NodePhoenix203 begin
   Reply(512)
   NAnswer(513,1,NodePhoenix204)
end

procedure NodePhoenix204 begin
   zReply(514)
   NAnswer(515,1,NodePhoenix207)
end

procedure NodePhoenix205 begin
   Reply(519)
   NAnswer(520,1,NodePhoenix206)
end

procedure NodePhoenix206 begin
   Reply(521)
   NAnswer(522,1,NodePhoenix207)
end

procedure NodePhoenix207 begin
   setQuest(GVAR_RANGERS_QST_PHOENIX,qEnd);
   exp_add(1000);
   setBonusReaction(50)
   set_REP(5)

   set_map_var(MVAR_RNGRS_Councilium,1);
   move_to(self_obj,TILE_GEN_CONCILIUM,self_elevation);

   Reply(516)
   NAnswer(517,1,Node999)
end

//--- СОВЕТ КОМАНДИРОВ

procedure NodeConcilium begin
   Reply(590)
   NAnswer(591,1,NodeConcilium101)
end

procedure NodeConcilium101 begin
   /*
   Комментарий: Слишком резкий переход к теме Братства.
   По идеи здесь должна быть промежуточная миссия в Ногалесе, которая знакомит игрока с Братством.
   */
   Reply(mstr(592)+dude_name+mstr(593))
   NAnswer(594,1,NodeConcilium102)
end

procedure NodeConcilium102 begin
   Reply(595)
   NAnswer(596,1,NodeConcilium103)
end

procedure NodeConcilium103 begin
   Reply(597)
   NAnswer(598,1,NodeConcilium104)
end

procedure NodeConcilium104 begin
   Reply(599)
   NAnswer(600,1,NodeConcilium105)
end

procedure NodeConcilium105 begin
   Reply(601)
   NAnswer(602,1,NodeConcilium106)
end

procedure NodeConcilium106 begin
   Reply(603)
   NAnswer(604,1,NodeConcilium107)
end

procedure NodeConcilium107 begin
   Reply(dude_name+mstr(605))
   NAnswer(606,1,NodeConcilium108)
end

procedure NodeConcilium108 begin
   set_map_var(MVAR_RNGRS_Councilium,2);
   setQuest(GVAR_RANGERS_QST_PUERTO,qStart);
   //!!! Мне нужны крышки, припасы и оружие, чтобы провести разведку.
   Reply(607)
   NAnswer(608,1,NodeConcilium109)
   BAnswer(611,1,NodeConcilium110)
   NAnswer(610,1,Node999)
end

procedure NodeConcilium109 begin
   Reply(609)
   BAnswer(611,1,NodeConcilium110)
   NAnswer(610,1,Node999)
end

procedure NodeConcilium110 begin
   Reply(612)
   NAnswer(613,1,Node999)
   BAnswer(614,1,Node999)
end

procedure NodeConcilium200 begin
   Reply(651)
   if ((town_known(AREA_PUERTO_STEEL) == MARK_STATE_VISITED) or (town_known(AREA_PUERTO_DESTROY) > MARK_STATE_UNKNOWN)) then begin
   GAnswer(620,1,NodeConcilium200b)
   end
   NAnswer(653,1,NodeConcilium210)
   NAnswer(652,1,NodeSndDialog)
end

procedure NodeConcilium200b begin
   Reply(621)

   if (getQuest(GVAR_PUERTO_BOS_DESTROY) >= qEnd) then begin
   GAnswer(637,1,NodeConcilium206)
   end
   else if (getQuest(GVAR_PUERTO_QST_RANGERS) >= qStart) then begin
   BAnswer(628,1,NodeConcilium203)
   end
   NAnswer(626,1,NodeConcilium202)
   NAnswer(622,1,NodeConcilium201)
end

procedure NodeConcilium201 begin
   Reply(623)
   NAnswer(625,1,NodeSndDialog)
   NAnswer(624,1,Node999)
end

procedure NodeConcilium202 begin
   Reply(627)
   NAnswer(625,1,NodeSndDialog)
   NAnswer(624,1,Node999)
end

procedure NodeConcilium203 begin
   Reply(629)
   NAnswer(630,1,NodeConcilium204)
   BAnswer(633,1,NodeConcilium205)
end

procedure NodeConcilium204 begin
   Reply(631)
   NAnswer(632,1,Node999)
end

procedure NodeConcilium205 begin
   Reply(634)
   NAnswer(635,1,Node998)
   BAnswer(636,1,Node998)
end

procedure NodeConcilium206 begin
   setQuest(GVAR_VILLA_ALLIANCE_RANGER,qEnd);
   setQuest(GVAR_VILLA_ALLIANCE_BOS,qStop);
   setQuest(GVAR_RANGERS_QST_PUERTO,qEnd);
   setQuest(GVAR_PUERTO_QST_RANGERS,qStop);
   exp_add(2500);
   setBonusReaction(100)
   set_REP(15)

   Reply(638)
   NAnswer(639,1,NodeConcilium207)
end

procedure NodeConcilium207 begin
   if (global_var(GVAR_TITUL_ARIZONA_RANGER) != 0) then begin
   Reply(647)
   zNAnswer(643,1,Node999)
   end
   else begin
   Reply(640)
   GAnswer(644,1,NodeConcilium209)
   NAnswer(641,1,NodeConcilium208)
   end
end

procedure NodeConcilium208 begin
   Reply(642)
   zNAnswer(643,1,Node999)
end

procedure NodeConcilium209 begin
   call NodeDudeRanger18;
   Reply(645)
   NAnswer(646,1,Node999)
end

procedure NodeConcilium210 begin
   Reply(654)
   NAnswer(655,1,NodeConcilium211)
   NAnswer(648,1,NodeConcilium213)
   NAnswer(659,1,NodeConcilium200)
end

procedure NodeConcilium211 begin
   Reply(656)
   NAnswer(657,1,NodeConcilium212)
   NAnswer(648,1,NodeConcilium213)
   NAnswer(659,1,NodeConcilium200)
end

procedure NodeConcilium212 begin
   Reply(658)
   NAnswer(655,1,NodeConcilium211)
   NAnswer(648,1,NodeConcilium213)
   NAnswer(659,1,NodeConcilium200)
end

procedure NodeConcilium213 begin
   Reply(649)
   NAnswer(655,1,NodeConcilium211)
   NAnswer(659,1,NodeConcilium200)
end
