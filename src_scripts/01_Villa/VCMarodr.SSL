#include "..\headers\define.h"
//---------------------

#define NAME                            SCRIPT_VCMarodr
#define TOWN_REP_VAR                    -1   // привязка к репутации (-1, если не нужно)

#define NUM_TEAM                        TEAM_BANDITS          // группа, к которой принадлежит персонаж (-1 если нужно оставить Team, заданный в редакторе)
#define NUM_AI                          AI_BANDIT             // AI персонажа (-1 если нужно оставить AI, заданный в редакторе)
#define KILL_SLAVA                      SLAVA_KILL_BAD        // слава за убийство этого персонажа (SLAVA_KILL_GOOD, SLAVA_KILL_BAD, SLAVA_KILL_NEITRAL, SLAVA_KILL_CHILD)
#define KILL_TOWN_REP                   REP_KILL_NEITRAL      // репутация за убийство этого персонажа (REP_KILL_GOOD, REP_KILL_BAD, REP_KILL_NEITRAL, REP_KILL_CRITICAL для враждебности всего поселения, REP_KILL_CHILD)
#define KILL_FRAC_REP                   FRAC_NON              // принадлежность определенной фракции (-1, если не нужно)

#define FLAG_STEAL                      60 // Нижний порог навыка для кражи (для торговцев 200, для животных 300)
#define FLAG_ALCOHOL                    1  // Примет ли алкоголь от ГГ: 0 - принимает, 1 - не принимает
#define FLAG_FOOD                       1  // Примет ли пищу от ГГ: 0 - принимает, 1 - не принимает

//---------------------

procedure start;
procedure map_enter_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;
procedure pickup_p_proc;
procedure combat_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;

procedure Node998;
procedure Node999;
procedure NodeStartDialog;
procedure NodeDialog000;
procedure NodeDialog001;
procedure NodeDialog001b;
procedure NodeDialog002;
procedure NodeDialog003;
procedure NodeDialog004;
procedure NodeDialog005;
procedure NodeDialog006;
procedure NodeDialog007;
procedure NodeDialog008;
procedure NodeDialog009;

//---------------------

//#define LVAR_Flag_A                     (6) // отвечает за ...

//---------------------



procedure start begin
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
       set_start_home
       if (global_var(GVAR_VILLA_MARAUDERS_RUN) != 0) then begin
          rm_timer_event(self_obj);
          set_obj_visibility(self_obj, 1);
          destroy_object(self_obj);
       end
   end
end

procedure look_at_p_proc begin
   lookProcMsg
end

procedure description_p_proc begin
   dsrptProcMsg
end

procedure timed_event_p_proc begin
   // Таймер за номером 1 рарезервирован под перемещение персонажа по локации
   // Таймер за номером 2 рарезервирован под всплывающие реплики
   if (fixed_param == 1) then begin
       if (global_var(GVAR_VILLA_MARAUDERS_RUN) == 0) then begin
       timer_move_to_home
       end
   end
   if (fixed_param == 2) then begin
       //timer_float_reply(120,121)
   end
end

procedure critter_p_proc begin
   set_critter_options(0,0) //условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ

   //--- Персонаж уходит с карты
   if (global_var(GVAR_VILLA_MARAUDERS_RUN) >= 10) then begin
      if (combat_is_initialized == false) then begin
          if (self_tile == 19457) then begin
          set_obj_visibility(self_obj, 1);
          destroy_object(self_obj);
          end
          else begin
          move_to_tile(0,19457,0)
          end
      end
   end
end

procedure use_skill_on_p_proc begin
   skillUseSteal
   skillUseFAid
end

procedure pickup_p_proc begin
   pickupProcCritter
end

procedure use_obj_on_p_proc begin
   UsedItemToCrit_Proc
end

procedure combat_p_proc begin
   effectAttak(0,0,0,1,1) //отравление min, облучение min, травма ног %, травма рук %, травма глаз %
end

procedure damage_p_proc begin
   set_damage_dude
end

procedure destroy_p_proc begin
   kill_critter_opt
   set_global_var(GVAR_VILLA_MARAUDERS_RUN,1);
end

procedure talk_p_proc begin
   if (global_var(GVAR_VILLA_MARAUDERS_RUN) == 0) then begin
      setReaction(-1) // Отношение к ГГ(1 - персонаж положительный, -1 отрицательный, 0 нейтральный)
      fracReaction(GVAR_TITUL_ARIZONA_RANGER,-20) // Поправка к отношению за принадлежность к фракциям
      dialog_proc(1,1,NodeStartDialog,-1,-1)
   end
   else begin
      floater(self_obj,random(110,113),COLOR_MSG_NORMAL);
   end
end

procedure Node998 begin
   set_enemy_dude;
end

procedure Node999 begin
end

procedure NodeStartDialog begin
   if (map_var(MVAR_VILLA_MaraudersKnow) == 0) then begin
   call NodeDialog000;
   end
   else begin
   call NodeDialog001b;
   end
end

procedure NodeDialog000 begin
   inc_map_var(MVAR_VILLA_MaraudersKnow);
   Reply(200)
   NAnswer(201,1,NodeDialog001)
end

procedure NodeDialog001 begin
   Reply(202)
   NAnswer(203,1,NodeDialog002)
   NAnswer(205,1,NodeDialog003)
   NAnswer(207,1,NodeDialog004)
   NAnswer(209,4,NodeDialog005)
   if (item_amnt(dude_obj,PID_CORN_FLOUR) >= 10) then begin
   GAnswer(213,1,NodeDialog008)
   end
   if (getQuest(GVAR_RANGERS_QST_PUERTO)>=qEnd) then begin
   BAnswer(223,1,NodeDialog009)
   end
   NAnswer(215,1,Node999)
end

procedure NodeDialog001b begin
   Reply(218)
   NAnswer(203,1,NodeDialog002)
   NAnswer(205,1,NodeDialog003)
   NAnswer(207,1,NodeDialog004)
   NAnswer(209,4,NodeDialog005)
   if (item_amnt(dude_obj,PID_CORN_FLOUR) >= 10) then begin
   GAnswer(213,1,NodeDialog008)
   end
   if (getQuest(GVAR_RANGERS_QST_PUERTO)>=qEnd) then begin
   BAnswer(223,1,NodeDialog009)
   end
   NAnswer(215,1,Node999)
end

procedure NodeDialog002 begin
   Reply(204)
   NAnswer(205,1,NodeDialog003)
   NAnswer(207,1,NodeDialog004)
   NAnswer(209,4,NodeDialog005)
   if (item_amnt(dude_obj,PID_CORN_FLOUR) >= 10) then begin
   GAnswer(213,1,NodeDialog008)
   end
   if (getQuest(GVAR_RANGERS_QST_PUERTO)>=qEnd) then begin
   BAnswer(223,1,NodeDialog009)
   end
   NAnswer(215,1,Node999)
end

procedure NodeDialog003 begin
   Reply(206)
   NAnswer(203,1,NodeDialog002)
   NAnswer(207,1,NodeDialog004)
   NAnswer(209,4,NodeDialog005)
   if (item_amnt(dude_obj,PID_CORN_FLOUR) >= 10) then begin
   GAnswer(213,1,NodeDialog008)
   end
   if (getQuest(GVAR_RANGERS_QST_PUERTO)>=qEnd) then begin
   BAnswer(223,1,NodeDialog009)
   end
   NAnswer(215,1,Node999)
end

procedure NodeDialog004 begin
   Reply(208)
   NAnswer(203,1,NodeDialog002)
   NAnswer(205,1,NodeDialog003)
   NAnswer(209,4,NodeDialog005)
   if (item_amnt(dude_obj,PID_CORN_FLOUR) >= 10) then begin
   GAnswer(213,1,NodeDialog008)
   end
   if (getQuest(GVAR_RANGERS_QST_PUERTO)>=qEnd) then begin
   BAnswer(223,1,NodeDialog009)
   end
   NAnswer(215,1,Node999)
end

procedure NodeDialog005 begin
   Reply(210)
   NAnswerSkill(211,4,NodeDialog006,SKILL_CONVERSANT,50,219,NodeDialog007)
   NAnswer(203,1,NodeDialog002)
   NAnswer(205,1,NodeDialog003)
   NAnswer(207,1,NodeDialog004)
   if (item_amnt(dude_obj,PID_CORN_FLOUR) >= 10) then begin
   GAnswer(213,1,NodeDialog008)
   end
   if (getQuest(GVAR_RANGERS_QST_PUERTO)>=qEnd) then begin
   BAnswer(223,1,NodeDialog009)
   end
   NAnswer(215,1,Node999)
end

procedure NodeDialog006 begin
   bitDialogExp(bit_10,100)
   set_global_var(GVAR_VILLA_MARAUDERS_RUN,10);
   display_mstr(217);
   Reply(212)
   NAnswer(216,1,Node999)
end

procedure NodeDialog007 begin
   Reply(220)
   NAnswer(221,1,Node999)
   NAnswer(222,1,Node999)
end

procedure NodeDialog008 begin
   if (item_amnt(dude_obj,PID_CORN_FLOUR) >= 10) then begin
   bitDialogExp(bit_10,100)
   set_global_var(GVAR_VILLA_MARAUDERS_RUN,11);
   display_mstr(217);
   item_remove(dude_obj, PID_CORN_FLOUR, 10)
   item_add(self_obj,PID_CORN_FLOUR,10)
   Reply(214)
   NAnswer(216,1,Node999)
   end
   else begin
   call Node999;
   end
end

procedure NodeDialog009 begin
   bitDialogExp(bit_10,100)
   set_global_var(GVAR_VILLA_MARAUDERS_RUN,10);
   display_mstr(217);
   Reply(224)
   NAnswer(225,1,Node999)
   BAnswer(226,1,Node999)
end
