#include "..\headers\define.h"
//---------------------

#define NAME                            SCRIPT_BCCrvnQt
#define TOWN_REP_VAR                    GVAR_TOWN_REP_PHOENIX // привязка к репутации (-1, если не нужно)

#define NUM_TEAM                        TEAM_CARAVAN          // группа, к которой принадлежит персонаж (-1 если нужно оставить Team, заданный в редакторе)
#define NUM_AI                          AI_CARAVAN_DRIVER     // AI персонажа (-1 если нужно оставить AI, заданный в редакторе)
#define KILL_SLAVA                      SLAVA_KILL_GOOD       // слава за убийство этого персонажа (SLAVA_KILL_GOOD, SLAVA_KILL_BAD, SLAVA_KILL_NEITRAL, SLAVA_KILL_CHILD)
#define KILL_TOWN_REP                   REP_KILL_GOOD         // репутация за убийство этого персонажа (REP_KILL_GOOD, REP_KILL_BAD, REP_KILL_NEITRAL, REP_KILL_CRITICAL для враждебности всего поселения, REP_KILL_CHILD)
#define KILL_FRAC_REP                   FRAC_NON              // принадлежность определенной фракции (-1, если не нужно)

#define FLAG_STEAL                      50 // Нижний порог навыка для кражи (для торговцев 200, для животных 300)
#define FLAG_ALCOHOL                    1  // Примет ли алкоголь от ГГ: 0 - принимает, 1 - не принимает
#define FLAG_FOOD                       1  // Примет ли пищу от ГГ: 0 - принимает, 1 - не принимает

//---------------------

procedure start;
procedure map_enter_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;
procedure pickup_p_proc;
procedure combat_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;

procedure Node998;
procedure Node999;
procedure NodeStartDialog;
procedure NodeFstDialog;
procedure NodeFstDialog2;
procedure NodeFstDialog3;
procedure NodeFstDialog4;
procedure NodeSndDialog;
procedure NodeCaravan01;
procedure NodeCaravan02;
procedure NodeCaravan04;
procedure NodeCaravan05;
procedure NodeCaravan06;
procedure NodeCaravan03;
procedure NodeCaravan08;
procedure NodeCaravan09;
procedure NodeAnswer100;
procedure NodeAnswer101;
procedure NodeFinalDialog;

//---------------------

//#define LVAR_Flag_A                     (6) // отвечает за ...

import variable obj_critter_a; // указатель на криттера 1

//---------------------



procedure start begin
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
       set_start_home

       add_timer_event(self_obj, 5, 4);
       party_remove(self_obj);
       critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,NUM_TEAM);

       if ((cur_map_index == MAP_SB_CARAVAN_A) or (cur_map_index == MAP_SB_CARAVAN_B) or (cur_map_index == MAP_SB_CARAVAN_C)) then begin
          setQuest(GVAR_BRAHMIN_QST_CARAVANPNX,qZero);
          /*
          if (global_var(GVAR_BRAHMIN_QST_CARAVAN_DUDEENEMY) == 0) then begin
             critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_PLAYER);
          end
          */
          add_timer_event(self_obj, 1, 5);
          if (item_amnt(self_obj,PID_W_PISTOL_10)>0) then begin
             if (item_amnt(self_obj,PID_AMMO_10_MM)<=6) then begin
             item_add(self_obj,PID_AMMO_10_MM,12)
             end
             reg_anim_clear(self_obj);
             reg_anim_begin();
             wield_obj(obj_carrying_pid_obj(self_obj, PID_W_PISTOL_10));
             reg_anim_end();
          end
       end

       if ((getQuest(GVAR_BRAHMIN_QST_CARAVANPNX) <= qStop) or (getQuest(GVAR_BRAHMIN_QST_CARAVANPNX) >= qEnd)) then begin
          rm_timer_event(self_obj);
          set_obj_visibility(self_obj, 1);
          destroy_object(self_obj);
       end
   end
end

procedure look_at_p_proc begin
   lookProcMsg
end

procedure description_p_proc begin
   dsrptProcMsg
end

procedure timed_event_p_proc begin
   // Таймер за номером 1 рарезервирован под перемещение персонажа по локации
   // Таймер за номером 2 рарезервирован под всплывающие реплики
   if (fixed_param == 1) then begin
       timer_move_to_home
   end
   if (fixed_param == 2) then begin
       //timer_float_reply(110,119)
   end
   if (fixed_param == 3) then begin
       set_global_var(GVAR_BRAHMIN_QST_CARAVAN_ENEMY, 0);
       if (getQuest(GVAR_BRAHMIN_QST_CARAVANPNX) == qZero) then begin
       setQuest(GVAR_BRAHMIN_QST_CARAVANPNX,qStart);
       end
       critter_uninjure(self_obj, (DAM_BLIND bwor DAM_CRIP_LEG_LEFT bwor DAM_CRIP_LEG_RIGHT bwor DAM_CRIP_ARM_LEFT bwor DAM_CRIP_ARM_RIGHT));
       critter_heal(self_obj,self_max_hits-self_cur_hits);
       party_add(self_obj);

       if (cur_map_index == MAP_SAN_BRAHMIN_GARD) then begin
       wm_area_set_pos(AREA_SHADOW_WORLDS, 725, 525); //! уточнить координаты
       dude_teleport_map(1,1,MAP_SB_CARAVAN_A,4);
       end
       if (cur_map_index == MAP_SB_CARAVAN_A) then begin
       wm_area_set_pos(AREA_SHADOW_WORLDS, 875, 525); //! уточнить координаты
       dude_teleport_map(1,1,MAP_SB_CARAVAN_B,10);
       end
       if (cur_map_index == MAP_SB_CARAVAN_B) then begin
       wm_area_set_pos(AREA_SHADOW_WORLDS, 875, 575); //! уточнить координаты
       dude_teleport_map(1,1,MAP_SB_CARAVAN_C,12);
       end
       if (cur_map_index == MAP_SB_CARAVAN_C) then begin
       dude_teleport_map(1,1,MAP_PHOENIX_SIGN,2);
       end
   end
   if (fixed_param == 4) then begin
       set_local_var(LVAR_Home_Tile,self_tile);
       set_local_var(LVAR_Home_Rotat,self_cur_rot);
   end
   if (fixed_param == 5) then begin
       if (global_var(GVAR_BRAHMIN_QST_CARAVAN_NO_ENEMY) >0) then begin
       floater(self_obj,103,COLOR_MSG_NORMAL);
       display_mstr(106);
       end
       else if (cur_map_index == MAP_SB_CARAVAN_A) then begin
       floater(self_obj,250,COLOR_MSG_NORMAL);
       end
       else if (cur_map_index == MAP_SB_CARAVAN_B) then begin
       floater(self_obj,251,COLOR_MSG_NORMAL);
       end
       else if (cur_map_index == MAP_SB_CARAVAN_C) then begin
       floater(self_obj,252,COLOR_MSG_NORMAL);
       end
   end
end

procedure critter_p_proc begin
   if ((cur_map_index == MAP_SB_CARAVAN_A) or (cur_map_index == MAP_SB_CARAVAN_B) or (cur_map_index == MAP_SB_CARAVAN_C)) then begin
      if (random(0,5) == 1) then begin
      obj_critter_a := self_obj;
      end
   end
   if (global_var(GVAR_BRAHMIN_QST_CARAVAN_DUDEENEMY) >0) then begin
      critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,NUM_TEAM);
      set_critter_options(2,0) //условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ
   end
   else begin
      set_critter_options(0,0) //условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ
   end
end

procedure use_skill_on_p_proc begin
   skillUseSteal
   skillUseFAid
end

procedure pickup_p_proc begin
   pickupProcCritter
end

procedure use_obj_on_p_proc begin
   UsedItemToCrit_Proc
end

procedure combat_p_proc begin
   effectAttak(0,0,1,1,1) //отравление min, облучение min, травма ног %, травма рук %, травма глаз %
end

procedure damage_p_proc begin
   set_damage_dude
end

procedure destroy_p_proc begin
   if (obj_critter_a == self_obj) then begin
   obj_critter_a := 0;
   end
   brahminCaravanDudeEnemy
   set_global_var(GVAR_BRAHMIN_QST_CARAVAN_ENEMY, 0);
   set_global_var(GVAR_BRAHMIN_QST_CARAVAN_NO_ENEMY,0);
   if (getQuest(GVAR_BRAHMIN_QST_CARAVANPNX) < qEnd) then begin
   item_add(self_obj,PID_BOOTLE_CAPS,500)
   //display_mstr(104);
   display_msg(obj_name(self_obj)+mstr(107));
   setQuest(GVAR_BRAHMIN_QST_CARAVANPNX,qStop);
   end
   kill_critter_opt
end

procedure talk_p_proc begin
   setReaction(0) // Отношение к ГГ (1 - персонаж положительный, -1 отрицательный, 0 нейтральный)

   if (global_var(GVAR_BRAHMIN_QST_CARAVAN_DUDEENEMY) >0) then begin
      floater(self_obj,105,COLOR_MSG_PURPLE);
   end
   else if (cur_map_index == MAP_SAN_BRAHMIN_GARD) then begin
      dialog_proc(0,0,NodeStartDialog,-1,-1)
   end
   else if (cur_map_index == MAP_PHOENIX_SIGN) then begin
      if (getQuest(GVAR_BRAHMIN_QST_CARAVANPNX) >= qEnd) then begin
      floater(self_obj,102,COLOR_MSG_NORMAL);
      end
      else begin
      dialog_proc(0,0,NodeFinalDialog,-1,-1)
      end
   end
   else begin
      if (global_var(GVAR_BRAHMIN_QST_CARAVAN_ENEMY) > 0) then begin
         if (cur_map_index == MAP_SB_CARAVAN_A) then begin
         floater(self_obj,250,COLOR_MSG_NORMAL);
         end
         if (cur_map_index == MAP_SB_CARAVAN_B) then begin
         floater(self_obj,251,COLOR_MSG_NORMAL);
         end
         if (cur_map_index == MAP_SB_CARAVAN_C) then begin
         floater(self_obj,252,COLOR_MSG_NORMAL);
         end
      end
      else begin
         dialog_proc(0,0,NodeCaravan04,-1,-1)
      end
   end
end

procedure Node998 begin
   set_enemy_dude;
end

procedure Node999 begin
end

procedure NodeStartDialog begin
   if (bitDialog(bit_2) != 0) then begin
   call NodeCaravan01;
   end
   else if (bitDialog(bit_1) == 0) then begin
   call NodeFstDialog;
   end
   else begin
   call NodeSndDialog;
   end
end

//--- ПЕРВЫЙ ДИАЛОГ

procedure NodeFstDialog begin
   bitDialogExp(bit_1,0)
   zReply(200)
   NAnswer(201,1,NodeFstDialog2)
   NAnswer(204,1,NodeFstDialog3)
end

procedure NodeFstDialog2 begin
   Reply(202)
   NAnswer(203,1,NodeFstDialog4)
end

procedure NodeFstDialog3 begin
   Reply(205)
   NAnswer(206,1,NodeFstDialog4)
end

procedure NodeFstDialog4 begin
   Reply(207)
   NAnswer(210,1,NodeAnswer100)
   NAnswer(212,1,NodeAnswer101)
   NAnswer(209,1,NodeCaravan01)
   NAnswer(208,1,Node999)
end

//--- ПОВТОРНЫЙ ДИАЛОГ

procedure NodeSndDialog begin
   Reply(220)

   if (getQuest(GVAR_BRAHMIN_QST_CARAVANPNX) == qZero) then begin
   NAnswer(210,1,NodeAnswer100)
   NAnswer(212,1,NodeAnswer101)
   NAnswer(222,1,NodeCaravan01)
   end
   zNAnswer(221,1,Node999)
end

procedure NodeCaravan01 begin
   bitDialogExp(bit_2,0)
   Reply(230)
   NAnswer(232,1,NodeCaravan02)
   NAnswer(231,1,Node999)
end

procedure NodeCaravan02 begin
   if (item_amnt(self_obj,PID_M_STIMPAK)<=0) then begin
   item_add(self_obj,PID_M_STIMPAK,1)
   end
   if (item_amnt(self_obj,PID_W_PISTOL_10)>0) then begin
   item_remove(self_obj,PID_W_PISTOL_10,1)
   end
   item_add(self_obj,PID_W_PISTOL_10,1)
   add_timer_event(self_obj, 5, 3);
   call Node999;
end

procedure NodeCaravan03 begin
   set_global_var(GVAR_BRAHMIN_QST_CARAVAN_NO_ENEMY,1);
   call NodeCaravan02;
end

procedure NodeCaravan04 begin
   set_global_var(GVAR_BRAHMIN_QST_CARAVAN_NO_ENEMY,0);
   if (global_var(GVAR_BRAHMIN_QST_CARAVAN_DEADBRAM) >= map_var(MVAR_BRAHMIN_CARAVAN_BRAM)) then begin
      Reply(257)
      NAnswer(258,1,NodeCaravan02)
      if (cur_map_index == MAP_SB_CARAVAN_A) then begin
      NAnswerSkill(272,2,NodeCaravan09,SKILL_SCOUT,35,270,NodeCaravan08)
      end
      if (cur_map_index == MAP_SB_CARAVAN_B) then begin
      NAnswerSkill(266,2,NodeCaravan06,SKILL_SCOUT,60,264,NodeCaravan05)
      end
      NAnswer(259,1,Node999)
   end
   else begin
      Reply(254)
      NAnswer(256,1,NodeCaravan02)
      if (cur_map_index == MAP_SB_CARAVAN_A) then begin
      NAnswerSkill(272,2,NodeCaravan09,SKILL_SCOUT,35,270,NodeCaravan08)
      end
      if (cur_map_index == MAP_SB_CARAVAN_B) then begin
      NAnswerSkill(266,2,NodeCaravan06,SKILL_SCOUT,60,264,NodeCaravan05)
      end
      NAnswer(255,1,Node999)
   end
end

procedure NodeCaravan05 begin
   Reply(265)
   NAnswer(256,1,NodeCaravan02)
   NAnswer(255,1,Node999)
end

procedure NodeCaravan06 begin
   Reply(267)
   NAnswer(268,1,NodeCaravan03)
   NAnswer(269,1,Node999)
end

procedure NodeCaravan08 begin
   Reply(271)
   NAnswer(256,1,NodeCaravan02)
   NAnswer(255,1,Node999)
end

procedure NodeCaravan09 begin
   Reply(273)
   NAnswer(274,1,NodeCaravan03)
   NAnswer(275,1,NodeCaravan02)
   NAnswer(276,1,Node999)
end

procedure NodeAnswer100 begin
   Reply(211)
   NAnswer(212,1,NodeAnswer101)
   NAnswer(209,1,NodeCaravan01)
   NAnswer(208,1,Node999)
end

procedure NodeAnswer101 begin
   Reply(213)
   NAnswer(210,1,NodeAnswer100)
   NAnswer(209,1,NodeCaravan01)
   NAnswer(208,1,Node999)
end

//--- ЗАВЕРШЕНИЕ КВЕСТА

procedure NodeFinalDialog begin
   variable coastCaravan;
   coastCaravan := 500;
   coastCaravan -= (global_var(GVAR_BRAHMIN_QST_CARAVAN_DEADBRAM)*100);

   setQuest(GVAR_BRAHMIN_QST_CARAVANPNX,qEnd);
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,NUM_TEAM);
   item_add(dude_obj,PID_BOOTLE_CAPS,coastCaravan)
   set_REP(1)
   exp_add(500);

   if (global_var(GVAR_BRAHMIN_QST_CARAVAN_DEADBRAM) > 0) then begin
   Reply(mstr(262)+coastCaravan+mstr(263))
   NAnswer(261,1,Node999)
   end
   else begin
   Reply(260)
   NAnswer(261,1,Node999)
   end
end
