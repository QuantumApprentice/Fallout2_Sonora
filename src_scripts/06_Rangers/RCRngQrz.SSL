#include "..\headers\define.h"
//---------------------

#define NAME                            SCRIPT_RCRngQrz
#define TOWN_REP_VAR                    GVAR_TOWN_REP_RANGERS // привязка к репутации (-1, если не нужно)

#define NUM_TEAM                        TEAM_RANGERS          // группа, к которой принадлежит персонаж (-1 если нужно оставить Team, заданный в редакторе)
#define NUM_AI                          AI_RANGER             // AI персонажа (-1 если нужно оставить AI, заданный в редакторе)
#define KILL_SLAVA                      SLAVA_KILL_GOOD       // слава за убийство этого персонажа (SLAVA_KILL_GOOD, SLAVA_KILL_BAD, SLAVA_KILL_NEITRAL, SLAVA_KILL_CHILD)
#define KILL_TOWN_REP                   REP_KILL_GOOD         // репутация за убийство этого персонажа (REP_KILL_GOOD, REP_KILL_BAD, REP_KILL_NEITRAL, REP_KILL_CRITICAL для враждебности всего поселения, REP_KILL_CHILD)
#define KILL_FRAC_REP                   FRAC_NON              // принадлежность определенной фракции (-1, если не нужно)

#define FLAG_STEAL                      60 // Нижний порог навыка для кражи (для торговцев 200, для животных 300)
#define FLAG_ALCOHOL                    1  // Примет ли алкоголь от ГГ: 0 - принимает, 1 - не принимает
#define FLAG_FOOD                       0  // Примет ли пищу от ГГ: 0 - принимает, 1 - не принимает

//---------------------

procedure start;
procedure map_enter_p_proc;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure push_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;
procedure pickup_p_proc;
procedure combat_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;

procedure Node998;
procedure Node999;
procedure NodeStartDialog;
procedure NodeFstDialog;
procedure NodeFstDialog2;
procedure NodeFstDialog3;
procedure NodeFstDialog4;
procedure NodeFstDialog5;
procedure NodeFstDialog6;
procedure NodeFstDialog7;
procedure NodeFstDialog8;
procedure NodeSndDialog;
procedure NodeAnswer100;
procedure NodeAnswer101;
procedure NodeAnswer102;
procedure NodeAnswer200;
procedure NodeAnswer300;
procedure NodeAnswer301;
procedure NodeAnswer302;
procedure NodeAnswer303;
procedure NodeAnswer304;
procedure NodeAnswer305;

//---------------------

#define LVAR_Party_Status               (6) // приобщение персонажа к партии ГГ: 0 - не приобщен, 1 - приобщен

//---------------------



procedure start begin
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
       set_local_var(LVAR_Party_Status,0);
       set_start_home
       obj_set_light_level(self_obj, 80, 8);
       if (getQuest(GVAR_RANGERS_QST_QUARTZ) >= qEnd) then begin
       set_obj_visibility(self_obj, 1);
       end
   end
end

procedure timed_event_p_proc begin
   // Таймер за номером 1 рарезервирован под перемещение персонажа по локации
   // Таймер за номером 2 рарезервирован под всплывающие реплики
   if (fixed_param == 1) then begin
       //timer_move_to_home
   end
   if (fixed_param == 2) then begin
       //timer_float_reply(110,119)
   end
end

procedure critter_p_proc begin
   set_critter_options(0,0) //условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ
   //--- Следует за ГГ
   if (local_var(LVAR_Party_Status) > 0) then begin
   partyRunToDude
   end
end

procedure push_p_proc begin
end

procedure use_skill_on_p_proc begin
   skillUseSteal
   skillUseFAid
end

procedure pickup_p_proc begin
   pickupProcCritter
end

procedure use_obj_on_p_proc begin
   UsedItemToCrit_Proc
   if (objUsedPid == PID_AMMO_308) then begin
       script_overrides;
       floater(self_obj,102,COLOR_MSG_NORMAL);
       item_add(self_obj,PID_AMMO_308,1)
       item_remove(dude_obj, objUsedPid, 1)
   end
end

procedure combat_p_proc begin
   effectAttak(0,0,1,1,1) //отравление min, облучение min, травма ног %, травма рук %, травма глаз %
end

procedure damage_p_proc begin
   set_damage_dude
end

procedure destroy_p_proc begin
   kill_critter_opt
end

procedure talk_p_proc begin
   setReaction(1) // Отношение к ГГ(1 - персонаж положительный, -1 отрицательный, 0 нейтральный)

   fracReaction(GVAR_TITUL_ARIZONA_RANGER,70) // Поправка к отношению за принадлежность к фракциям
   fracReaction(GVAR_TITUL_PHENIX_CULT,-40) // Поправка к отношению за принадлежность к фракциям
   fracReaction(GVAR_TITUL_BOS,-40) // Поправка к отношению за принадлежность к фракциям
   fracReaction(GVAR_TITUL_BOS_TRAITOR,20) // Поправка к отношению за принадлежность к фракциям
   fracReaction(GVAR_TITUL_WARRIOR_ROAD,-20) // Поправка к отношению за принадлежность к фракциям

   dialog_proc(0,0,NodeStartDialog,-1,-1)
end

procedure Node998 begin
   set_enemy_dude;
end

procedure Node999 begin
end

procedure NodeStartDialog begin
   if (bitDialog(bit_1) == 0) then begin
   call NodeFstDialog;
   end
   else begin
   call NodeSndDialog;
   end
end

//--- ПЕРВЫЙ ДИАЛОГ

procedure NodeFstDialog begin
   bitDialogExp(bit_1,0)
   zReply(200)
   if (getQuest(GVAR_RANGERS_QST_QUARTZ) > qZero) then begin
   NAnswer(201,1,NodeFstDialog2)
   end
   if (getQuest(GVAR_RANGERS_QST_QUARTZMAP) > qZero) then begin
   NAnswer(203,1,NodeFstDialog3)
   end
   NAnswer(205,1,NodeFstDialog4)
   BAnswer(207,1,NodeFstDialog5)
end

procedure NodeFstDialog2 begin
   setBonusReaction(20)
   Reply(202)
   NAnswer(209,1,NodeFstDialog6)
end

procedure NodeFstDialog3 begin
   setBonusReaction(20)
   Reply(204)
   NAnswer(209,1,NodeFstDialog6)
end

procedure NodeFstDialog4 begin
   Reply(206)
   NAnswer(209,1,NodeFstDialog6)
end

procedure NodeFstDialog5 begin
   setBonusReaction(-40)
   Reply(208)
   NAnswer(209,1,NodeFstDialog6)
end

procedure NodeFstDialog6 begin
   Reply(210)
   GAnswer(211,1,NodeFstDialog7)
   BAnswer(212,1,NodeFstDialog8)
   NAnswer(213,1,Node999)
end

procedure NodeFstDialog7 begin
   setBonusReaction(20)
   call Node999;
end

procedure NodeFstDialog8 begin
   setBonusReaction(-10)
   call Node999;
end

//--- ПОВТОРНЫЙ ДИАЛОГ

procedure NodeSndDialog begin
   if ((local_var(LVAR_Party_Status)>0) and (map_var(MVAR_Quartz_GhoulDead)>=6)) then begin
   Reply(223)
   end
   else begin
   Reply(220)
   end

   NAnswer(230,1,NodeAnswer100)
   NAnswer(240,1,NodeAnswer200)
   NAnswer(250,1,NodeAnswer300)
   NAnswer(221,1,Node999)
end

procedure NodeAnswer100 begin
   if (dude_elevation == 2) then begin
   Reply(237)
   end
   else begin
   Reply(232)
   end
   NAnswer(233,1,NodeAnswer101)
   NAnswer(235,1,NodeAnswer102)
   NAnswer(222,1,NodeSndDialog)
end

procedure NodeAnswer101 begin
   Reply(234)
   NAnswer(235,1,NodeAnswer102)
   NAnswer(222,1,NodeSndDialog)
end

procedure NodeAnswer102 begin
   Reply(236)
   NAnswer(233,1,NodeAnswer101)
   NAnswer(222,1,NodeSndDialog)
end

procedure NodeAnswer200 begin
   Reply(241)
   NAnswer(222,1,NodeSndDialog)
end

procedure NodeAnswer300 begin
   Reply(251)
   NAnswer(252,1,NodeAnswer301)
   if (local_var(LVAR_Party_Status) == 0) then begin
   NAnswer(254,1,NodeAnswer302)
   end
   NAnswer(222,1,NodeSndDialog)
end

procedure NodeAnswer301 begin
   Reply(253)
   if (local_var(LVAR_Party_Status) == 0) then begin
      if (get_SLAVA >0) then begin
      NAnswer(254,1,NodeAnswer302)
      end
      else begin
      NAnswer(254,1,NodeAnswer303)
      end
   end
   NAnswer(222,1,NodeSndDialog)
end

procedure NodeAnswer302 begin
   Reply(255)
   NAnswerSkill(256,3,NodeAnswer305,SKILL_CONVERSANT,75,256,NodeAnswer304)
   if (global_var(GVAR_TITUL_ARIZONA_RANGER) > 0) then begin
   NAnswer(257,1,NodeAnswer305)
   end
   NAnswer(222,1,NodeSndDialog)
end

procedure NodeAnswer303 begin
   Reply(258)
   NAnswer(259,1,NodeSndDialog)
end

procedure NodeAnswer304 begin
   Reply(260)
   NAnswer(261,1,NodeSndDialog)
   NAnswer(262,1,Node999)
end

procedure NodeAnswer305 begin
   inc_local_var(LVAR_Party_Status);
   setBonusReaction(20)
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_PLAYER);
   Reply(263)
   NAnswer(264,1,Node999)
   NAnswer(265,1,Node999)
end
