#include "..\headers\define.h"
//---------------------

#define NAME                            SCRIPT_SCInstr
#define TOWN_REP_VAR                    GVAR_TOWN_REP_PUERTO // привязка к репутации (-1, если не нужно)

#define NUM_TEAM                        TEAM_BROTHERS_STEEL   // группа, к которой принадлежит персонаж (-1 если нужно оставить Team, заданный в редакторе)
#define NUM_AI                          AI_BOS_PALADIN         // AI персонажа (-1 если нужно оставить AI, заданный в редакторе)
#define KILL_SLAVA                      SLAVA_KILL_GOOD       // слава за убийство этого персонажа (SLAVA_KILL_GOOD, SLAVA_KILL_BAD, SLAVA_KILL_NEITRAL, SLAVA_KILL_CHILD)
#define KILL_TOWN_REP                   REP_KILL_CRITICAL     // репутация за убийство этого персонажа (-100 для враждебности всего поселения)
#define KILL_FRAC_REP                   FRAC_BOS              // принадлежность определенной фракции (-1, если не нужно)

#define FLAG_STEAL                      90 // Нижний порог навыка для кражи (для торговцев 200, для животных 300)
#define FLAG_ALCOHOL                    1  // Примет ли алкоголь от ГГ: 0 - принимает, 1 - не принимает
#define FLAG_FOOD                       1  // Примет ли пищу от ГГ: 0 - принимает, 1 - не принимает

//---------------------

procedure start;
procedure map_enter_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;
procedure pickup_p_proc;
procedure combat_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;

procedure Node998;
procedure Node999;
procedure NodeStartDialog;
procedure NodeFstDialog;
procedure NodeFstDialog102;
procedure NodeFstDialog103;
procedure NodeFstDialog104;
procedure NodeFstDialog105;
procedure NodeFstDialog106;
procedure NodeFstDialog107;
procedure NodeFstDialog200;
procedure NodeFstDialog201;
procedure NodeFstDialog202;
procedure NodeFstDialog203;
procedure NodeFstDialog204;
procedure NodeFstDialog205;
procedure NodeFstDialog206;
procedure NodeSndDialog;

//---------------------

//#define LVAR_Flag_A                     (6) // отвечает за ...

import variable box_stop_use;

//---------------------



procedure start begin
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
       set_start_home
   end
end

procedure look_at_p_proc begin
   lookProcMsg
end

procedure description_p_proc begin
   dsrptProcMsg
end

procedure timed_event_p_proc begin
   // Таймер за номером 1 рарезервирован под перемещение персонажа по локации
   // Таймер за номером 2 рарезервирован под всплывающие реплики
   if (fixed_param == 1) then begin
       timer_move_to_home
   end
   if (fixed_param == 2) then begin
       //timer_float_reply(110,119)
   end
end

procedure critter_p_proc begin
   set_using_stop // привязка хозяина и объекта (удалить, если не нужно)
   set_critter_options(1,0) //условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ
   if self_enemy_dude then begin
   BarklyAttakDude
   end
end

procedure use_skill_on_p_proc begin
   skillUseSteal
   skillUseFAid
end

procedure pickup_p_proc begin
   pickupProcCritter
end

procedure use_obj_on_p_proc begin
   UsedItemToCrit_Proc
end

procedure combat_p_proc begin
   effectAttak(0,0,1,1,1) //отравление min, облучение min, травма ног %, травма рук %, травма глаз %
end

procedure damage_p_proc begin
   if (source_obj == dude_obj) then begin
   BarklyAttakDude
   end
   set_damage_dude
end

procedure destroy_p_proc begin
   kill_critter_opt
   kill_critter_BoS_item
end

procedure talk_p_proc begin
   setReaction(0) // Отношение к ГГ (1 - персонаж положительный, -1 отрицательный, 0 нейтральный)
   dialog_proc(0,0,NodeStartDialog,-1,-1)
end

procedure Node998 begin
   set_enemy_dude;
end

procedure Node999 begin
end

procedure NodeStartDialog begin
   if (bitDialog(bit_1) == 0) then begin
   call NodeFstDialog;
   end
   else begin
   call NodeSndDialog;
   end
end

//--- ПЕРВЫЙ ДИАЛОГ

procedure NodeFstDialog begin
   Reply(200)
   NAnswer(202,1,NodeFstDialog102)
   NAnswer(201,1,Node999)
end

procedure NodeFstDialog102 begin
   Reply(203)
   if (global_var(GVAR_TITUL_BOS_PALADIN)>0) then begin
   NAnswer(221,1,NodeFstDialog106)
   end
   if (global_var(GVAR_TITUL_BOS_SCOUT)>0) then begin
   NAnswer(222,1,NodeFstDialog106)
   end
   NAnswer(208,1,NodeFstDialog104)
   NAnswer(204,1,NodeFstDialog103)
   NAnswer(207,1,Node999)
end

procedure NodeFstDialog103 begin
   Reply(205)
   if (global_var(GVAR_TITUL_BOS_PALADIN)>0) then begin
   NAnswer(221,1,NodeFstDialog106)
   end
   if (global_var(GVAR_TITUL_BOS_SCOUT)>0) then begin
   NAnswer(222,1,NodeFstDialog106)
   end
   NAnswer(209,1,NodeFstDialog104)
   NAnswer(206,1,Node999)
end

procedure NodeFstDialog104 begin
   Reply(210)
   NAnswer(212,1,NodeFstDialog105)
   NAnswer(211,1,Node999)
end

procedure NodeFstDialog105 begin
   Reply(213)
   if (item_amnt(dude_obj,PID_BOOTLE_CAPS) >= 500) then begin
   call NodeFstDialog107;
   end
   NAnswer(214,1,Node999)
end

procedure NodeFstDialog106 begin
   Reply(223)
   call NodeFstDialog107;
   NAnswer(224,1,Node999)
end

procedure NodeFstDialog107 begin
   if (has_skill(dude_obj, SKILL_SMALL_GUNS) < 100) then begin
   NAnswer(215,1,NodeFstDialog201)
   end
   if (has_skill(dude_obj, SKILL_BIG_GUNS) < 100) then begin
   NAnswer(216,1,NodeFstDialog202)
   end
   if (has_skill(dude_obj, SKILL_ENERGY_WEAPONS) < 100) then begin
   NAnswer(217,1,NodeFstDialog203)
   end
   if (has_skill(dude_obj, SKILL_UNARMED_COMBAT) < 100) then begin
   NAnswer(218,1,NodeFstDialog204)
   end
   if (has_skill(dude_obj, SKILL_MELEE) < 100) then begin
   NAnswer(219,1,NodeFstDialog205)
   end
   if (has_skill(dude_obj, SKILL_THROWING) < 100) then begin
   NAnswer(220,1,NodeFstDialog206)
   end
end

procedure NodeFstDialog200 begin
   bitDialogExp(bit_1,0)
   PLAY_FADE
   if (global_var(GVAR_TITUL_BOS) <= 0) then begin
   item_remove(dude_obj,PID_BOOTLE_CAPS,500)
   end
   call Node999;
end

procedure NodeFstDialog201 begin
   skill_up(SKILL_SMALL_GUNS,10);
   call NodeFstDialog200;
end
procedure NodeFstDialog202 begin
   skill_up(SKILL_BIG_GUNS,10);
   call NodeFstDialog200;
end
procedure NodeFstDialog203 begin
   skill_up(SKILL_ENERGY_WEAPONS,10);
   call NodeFstDialog200;
end
procedure NodeFstDialog204 begin
   skill_up(SKILL_UNARMED_COMBAT,10);
   call NodeFstDialog200;
end
procedure NodeFstDialog205 begin
   skill_up(SKILL_MELEE,10);
   call NodeFstDialog200;
end
procedure NodeFstDialog206 begin
   skill_up(SKILL_THROWING,10);
   call NodeFstDialog200;
end

//--- ПОВТОРНЫЙ ДИАЛОГ

procedure NodeSndDialog begin
   Reply(225)
   NAnswer(226,1,Node999)
end
