#include "..\headers\define.h"
//---------------------

#define NAME                            SCRIPT_FCTraper
#define TOWN_REP_VAR                    GVAR_TOWN_REP_FLAGSTAFF   // привязка к репутации (-1, если не нужно)

#define NUM_TEAM                        TEAM_FLAGSTAFF_CITIZEN // группа, к которой принадлежит персонаж (-1 если нужно оставить Team, заданный в редакторе)
#define NUM_AI                          AI_SLAVER             // AI персонажа (-1 если нужно оставить AI, заданный в редакторе)
#define KILL_SLAVA                      SLAVA_KILL_GOOD       // слава за убийство этого персонажа (SLAVA_KILL_GOOD, SLAVA_KILL_BAD, SLAVA_KILL_NEITRAL, SLAVA_KILL_CHILD)
#define KILL_TOWN_REP                   REP_KILL_NEITRAL      // репутация за убийство этого персонажа (REP_KILL_GOOD, REP_KILL_BAD, REP_KILL_NEITRAL, REP_KILL_CRITICAL для враждебности всего поселения, REP_KILL_CHILD)
#define KILL_FRAC_REP                   FRAC_NON              // принадлежность определенной фракции (-1, если не нужно)

#define FLAG_STEAL                      0  // Нижний порог навыка для кражи (для торговцев 200, для животных 300)
#define FLAG_ALCOHOL                    1  // Примет ли алкоголь от ГГ: 0 - принимает, 1 - не принимает
#define FLAG_FOOD                       0  // Примет ли пищу от ГГ: 0 - принимает, 1 - не принимает

//---------------------

procedure start;
procedure map_enter_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;
procedure pickup_p_proc;
procedure push_p_proc;
procedure combat_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;

procedure Node998;
procedure Node999;
procedure NodeStartDialog;
procedure NodeFstDialog;
procedure NodeFstDialog2;
procedure NodeFstDialog3;
procedure NodeFstDialog4;
procedure NodePartyAdd;
procedure NodePartyDelete;
procedure NodeSndDialog;
procedure NodeSndDialog2;
procedure NodeAnswer100;
procedure NodeAnswer101;
procedure NodeAnswer102;
procedure NodeAnswer103;
procedure NodeAnswer104;
procedure NodeAnswer105;
procedure NodeAnswer106;
procedure NodeAnswer107;


//---------------------

//#define LVAR_Flag_A                     (6) // отвечает за ...

//---------------------



procedure start begin
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
       set_start_home
       if (global_var(GVAR_FLAGSTAFF_STATUS_TRAPER) > 1) then begin
       rm_timer_event(self_obj);
       destroy_object(self_obj);
       end
       else if ((global_var(GVAR_FLAGSTAFF_STATUS_TRAPER) == 1) and (cur_map_index != MAP_FLAGSTAFF_WINON)) then begin
       rm_timer_event(self_obj);
       add_timer_event(self_obj, 5, 3);
       end
   end
end

procedure look_at_p_proc begin
   lookProcMsg
end

procedure description_p_proc begin
   dsrptProcMsg
end

procedure timed_event_p_proc begin
   // Таймер за номером 1 рарезервирован под перемещение персонажа по локации
   // Таймер за номером 2 рарезервирован под всплывающие реплики
   if (fixed_param == 1) then begin
       timer_move_to_home
   end
   if (fixed_param == 2) then begin
       //timer_float_reply(120,121)
   end
   if (fixed_param == 3) then begin
       inven_unwield(self_obj);
       dialogue_system_enter;
   end
   if (fixed_param == 4) then begin
       set_obj_visibility(self_obj, 1);
       destroy_object(self_obj);
   end
end

procedure critter_p_proc begin
   set_critter_options(0,0) //условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ
   if (global_var(GVAR_FLAGSTAFF_STATUS_TRAPER) == 1) then begin
   partyRunToDude
   end
end

procedure use_skill_on_p_proc begin
   skillUseSteal
   skillUseFAid
end

procedure pickup_p_proc begin
   pickupProcCritter
end

procedure push_p_proc begin
   if (global_var(GVAR_FLAGSTAFF_STATUS_TRAPER) == 1) then begin
   end
   else begin
   push_options(HomeTile,2,-1) //Толкать (точка отсчета, макс расстояние, фраза нпс при толкании), удалить если не надо
   end
end

procedure use_obj_on_p_proc begin
   UsedItemToCrit_Proc
end

procedure combat_p_proc begin
   effectAttak(0,0,1,1,1) //отравление min, облучение min, травма ног %, травма рук %, травма глаз %
end

procedure damage_p_proc begin
   set_damage_dude
end

procedure destroy_p_proc begin
   set_global_var(GVAR_FLAGSTAFF_STATUS_TRAPER, -1);
   kill_critter_opt
end

procedure talk_p_proc begin
   if ((tile_distance_objs(self_obj,dude_obj)>4) and (global_var(GVAR_FLAGSTAFF_STATUS_TRAPER) == 0)) then begin
   floater(self_obj, 102, COLOR_MSG_LIGHT_GREY);
   end
   else begin
   dialog_proc(0,0,NodeStartDialog,-1,-1)
   end
end

procedure Node998 begin
end

procedure Node999 begin
end

procedure NodeStartDialog begin
   if (global_var(GVAR_FLAGSTAFF_STATUS_TRAPER) > 1) then begin
      call Node999;
   end
   else if (cur_map_index != MAP_FLAGSTAFF_WINON) then begin
      call NodeSndDialog2;
   end
   else if (global_var(GVAR_FLAGSTAFF_STATUS_TRAPER) == 0) then begin
      if (bitDialog(bit_1) == 0) then begin
      call NodeFstDialog;
      end
      else begin
      call NodeFstDialog4;
      end
   end
   else begin
      call NodeSndDialog;
   end
end

//--- ПЕРВЫЙ ДИАЛОГ

procedure NodeFstDialog begin
   bitDialogExp(bit_1,0)
   Reply(200)
   if (getQuest(GVAR_FLAGSTAFF_QST_TRAPERS) == qStart) then begin
   NAnswer(201,1,NodeFstDialog2)
   end
   zNAnswer(203,1,NodeFstDialog3)
end

procedure NodeFstDialog2 begin
   Reply(202)
   NAnswer(205,1,NodeFstDialog4)
end

procedure NodeFstDialog3 begin
   Reply(204)
   NAnswer(205,1,NodeFstDialog4)
end

procedure NodeFstDialog4 begin
   Reply(207)
   NAnswer(210,1,NodePartyAdd)
   NAnswer(random(208,209),1,Node999)
end

procedure NodePartyAdd begin
   set_global_var(GVAR_FLAGSTAFF_STATUS_TRAPER, 1);
   rm_timer_event(self_obj);
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_PLAYER);
   party_add(self_obj);
   call Node999;
end

procedure NodePartyDelete begin
   set_global_var(GVAR_FLAGSTAFF_STATUS_TRAPER, 2);
   move_obj_inven_to_obj(self_obj, dude_obj);
   set_REP(2)
   set_SLAVA(5)
   setBonusReaction(90)
   exp_add(100);
   display_mstr(103);

   rm_timer_event(self_obj);
   party_remove(self_obj);
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,NUM_TEAM);
   add_timer_event(self_obj, 10, 4);
   set_obj_visibility(self_obj, 1);
   call Node999;
end

//--- ВТОРОЙ ДИАЛОГ

procedure NodeSndDialog begin
   Reply(211)
   NAnswer(232,4,NodeAnswer105)
   NAnswer(220,4,NodeAnswer100)
   NAnswer(227,4,NodeAnswer103)
   if (tile_distance(self_tile,24557)<=20) then begin
   //NAnswer(240,1,NodeSndDialog2)
   end
   NAnswer(212,1,Node999)
end

procedure NodeSndDialog2 begin
   Reply(241)
   NAnswer(242,1,NodePartyDelete)
end

//--- РАССПРОСЫ

procedure NodeAnswer100 begin
   Reply(221)
   NAnswer(223,4,NodeAnswer101)
   NAnswer(225,4,NodeAnswer102)
   NAnswer(227,4,NodeAnswer103)
   NAnswer(222,1,Node999)
end

procedure NodeAnswer101 begin
   Reply(224)
   NAnswer(225,1,NodeAnswer102)
   NAnswer(222,1,Node999)
end

procedure NodeAnswer102 begin
   Reply(226)
   NAnswer(232,4,NodeAnswer105)
   NAnswer(227,4,NodeAnswer103)
   NAnswer(222,1,Node999)
end

procedure NodeAnswer103 begin
   Reply(228)
   if (cur_map_index == MAP_FLAGSTAFF_WINON) then begin
   if (map_var(MVAR_FLAG_WINON_TRAPS_VSBL) == 0) then begin
   NAnswer(229,1,NodeAnswer104)
   end
   end
   if (town_known(AREA_FLAGSTAFF) < MARK_STATE_VISITED) then begin
   NAnswer(236,1,NodeAnswer107)
   end
   NAnswer(220,4,NodeAnswer100)
   NAnswer(222,1,Node999)
end

procedure NodeAnswer104 begin
   inc_map_var(MVAR_FLAG_WINON_TRAPS_VSBL);
   Reply(230)
   NAnswer(231,1,NodeSndDialog)
end

procedure NodeAnswer105 begin
   Reply(233)
   NAnswer(234,4,NodeAnswer106)
   NAnswer(220,4,NodeAnswer100)
   NAnswer(227,4,NodeAnswer103)
   NAnswer(222,1,Node999)
end

procedure NodeAnswer106 begin
   Reply(235)
   NAnswer(220,4,NodeAnswer100)
   NAnswer(227,4,NodeAnswer103)
   NAnswer(222,1,Node999)
end

procedure NodeAnswer107 begin
   mark_on_map(AREA_FLAGSTAFF)
   Reply(237)
   NAnswer(238,1,NodeSndDialog)
end
