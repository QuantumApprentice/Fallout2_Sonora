#include "..\headers\define.h"
//---------------------

#define NAME                            SCRIPT_FCMinGrd
#define TOWN_REP_VAR                    GVAR_TOWN_REP_FLAGSTAFF_MERC   // привязка к репутации (-1, если не нужно)

#define NUM_TEAM                        TEAM_FLAGSTAFF_MERC   // группа, к которой принадлежит персонаж (-1 если нужно оставить Team, заданный в редакторе)
#define NUM_AI                          AI_SLAVER             // AI персонажа (-1 если нужно оставить AI, заданный в редакторе)
#define KILL_SLAVA                      SLAVA_KILL_BAD        // слава за убийство этого персонажа (SLAVA_KILL_GOOD, SLAVA_KILL_BAD, SLAVA_KILL_NEITRAL, SLAVA_KILL_CHILD)
#define KILL_TOWN_REP                   REP_KILL_GOOD         // репутация за убийство этого персонажа (REP_KILL_GOOD, REP_KILL_BAD, REP_KILL_NEITRAL, REP_KILL_CRITICAL для враждебности всего поселения, REP_KILL_CHILD)
#define KILL_FRAC_REP                   FRAC_NON              // принадлежность определенной фракции (-1, если не нужно)

#define FLAG_STEAL                      35 // Нижний порог навыка для кражи (для торговцев 200, для животных 300)
#define FLAG_ALCOHOL                    0  // Примет ли алкоголь от ГГ: 0 - принимает, 1 - не принимает
#define FLAG_FOOD                       1  // Примет ли пищу от ГГ: 0 - принимает, 1 - не принимает

//---------------------

procedure start;
procedure map_enter_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;
procedure pickup_p_proc;
procedure combat_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;

procedure Node998;
procedure Node999;
procedure NodeStartDialog;
procedure NodeDialog;
procedure NodeDialog100;
procedure NodeDialog101;
procedure NodeDialog102;
procedure NodeDialog103;
procedure NodeDialog200;
procedure NodeDialog201;
procedure NodeDialog202;
procedure NodeDialog203;
procedure NodeDialog204;

//---------------------

#define LVAR_Flag_A                     (6) // фиксирует свершение диалога про побитого раба

import variable obj_critter_a; // указатель на криттера 1

//---------------------



procedure start begin
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
       set_start_home
       if (global_var(GVAR_STATUS_FLAGSTAFF_EXWARDEN) >= 3) then begin
       set_local_var(LVAR_Home_Rotat, 2);
       end

       if ((getQuest(GVAR_FLAGSTAFF_SLAVES_RUN) >= qEnd) or (getQuest(GVAR_FLAGSTAFF_MINES_DESTROY) >= qEnd)) then begin
          if (getQuest(GVAR_FLAGSTAFF_EVIL_WARDEN) == qStart) then begin
          setQuest(GVAR_FLAGSTAFF_EVIL_WARDEN, qStop);
          end
          rm_timer_event(self_obj);
          set_obj_visibility(self_obj, 1);
       end
       else if ((getQuest(GVAR_FLAGSTAFF_EVIL_WARDEN) >= qEnd) or (map_var(MVAR_FLAG_EVIL_WARDEN) != 0)) then begin
          rm_timer_event(self_obj);
          set_obj_visibility(self_obj, 1);
       end
   end
end

procedure look_at_p_proc begin
   lookProcMsg
end

procedure description_p_proc begin
   dsrptProcMsg
end

procedure timed_event_p_proc begin
   // Таймер за номером 1 рарезервирован под перемещение персонажа по локации
   // Таймер за номером 2 рарезервирован под всплывающие реплики
   if (fixed_param == 1) then begin
       if (global_var(GVAR_STATUS_FLAGSTAFF_EXWARDEN) >= 3) then begin
       timer_move_to_home
       end
       else begin
       timer_move_patrol(HomeTile,tile_num_in_direction(HomeTile,5,11))
       end
   end
   if (fixed_param == 2) then begin
       timer_float_reply(110,116)
   end
end

procedure critter_p_proc begin
   obj_critter_a := self_obj;
   set_critter_options(0,0) //условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ
end

procedure use_skill_on_p_proc begin
   skillUseSteal
   skillUseFAid
end

procedure pickup_p_proc begin
   pickupProcCritter
end

procedure use_obj_on_p_proc begin
   UsedItemToCrit_Proc
end

procedure combat_p_proc begin
   effectAttak(0,0,1,1,1) //отравление min, облучение min, травма ног %, травма рук %, травма глаз %
end

procedure damage_p_proc begin
   set_damage_dude
end

procedure destroy_p_proc begin
   obj_critter_a := 0;
   set_global_var(GVAR_STATUS_FLAGSTAFF_TUWARDEN, -1);
   set_map_var(MVAR_FLAG_EVIL_WARDEN, -1);
   if (global_var(GVAR_STATUS_FLAGSTAFF_EXWARDEN) < 3) then begin
   set_map_var(MVAR_FLAG_EVIL_WARDEN, -10);
   end
   set_map_var(MVAR_FLAG_ATTAK_WARDEN, 0);
   kill_critter_opt
end

procedure talk_p_proc begin
   if (map_var(MVAR_FLAG_EVIL_WARDEN) != 0) then begin
   floater(self_obj, 104, COLOR_MSG_RED);
   end
   else if (getQuest(GVAR_FLAGSTAFF_EVIL_WARDEN) >= qEnd) then begin
   floater(self_obj, 104, COLOR_MSG_RED);
   end
   else if (global_var(GVAR_STATUS_FLAGSTAFF_EXWARDEN) == 3) then begin
   //floater(self_obj, 103, COLOR_MSG_RED);
   dialog_proc(0,0,NodeStartDialog,-1,-1)
   end
   else if ((map_var(MVAR_FLAG_SlavMineDlg) != 0) and (local_var(LVAR_Flag_A) == 0)) then begin
   dialog_proc(0,0,NodeStartDialog,-1,-1)
   end
   else begin
   floater(self_obj, 102, COLOR_MSG_NORMAL);
   end
end

procedure Node998 begin
   set_enemy_dude;
end

procedure Node999 begin
end

procedure NodeStartDialog begin
   if (global_var(GVAR_STATUS_FLAGSTAFF_EXWARDEN) >= 3) then begin
   call NodeDialog200;
   end
   else begin
   call NodeDialog;
   end
end

procedure NodeDialog begin
   inc_local_var(LVAR_Flag_A);
   zReply(200)
   NAnswer(201,1,NodeDialog100)
end

procedure NodeDialog100 begin
   Reply(202)
   BAnswer(204,1,NodeDialog101)
   NAnswer(210,1,NodeDialog103)
end

procedure NodeDialog101 begin
   Reply(205)
   BAnswer(207,1,NodeDialog102)
   NAnswer(206,1,Node999)
end

procedure NodeDialog102 begin
   zReply(208)
   NAnswer(209,1,Node998)
end

procedure NodeDialog103 begin
   Reply(211)
   BAnswer(212,1,NodeDialog102)
   NAnswer(213,1,Node999)
end

procedure NodeDialog200 begin
   Reply(220)
   NAnswerSkill(225,3,NodeDialog204,SKILL_CONVERSANT,50,223,NodeDialog203)
   NAnswer(222,1,NodeDialog202)
   BAnswer(221,1,NodeDialog201)
end

procedure NodeDialog201 begin
   set_map_var(MVAR_FLAG_ATTAK_WARDEN, 1);
   call Node999;
end

procedure NodeDialog202 begin
   set_map_var(MVAR_FLAG_ATTAK_WARDEN, 1);
   call Node999;
end

procedure NodeDialog203 begin
   Reply(224)
   NAnswer(222,1,NodeDialog202)
   BAnswer(221,1,NodeDialog201)
end

procedure NodeDialog204 begin
   set_map_var(MVAR_FLAG_EVIL_WARDEN, 1);
   set_local_var(LVAR_Home_Tile, 30085);
   set_local_var(LVAR_Home_Rotat, 2);
   Reply(226)
   NAnswer(227,1,Node999)
end
