#include "..\headers\define.h"
//---------------------

#define NAME                            SCRIPT_FCSlvDor
#define TOWN_REP_VAR                    GVAR_TOWN_REP_FLAGSTAFF_MERC   // привязка к репутации (-1, если не нужно)

#define NUM_TEAM                        TEAM_FLAGSTAFF_MERC    // группа, к которой принадлежит персонаж (-1 если нужно оставить Team, заданный в редакторе)
#define NUM_AI                          AI_SLAVER             // AI персонажа (-1 если нужно оставить AI, заданный в редакторе)
#define KILL_SLAVA                      SLAVA_KILL_BAD        // слава за убийство этого персонажа (SLAVA_KILL_GOOD, SLAVA_KILL_BAD, SLAVA_KILL_NEITRAL, SLAVA_KILL_CHILD)
#define KILL_TOWN_REP                   REP_KILL_GOOD         // репутация за убийство этого персонажа (REP_KILL_GOOD, REP_KILL_BAD, REP_KILL_NEITRAL, REP_KILL_CRITICAL для враждебности всего поселения, REP_KILL_CHILD)
#define KILL_FRAC_REP                   FRAC_NON              // принадлежность определенной фракции (-1, если не нужно)

#define FLAG_STEAL                      60 // Нижний порог навыка для кражи (для торговцев 200, для животных 300)
#define FLAG_ALCOHOL                    0  // Примет ли алкоголь от ГГ: 0 - принимает, 1 - не принимает
#define FLAG_FOOD                       1  // Примет ли пищу от ГГ: 0 - принимает, 1 - не принимает

//---------------------

procedure start;
procedure map_enter_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;
procedure pickup_p_proc;
procedure combat_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;

//---------------------

#define LVAR_Alcoghol                   (6) // алкоголь: 0 - не использован, 1 - использован
#define LVAR_StopReply                  (7) // факт первой остановки ГГ при попытке пройти в загон

import variable box_stop_use;

//---------------------



procedure start begin
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
       set_start_home
       set_local_var(LVAR_Alcoghol,0);
       set_local_var(LVAR_StopReply,0);
   end
end

procedure look_at_p_proc begin
   lookProcMsg
end

procedure description_p_proc begin
   dsrptProcMsg
end

procedure timed_event_p_proc begin
   // Таймер за номером 1 рарезервирован под перемещение персонажа по локации
   // Таймер за номером 2 рарезервирован под всплывающие реплики
   if (fixed_param == 1) then begin
       timer_move_to_home
   end
   if (fixed_param == 2) then begin
       //timer_float_reply(120,121)
   end
end

procedure critter_p_proc begin
   set_using_stop // привязка хозяина и объекта (удалить, если не нужно)

   if (local_var(LVAR_Alcoghol) == 0) then begin
   set_critter_options(1,0) //условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ
   end
   else begin
   set_critter_options(0,0) //условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ
   end

   if (local_var(LVAR_StopReply) == 0) then begin
       if (tile_distance_objs(self_obj, dude_obj) < 5) then begin
           if self_can_see_dude then begin
              if (map_var(MVAR_RoyDopusk) == 0) then begin
              floater(self_obj,105,COLOR_MSG_RED);
              end
              else begin
              floater(self_obj,107,COLOR_MSG_RED);
              end
              inc_local_var(LVAR_StopReply);
           end
       end
   end
   if (map_var(MVAR_DoorSlavActiv) != 0) then begin
       if ((map_var(MVAR_RoyDopusk) == 0) and (local_var(LVAR_Alcoghol) == 0)) then begin
           if ((tile_distance(self_tile, HomeTile) < 7) and (tile_distance(dude_tile, self_tile) < 15)) then begin
              self_attack_dude;
              if (local_var(LVAR_StopReply) == 1) then begin
              floater(self_obj,106,COLOR_MSG_RED);
              inc_local_var(LVAR_StopReply);
              end
           end
       end
   end
end

procedure use_skill_on_p_proc begin
   skillUseSteal
   skillUseFAid
end

procedure pickup_p_proc begin
   pickupProcCritter
end

procedure use_obj_on_p_proc begin
   UsedItemToCrit_Proc
   if (itemTypeUseDude == 2) then begin
       display_mstr(103);
       floater(self_obj,102,COLOR_MSG_GREEN);
       inc_local_var(LVAR_Alcoghol);
   end
end

procedure combat_p_proc begin
   effectAttak(0,0,1,1,1) //отравление min, облучение min, травма ног %, травма рук %, травма глаз %
end

procedure damage_p_proc begin
   set_damage_dude
end

procedure destroy_p_proc begin
   dec_global_var(GVAR_FLAGSTAFF_MERCS_LIFE);
   kill_critter_opt
end

procedure talk_p_proc begin
   if (local_var(LVAR_Alcoghol) > 0) then begin
   floater(self_obj,104,COLOR_MSG_NORMAL);
   end
   else if (map_var(MVAR_RoyDopusk) == 0) then begin
   floater(self_obj,random(110,114),COLOR_MSG_RED);
   end
   else begin
   floater(self_obj,random(120,123),COLOR_MSG_NORMAL);
   end
end
