#include "..\headers\define.h"
//---------------------

#define NAME                            SCRIPT_SCSlave
#define TOWN_REP_VAR                    GVAR_TOWN_REP_PUERTO // привязка к репутации (-1, если не нужно)

#define NUM_TEAM                        TEAM_FLAGSTAFF_SLAVES  // группа, к которой принадлежит персонаж (-1 если нужно оставить Team, заданный в редакторе)
#define NUM_AI                          AI_SLAVER             // AI персонажа (-1 если нужно оставить AI, заданный в редакторе)
#define KILL_SLAVA                      SLAVA_KILL_GOOD       // слава за убийство этого персонажа (SLAVA_KILL_GOOD, SLAVA_KILL_BAD, SLAVA_KILL_NEITRAL, SLAVA_KILL_CHILD)
#define KILL_TOWN_REP                   REP_KILL_NEITRAL      // репутация за убийство этого персонажа (-100 для враждебности всего поселения)
#define KILL_FRAC_REP                   FRAC_NON              // принадлежность определенной фракции (-1, если не нужно)

#define FLAG_STEAL                      25 // Нижний порог навыка для кражи (для торговцев 200, для животных 300)
#define FLAG_ALCOHOL                    1  // Примет ли алкоголь от ГГ: 0 - принимает, 1 - не принимает
#define FLAG_FOOD                       0  // Примет ли пищу от ГГ: 0 - принимает, 1 - не принимает

//---------------------

procedure start;
procedure map_enter_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;
procedure pickup_p_proc;
procedure combat_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;

procedure Node998;
procedure Node999;
procedure NodeStartDialog;
procedure NodeFstDialog;
procedure NodeFstDialog2;
procedure NodeSndDialog;
procedure NodeSndDialog2;
procedure NodeSndDialog3;
procedure NodeSndDialog4;
procedure NodeAnswer;
procedure NodeAnswer100;
procedure NodeAnswer101;
procedure NodeAnswer102;
procedure NodeAnswer103;
procedure NodeAnswer104;
procedure NodeAnswer200;
procedure NodeAnswer201;
procedure NodeAnswer300;
procedure NodeAnswer301;
procedure NodeAnswer302;

//---------------------

#define LVAR_Flag_Food                     (6) // фиксирует факт применения еды

//---------------------



procedure start begin
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
       set_start_home
   end
end

procedure look_at_p_proc begin
   lookProcMsg
end

procedure description_p_proc begin
   dsrptProcMsg
end

procedure timed_event_p_proc begin
   // Таймер за номером 1 рарезервирован под перемещение персонажа по локации
   // Таймер за номером 2 рарезервирован под всплывающие реплики
   if (fixed_param == 1) then begin
       timer_move_random_home(3)
   end
   if (fixed_param == 2) then begin
       //timer_float_reply(110,119)
   end
end

procedure critter_p_proc begin
   set_critter_options(0,0) //условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ
end

procedure use_skill_on_p_proc begin
   skillUseSteal
   skillUseFAid
end

procedure pickup_p_proc begin
   pickupProcCritter
end

procedure use_obj_on_p_proc begin
   UsedItemToCrit_Proc
   if (itemTypeUseDude == 3) then begin
   inc_local_var(LVAR_Flag_Food);
   end
end

procedure combat_p_proc begin
   effectAttak(0,0,0,0,0) //отравление min, облучение min, травма ног %, травма рук %, травма глаз %
end

procedure damage_p_proc begin
   set_damage_dude
end

procedure destroy_p_proc begin
   kill_critter_opt
end

procedure talk_p_proc begin
   if (global_var(GVAR_TITUL_BOS)>0) then begin
   z_floater(self_obj,random(110,114),COLOR_MSG_NORMAL);
   end
   else begin
   setReaction(0) // Отношение к ГГ (1 - персонаж положительный, -1 отрицательный, 0 нейтральный)
   dialog_proc(0,0,NodeStartDialog,-1,-1)
   end
end

procedure Node998 begin
   set_enemy_dude;
end

procedure Node999 begin
end

procedure NodeStartDialog begin
   if (bitDialog(bit_1) == 0) then begin
   call NodeFstDialog;
   end
   else begin
   call NodeSndDialog;
   end
end

//--- ПЕРВЫЙ ДИАЛОГ

procedure NodeFstDialog begin
   bitDialogExp(bit_1,0)
   zReply(200)
   NAnswer(201,1,NodeFstDialog2)
end

procedure NodeFstDialog2 begin
   Reply(202)
   if (local_var(LVAR_Flag_Food)>0) then begin
   NAnswer(212,1,NodeAnswer)
   end
   else begin
   NAnswer(212,1,NodeSndDialog2)
   end
   NAnswer(203,1,Node999)
end

//--- ПОВТОРНЫЙ ДИАЛОГ

procedure NodeSndDialog begin
   Reply(210)
   //!!! Чем выше Красноречие, Наука и Медицина, тем больше ГГ сможет выведать у раба.
   if (local_var(LVAR_Flag_Food)>0) then begin
   NAnswer(212,1,NodeAnswer)
   end
   else begin
   NAnswer(212,1,NodeSndDialog2)
   end
   NAnswer(211,1,Node999)
end

procedure NodeSndDialog2 begin
   Reply(213)
   NAnswerStat(232,3,NodeSndDialog4,STAT_st,7,217,NodeSndDialog3)
   NAnswer(214,1,Node999)
end

procedure NodeSndDialog3 begin
   Reply(218)
   NAnswer(219,1,Node999)
end

procedure NodeSndDialog4 begin
   setBonusReaction(-35)
   set_SLAVA(-2)
   set_REP(-1)
   inc_local_var(LVAR_Flag_Food);
   Reply(233)
   NAnswer(234,1,NodeAnswer)
end

procedure NodeAnswer begin
   Reply(215)
   NAnswer(220,3,NodeAnswer100)
   NAnswer(240,3,NodeAnswer200)
   NAnswer(250,3,NodeAnswer300)
   NAnswer(216,1,Node999)
end

procedure NodeAnswer100 begin
   zReply(221)
   NAnswer(223,1,NodeAnswer101)
   NAnswer(222,1,NodeAnswer)
end

procedure NodeAnswer101 begin
   set_map_var(MVAR_PUERTO_SLAVE_DIALOG,1);
   Reply(224)
   NAnswer(225,1,NodeAnswer102)
   NAnswer(227,1,NodeAnswer103)
   NAnswer(230,1,NodeAnswer104)
   NAnswer(222,1,NodeAnswer)
end

procedure NodeAnswer102 begin
   Reply(226)
   NAnswer(227,1,NodeAnswer103)
   NAnswer(230,1,NodeAnswer104)
   NAnswer(222,1,NodeAnswer)
end

procedure NodeAnswer103 begin
   Reply(228)
   NAnswer(225,1,NodeAnswer102)
   NAnswer(230,1,NodeAnswer104)
   NAnswer(222,1,NodeAnswer)
end

procedure NodeAnswer104 begin
   Reply(231)
   //!!! Наукой 100 урегулировать ошейник так, чтобы раб умер. После этого можно поговорить с учеными и признаться, но вместо огорчения они будут удивлены, что какой-то крестьянин из пустошей так хорошо разбирается в Науке.
   NAnswer(225,1,NodeAnswer102)
   NAnswer(227,1,NodeAnswer103)
   NAnswer(222,1,NodeAnswer)
end

procedure NodeAnswer200 begin
   Reply(241)
   NAnswer(243,1,NodeAnswer201)
   NAnswer(242,1,NodeAnswer)
end

procedure NodeAnswer201 begin
   Reply(244)
   NAnswer(245,1,NodeAnswer)
end

procedure NodeAnswer300 begin
   Reply(251)
   //!!! (Красноречие + Отношение + ГГ не является членом БС) Выведать тайник на корабле, где рабы хранят свои находки. Без этого разговора тайник не найти.
   NAnswer(253,1,NodeAnswer301)
   NAnswer(256,1,NodeAnswer302)
   NAnswer(252,1,NodeAnswer)
end

procedure NodeAnswer301 begin
   Reply(254)
   NAnswer(256,1,NodeAnswer302)
   NAnswer(255,1,NodeAnswer)
end

procedure NodeAnswer302 begin
   Reply(257)
   NAnswer(253,1,NodeAnswer301)
   NAnswer(258,1,NodeAnswer)
end
