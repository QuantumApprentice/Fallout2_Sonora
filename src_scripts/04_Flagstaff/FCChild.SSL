#include "..\headers\define.h"
//---------------------

#define NAME                            SCRIPT_FCChild
#define TOWN_REP_VAR                    GVAR_TOWN_REP_FLAGSTAFF   // привязка к репутации (-1, если не нужно)
#define TILE_HOME_VOR                   26696 // центр дома, где обитают воришки, обокравшие мисс Брендли

#define NUM_TEAM                        TEAM_FLAGSTAFF_CITIZEN // группа, к которой принадлежит персонаж (-1 если нужно оставить Team, заданный в редакторе)
#define NUM_AI                          AI_CHILD              // AI персонажа (-1 если нужно оставить AI, заданный в редакторе)
#define KILL_SLAVA                      SLAVA_KILL_CHILD      // слава за убийство этого персонажа (SLAVA_KILL_GOOD, SLAVA_KILL_BAD, SLAVA_KILL_NEITRAL, SLAVA_KILL_CHILD)
#define KILL_TOWN_REP                   REP_KILL_CRITICAL     // репутация за убийство этого персонажа (REP_KILL_GOOD, REP_KILL_BAD, REP_KILL_NEITRAL, REP_KILL_CRITICAL для враждебности всего поселения, REP_KILL_CHILD)
#define KILL_FRAC_REP                   FRAC_NON              // принадлежность определенной фракции (-1, если не нужно)

#define FLAG_STEAL                      0  // Нижний порог навыка для кражи (для торговцев 200, для животных 300)
#define FLAG_ALCOHOL                    1  // Примет ли алкоголь от ГГ: 0 - принимает, 1 - не принимает
#define FLAG_FOOD                       0  // Примет ли пищу от ГГ: 0 - принимает, 1 - не принимает

//---------------------

procedure start;
procedure map_enter_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;
procedure pickup_p_proc;
procedure push_p_proc;
procedure combat_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;

procedure Node998;
procedure Node999;
procedure NodeStartDialog;
procedure NodeDialogVor;
procedure NodeDialogVor2;
procedure NodeDialogVor3;
procedure NodeDialogVor4;
procedure NodeDialogVor5;
procedure NodeDialogVor6;
procedure NodeDialogVor7;

//---------------------

//#define LVAR_Flag_A                     (6) // отвечает за ...

//---------------------



procedure start begin
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
       set_start_home
   end
end

procedure look_at_p_proc begin
   lookProcMsg
end

procedure description_p_proc begin
   dsrptProcMsg
end

procedure timed_event_p_proc begin
   // Таймер за номером 1 рарезервирован под перемещение персонажа по локации
   // Таймер за номером 2 рарезервирован под всплывающие реплики
   if (fixed_param == 1) then begin
       if (game_time_hour < 0800) then begin
       timer_move_to_home
       end
       else begin
       timer_move_random_home(4)
       end
   end
   if (fixed_param == 2) then begin
       //timer_float_reply(120,121)
   end
end

procedure critter_p_proc begin
   set_critter_options(1,0) //условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ
end

procedure use_skill_on_p_proc begin
   skillUseSteal
   skillUseFAid
end

procedure pickup_p_proc begin
   pickupProcCritter
end

procedure push_p_proc begin
   push_options(HomeTile,5,102) //Толкать (точка отсчета, макс расстояние, фраза нпс при толкании), удалить если не надо
end

procedure use_obj_on_p_proc begin
   UsedItemToCrit_Proc
end

procedure combat_p_proc begin
   effectAttak(0,0,0,0,1) //отравление min, облучение min, травма ног %, травма рук %, травма глаз %
end

procedure damage_p_proc begin
   set_damage_dude
end

procedure destroy_p_proc begin
   kill_critter_opt
   if (tile_distance(HomeTile,TILE_HOME_VOR)<=10) then begin
   call NodeDialogVor7;
   end
end

procedure talk_p_proc begin
   if (bitDialog(bit_2) != 0) then begin
      floater(self_obj,104,COLOR_MSG_NORMAL);
   end
   else if ((getQuest(GVAR_FLAGSTAFF_POST_VOR) == qStart) and (map_var(MVAR_FLAG_Vor) == 1) and (tile_distance(HomeTile,TILE_HOME_VOR)<=10)) then begin
      dialog_proc(0,0,NodeStartDialog,-1,-1)
   end
   else if (game_time_hour < 0600) then begin
      floater(self_obj,103,COLOR_MSG_NORMAL);
   end
   else if town_rep_is_liked then begin
      floater(self_obj,random(110,114),COLOR_MSG_NORMAL);
   end
   else if town_rep_is_hated then begin
      floater(self_obj,random(115,119),COLOR_MSG_NORMAL);
   end
   else begin
      floater(self_obj,random(120,124),COLOR_MSG_NORMAL);
   end
end

procedure Node998 begin
   set_enemy_dude;
end

procedure Node999 begin
end

procedure NodeStartDialog begin
   call NodeDialogVor;
end

procedure NodeDialogVor begin
   Reply(200)
   NAnswer(202,1,NodeDialogVor2)
   NAnswer(201,1,Node999)
end

procedure NodeDialogVor2 begin
   Reply(203)
   NAnswer(204,1,NodeDialogVor3)
end

procedure NodeDialogVor3 begin
   Reply(205)
   NAnswer(206,1,NodeDialogVor4)
   BAnswer(209,1,NodeDialogVor4)
end

procedure NodeDialogVor4 begin
   Reply(207)
   NAnswer(208,1,NodeDialogVor5)
end

procedure NodeDialogVor5 begin
   call NodeDialogVor7;
   Reply(210)
   NAnswer(211,1,Node999)
   BAnswer(212,1,NodeDialogVor6)
end

procedure NodeDialogVor6 begin
   item_add(dude_obj,PID_BOOTLE_CAPS,5)
   set_SLAVA(-10)
   set_REP(-2)
   zReply(213)
   NAnswer(214,1,Node999)
end

procedure NodeDialogVor7 begin
   bitDialogExp(bit_2,0)
   /*
   variable containerObj;
   if ((map_var(MVAR_FLAG_Vor) == 0) or (map_var(MVAR_FLAG_Vor) == 1)) then begin
   containerObj := create_object_sid(PID_CNTR_SOILJUNK, 24145, 0, SCRIPT_ZSSoil);
   item_add(containerObj,PID_Q_PORTMONE,1)
   containerObj := 0;
   set_map_var(MVAR_FLAG_Vor,2);
   end
   */

   if ((map_var(MVAR_FLAG_Vor) == 0) or (map_var(MVAR_FLAG_Vor) == 1)) then begin
   create_object_sid(PID_Q_PORTMONE, 24145, 0, -1);
   set_map_var(MVAR_FLAG_Vor,2);
   end
end
