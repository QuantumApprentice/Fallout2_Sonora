#include "..\headers\define.h"
//---------------------

#define NAME                            SCRIPT_NCQuestB
#define TOWN_REP_VAR                    GVAR_TOWN_REP_CASA_NUEVA // привязка к репутации (-1, если не нужно)

#define NUM_TEAM                        TEAM_CASANUEVA_PEASAN // группа, к которой принадлежит персонаж (-1 если нужно оставить Team, заданный в редакторе)
#define NUM_AI                          AI_PERSON_TOUGH       // AI персонажа (-1 если нужно оставить AI, заданный в редакторе)
#define KILL_SLAVA                      SLAVA_KILL_GOOD       // слава за убийство этого персонажа (SLAVA_KILL_GOOD, SLAVA_KILL_BAD, SLAVA_KILL_NEITRAL, SLAVA_KILL_CHILD)
#define KILL_TOWN_REP                   REP_KILL_CRITICAL     // репутация за убийство этого персонажа (REP_KILL_GOOD, REP_KILL_NEITRAL, REP_KILL_BAD, REP_KILL_CHILD, REP_KILL_CRITICAL)
#define KILL_FRAC_REP                   FRAC_NON              // принадлежность определенной фракции (-1, если не нужно)

#define FLAG_STEAL                      50 // Нижний порог навыка для кражи (для торговцев 200, для животных 300)
#define FLAG_ALCOHOL                    1  // Примет ли алкоголь от ГГ: 0 - принимает, 1 - не принимает
#define FLAG_FOOD                       1  // Примет ли пищу от ГГ: 0 - принимает, 1 - не принимает

//---------------------

procedure start;
procedure map_enter_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;
procedure pickup_p_proc;
procedure combat_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;

procedure Node998;
procedure Node999;
procedure NodeStartDialog;
procedure NodeFstDialog;
procedure NodeSndDialog;
procedure NodeAnswer100;
procedure NodeAnswer101;
procedure NodeAnswer102;
procedure NodeAnswer200;
procedure NodeAnswer201;
procedure NodeAnswer202;
procedure NodeAnswer203;
procedure NodeAnswer300;
procedure NodeAnswer301;
procedure NodeAnswer302;

//---------------------

//#define LVAR_Flag_A                     (6) // отвечает за ...

//---------------------



procedure start begin
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
       set_start_home
   end
end

procedure look_at_p_proc begin
   lookProcMsg
end

procedure description_p_proc begin
   dsrptProcMsg
end

procedure timed_event_p_proc begin
   // Таймер за номером 1 зарезервирован под перемещение персонажа по локации
   // Таймер за номером 2 зарезервирован под всплывающие реплики
   if (fixed_param == 1) then begin
       timer_move_random_home(2)
   end
   if (fixed_param == 2) then begin
       //timer_float_reply(120,121)
   end
end

procedure critter_p_proc begin
   set_critter_options(1,0) //условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ
end

procedure use_skill_on_p_proc begin
   skillUseSteal
   skillUseFAid
end

procedure pickup_p_proc begin
   pickupProcCritter
end

procedure use_obj_on_p_proc begin
   UsedItemToCrit_Proc
end

procedure combat_p_proc begin
   effectAttak(0,0,1,1,1) //отравление min, облучение min, травма ног %, травма рук %, травма глаз %
end

procedure damage_p_proc begin
   set_damage_dude
end

procedure destroy_p_proc begin
   kill_critter_opt
end

procedure talk_p_proc begin
   if (game_time_hour < 0600) then begin
   floater(self_obj,102,COLOR_MSG_RED);
   end
   else begin
   setReaction(1) // Отношение к ГГ (1 - персонаж положительный, -1 отрицательный, 0 нейтральный)
   dialog_proc(0,0,NodeStartDialog,-1,-1)
   end
end

procedure Node998 begin
   set_enemy_dude;
end

procedure Node999 begin
end

procedure NodeStartDialog begin
   if (bitDialog(bit_1) == 0) then begin
   call NodeFstDialog;
   end
   else begin
   call NodeSndDialog;
   end
end

//--- ПЕРВЫЙ ДИАЛОГ

procedure NodeFstDialog begin
   bitDialogExp(bit_1,0)
   call NodeSndDialog;
   //Reply(200)
   //NAnswer(201,1,NodeSndDialog)
end

//--- ВТОРОЙ ДИАЛОГ

procedure NodeSndDialog begin
   if town_rep_is_liked then begin
   Reply(random(220,224))
   end
   else begin
   Reply(random(210,215))
   end

   NAnswer(230,1,NodeAnswer100)

   if (bitDialog(bit_2) == 0) then begin
   NAnswer(240,1,NodeAnswer200)
   end

   if ((bitDialog(bit_3) == 0) and town_rep_is_idolized) then begin
   NAnswer(229,1,NodeAnswer300)
   end
   else begin
   NAnswer(229,1,Node999)
   end
end

procedure NodeAnswer100 begin
   Reply(231)
   NAnswer(232,1,NodeAnswer101)
end

procedure NodeAnswer101 begin
   Reply(233)
   NAnswer(234,1,NodeAnswer102)
end

procedure NodeAnswer102 begin
   Reply(235)
   GAnswer(237,1,NodeSndDialog)
   BAnswer(236,1,NodeSndDialog)
end

procedure NodeAnswer200 begin
   Reply(241)
   //NAnswer(243,1,NodeAnswer201)
   NAnswerSkill(247,3,NodeAnswer202,SKILL_CONVERSANT,90,243,NodeAnswer201)
   NAnswer(242,1,NodeSndDialog)
end

procedure NodeAnswer201 begin
   Reply(244)
   NAnswer(245,1,NodeSndDialog)
   NAnswer(246,1,NodeSndDialog)
end

procedure NodeAnswer202 begin
   Reply(248)
   NAnswer(249,1,NodeAnswer203)
end

procedure NodeAnswer203 begin
   bitDialogExp(bit_2,100)
   Reply(250)
   NAnswer(251,1,Node999)
end

procedure NodeAnswer300 begin
   zReply(252)
   NAnswer(253,1,NodeAnswer301)
   NAnswer(254,1,NodeAnswer301)
end

procedure NodeAnswer301 begin
   Reply(255)
   NAnswer(256,1,NodeAnswer302)
   BAnswer(257,1,NodeAnswer302)
end

procedure NodeAnswer302 begin
   bitDialogExp(bit_3,100)
   if (random(0,2) == 1) then begin
   item_add(dude_obj,PID_AMMO_44_MAGNUM,24)
   end
   else if (random(0,1) == 1) then begin
   item_add(dude_obj,PID_AMMO_10_MM,24)
   end
   else begin
   item_add(dude_obj,PID_AMMO_22_LR,10) // в пачке 5 патронов
   end
   Reply(258)
   NAnswer(259,1,Node999)
   NAnswer(260,1,Node999)
end
