#include "..\headers\define.h"
//---------------------

#define NAME                            SCRIPT_FCSlvStn
#define TOWN_REP_VAR                    GVAR_TOWN_REP_FLAGSTAFF_MERC   // привязка к репутации (-1, если не нужно)

#define NUM_TEAM                        TEAM_FLAGSTAFF_MERC    // группа, к которой принадлежит персонаж (-1 если нужно оставить Team, заданный в редакторе)
#define NUM_AI                          AI_SLAVER             // AI персонажа (-1 если нужно оставить AI, заданный в редакторе)
#define KILL_SLAVA                      SLAVA_KILL_BAD        // слава за убийство этого персонажа (SLAVA_KILL_GOOD, SLAVA_KILL_BAD, SLAVA_KILL_NEITRAL, SLAVA_KILL_CHILD)
#define KILL_TOWN_REP                   REP_KILL_NEITRAL      // репутация за убийство этого персонажа (REP_KILL_GOOD, REP_KILL_BAD, REP_KILL_NEITRAL, REP_KILL_CRITICAL для враждебности всего поселения, REP_KILL_CHILD)
#define KILL_FRAC_REP                   FRAC_NON              // принадлежность определенной фракции (-1, если не нужно)

#define FLAG_STEAL                      40 // Нижний порог навыка для кражи (для торговцев 200, для животных 300)
#define FLAG_ALCOHOL                    0  // Примет ли алкоголь от ГГ: 0 - принимает, 1 - не принимает
#define FLAG_FOOD                       1  // Примет ли пищу от ГГ: 0 - принимает, 1 - не принимает

//---------------------

procedure start;
procedure map_enter_p_proc;
procedure map_update_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;
procedure pickup_p_proc;
procedure combat_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;

procedure Node998;
procedure Node999;
procedure NodeStartDialog;
procedure NodeFstDialog;
procedure NodeFstDialog2;
procedure NodeSndDialog;
procedure NodeSndDialog2;
procedure NodeSndDialog3;
procedure NodeSndDialog4;
procedure NodeSndDialog4b;
procedure NodeSndDialog5;
procedure NodeSndDialog6;

//---------------------

#define LVAR_Alcoghol                   (6) // алкоголь: 0 - не использован, 1 - использован

//---------------------



procedure start begin
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
       set_start_home
       set_local_var(LVAR_Alcoghol,0);
       if (map_first_run) then begin
       item_add(self_obj,PID_BOOTLE_CAPS,110)
       end
   end
end

procedure map_update_p_proc begin
   if (map_var(MVAR_DolgSten) >0) then begin
       if town_rep_is_vilified then begin
       critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_FLAGSTAFF_MERC);
       end
       else begin
       critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_BANDITS);
       end
   end
end

procedure look_at_p_proc begin
   lookProcMsg
end

procedure description_p_proc begin
   dsrptProcMsg
end

procedure timed_event_p_proc begin
   // Таймер за номером 1 рарезервирован под перемещение персонажа по локации
   // Таймер за номером 2 рарезервирован под всплывающие реплики
   if (fixed_param == 1) then begin
       timer_move_to_home
   end
   if (fixed_param == 2) then begin
       //timer_float_reply(120,121)
   end
end

procedure critter_p_proc begin
   if (local_var(LVAR_Alcoghol) == 0) then begin
   set_critter_options(1,0) //условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ
   end
   else begin
   set_critter_options(0,0) //условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ
   end

   if (map_var(MVAR_DolgSten) == 1) then begin
       if (self_cur_hits <= (self_max_hits/2)) then begin
           terminate_combat;
           critter_stop_attacking(self_obj);
           floater(self_obj,102,COLOR_MSG_RED);
           set_map_var(MVAR_DolgSten,2);
       end
   end
   else begin
      if (bitDialog(bit_1) == 0) then begin
      dialogue_system_forcing(4)
      end
   end
end

procedure use_skill_on_p_proc begin
   skillUseSteal
   skillUseFAid
end

procedure pickup_p_proc begin
   pickupProcCritter
end

procedure use_obj_on_p_proc begin
   UsedItemToCrit_Proc
   if (itemTypeUseDude == 2) then begin
       inc_local_var(LVAR_Alcoghol);
   end
end

procedure combat_p_proc begin
   effectAttak(0,0,1,1,1) //отравление min, облучение min, травма ног %, травма рук %, травма глаз %
end

procedure damage_p_proc begin
   set_damage_dude
end

procedure destroy_p_proc begin
   dec_global_var(GVAR_FLAGSTAFF_MERCS_LIFE);
   kill_critter_opt
end

procedure talk_p_proc begin
   setReaction(-1) // Отношение к ГГ(1 - персонаж положительный, -1 отрицательный, 0 нейтральный)
   fracReaction(GVAR_TITUL_ARIZONA_RANGER,-20) // Поправка к отношению за принадлежность к фракциям
   dialog_proc(0,0,NodeStartDialog,-1,-1)
end

procedure Node998 begin
   set_enemy_dude;
end

procedure Node999 begin
end

procedure NodeStartDialog begin
   if ((map_var(MVAR_DolgSten) == 2) and (bitDialog(bit_2) == 0)) then begin
   call NodeSndDialog6;
   end
   else if (bitDialog(bit_1) == 0) then begin
   call NodeFstDialog;
   end
   else begin
   call NodeSndDialog;
   end
end

procedure NodeFstDialog begin
   bitDialogExp(bit_1,0)
   Reply(200)
   NAnswer(201,1,NodeFstDialog2)
   BAnswer(202,1,NodeFstDialog2)
   NAnswer(203,1,NodeFstDialog2)
end

procedure NodeFstDialog2 begin
   Reply(204)

   if (getQuest(GVAR_FLAGSTAFF_BARMEN_DOLG) == qStart) then begin
      if (bitDialog(bit_2) == 0) then begin
        if (item_amnt(self_obj,PID_BOOTLE_CAPS) >= 100) then begin
        zNAnswer(211,1,NodeSndDialog2)
        end
      end
   end

   NAnswer(205,1,Node999)
end

procedure NodeSndDialog begin
   if (game_time_hour <= 0600) then begin
   Reply(206)
   end
   else begin
   Reply(210)
   end

   if (getQuest(GVAR_FLAGSTAFF_BARMEN_DOLG) == qStart) then begin
      if (bitDialog(bit_2) == 0) then begin
        if (item_amnt(self_obj,PID_BOOTLE_CAPS) >= 100) then begin
        zNAnswer(211,1,NodeSndDialog2)
        end
      end
   end
   NAnswer(212,1,Node999)
end

procedure NodeSndDialog2 begin
   Reply(213)
   NAnswer(214,1,NodeSndDialog3)
   NAnswerSkill(215,1,NodeSndDialog4,SKILL_CONVERSANT,55,215,NodeSndDialog4b)
   BAnswer(216,1,NodeSndDialog5)
   NAnswer(217,1,Node999)
end

procedure NodeSndDialog3 begin
   Reply(218)
   NAnswerSkill(219,1,NodeSndDialog4,SKILL_CONVERSANT,55,219,NodeSndDialog4b)
   BAnswer(220,1,NodeSndDialog5)
   NAnswer(221,1,Node999)
end

procedure NodeSndDialog4 begin
   bitDialogExp(bit_2,50)
   item_add(dude_obj,PID_BOOTLE_CAPS,100)
   item_remove(self_obj,PID_BOOTLE_CAPS,100)
   Reply(225)
   NAnswer(226,1,Node999)
end

procedure NodeSndDialog4b begin
   Reply(224)
   BAnswer(220,1,NodeSndDialog5)
   NAnswer(225,1,Node999)
end

procedure NodeSndDialog5 begin
   Reply(222)
   NAnswer(223,1,Node999)
end

procedure NodeSndDialog6 begin
   bitDialogExp(bit_2,50)
   item_add(dude_obj,PID_BOOTLE_CAPS,100)
   item_remove(self_obj,PID_BOOTLE_CAPS,100)
   Reply(227)
   NAnswer(228,1,Node999)
end
