#include "..\headers\define.h"
//---------------------

#define NAME                            SCRIPT_TCMotel
#define TOWN_REP_VAR                    GVAR_TOWN_REP_TWO_SUN   // привязка к репутации (-1, если не нужно)

#define NUM_TEAM                        TEAM_TWOSUN_PEASAN    // группа, к которой принадлежит персонаж (-1 если нужно оставить Team, заданный в редакторе)
#define NUM_AI                          AI_PERSON_TOUGH       // AI персонажа (-1 если нужно оставить AI, заданный в редакторе)
#define KILL_SLAVA                      SLAVA_KILL_GOOD       // слава за убийство этого персонажа (SLAVA_KILL_GOOD, SLAVA_KILL_BAD, SLAVA_KILL_NEITRAL, SLAVA_KILL_CHILD)
#define KILL_TOWN_REP                   REP_KILL_GOOD         // репутация за убийство этого персонажа (REP_KILL_GOOD, REP_KILL_BAD, REP_KILL_NEITRAL, REP_KILL_CRITICAL для враждебности всего поселения, REP_KILL_CHILD)
#define KILL_FRAC_REP                   FRAC_NON              // принадлежность определенной фракции (-1, если не нужно)

#define FLAG_STEAL                      40 // Нижний порог навыка для кражи (для торговцев 200, для животных 300)
#define FLAG_ALCOHOL                    0  // Примет ли алкоголь от ГГ: 0 - принимает, 1 - не принимает
#define FLAG_FOOD                       0  // Примет ли пищу от ГГ: 0 - принимает, 1 - не принимает

//---------------------

procedure start;
procedure map_enter_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;
procedure pickup_p_proc;
procedure combat_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;

procedure Node998;
procedure Node999;
procedure NodeStartDialog;
procedure NodeFstDialog;
procedure NodeFstDialog2;
procedure NodeFstDialog3;
procedure NodeFstDialog4;
procedure NodeFstDialog5;
procedure NodeFstDialog6;
procedure NodeFstDialog7;
procedure NodeSndDialog;
procedure NodeAnswer100;
procedure NodeAnswer200;
procedure NodeAnswer300;

//---------------------

//#define LVAR_Flag_A                     (6) // отвечает за ...

//---------------------



procedure start begin
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
       set_start_home
   end
end

procedure look_at_p_proc begin
   lookProcMsg
end

procedure description_p_proc begin
   dsrptProcMsg
end

procedure timed_event_p_proc begin
   // Таймер за номером 1 рарезервирован под перемещение персонажа по локации
   // Таймер за номером 2 рарезервирован под всплывающие реплики
   if (fixed_param == 1) then begin
       timer_move_to_home
   end
   if (fixed_param == 2) then begin
       //timer_float_reply(110,119)
   end
end

procedure critter_p_proc begin
   set_critter_options(0,0) //условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ
end

procedure use_skill_on_p_proc begin
   skillUseSteal
   skillUseFAid
end

procedure pickup_p_proc begin
   pickupProcCritter
end

procedure use_obj_on_p_proc begin
   UsedItemToCrit_Proc
end

procedure combat_p_proc begin
   effectAttak(0,0,0,0,0) //отравление min, облучение min, травма ног %, травма рук %, травма глаз %
end

procedure damage_p_proc begin
   set_damage_dude
end

procedure destroy_p_proc begin
   kill_critter_opt
end

procedure talk_p_proc begin
   if (global_var(GVAR_TWOSUN_STATUS_ZAX) == 1) then begin
      floater(self_obj,102,COLOR_MSG_RED);
   end
   else begin
      setReaction(1) // Отношение к ГГ(1 - персонаж положительный, -1 отрицательный, 0 нейтральный)
      fracReaction(GVAR_TITUL_BOS,50) // Поправка к отношению за принадлежность к фракциям
      fracReaction(GVAR_TITUL_PHENIX_CULT,20) // Поправка к отношению за принадлежность к фракциям
      fracReaction(GVAR_TITUL_WARRIOR_ROAD,-20) // Поправка к отношению за принадлежность к фракциям
      fracReaction(GVAR_TITUL_DUDE_IS_SLAVE,-20) // Поправка к отношению за принадлежность к фракциям
      dialog_proc(0,0,NodeStartDialog,-1,-1)
   end
end

procedure Node998 begin
   set_enemy_dude;
end

procedure Node999 begin
end

procedure NodeStartDialog begin
   if (bitDialog(bit_1) == 0) then begin
   call NodeFstDialog;
   end
   else if (bitDialog(bit_2) == 0) then begin
   call NodeFstDialog4;
   end
   else begin
   call NodeSndDialog;
   end
end

//--- ПЕРВЫЙ ДИАЛОГ

procedure NodeFstDialog begin
   bitDialogExp(bit_1,0)
   Reply(200)
   NAnswer(201,1,NodeFstDialog2)
end

procedure NodeFstDialog2 begin
   Reply(202)
   NAnswer(203,1,NodeFstDialog3)
end

procedure NodeFstDialog3 begin
   Reply(204)
   NAnswer(205,1,Node999)
end

//--- ПОВТОРНЫЙ ДИАЛОГ

procedure NodeFstDialog4 begin
   Reply(210)

   NAnswerSkill(211,4,NodeFstDialog6,SKILL_CONVERSANT,80,211,NodeFstDialog5)

   if (global_var(GVAR_TITUL_PHENIX_CULT) >0) then begin
   NAnswer(217,3,NodeFstDialog6)
   end
   if (global_var(GVAR_TITUL_BOS) >0) then begin
   NAnswer(218,3,NodeFstDialog6)
   end

   NAnswerRep(219,3,NodeFstDialog6,15,219,NodeFstDialog7)

   NAnswer(212,1,Node999)
end

procedure NodeFstDialog5 begin
   Reply(213)
   NAnswer(214,1,Node999)
end

procedure NodeFstDialog6 begin
   bitDialogExp(bit_2,100)
   Reply(215)
   NAnswer(216,1,NodeSndDialog)
end

procedure NodeFstDialog7 begin
   Reply(220)
   NAnswer(221,1,Node999)
end

procedure NodeSndDialog begin
   Reply(250)
   NAnswer(260,1,NodeAnswer100)
   NAnswer(263,1,NodeAnswer200)
   NAnswer(265,1,NodeAnswer300)
   NAnswer(251,1,Node999)
end

procedure NodeAnswer100 begin
   if (global_var(GVAR_TWOSUN_STATUS_ZAX) < 0) then begin
   Reply(262)
   end else begin
   Reply(261)
   end
   NAnswer(263,1,NodeAnswer200)
   NAnswer(265,1,NodeAnswer300)
   NAnswer(252,1,Node999)
end

procedure NodeAnswer200 begin
   Reply(264)
   NAnswer(260,1,NodeAnswer100)
   NAnswer(265,1,NodeAnswer300)
   NAnswer(252,1,Node999)
end

procedure NodeAnswer300 begin
   Reply(266)
   NAnswer(260,1,NodeAnswer100)
   NAnswer(263,1,NodeAnswer200)
   NAnswer(252,1,Node999)
end
