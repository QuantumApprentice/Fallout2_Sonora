#include "..\headers\define.h"
//---------------------

#define NAME                            SCRIPT_VVCGhost
#define TOWN_REP_VAR                    -1   // привязка к репутации (-1, если не нужно)

#define NUM_TEAM                        TEAM_VAULT_GHOST      // группа, к которой принадлежит персонаж (-1 если нужно оставить Team, заданный в редакторе)
#define NUM_AI                          -1                    // AI персонажа (-1 если нужно оставить AI, заданный в редакторе)
#define KILL_SLAVA                      SLAVA_KILL_GHOUL      // слава за убийство этого персонажа (SLAVA_KILL_GOOD, SLAVA_KILL_BAD, SLAVA_KILL_NEITRAL, SLAVA_KILL_CHILD)
#define KILL_TOWN_REP                   REP_KILL_NEITRAL      // репутация за убийство этого персонажа (REP_KILL_GOOD, REP_KILL_BAD, REP_KILL_NEITRAL, REP_KILL_CRITICAL для враждебности всего поселения, REP_KILL_CHILD)
#define KILL_FRAC_REP                   FRAC_NON              // принадлежность определенной фракции (-1, если не нужно)

#define FLAG_STEAL                      40 // Нижний порог навыка для кражи (для торговцев 200, для животных 300)
#define FLAG_ALCOHOL                    1  // Примет ли алкоголь от ГГ: 0 - принимает, 1 - не принимает
#define FLAG_FOOD                       1  // Примет ли пищу от ГГ: 0 - принимает, 1 - не принимает

//---------------------

procedure start;
procedure map_enter_p_proc;
procedure map_update_p_proc;
procedure loot_gener;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;
procedure pickup_p_proc;
procedure combat_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;

//---------------------

#define LVAR_HomeTile_Music             (6) // точка перехода после включения музыки, при <=0 не работает

//---------------------



procedure start begin
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
       set_start_home
       if (map_first_run) then begin
           if ((cur_map_index == MAP_VAULT_27_INTERIER) and (self_elevation == 1)) then begin
           set_local_var(LVAR_HomeTile_Music,tile_num_in_direction(16900, random(0,5), random(1,3)));
           end
           call loot_gener;
       end
       set_obj_visibility(self_obj, 1);

       if (cur_map_index == MAP_VAULT_27_EXTERIER) then begin
           if (global_var(GVAR_VAULT27_STATUS_MERC) == 0) then begin
           move_to(self_obj, 13160, 1);
           set_obj_visibility(self_obj, 1);
           end
       end
   end
end

procedure map_update_p_proc begin
   if (obj_is_visible_flag(self_obj) == false) then begin
       if (global_var(GVAR_VAULT27_STATUS_MERC) != 0) then begin
          set_obj_visibility(self_obj, 0);
       end
       if (getQuest(GVAR_VAULT27_GENERATOR) >= qEnd) then begin
          if (local_var(LVAR_HomeTile_Music)>0) then begin
          set_obj_visibility(self_obj, 0);
          end
       end
   end
end

procedure loot_gener begin
   variable lootID;
   lootID := random(0,5);

   if (lootID == 0) then begin
       item_add(self_obj,PID_BOOTLE_CAPS,random(1,3))
   end
   if (lootID == 1) then begin
       item_add(self_obj,PID_MANY,1)
   end
   if (lootID == 2) then begin
       item_add(self_obj,PID_AMMO_MICRO_FUSION_CELL,random(1,2))
   end
   if (lootID == 3) then begin
       item_add(self_obj,PID_AMMO_10_MM,random(1,3))
   end
end

procedure timed_event_p_proc begin
   // Таймер за номером 1 зарезервирован под перемещение персонажа по локации
   // Таймер за номером 2 зарезервирован под всплывающие реплики
   if (fixed_param == 1) then begin
       if ((local_var(LVAR_HomeTile_Music)>0) and (map_var(MVAR_Vault_Music) > 0)) then begin
       set_obj_visibility(self_obj, 0);
       timer_move_to_tile(0,local_var(LVAR_HomeTile_Music),5)
       end
       else begin
       timer_move_to_tile(0,self_tile,self_cur_rot)
       end
   end
   if (fixed_param == 2) then begin
   end
end

procedure critter_p_proc begin
   invisToDude(0,102) // Скрывание нпс от ГГ до некоторой дистанции, в скобках реплика для лога; не забыть set_obj_visibility(self_obj, 1) в map_enter_p_proc.
   set_critter_options(2,0) //условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ
end

procedure use_skill_on_p_proc begin
   skillUseSteal
   skillUseFAid
end

procedure pickup_p_proc begin
   pickupProcCritter
end

procedure use_obj_on_p_proc begin
   UsedItemToCrit_Proc
end

procedure combat_p_proc begin
   effectAttak(0,1,1,1,2) //отравление min, облучение min, травма ног %, травма рук %, травма глаз %
   effectRegeratHP
end

procedure damage_p_proc begin
   set_damage_dude
   set_global_var(GVAR_VAULT27_GHOST_KNOW,1);
end

procedure destroy_p_proc begin
   set_global_var(GVAR_VAULT27_GHOST_KNOW,1);
   if (random(0,99)<75) then begin
   item_add(self_obj,PID_TOOL_GAS_MASK,1)
   end
   kill_critter_opt
end

procedure talk_p_proc begin
   g_floater(self_obj,510,COLOR_MSG_RED);
end
