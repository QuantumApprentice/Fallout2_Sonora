#include "..\headers\define.h"
//---------------------

#define NAME                            SCRIPT_TCCody
#define TOWN_REP_VAR                    GVAR_TOWN_REP_TWO_SUN   // привязка к репутации (-1, если не нужно)

#define NUM_TEAM                        TEAM_TWOSUN_PEASAN    // группа, к которой принадлежит персонаж (-1 если нужно оставить Team, заданный в редакторе)
#define NUM_AI                          AI_CHILD              // AI персонажа (-1 если нужно оставить AI, заданный в редакторе)
#define KILL_SLAVA                      SLAVA_KILL_CHILD      // слава за убийство этого персонажа (SLAVA_KILL_GOOD, SLAVA_KILL_BAD, SLAVA_KILL_NEITRAL, SLAVA_KILL_CHILD)
#define KILL_TOWN_REP                   REP_KILL_CHILD        // репутация за убийство этого персонажа (-100 для враждебности всего поселения)
#define KILL_FRAC_REP                   FRAC_NON              // принадлежность определенной фракции (-1, если не нужно)

#define FLAG_STEAL                      25 // Нижний порог навыка для кражи (для торговцев 200, для животных 300)
#define FLAG_ALCOHOL                    1  // Примет ли алкоголь от ГГ: 0 - принимает, 1 - не принимает
#define FLAG_FOOD                       1  // Примет ли пищу от ГГ: 0 - принимает, 1 - не принимает

//---------------------

procedure start;
procedure map_enter_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;
procedure pickup_p_proc;
procedure combat_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;

procedure Node998;
procedure Node999;
procedure NodeStartDialog;
procedure NodeFstDialog;
procedure NodeFstDialog2;
procedure NodeFstDialog3;
procedure NodeFstDialog4;
procedure NodeFstDialog5;
procedure NodeSndDialog;
procedure NodeAnswer100;
procedure NodeAnswer101;
procedure NodeAnswer102;
procedure NodeQuest100;
procedure NodeQuest101;
procedure NodeQuest200;
procedure NodeQuest201;
procedure NodeQuest202;
procedure NodeQuest203;
procedure NodeQuest204;
procedure NodeQuest205;

//---------------------

//#define LVAR_Flag_A                     (6) // отвечает за ...

//---------------------



procedure start begin
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
       set_start_home
       if (global_var(GVAR_TWOSUN_STATUS_ZAX) > 0) then begin
       rm_timer_event(self_obj);
       set_obj_visibility(self_obj, 1);
       end
   end
end

procedure look_at_p_proc begin
   lookProcMsg
end

procedure description_p_proc begin
   dsrptProcMsg
end

procedure timed_event_p_proc begin
   // Таймер за номером 1 рарезервирован под перемещение персонажа по локации
   // Таймер за номером 2 рарезервирован под всплывающие реплики
   if (fixed_param == 1) then begin
       timer_move_to_home
   end
   if (fixed_param == 2) then begin
       //timer_float_reply(110,111)
   end
end

procedure critter_p_proc begin
   set_critter_options(0,0) //условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ
end

procedure use_skill_on_p_proc begin
   skillUseSteal
   skillUseFAid
end

procedure pickup_p_proc begin
   pickupProcCritter
end

procedure use_obj_on_p_proc begin
   UsedItemToCrit_Proc
end

procedure combat_p_proc begin
   effectAttak(0,0,0,0,0) //отравление min, облучение min, травма ног %, травма рук %, травма глаз %
end

procedure damage_p_proc begin
   set_damage_dude
end

procedure destroy_p_proc begin
   set_gvar_bit_on(GVAR_TWOSUN_QST_FAMILY_STATUS, bit_1);
   if (getQuest(GVAR_TWOSUN_QST_FAMILY_PIP) == qStart) then begin
   setQuest(GVAR_TWOSUN_QST_FAMILY_PIP,qStop);
   end
   kill_critter_opt
end

procedure talk_p_proc begin
   if (getQuest(GVAR_TWOSUN_QST_FAMILY_PIP) >= qEnd) then begin
   floater(self_obj,102,COLOR_MSG_GREEN);
   end
   else begin
   setReaction(0) // Отношение к ГГ (1 - персонаж положительный, -1 отрицательный, 0 нейтральный)
   dialog_proc(0,0,NodeStartDialog,-1,-1)
   end
end

procedure Node998 begin
   set_enemy_dude;
end

procedure Node999 begin
end

procedure NodeStartDialog begin
   if (bitDialog(bit_1) == 0) then begin
   call NodeFstDialog;
   end
   else begin
   call NodeSndDialog;
   end
end

//--- ПЕРВЫЙ ДИАЛОГ

procedure NodeFstDialog begin
   Reply(200)
   NAnswer(201,1,NodeFstDialog2)
   NAnswer(205,1,Node999)
end

procedure NodeFstDialog2 begin
   bitDialogExp(bit_1,0)
   Reply(202)
   BAnswer(203,1,NodeFstDialog3)
   GAnswer(204,1,NodeFstDialog3)
end

procedure NodeFstDialog3 begin
   Reply(206)
   NAnswer(207,1,NodeFstDialog4)
   BAnswer(209,1,NodeFstDialog5)
end

procedure NodeFstDialog4 begin
   Reply(208)
   NAnswer(211,1,Node999)
   BAnswer(212,1,Node999)
end

procedure NodeFstDialog5 begin
   Reply(210)
   NAnswer(211,1,Node999)
   BAnswer(212,1,Node999)
end

//--- ПОВТОРНЫЙ ДИАЛОГ

procedure NodeSndDialog begin
   Reply(250)

   NAnswer(220,1,NodeAnswer100)
   NAnswer(223,1,NodeAnswer101)
   NAnswer(226,1,NodeAnswer102)

   if (getQuest(GVAR_TWOSUN_QST_FAMILY_PIP) == qZero) then begin
      NAnswer(252,1,NodeQuest100)
   end
   if (getQuest(GVAR_TWOSUN_QST_FAMILY_PIP) == qStart) then begin
      if (gvar_bit(GVAR_TWOSUN_QST_FAMILY_STATUS, bit_5) != 0) then begin
      NAnswer(260,1,NodeQuest200)
      end
      if (gvar_bit(GVAR_TWOSUN_QST_FAMILY_STATUS, bit_3) != 0) then begin
      NAnswer(276,1,NodeQuest101)
      end
   end

   NAnswer(251,1,Node999)
end

procedure NodeAnswer100 begin
   Reply(221)
   NAnswer(222,1,NodeSndDialog)
end

procedure NodeAnswer101 begin
   Reply(224)
   NAnswer(225,1,NodeSndDialog)
end

procedure NodeAnswer102 begin
   Reply(227)
   NAnswer(228,1,NodeSndDialog)
end

procedure NodeQuest100 begin
   setQuest(GVAR_TWOSUN_QST_FAMILY_PIP,qStart);
   Reply(253)
   NAnswer(254,1,Node999)
   BAnswer(255,1,Node999)
end

procedure NodeQuest101 begin
   Reply(277)
   NAnswer(278,1,Node999)
end

procedure NodeQuest200 begin
   Reply(261)
   NAnswer(262,1,NodeQuest201)
end

procedure NodeQuest201 begin
   Reply(263)
   NAnswer(264,1,NodeQuest202)
   BAnswer(265,1,NodeQuest202)
end

procedure NodeQuest202 begin
   Reply(266)
   BAnswer(270,1,NodeQuest203)
   GAnswer(273,1,NodeQuest204)
   NAnswer(267,1,NodeQuest205)
end

procedure NodeQuest203 begin
   set_REP(-2)
   Reply(271)
   NAnswer(272,1,NodeQuest205)
end

procedure NodeQuest204 begin
   set_REP(2)
   Reply(274)
   NAnswer(275,1,NodeQuest205)
end

procedure NodeQuest205 begin
   set_gvar_bit_on(GVAR_TWOSUN_QST_FAMILY_STATUS, bit_6);
   setQuest(GVAR_TWOSUN_QST_FAMILY_PIP,qEnd);
   set_SLAVA(25)
   exp_add(350);

   Reply(268)
   NAnswer(269,1,Node999)
end
