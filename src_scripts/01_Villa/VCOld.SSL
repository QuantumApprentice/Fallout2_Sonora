#include "..\headers\define.h"
//---------------------

#define NAME                            SCRIPT_VCOld
#define TOWN_REP_VAR                    GVAR_TOWN_REP_VILLA   // привязка к репутации (-1, если не нужно)

#define NUM_TEAM                        TEAM_VILLA_PEASAN     // группа, к которой принадлежит персонаж (-1 если нужно оставить Team, заданный в редакторе)
#define NUM_AI                          AI_TRIBLE_VILLA       // AI персонажа (-1 если нужно оставить AI, заданный в редакторе)
#define KILL_SLAVA                      SLAVA_KILL_GOOD       // слава за убийство этого персонажа (SLAVA_KILL_GOOD, SLAVA_KILL_BAD, SLAVA_KILL_NEITRAL, SLAVA_KILL_CHILD)
#define KILL_TOWN_REP                   REP_KILL_CRITICAL     // репутация за убийство этого персонажа (REP_KILL_GOOD, REP_KILL_BAD, REP_KILL_NEITRAL, REP_KILL_CRITICAL для враждебности всего поселения, REP_KILL_CHILD)
#define KILL_FRAC_REP                   FRAC_NON              // принадлежность определенной фракции (-1, если не нужно)

#define FLAG_STEAL                      20  // Нижний порог навыка для кражи (для торговцев 200, для животных 300)
#define FLAG_ALCOHOL                    1   // Примет ли алкоголь от ГГ: 0 - принимает, 1 - не принимает
#define FLAG_FOOD                       1   // Примет ли пищу от ГГ: 0 - принимает, 1 - не принимает

//---------------------

procedure start;
procedure map_enter_p_proc;
procedure map_update_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;
procedure pickup_p_proc;
procedure combat_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;

procedure Node998;
procedure Node999;
procedure NodeStartDialog;
procedure NodeFstDialog;
procedure NodeFstDialog2;
procedure NodeFstDialog3;
procedure NodeFstDialog4;
procedure NodeFstDialog5;
procedure NodeFstDialog6;
procedure NodeFstDialog7;
procedure NodeFstDialog8;
procedure NodeSndDialog;
procedure NodeSndDialog2;
procedure NodeSndDialog3;
procedure NodeConcuilAStart;
procedure NodeConcuilA;
procedure NodeConcuilA100;
procedure NodeConcuilA101;
procedure NodeConcuilA102;
procedure NodeConcuilA103;
procedure NodeConcuilA104;
procedure NodeConcuilA105;
procedure NodeConcuilA106;
procedure NodeConcuilA107;
procedure NodeConcuilA200;
procedure NodeConcuilA201;
procedure NodeConcuilA202;
procedure NodeConcuilA203;
procedure NodeConcuilA300;
procedure NodeConcuilA301;
procedure NodeConcuilA302;
procedure NodeConcuilA303;
procedure NodeConcuilA304;
procedure NodeConcuilA305;
procedure NodeConcuilA306;
procedure NodeConcuilA307;
procedure NodeConcuilA308;
procedure NodeConcuilA400;
procedure NodeConcuilA401;
procedure NodeConcuilA402;
procedure NodeConcuilA403;
procedure NodeConcuilA404;
procedure NodeConcuilA405;
procedure NodeConcuilA406;
procedure NodeConcuilA407;
procedure NodeConcuilA408;
procedure NodeConcuilA409;
procedure NodeConcuilA410;
procedure NodeConcuilA411;
procedure NodeConcuilA412;
procedure NodeConcuilA500;
procedure NodeConcuilA501;
procedure NodeConcuilA502;
procedure NodeConcuilA503;
procedure NodeConcuilA504;
procedure NodeConcuilA505;
procedure NodeConcuilA600;
procedure NodeConcuilA601;
procedure NodeConcuilA602;
procedure NodeConcuilA603;
procedure NodeConcuilA604;
procedure NodeConcuilA605;
procedure NodeConcuilAFinish;
procedure NodeConcuilAFinish2;
procedure NodeConcuilB100;
procedure NodeConcuilB101;
procedure NodeConcuilB102;
procedure NodeConcuilB103;
procedure NodeConcuilB104;
procedure NodeConcuilB105;
procedure NodeConcuilB106;
procedure NodeConcuilB107;
procedure NodeConcuilB108;
procedure NodeConcuilB200;
procedure NodeConcuilB201;
procedure NodeConcuilB202;
procedure NodeConcuilB203;
procedure NodeConcuilB204;
procedure NodeConcuilB205;
procedure NodeConcuilB206;
procedure NodeConcuilB207;
procedure NodeConcuilB208;
procedure NodeConcuilB209;
procedure NodeConcuilB210;
procedure NodeConcuilB211;
procedure NodeConcuilB212;
procedure NodeConcuilB213;

//---------------------

//#define LVAR_Flag_A                     (6) // отвечает за ...

variable replyNum;

//---------------------



procedure start begin
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
       if (getQuest(GVAR_MAIN_QUEST_SYNOD_VILLA) == qZero) then begin
       if (global_var(GVAR_VILLA_STATUS_MADRE) < 0) then begin
       if (global_var(GVAR_VILLA_STATUS_PADRE) < 0) then begin
       setQuest(GVAR_MAIN_QUEST_SYNOD_VILLA,qStart);
       end
       end
       end

       set_start_home
       set_obj_visibility(self_obj, 1);
   end
end

procedure map_update_p_proc begin
   if (getQuest(GVAR_MAIN_QUEST_SYNOD_VILLA) > qZero) then begin
   set_obj_visibility(self_obj, 0);
   end
end

procedure look_at_p_proc begin
   lookProcMsg
end

procedure description_p_proc begin
   dsrptProcMsg
end

procedure timed_event_p_proc begin
   // Таймер за номером 1 рарезервирован под перемещение персонажа по локации
   // Таймер за номером 2 рарезервирован под всплывающие реплики
   if (fixed_param == 1) then begin
       timer_move_to_home
   end
   if (fixed_param == 2) then begin
       //timer_float_reply(120,121)
   end
   if (fixed_param == 3) then begin
       if (getQuest(GVAR_VILLA_ALLIANCE_PIP) > qStop) then begin
       setQuest(GVAR_VILLA_ALLIANCE_PIP,qEnd);
       end
       dude_teleport_map(0,0,MAP_VILLA_HOME,1);
   end

   if (fixed_param == 4) then begin
      game_ui_disable;
      gfade_out(ONE_GAME_SECOND);
      call NodeFstDialog8;
      game_time_advance(ONE_GAME_HOUR);
      move_to(self_obj,HomeTile,self_elevation);
      move_to(dude_obj,13288,0);
      tile_set_center(self_tile);
      create_object_sid(PID_HEX_SOLAR,self_tile,0,-1);
      set_map_var(MVAR_Plaza_Council,1);
      if (bitDialog(bit_2) != 0) then begin
         if (bitDialog(bit_3) == 0) then begin
         skill_up(SKILL_GAMBLING,5);
         bitDialogExp(bit_3,0)
         end
      end
      add_timer_event(self_obj,20,5);
   end
   if (fixed_param == 5) then begin
      gfade_in(ONE_GAME_SECOND);
      display_mstr(230);
      add_timer_event(self_obj,20,6);
   end
   if (fixed_param == 6) then begin
      floater(self_obj,232,COLOR_MSG_RED);
      add_timer_event(self_obj,60,7);
   end
   if (fixed_param == 7) then begin
      game_ui_enable;
      PLAY_FADE
      floater(self_obj,233,COLOR_MSG_RED);
      dialogue_system_enter;
   end
end

procedure critter_p_proc begin
   set_critter_options(1,0) //условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ
end

procedure use_skill_on_p_proc begin
   skillUseSteal
   skillUseFAid
end

procedure pickup_p_proc begin
   pickupProcCritter
end

procedure use_obj_on_p_proc begin
   UsedItemToCrit_Proc
end

procedure combat_p_proc begin
   effectAttak(0,0,1,1,1) //отравление min, облучение min, травма ног %, травма рук %, травма глаз %
end

procedure damage_p_proc begin
   set_damage_dude
end

procedure destroy_p_proc begin
   set_map_var(MVAR_Plaza_Council,0);
   set_global_var(GVAR_VILLA_STATUS_OLD,-100);
   setQuest(GVAR_MAIN_QUEST_SYNOD_VILLA,qStop);
   setQuest(GVAR_VILLA_ALLIANCE_PIP,qStop);
   kill_critter_opt
end

procedure talk_p_proc begin
   if (global_var(GVAR_GAME_END) != 0) then begin
      if (global_var(GVAR_TITUL_VILLA_TRADITION)>0) then begin
      floater(self_obj,102,COLOR_MSG_GREEN);
      end
      else if (global_var(GVAR_TITUL_VILLA_DISTROY)>0) then begin
      floater(self_obj,103,COLOR_MSG_RED);
      end
      else begin
      z_floater(self_obj,104,COLOR_MSG_NORMAL);
      end
   end
   else begin
      setReaction(0) // Отношение к ГГ (1 - персонаж положительный, -1 отрицательный, 0 нейтральный)
      dialog_proc(0,105,NodeStartDialog,-1,-1)
   end
end

procedure Node998 begin
   set_map_var(MVAR_Plaza_Council,0);
   set_enemy_dude;
end

procedure Node999 begin
   set_map_var(MVAR_Plaza_Council,0);
end

procedure NodeStartDialog begin
   if (getQuest(GVAR_VILLA_ALLIANCE_PIP) == qStart) then begin
      call NodeConcuilB200;
   end
   else if (getQuest(GVAR_MAIN_QUEST_SYNOD_VILLA) >= qEnd) then begin
      call NodeSndDialog3;
   end
   else if (map_var(MVAR_Plaza_Council) > 0) then begin
      call NodeConcuilA;
   end
   else if (bitDialog(bit_1) == 0) then begin
      call NodeFstDialog;
   end
   else begin
      if (game_time_hour < 0600) then begin
      call NodeSndDialog2;
      end
      else begin
      call NodeSndDialog;
      end
   end
end

//--- ПЕРВЫЙ ДИАЛОГ

procedure NodeFstDialog begin
   bitDialogExp(bit_1,0)
   Reply(200)
   NAnswer(201,1,NodeFstDialog2)
end

procedure NodeFstDialog2 begin
   Reply(mstr(202)+dude_name+mstr(203))
   NAnswer(204,1,NodeFstDialog3)
end

procedure NodeFstDialog3 begin
   Reply(205)
   NAnswer(206,1,NodeFstDialog4)
   NAnswer(213,1,NodeFstDialog6)
   GAnswer(212,7,NodeFstDialog7)
   NAnswer(210,1,Node999)
end

procedure NodeFstDialog4 begin
   Reply(207)
   NAnswer(208,1,NodeFstDialog5)
   NAnswer(213,1,NodeFstDialog6)
   GAnswer(212,7,NodeFstDialog7)
   NAnswer(210,1,Node999)
end

procedure NodeFstDialog5 begin
   Reply(209)
   NAnswer(213,1,NodeFstDialog6)
   GAnswer(212,7,NodeFstDialog7)
   NAnswer(211,1,Node999)
end

procedure NodeFstDialog6 begin
   //call NodeFstDialog8;
   call NodeConcuilAStart;
end

procedure NodeFstDialog7 begin
   bitDialogExp(bit_2,0)
   call NodeFstDialog6;
end

procedure NodeFstDialog8 begin
   if (game_time_hour < 1800) then begin
      if (game_time_hour < 1600) then begin
      game_time_advance(ONE_GAME_HOUR);
      end
      game_time_advance(ONE_GAME_HOUR);
      call NodeFstDialog8;
   end
end

//--- ВТОРОЙ ДИАЛОГ

procedure NodeSndDialog begin
   Reply(dude_name+mstr(220))

   if (game_time_hour >= 1800) then begin
   NAnswer(222,1,NodeConcuilAStart)
   end
   else begin
   NAnswer(224,1,NodeFstDialog6)
   GAnswer(223,7,NodeFstDialog7)
   end

   NAnswer(221,1,Node999)
end

procedure NodeSndDialog2 begin
   Reply(226)
   NAnswer(227,1,Node999)
end

procedure NodeSndDialog3 begin
   Reply(226)

   if (getQuest(GVAR_FLAGSTAFF_MINES_DESTROY) == qEnd) then begin
   NAnswer(450,1,NodeConcuilB100)
   end
   if (getQuest(GVAR_FLAGSTAFF_MINES_DESTROY) == qStart) then begin
   if (getQuest(GVAR_FLAGSTAFF_ROBOT_REPAIR) >= qEnd) then begin
   NAnswer(453,1,NodeConcuilB101)
   end
   end

   if (getQuest(GVAR_GARAGE_STANOK_DESTROY) == qEnd) then begin
   NAnswer(456,1,NodeConcuilB102)
   end
   if (getQuest(GVAR_GARAGE_STANOK_DESTROY) == qStart) then begin
   if (gvar_bit(GVAR_DIALOGS_FLAGS_BIT, bit_29) != 0) then begin
   NAnswer(459,1,NodeConcuilB103)
   end
   end

   NAnswer(227,1,Node999)
end

//--- ПЕРВОЕ СОБРАНИЕ

procedure NodeConcuilAStart begin
   add_timer_event(self_obj,5,4);
   call Node999;
end

procedure NodeConcuilA begin
   Reply(250)
   NAnswer(251,1,NodeConcuilA100)
end

procedure NodeConcuilA100 begin
   Reply(252)
   NAnswer(253,1,NodeConcuilA101)
end

procedure NodeConcuilA101 begin
   Reply(254)
   NAnswer(255,1,NodeConcuilA102)
end

procedure NodeConcuilA102 begin
   Reply(256)
   NAnswer(257,1,NodeConcuilA103)
end

procedure NodeConcuilA103 begin
   Reply(mstr(258)+dude_name+mstr(259))
   NAnswer(260,1,NodeConcuilA104)
end

procedure NodeConcuilA104 begin
   if dude_is_female then begin
   Reply(mstr(1261)+dude_name+mstr(1262))
   end
   else begin
   Reply(mstr(261)+dude_name+mstr(262))
   end
   BAnswer(263,1,NodeConcuilA105)
   zNAnswer(264,1,NodeConcuilA105)
end

procedure NodeConcuilA105 begin
   if town_rep_is_antipaty then begin
      dec_map_var(MVAR_Plaza_OldRespect);
      if dude_is_female then begin
      Reply(mstr(1266)+dude_name+mstr(1267))
      end
      else begin
      Reply(mstr(266)+dude_name+mstr(267))
      end
      NAnswer(269,1,NodeConcuilA106)
      BAnswer(268,1,NodeConcuilA107)
   end
   else if town_rep_is_liked then begin
      inc_map_var(MVAR_Plaza_OldRespect);
      Reply(mstr(270)+dude_name+mstr(271)+dude_name+mstr(272))
      NAnswer(273,1,NodeConcuilA106)
      BAnswer(274,1,NodeConcuilA107)
   end
   else begin
      if dude_is_female then begin
      Reply(mstr(275)+dude_name+mstr(1276))
      end
      else begin
      Reply(mstr(275)+dude_name+mstr(276))
      end
      NAnswer(277,1,NodeConcuilA106)
      BAnswer(278,1,NodeConcuilA107)
   end
end

procedure NodeConcuilA106 begin
   call NodeConcuilA200;
end

procedure NodeConcuilA107 begin
   set_REP(-1)
   call NodeConcuilA106;
end

//--- СОВЕТ: ГГ на пост управленца

procedure NodeConcuilA200 begin
   /*
   [падре погиб] Теперь решим такой вопрос. Сеньор Андрес так и не вернулся из плена. Ведь ты, name, следовал по стапам своего падре. Расскажи все, что знаешь.
   Забудьте про моего падре. Его больше нет.
   (сокрушенное молчание) Что ж, мы были к этому готовы. Твой падре был суровым, но верным лидером. name, ты наместник своего падре. Тебе перенимать правление общиной после его смерти.
   */
   if (global_var(GVAR_VILLA_STATUS_PADRE) < 0) then begin
   dec_map_var(MVAR_Plaza_OldRespect);
   Reply(mstr(284)+dude_name+mstr(285))
   BAnswer(287,1,NodeConcuilA201)
   NAnswer(286,1,NodeConcuilA201)
   end
   else if (global_var(GVAR_VILLA_STATUS_PADRE) > 0) then begin
   inc_map_var(MVAR_Plaza_OldRespect);
   Reply(mstr(280)+dude_name+mstr(281))
   BAnswer(283,1,NodeConcuilA201)
   NAnswer(282,1,NodeConcuilA201)
   end
   else begin
   call NodeConcuilA201;
   end
end

procedure NodeConcuilA201 begin
   if (global_var(GVAR_VILLA_STATUS_MADRE) < 0) then begin
   dec_map_var(MVAR_Plaza_OldRespect);
   Reply(290)
   NAnswer(291,1,NodeConcuilA203)
   BAnswer(292,1,NodeConcuilA202)
   end
   else if (global_var(GVAR_VILLA_STATUS_MADRE) > 0) then begin
   inc_map_var(MVAR_Plaza_OldRespect);
   Reply(mstr(293)+dude_name+mstr(294))
   NAnswer(295,1,NodeConcuilA203)
   BAnswer(296,1,NodeConcuilA202)
   end
   else begin
   call NodeConcuilA203;
   end
end

procedure NodeConcuilA202 begin
   set_REP(-1)
   call NodeConcuilA203;
end

procedure NodeConcuilA203 begin
   call NodeConcuilA300;
end

procedure NodeConcuilA300 begin
   //{300}{}{(другой старик подает брюзжащий голос из зала) Не спеши радоваться новым обноскам, мантис-попрыгун. Старики не доверяют молодым. У вас мало опыта. Хоть тебе и удалось спасти людей из рабства, а что еще ты можешь?}
   //{301}{}{Я...}
   Reply(300)
   BAnswer(302,1,NodeConcuilA301)
   NAnswer(301,1,NodeConcuilA301)
end

procedure NodeConcuilA301 begin
   replyNum := 0;
   Reply(mstr(303)+dude_name+mstr(304))

   if (global_var(GVAR_VILLA_STATUS_OLD) == 0) then begin
   NAnswer(305,1,NodeConcuilA302)
   end
   else begin
   replyNum += 1;
   end

   if (getQuest(GVAR_FLAGSTAFF_MINES_DESTROY) == qZero) then begin
   NAnswer(306,1,NodeConcuilA500)
   end
   else begin
   replyNum += 1;
   end

   if (getQuest(GVAR_GARAGE_STANOK_DESTROY) == qZero) then begin
   NAnswer(307,1,NodeConcuilA600)
   end
   else begin
   replyNum += 1;
   end

   if (replyNum>=3) then begin
   NAnswer(400,1,NodeConcuilAFinish)
   end
end

//--- СОВЕТ: конфликт со стариком Пабло

procedure NodeConcuilA302 begin
   replyNum := 0;
   call NodeConcuilA303;
end

procedure NodeConcuilA303 begin
   if (replyNum > 0) then begin
   Reply(311)
   end
   else begin
   Reply(310)
   end

   if ((bitDialog(bit_4) == 0) and (getQuest(GVAR_VILLA_METEO) >= qEnd)) then begin
   NAnswer(320,1,NodeConcuilA304)
   end

   if ((bitDialog(bit_5) == 0) and (getQuest(GVAR_VILLA_MARAUDERS) >= qEnd)) then begin
   NAnswer(323,1,NodeConcuilA305)
   end

   if ((bitDialog(bit_6) == 0) and (getQuest(GVAR_VILLA_MEDIC_HELP) >= qEnd)) then begin
   NAnswer(330,1,NodeConcuilA306)
   end

   if ((bitDialog(bit_7) == 0) and (getQuest(GVAR_VILLA_KID_RUN) >= qEnd)) then begin
   NAnswer(337,1,NodeConcuilA307)
   end

   if (replyNum > 0) then begin
   NAnswer(314,1,NodeConcuilA308)
   BAnswer(313,1,NodeConcuilA308)
   end
   else begin
   BAnswer(312,1,NodeConcuilA308)
   end
end

procedure NodeConcuilA304 begin
   bitDialogExp(bit_5,25)
   replyNum += 1;
   inc_map_var(MVAR_Plaza_OldRespect);
   Reply(321)
   NAnswer(322,1,NodeConcuilA303)
end

procedure NodeConcuilA305 begin
   bitDialogExp(bit_5,25)
   replyNum += 1;
   if (global_var(GVAR_VILLA_MARAUDERS_RUN) == 11) then begin
   inc_map_var(MVAR_Plaza_OldRespect);
   Reply(324)
   NAnswer(325,1,NodeConcuilA303)
   BAnswer(326,1,NodeConcuilA303)
   end
   else begin
   dec_map_var(MVAR_Plaza_OldRespect);
   Reply(327)
   NAnswer(328,1,NodeConcuilA303)
   BAnswer(329,1,NodeConcuilA303)
   end
end

procedure NodeConcuilA306 begin
   bitDialogExp(bit_6,25)
   replyNum += 1;
   if (map_var(MVAR_Plaza_MedicMetod) >= 2) then begin
   dec_map_var(MVAR_Plaza_OldRespect);
   Reply(331)
   NAnswer(332,1,NodeConcuilA303)
   BAnswer(333,1,NodeConcuilA303)
   end
   else begin
   inc_map_var(MVAR_Plaza_OldRespect);
   Reply(334)
   NAnswer(335,1,NodeConcuilA303)
   BAnswer(336,1,NodeConcuilA303)
   end
end

procedure NodeConcuilA307 begin
   bitDialogExp(bit_7,25)
   replyNum += 1;
   inc_map_var(MVAR_Plaza_OldRespect);
   Reply(338)
   NAnswer(339,1,NodeConcuilA303)
   BAnswer(340,1,NodeConcuilA303)
end

procedure NodeConcuilA308 begin
   if (replyNum >= 2) then begin
   Reply(318)
   NAnswer(319,1,NodeConcuilA400)
   end
   else begin
   Reply(315)
   NAnswer(316,1,NodeConcuilA400)
   end
end

//--- СОВЕТ: реакция на местные квесты и события

procedure NodeConcuilA400 begin
   if (getQuest(GVAR_VILLA_QST_CHRIS) >= 3) then begin
   dec_map_var(MVAR_Plaza_OldRespect);
   Reply(mstr(350)+dude_name+mstr(351))
   NAnswer(352,1,NodeConcuilA402)
   end
   else if (getQuest(GVAR_VILLA_QST_CHRIS) == 2) then begin
   inc_map_var(MVAR_Plaza_OldRespect);
   Reply(mstr(363)+dude_name+mstr(364)+dude_name+mstr(365))
   NAnswer(366,1,NodeConcuilA405)
   end
   else begin
   call NodeConcuilA401;
   end
end

procedure NodeConcuilA401 begin
   call NodeConcuilA406;
end

procedure NodeConcuilA402 begin
   Reply(mstr(353)+dude_name+mstr(354)+dude_name+mstr(355))
   zNAnswer(356,1,NodeConcuilA403)
   NAnswerSkill(360,3,NodeConcuilA404,SKILL_CONVERSANT,50,359,NodeConcuilA403)
end

procedure NodeConcuilA403 begin
   Reply(357)
   NAnswer(358,1,NodeConcuilA406)
end

procedure NodeConcuilA404 begin
   inc_map_var(MVAR_Plaza_OldRespect);
   Reply(361)
   NAnswer(362,1,NodeConcuilA406)
end

procedure NodeConcuilA405 begin
   Reply(367)
   NAnswer(368,1,NodeConcuilA406)
end

procedure NodeConcuilA406 begin
   if (map_var(MVAR_Plaza_PwrArmr)>0) then begin
   dec_map_var(MVAR_Plaza_OldRespect);
   Reply(mstr(370)+dude_name+mstr(371))
   NAnswer(372,1,NodeConcuilA407)
   end
   else if (global_var(GVAR_CAR_MAP_INDEX) == cur_map_index) then begin
   dec_map_var(MVAR_Plaza_OldRespect);
   Reply(mstr(382)+dude_name+mstr(383))
   NAnswer(384,1,NodeConcuilA407)
   end
   //!!! добавить сюда реакцию на робота если таковой будет в напарниках
   /*
   !!! Отдельными ветками добавить реакцию на:
      -- ученый Каррингтон появился на Вилле
      -- Мигель вернулся на Виллу
      -- хорошая репутация во враждебных городах типа Флагпула и Гаража: "Нам стало известно, что ты завел друзей среди чужаков. Ты осознаешь, к каким последствиям может привести это безрассудство?"
      -- титулы: Последователь Огня, член БС, рейнджер, воин дороги
      -- ГГ выведал тайну убежища 27 и рассказал об этом кому-то из общины
      -- гулификация (сделать в самом начале совета)
   */

   else begin
   call NodeConcuilA410;
   end
end

procedure NodeConcuilA407 begin
   Reply(mstr(373)+dude_name+mstr(374))
   NAnswerSkill(376,3,NodeConcuilA408,SKILL_CONVERSANT,50,379,NodeConcuilA409)
   NAnswer(375,1,NodeConcuilA410)
end

procedure NodeConcuilA408 begin
   inc_map_var(MVAR_Plaza_OldRespect);
   Reply(377)
   NAnswer(378,1,NodeConcuilA410)
end

procedure NodeConcuilA409 begin
   Reply(380)
   NAnswer(381,1,NodeConcuilA410)
end

procedure NodeConcuilA410 begin
   Reply(mstr(385)+dude_name+mstr(386))
   NAnswer(387,1,NodeConcuilA411)
   BAnswer(388,1,NodeConcuilA411)
end

procedure NodeConcuilA411 begin
   if (map_var(MVAR_Plaza_OldRespect)>=3) then begin
   set_global_var(GVAR_VILLA_STATUS_OLD,10);
   set_REP(5)
   Reply(mstr(389)+dude_name+mstr(390))
   BAnswer(392,1,NodeConcuilA412)
   NAnswer(391,1,NodeConcuilA412)
   end
   else begin
   set_global_var(GVAR_VILLA_STATUS_OLD,1);
   set_REP(-5)
   Reply(mstr(393)+dude_name+mstr(394))
   BAnswer(396,1,NodeConcuilA412)
   NAnswer(395,1,NodeConcuilA412)
   end
end

procedure NodeConcuilA412 begin
   call NodeConcuilA301;
end

//--- СОВЕТ: Флагпул

procedure NodeConcuilA500 begin
   setQuest(GVAR_FLAGSTAFF_MINES_DESTROY,qStart);
   if (global_var(GVAR_VILLA_STATUS_MADRE) > 0) then begin
   Reply(410)
   end
   else begin
   Reply(411)
   end
   NAnswer(412,1,NodeConcuilA501)
end

procedure NodeConcuilA501 begin
   if (global_var(GVAR_VILLA_STATUS_SANCHO) >= 2) then begin
   Reply(mstr(414)+mstr(415))
   NAnswer(416,1,NodeConcuilA502)
   end
   else if (global_var(GVAR_VILLA_STATUS_HUGO) >= 2) then begin
   Reply(mstr(427)+mstr(415))
   NAnswer(416,1,NodeConcuilA502)
   end
   else begin
   Reply(mstr(413)+mstr(415))
   NAnswer(416,1,NodeConcuilA502)
   end
end

procedure NodeConcuilA502 begin
   Reply(417)
   BAnswer(418,1,NodeConcuilA503)
   GAnswer(421,1,NodeConcuilA504)
   NAnswer(424,1,NodeConcuilA505)
end

procedure NodeConcuilA503 begin
   set_REP(-1)
   Reply(419)
   NAnswer(420,1,NodeConcuilA301)
end

procedure NodeConcuilA504 begin
   set_REP(1)
   Reply(422)
   NAnswer(423,1,NodeConcuilA301)
end

procedure NodeConcuilA505 begin
   Reply(425)
   NAnswer(426,1,NodeConcuilA301)
end


//--- СОВЕТ: ГаражСити

procedure NodeConcuilA600 begin
   setQuest(GVAR_GARAGE_STANOK_DESTROY,qStart);
   if (global_var(GVAR_VILLA_STATUS_JOSE) < 0) then begin
   Reply(430)
   end
   else begin
   Reply(mstr(432)+mstr(430))
   end
   NAnswer(431,1,NodeConcuilA601)
end

procedure NodeConcuilA601 begin
   Reply(433)
   NAnswer(434,1,NodeConcuilA602)
end

procedure NodeConcuilA602 begin
   Reply(435)
   if (global_var(GVAR_TOWN_REP_GARAGE)>0) then begin
   BAnswer(436,1,NodeConcuilA603)
   end
   else begin
   BAnswer(437,1,NodeConcuilA603)
   end
   GAnswer(440,1,NodeConcuilA604)
   NAnswer(443,1,NodeConcuilA605)
end

procedure NodeConcuilA603 begin
   set_REP(-1)
   Reply(438)
   NAnswer(439,1,NodeConcuilA301)
end

procedure NodeConcuilA604 begin
   set_REP(1)
   Reply(441)
   NAnswer(442,1,NodeConcuilA301)
end

procedure NodeConcuilA605 begin
   Reply(444)
   NAnswer(445,1,NodeConcuilA301)
end

procedure NodeConcuilAFinish begin
   Reply(mstr(401)+dude_name+mstr(402))
   NAnswer(403,1,NodeConcuilAFinish2)
end

procedure NodeConcuilAFinish2 begin
   game_time_advance(ONE_GAME_HOUR*5);
   set_map_var(MVAR_Plaza_Council,0);
   setQuest(GVAR_MAIN_QUEST_SYNOD_VILLA,qEnd);
   if (getQuest(GVAR_MAIN_QUEST_VILLA) == qStart) then begin
   setQuest(GVAR_MAIN_QUEST_VILLA,qEnd);
   end
   if (getQuest(GVAR_FLAGSTAFF_MINES_DESTROY) == qZero) then begin
   setQuest(GVAR_FLAGSTAFF_MINES_DESTROY,qStart);
   end
   if (getQuest(GVAR_GARAGE_STANOK_DESTROY) == qZero) then begin
   setQuest(GVAR_GARAGE_STANOK_DESTROY,qStart);
   end
   exp_add(1000);
   display_mstr(231);
   call Node999;
end

//--- ВТОРОЕ СОБРАНИЕ

procedure NodeConcuilB100 begin
   setQuest(GVAR_FLAGSTAFF_MINES_DESTROY,qEnd+1);
   inc_map_var(MVAR_Plaza_OldRespect);
   Reply(451)
   if (getQuest(GVAR_GARAGE_STANOK_DESTROY) > qEnd) then begin
   NAnswer(462,1,NodeConcuilB104)
   end
   else if (getQuest(GVAR_GARAGE_STANOK_DESTROY) <= qStop) then begin
   NAnswer(462,1,NodeConcuilB104)
   end
   else begin
   NAnswer(452,1,Node999)
   end
end

procedure NodeConcuilB101 begin
   setQuest(GVAR_FLAGSTAFF_MINES_DESTROY,qStop);
   dec_map_var(MVAR_Plaza_OldRespect);
   Reply(454)
   if (getQuest(GVAR_GARAGE_STANOK_DESTROY) > qEnd) then begin
   NAnswer(462,1,NodeConcuilB104)
   end
   else if (getQuest(GVAR_GARAGE_STANOK_DESTROY) <= qStop) then begin
   NAnswer(462,1,NodeConcuilB104)
   end
   else begin
   NAnswer(455,1,Node999)
   end
end

procedure NodeConcuilB102 begin
   setQuest(GVAR_GARAGE_STANOK_DESTROY,qEnd+1);
   inc_map_var(MVAR_Plaza_OldRespect);
   Reply(457)
   if (getQuest(GVAR_FLAGSTAFF_MINES_DESTROY) > qEnd) then begin
   NAnswer(462,1,NodeConcuilB104)
   end
   else if (getQuest(GVAR_FLAGSTAFF_MINES_DESTROY) <= qStop) then begin
   NAnswer(462,1,NodeConcuilB104)
   end
   else begin
   NAnswer(458,1,Node999)
   end
end

procedure NodeConcuilB103 begin
   setQuest(GVAR_GARAGE_STANOK_DESTROY,qStop);
   dec_map_var(MVAR_Plaza_OldRespect);
   Reply(460)
   if (getQuest(GVAR_FLAGSTAFF_MINES_DESTROY) > qEnd) then begin
   NAnswer(462,1,NodeConcuilB104)
   end
   else if (getQuest(GVAR_FLAGSTAFF_MINES_DESTROY) <= qStop) then begin
   NAnswer(462,1,NodeConcuilB104)
   end
   else begin
   NAnswer(461,1,Node999)
   end
end

procedure NodeConcuilB104 begin
   Reply(463)
   NAnswer(464,1,NodeConcuilB105)
end

procedure NodeConcuilB105 begin
   setQuest(GVAR_VILLA_ALLIANCE_PIP,qStart);
   PLAY_FADE
   Reply(dude_name+mstr(466))
   NAnswer(467,1,NodeConcuilB106)
end

procedure NodeConcuilB106 begin
   Reply(468)
   NAnswer(469,1,NodeConcuilB107)
end

procedure NodeConcuilB107 begin
   Reply(470)
   NAnswer(471,1,NodeConcuilB108)
   BAnswer(472,1,NodeConcuilB108)
end

procedure NodeConcuilB108 begin
   Reply(473)
   NAnswer(474,1,Node999)
   BAnswer(475,1,Node999)
end

procedure NodeConcuilB200 begin
   if (getQuest(GVAR_VILLA_ALLIANCE_BOS) >= qEnd) then begin
      Reply(482)
      NAnswer(483,1,NodeConcuilB201)
      NAnswer(490,1,Node999)
   end
   else if (getQuest(GVAR_VILLA_ALLIANCE_RANGER) >= qEnd) then begin
      Reply(488)
      NAnswer(489,1,NodeConcuilB204)
      NAnswer(490,1,Node999)
   end
   else begin
      zReply(480)
      if (getQuest(GVAR_VILLA_ALLIANCE_RANGER) <= qStop) then begin
         if (getQuest(GVAR_RANGERS_DUMB_DESTROY) >= qEnd) then begin
         NAnswer(495,1,NodeConcuilB207)
         end
         else begin
         NAnswer(496,1,NodeConcuilB207)
         end
      end
      NAnswer(481,1,Node999)
   end
end

procedure NodeConcuilB201 begin
   Reply(484)
   NAnswer(485,1,NodeConcuilB202)
end

procedure NodeConcuilB202 begin
   Reply(486)
   NAnswer(487,1,NodeConcuilB203)
end

procedure NodeConcuilB203 begin
   add_timer_event(self_obj,2,3);
   set_REP(-10)
   call Node999;
end

procedure NodeConcuilB204 begin
   Reply(491)
   NAnswer(492,1,NodeConcuilB205)
end

procedure NodeConcuilB205 begin
   Reply(493)
   NAnswer(494,1,NodeConcuilB206)
end

procedure NodeConcuilB206 begin
   add_timer_event(self_obj,2,3);
   set_REP(10)
   call Node999;
end

procedure NodeConcuilB207 begin
   Reply(497)
   NAnswer(498,1,NodeConcuilB208)
end

procedure NodeConcuilB208 begin
   Reply(499)
   //падре ГГ: "Находясь в рабстве я слышал, что Братство Стали намерено захватить власть в регионе и поработить все мелкие поселения. Им нужна провизия. А так как они знают о наших плантациях, то очевидно мы станем одними из первых в списке."
   if (town_known(AREA_PUERTO_STEEL) >= MARK_STATE_VISITED) then begin
   NAnswer(504,1,NodeConcuilB210)
   end
   else if (town_known(AREA_PUERTO_DESTROY) >= MARK_STATE_KNOWN) then begin
   NAnswer(504,1,NodeConcuilB210)
   end
   NAnswer(500,1,NodeConcuilB209)
end

procedure NodeConcuilB209 begin
   Reply(501)
   if (getQuest(GVAR_PUERTO_BOS_DESTROY) >= qEnd) then begin
   zGAnswer(506,1,NodeConcuilB211)
   end
   NAnswer(502,1,Node999)
   BAnswer(503,1,Node999)
end

procedure NodeConcuilB210 begin
   Reply(505)
   if (getQuest(GVAR_PUERTO_BOS_DESTROY) >= qEnd) then begin
   zGAnswer(506,1,NodeConcuilB211)
   end
   NAnswer(502,1,Node999)
   BAnswer(503,1,Node999)
end

procedure NodeConcuilB211 begin
   Reply(507)
   NAnswer(508,1,NodeConcuilB212)
end

procedure NodeConcuilB212 begin
   Reply(509)
   NAnswer(510,1,NodeConcuilB213)
end

procedure NodeConcuilB213 begin
   setQuest(GVAR_VILLA_ALLIANCE_PIP,qStop);
   set_REP(15)
   add_timer_event(self_obj,2,3);
   call Node999;
end
