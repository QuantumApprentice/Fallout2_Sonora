#include "..\headers\define.h"
//---------------------

#define NAME                            SCRIPT_DCCtznA
#define TOWN_REP_VAR                    GVAR_TOWN_REP_CASA_GRANDE // привязка к репутации (-1, если не нужно)

#define NUM_TEAM                        TEAM_CASAGRANDE_PEASAN // группа, к которой принадлежит персонаж (-1 если нужно оставить Team, заданный в редакторе)
#define NUM_AI                          AI_PERSON_TOUGH       // AI персонажа (-1 если нужно оставить AI, заданный в редакторе)
#define KILL_SLAVA                      SLAVA_KILL_GOOD       // слава за убийство этого персонажа (SLAVA_KILL_GOOD, SLAVA_KILL_BAD, SLAVA_KILL_NEITRAL, SLAVA_KILL_CHILD)
#define KILL_TOWN_REP                   REP_KILL_GOOD         // репутация за убийство этого персонажа (REP_KILL_GOOD, REP_KILL_BAD, REP_KILL_NEITRAL, REP_KILL_CRITICAL для враждебности всего поселения, REP_KILL_CHILD)
#define KILL_FRAC_REP                   FRAC_NON              // принадлежность определенной фракции (-1, если не нужно)

#define FLAG_STEAL                      50 // Нижний порог навыка для кражи (для торговцев 200, для животных 300)
#define FLAG_ALCOHOL                    0  // Примет ли алкоголь от ГГ: 0 - принимает, 1 - не принимает
#define FLAG_FOOD                       1  // Примет ли пищу от ГГ: 0 - принимает, 1 - не принимает

//---------------------

procedure start;
procedure map_enter_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure timed_event_p_proc;
procedure replyfloat_proc;
procedure critter_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;
procedure pickup_p_proc;
procedure combat_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;

//---------------------

//#define LVAR_Flag_A                     (6) // отвечает за ...

import variable box_stop_use;

variable replyFloat;

//---------------------



procedure start begin
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
       set_start_home
       if (map_first_run) then begin
       if (random(0,1) == 0) then begin
       item_add(self_obj,PID_BOOTLE_CAPS,random(2,6))
       end
       end
   end
end

procedure look_at_p_proc begin
   lookProcMsg
end

procedure description_p_proc begin
   dsrptProcMsg
end

procedure timed_event_p_proc begin
   // Таймер за номером 1 рарезервирован под перемещение персонажа по локации
   // Таймер за номером 2 рарезервирован под всплывающие реплики
   if (fixed_param == 1) then begin
       if ((game_time_hour > 1000) and (game_time_hour < 2000)) then begin
       timer_move_random_home(2)
       end
       else begin
       timer_move_to_home
       end
   end
   if (fixed_param == 2) then begin
       call replyfloat_proc;
       if (game_time_hour <= 0600) then begin
       timer_float_reply(110,112)
       end
       else begin
       timer_float_reply(replyFloat,replyFloat)
       end
   end
end

procedure replyfloat_proc begin
   replyFloat := random(0,7);

       // Магистралы
       if (replyFloat == 0) then begin
          if (getQuest(GVAR_RAIDER_OIL_DESTROY) >= qEnd) then begin
          replyFloat := random(130,134);
          end
          else if (getQuest(GVAR_CGRANDE_QST_ROADBOSS_KILL) >= qEnd) then begin
          replyFloat := random(135,139);
          end
          else if ((gvar_bit(GVAR_DIALOGS_FLAGS_BIT, bit_28) != 0) or (getQuest(GVAR_RAIDER_QST_GRANGE) >= qEnd)) then begin
          replyFloat := random(125,126);
          end
          else begin
          replyFloat := random(120,124);
          end
       end

       // ТуСан
       if (replyFloat == 1) then begin
          if (global_var(GVAR_TWOSUN_STATUS_ZAX) > 0) then begin
          replyFloat := random(145,149);
          end
          else begin
          replyFloat := random(140,144);
          end
       end

       // Феникс
       if (replyFloat == 2) then begin
          if ((random(0,2) == 0) and (gvar_bit(GVAR_EXPRESS_FLAGS_BIT, bit_2) != 0)) then begin
          replyFloat := 159;
          end
          else begin
          replyFloat := random(150,154);
          end
       end

       // Собор
       if (replyFloat == 3) then begin
          if (getQuest(GVAR_PHENIX_CULT_DESTROY) >= qEnd) then begin
          replyFloat := random(165,169);
          end
          else begin
          replyFloat := random(160,164);
          end
       end

       // Местные события
       if (replyFloat == 4) then begin
          replyFloat := random(170,178);
       end

       // Репутация
       if (replyFloat == 5) then begin
          if town_rep_is_liked then begin
             if dude_is_female then begin
             replyFloat := random(1187,1189);
             end
             else begin
             replyFloat := random(187,189);
             end
          end
          else if town_rep_is_hated then begin
             if dude_is_female then begin
             replyFloat := random(1180,1182);
             end
             else begin
             replyFloat := random(180,182);
             end
          end
          else begin
             if dude_is_female then begin
             replyFloat := random(1183,1186);
             end
             else begin
             replyFloat := random(183,186);
             end
          end
       end

       // Местная власть
       if (replyFloat == 6) then begin
          if (global_var(GVAR_CGRANDE_STATUS_MER) < 0) then begin
          replyFloat := random(190,192);
          end
          else if (global_var(GVAR_CGRANDE_STATUS_SHEFF) < 0) then begin
          replyFloat := random(197,199);
          end
          else begin
          replyFloat := random(193,196);
          end
       end

       // Местные события
       if (replyFloat == 7) then begin
          if ((random(0,1) == 0) and (global_var(GVAR_GRAVES_CASAGRANDE) > 0)) then begin
          replyFloat := 158;
          end
          else if (global_var(GVAR_CGRANDE_STATUS_BATHHOUSE) >= 2) then begin
          replyFloat := 156;
          end
          else begin
          replyFloat := 157;
          end
       end

end

procedure critter_p_proc begin
   set_using_stop // привязка хозяина и объекта (удалить, если не нужно)
   set_critter_options(1,0) //условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ
end

procedure use_skill_on_p_proc begin
   skillUseSteal
   skillUseFAid
end

procedure pickup_p_proc begin
   pickupProcCritter
end

procedure use_obj_on_p_proc begin
   UsedItemToCrit_Proc
end

procedure combat_p_proc begin
   effectAttak(0,0,0,1,0) //отравление min, облучение min, травма ног %, травма рук %, травма глаз %
end

procedure damage_p_proc begin
   set_damage_dude
end

procedure destroy_p_proc begin
   dec_global_var(GVAR_CGRANDE_STATUS_CTZN);
   kill_critter_opt
end

procedure talk_p_proc begin
   if town_rep_is_liked then begin
   z_floater(self_obj,random(220,229),COLOR_MSG_NORMAL);
   end
   else if town_rep_is_hated then begin
   floater(self_obj,random(210,219),COLOR_MSG_RED);
   end
   else begin
   floater(self_obj,random(200,209),COLOR_MSG_NORMAL);
   end
end
