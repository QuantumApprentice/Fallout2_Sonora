#include "..\headers\define.h"
//---------------------

#define NAME                            SCRIPT_GCBrmPsA
#define TOWN_REP_VAR                    GVAR_TOWN_REP_SAN_BRAHMIN // привязка к репутации (-1, если не нужно)

#define NUM_TEAM                        TEAM_SANBRAHMIN_PEASAN // группа, к которой принадлежит персонаж (-1 если нужно оставить Team, заданный в редакторе)
#define NUM_AI                          AI_CULT_BRAHMIN       // AI персонажа (-1 если нужно оставить AI, заданный в редакторе)
#define KILL_SLAVA                      SLAVA_KILL_GOOD       // слава за убийство этого персонажа (SLAVA_KILL_GOOD, SLAVA_KILL_BAD, SLAVA_KILL_NEITRAL, SLAVA_KILL_CHILD)
#define KILL_TOWN_REP                   REP_KILL_NEITRAL      // репутация за убийство этого персонажа (REP_KILL_GOOD, REP_KILL_NEITRAL, REP_KILL_BAD, REP_KILL_CHILD, REP_KILL_CRITICAL)
#define KILL_FRAC_REP                   FRAC_NON              // принадлежность определенной фракции (-1, если не нужно)

#define FLAG_STEAL                      40 // Нижний порог навыка для кражи (для торговцев 200, для животных 300)
#define FLAG_ALCOHOL                    0  // Примет ли алкоголь от ГГ: 0 - принимает, 1 - не принимает
#define FLAG_FOOD                       1  // Примет ли пищу от ГГ: 0 - принимает, 1 - не принимает

#define TILE_EXIT                       20861  // Тайл ухода с карты

//---------------------

procedure start;
procedure map_enter_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;
procedure pickup_p_proc;
procedure combat_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;
procedure NodeBarterItem;

procedure Node998;
procedure Node999;
procedure NodeStartDialog;
procedure NodeFstDialog;
procedure NodeSndDialog;
procedure NodeAnswer100;
procedure NodeAnswer101;
procedure NodeAnswer102;
procedure NodeAnswer200;
procedure NodeAnswer201;
procedure NodeAnswer202;
procedure NodeAnswer203;
procedure NodeAnswer204;
procedure NodeAnswer205;

//---------------------

//#define LVAR_Flag_A                     (6) // отвечает за ...

import variable obj_critter_a; // указатель скотовода

//---------------------



procedure start begin
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
       set_start_home
       set_obj_visibility(self_obj,0);
       if ((getQuest(GVAR_GARAGE_QST_BRAHMINATTAK) != qStart) or (global_var(GVAR_GARAGE_STATUS_PASTUH) > 0)) then begin
       set_obj_visibility(self_obj,1);
       rm_timer_event(self_obj);
       end
   end
end

procedure look_at_p_proc begin
   lookProcMsg
end

procedure description_p_proc begin
   dsrptProcMsg
end

procedure timed_event_p_proc begin
   // Таймер за номером 1 зарезервирован под перемещение персонажа по локации
   // Таймер за номером 2 зарезервирован под всплывающие реплики
   if (fixed_param == 1) then begin
       if (global_var(GVAR_GARAGE_STATUS_PASTUH) <= 0) then begin
       //timer_move_to_home
       timer_move_random_home(5)
       end
   end
   if (fixed_param == 2) then begin
       //timer_float_reply(120,121)
   end
end

procedure critter_p_proc begin
   if (self_visible == true) then begin
      obj_critter_a := self_obj;
      if (global_var(GVAR_GARAGE_STATUS_PASTUH) > 0) then begin
         if (self_tile == TILE_EXIT) then begin
         set_obj_visibility(self_obj, 1);
         obj_critter_a := 0;
         destroy_object(self_obj);
         end
         else begin
         move_to_tile(0,TILE_EXIT,0)
         end
      end

      if (global_var(GVAR_GARAGE_STATUS_PASTUH) < 0) then begin
      set_critter_options(2,0) //условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ
      end
      else begin
      set_critter_options(0,0) //условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ
      end
   end
end

procedure use_skill_on_p_proc begin
   skillUseSteal
   skillUseFAid
end

procedure pickup_p_proc begin
   pickupProcCritter
end

procedure use_obj_on_p_proc begin
   UsedItemToCrit_Proc
end

procedure combat_p_proc begin
   effectAttak(0,0,0,0,0) //отравление min, облучение min, травма ног %, травма рук %, травма глаз %
end

procedure damage_p_proc begin
   set_damage_dude
end

procedure destroy_p_proc begin
   obj_critter_a := 0;
   set_global_var(GVAR_GARAGE_STATUS_PASTUH,-1);
   kill_critter_opt
end

procedure talk_p_proc begin
   if (global_var(GVAR_GARAGE_STATUS_PASTUH) == 0) then begin
      dialog_proc(0,0,NodeStartDialog,-1,-1)
      call NodeBarterItem;
   end
   else if (global_var(GVAR_GARAGE_STATUS_PASTUH) < 0) then begin
      floater(self_obj,112,COLOR_MSG_RED);
   end
   else begin
      if (global_var(GVAR_GARAGE_STATUS_PASTUH) == 1) then begin
      floater(self_obj,111,COLOR_MSG_NORMAL);
      end
      else begin
      floater(self_obj,112,COLOR_MSG_RED);
      end
   end
end

procedure NodeBarterItem begin
   if (item_amnt(dude_obj,PID_Q_BRAHMIN_BARTER) > 0) then begin
      set_global_var(GVAR_GARAGE_STATUS_PASTUH,1);
      set_global_var(GVAR_GARAGE_STATUS_BRAHMIN,1);
      display_mstr(245);
      item_remove(dude_obj, PID_Q_BRAHMIN_BARTER, 1)
   end
   if (item_amnt(self_obj,PID_Q_BRAHMIN_BARTER) > 0) then begin
      item_remove(self_obj, PID_Q_BRAHMIN_BARTER, 1)
   end
   if (tile_contains_obj_pid(dude_tile, dude_elevation, PID_Q_BRAHMIN_BARTER) == TRUE) then begin
      destroy_object(tile_contains_pid_obj(dude_tile, dude_elevation, PID_Q_BRAHMIN_BARTER));
   end
end

procedure Node998 begin
   set_global_var(GVAR_GARAGE_STATUS_PASTUH,-1);
   set_enemy_dude;
end

procedure Node999 begin
end

procedure NodeStartDialog begin
   if (bitDialog(bit_1) == 0) then begin
      call NodeFstDialog;
   end
   else begin
      call NodeSndDialog;
   end
end

//--- ПЕРВЫЙ ДИАЛОГ

procedure NodeFstDialog begin
   bitDialogExp(bit_1,0)
   Reply(200)
   NAnswer(201,1,NodeSndDialog)
end

//--- ПОВТОРНЫЙ ДИАЛОГ

procedure NodeSndDialog begin
   Reply(202)
   if ((global_var(GVAR_GARAGE_STATUS_PASTUH) == 0) and (global_var(GVAR_GARAGE_STATUS_BRAHMIN) == 0)) then begin
   NAnswer(220,1,NodeAnswer100)
   NAnswer(230,1,NodeAnswer200)
   BAnswer(204,1,Node998)
   end
   NAnswer(203,1,Node999)
end

procedure NodeAnswer100 begin
   Reply(221)
   if (town_known(AREA_SAN_BRAHMIN) >= MARK_STATE_VISITED) then begin
   NAnswer(222,1,NodeAnswer101)
   end
   else begin
   NAnswer(225,1,NodeAnswer102)
   end
   NAnswer(230,1,NodeAnswer200)
   NAnswer(203,1,Node999)
end

procedure NodeAnswer101 begin
   Reply(223)
   NAnswer(230,1,NodeAnswer200)
   NAnswer(224,1,Node999)
end

procedure NodeAnswer102 begin
   Reply(226)
   NAnswer(230,1,NodeAnswer200)
   NAnswer(227,1,Node999)
end

procedure NodeAnswer200 begin
   Reply(231)
   NAnswerSkill(238,3,NodeAnswer202,SKILL_CONVERSANT,50,232,NodeAnswer201)
   NAnswerRep(236,3,NodeAnswer203,15,236,NodeAnswer201)
   if (item_amnt(dude_obj,PID_BOOTLE_CAPS) >= 100) then begin
   NAnswer(240,1,NodeAnswer204)
   end
   BAnswer(204,1,Node998)
   NAnswer(203,1,Node999)
end

procedure NodeAnswer201 begin
   Reply(233)
   BAnswer(204,1,Node998)
   NAnswer(203,1,Node999)
end

procedure NodeAnswer202 begin
   set_global_var(GVAR_GARAGE_STATUS_PASTUH,2);
   set_global_var(GVAR_GARAGE_STATUS_BRAHMIN,1);
   set_SLAVA(-35)
   set_REP(-5)
   Reply(234)
   NAnswer(235,1,Node999)
end

procedure NodeAnswer203 begin
   set_global_var(GVAR_GARAGE_STATUS_PASTUH,1);
   set_global_var(GVAR_GARAGE_STATUS_BRAHMIN,1);
   set_SLAVA(-10)
   Reply(237)
   NAnswer(235,1,Node999)
end

procedure NodeAnswer204 begin
   Reply(241)
   NAnswer(242,1,NodeAnswer205)
   BAnswer(244,1,Node998)
   NAnswer(243,1,Node999)
end

procedure NodeAnswer205 begin
   if (item_amnt(dude_obj,PID_Q_BRAHMIN_BARTER) > 0) then begin
   end
   else if (item_amnt(self_obj,PID_Q_BRAHMIN_BARTER) > 0) then begin
   end
   else if (tile_contains_obj_pid(dude_tile, dude_elevation, PID_Q_BRAHMIN_BARTER) == TRUE) then begin
   end
   else begin
   item_add(self_obj,PID_Q_BRAHMIN_BARTER,1)
   end

   goBarter(0);
   //call NodeStartDialog;

   Reply(246)
   NAnswer(247,1,Node999)
end
