#include "..\headers\define.h"
//---------------------

#define NAME                            SCRIPT_NCPinki
#define TOWN_REP_VAR                    GVAR_TOWN_REP_CASA_NUEVA // привязка к репутации (-1, если не нужно)

#define NUM_TEAM                        TEAM_RATS             // группа, к которой принадлежит персонаж (-1 если нужно оставить Team, заданный в редакторе)
#define NUM_AI                          AI_RAT_GOD            // AI персонажа (-1 если нужно оставить AI, заданный в редакторе)
#define KILL_SLAVA                      SLAVA_KILL_GOOD       // слава за убийство этого персонажа (SLAVA_KILL_GOOD, SLAVA_KILL_BAD, SLAVA_KILL_NEITRAL, SLAVA_KILL_CHILD)
#define KILL_TOWN_REP                   REP_KILL_NEITRAL      // репутация за убийство этого персонажа (REP_KILL_GOOD, REP_KILL_NEITRAL, REP_KILL_BAD, REP_KILL_CHILD, REP_KILL_CRITICAL)
#define KILL_FRAC_REP                   FRAC_NON              // принадлежность определенной фракции (-1, если не нужно)

#define FLAG_STEAL                      25 // Нижний порог навыка для кражи (для торговцев 200, для животных 300)
#define FLAG_ALCOHOL                    1  // Примет ли алкоголь от ГГ: 0 - принимает, 1 - не принимает
#define FLAG_FOOD                       1  // Примет ли пищу от ГГ: 0 - принимает, 1 - не принимает

//---------------------

procedure start;
procedure map_enter_p_proc;
procedure map_update_p_proc;
procedure uninjure_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;
procedure pickup_p_proc;
procedure combat_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;

procedure Node998;
procedure Node999;
procedure NodeStartDialog;
procedure NodeFstDialog;
procedure NodeSndDialog;
procedure NodeAnswer100;
procedure NodeAnswer101;

//---------------------

//#define LVAR_Flag_A                     (6) // отвечает за ...

//---------------------



procedure start begin
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
       set_start_home
       if (global_var(GVAR_NUEVA_STATUS_PINKI) == 0) then begin
       set_injure_start(0,0,1,0,0) // травмировать (отметить нужное цифрой 1): a - левая рука, b - правая рука, c - левая нога, d - правая нога, e - слепота
       end
       set_obj_visibility(self_obj, 0);

       if (cur_map_index == MAP_CASA_NUEVA_PLANT) then begin
       critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_PLAYER);
       end

       if (global_var(GVAR_NUEVA_STATUS_PINKI)<0) then begin
       rm_timer_event(self_obj);
       set_obj_visibility(self_obj, 1);
       end
       else if ((global_var(GVAR_NUEVA_STATUS_PINKI)<=0) and (cur_map_index == MAP_CASA_NUEVA_PLANT)) then begin
       rm_timer_event(self_obj);
       set_obj_visibility(self_obj, 1);
       end
   end
end

procedure map_update_p_proc begin
   if (global_var(GVAR_NUEVA_STATUS_PINKI) == 0) then begin
   set_injure_update(50,107)
   end
end

procedure uninjure_proc begin
   set_global_var(GVAR_NUEVA_STATUS_PINKI,1);
   //не удалять
end

procedure look_at_p_proc begin
   lookProcMsg
end

procedure description_p_proc begin
   dsrptProcMsg
end

procedure timed_event_p_proc begin
   // Таймер за номером 1 зарезервирован под перемещение персонажа по локации
   // Таймер за номером 2 зарезервирован под всплывающие реплики
   if (fixed_param == 1) then begin
       if (global_var(GVAR_NUEVA_STATUS_PINKI) < 2) then begin
       timer_move_random_home(1)
       end
   end
   if (fixed_param == 2) then begin
       //timer_float_reply(120,121)
   end
   if (fixed_param == 3) then begin
       KillScorpTail(106) // фраза над жертвой (-1 если не нужна)
   end
end

procedure critter_p_proc begin
   if (global_var(GVAR_NUEVA_STATUS_PINKI)>1) then begin
   partyRunToDude
   end
   set_critter_options(1,0) //условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ
end

procedure use_skill_on_p_proc begin
   skillUseSteal
   skillUseFAid
   skillUseDoctor(65)
end

procedure pickup_p_proc begin
   pickupProcCritter
end

procedure use_obj_on_p_proc begin
   UsedItemToCrit_Proc
   UsedToCrit_Stimpak_Doctor
   if (getQuest(GVAR_NUEVA_QST_PINKI) < qEnd) then begin
   if (global_var(GVAR_NUEVA_STATUS_PINKI) == 0) then begin
   UsedToCrit_Scorp(105,3) // фраза над жертвой (-1 если не нужна), номер таймера
   //Пинки чувствует себя совсем нехорошо
   end
   end
end

procedure combat_p_proc begin
   effectAttak(0,0,1,1,1) //отравление min, облучение min, травма ног %, травма рук %, травма глаз %
end

procedure damage_p_proc begin
   set_damage_dude
end

procedure destroy_p_proc begin
   if (source_obj == dude_obj) then begin
   set_global_var(GVAR_NUEVA_STATUS_PINKI,-99);
   end
   else if (cur_map_index == MAP_CASA_NUEVA_PLANT) then begin
   set_global_var(GVAR_NUEVA_STATUS_PINKI,-2);
   end
   else begin
   set_global_var(GVAR_NUEVA_STATUS_PINKI,-1);
   end
   kill_critter_opt
end

procedure talk_p_proc begin
   ScorpPoisonFlt
   else begin
   setReaction(0) // Отношение к ГГ (1 - персонаж положительный, -1 отрицательный, 0 нейтральный)
   dialog_proc(0,0,NodeStartDialog,-1,-1)
   end
end

procedure Node998 begin
   set_enemy_dude;
end

procedure Node999 begin
end

procedure NodeStartDialog begin
   if (bitDialog(bit_1) == 0) then begin
   call NodeFstDialog;
   end
   else begin
   call NodeSndDialog;
   end
end

//--- ПЕРВЫЙ ДИАЛОГ

procedure NodeFstDialog begin
   bitDialogExp(bit_1,0)
   call NodeSndDialog;
   /*
   Reply(200)
   NAnswer(201,1,NodeSndDialog)
   BAnswer(202,1,NodeSndDialog)
   */
end

//--- ВТОРОЙ ДИАЛОГ

procedure NodeSndDialog begin
   Reply(200)

   if (getQuest(GVAR_NUEVA_QST_PINKI) >= qEnd) then begin
   if (global_var(GVAR_NUEVA_STATUS_PINKI) == 1) then begin
   NAnswer(203,1,NodeAnswer100)
   end
   else if (global_var(GVAR_NUEVA_STATUS_PINKI) > 1) then begin
   NAnswer(206,1,NodeAnswer101)
   end
   end

   NAnswer(201,1,Node999)
   BAnswer(202,1,Node999)
end

procedure NodeAnswer100 begin
   set_global_var(GVAR_NUEVA_STATUS_PINKI,2);
   Reply(204)
   NAnswer(205,1,Node999)
end

procedure NodeAnswer101 begin
   set_global_var(GVAR_NUEVA_STATUS_PINKI,1);
   Reply(204)
   NAnswer(205,1,Node999)
end
