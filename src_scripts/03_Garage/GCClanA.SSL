#include "..\headers\define.h"
//---------------------

#define NAME                            SCRIPT_GCClanA
#define TOWN_REP_VAR                    GVAR_TOWN_REP_GARAGE   // привязка к репутации (-1, если не нужно)

#define NUM_TEAM                        TEAM_GARAGE_CLAN_A    // группа, к которой принадлежит персонаж (-1 если нужно оставить Team, заданный в редакторе)
#define NUM_AI                          AI_PERSON_TOUGH       // AI персонажа (-1 если нужно оставить AI, заданный в редакторе)
#define KILL_SLAVA                      SLAVA_KILL_GOOD       // слава за убийство этого персонажа (SLAVA_KILL_GOOD, SLAVA_KILL_BAD, SLAVA_KILL_NEITRAL, SLAVA_KILL_CHILD)
#define KILL_TOWN_REP                   REP_KILL_GOOD         // репутация за убийство этого персонажа (REP_KILL_GOOD, REP_KILL_NEITRAL, REP_KILL_BAD, REP_KILL_CHILD, REP_KILL_CRITICAL)
#define KILL_FRAC_REP                   FRAC_NON              // принадлежность определенной фракции (-1, если не нужно)

#define FLAG_STEAL                      35 // Нижний порог навыка для кражи (для торговцев 200, для животных 300)
#define FLAG_ALCOHOL                    0  // Примет ли алкоголь от ГГ: 0 - принимает, 1 - не принимает
#define FLAG_FOOD                       0  // Примет ли пищу от ГГ: 0 - принимает, 1 - не принимает

//---------------------

procedure start;
procedure map_enter_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;
procedure pickup_p_proc;
procedure combat_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;

procedure Node998;
procedure Node999;
procedure NodeStartDialog;
procedure NodeSndDialog;
procedure NodeDialog;
procedure NodeDialog2;
procedure NodeDialog3;
procedure NodeDialog4;
procedure NodeDialog5;
procedure NodeDialog6;
procedure NodeDialog7;
procedure NodeDialog7b;
procedure NodeDialog8;
procedure NodeDialog200;
procedure NodeDialog201;
procedure NodeDialog202;


//---------------------

//#define LVAR_Flag_A                     (6) // отвечает за ...

import variable obj_critter_a; // указатель на людей Лукаса

//---------------------



procedure start begin
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
       set_start_home
       if (map_first_run) then begin
       item_add(self_obj,PID_BOOTLE_CAPS,random(20,30))
       end
       set_map_var(MVAR_GARAGE_TRAYVON_ATACK,0);
       set_obj_visibility(self_obj, 0);
       if (getQuest(GVAR_GARAGE_QST_BOSS) <= qZero) then begin
       rm_timer_event(self_obj);
       set_obj_visibility(self_obj, 1);
       end
   end
end

procedure look_at_p_proc begin
   lookProcMsg
end

procedure description_p_proc begin
   dsrptProcMsg
end

procedure timed_event_p_proc begin
   // Таймер за номером 1 зарезервирован под перемещение персонажа по локации
   // Таймер за номером 2 зарезервирован под всплывающие реплики
   if (fixed_param == 1) then begin
       timer_move_to_home
   end
   if (fixed_param == 2) then begin
       if (getQuest(GVAR_GARAGE_QST_BOSS) == qStart) then begin
       if (map_var(MVAR_GARAGE_TRAYVON_ATACK) == 0) then begin
       timer_floater(111,119,COLOR_MSG_RED,40)
       end
       end
   end
end

procedure critter_p_proc begin
   set_critter_options(1,0) //условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ
   if (map_var(MVAR_GARAGE_TRAYVON_ATACK) >= 2) then begin
      if (obj_critter_a > 0) then begin
      attack(obj_critter_a);
      end
   end
end

procedure use_skill_on_p_proc begin
   skillUseSteal
   skillUseFAid
end

procedure pickup_p_proc begin
   pickupProcCritter
end

procedure use_obj_on_p_proc begin
   UsedItemToCrit_Proc
end

procedure combat_p_proc begin
   effectAttak(0,0,0,0,0) //отравление min, облучение min, травма ног %, травма рук %, травма глаз %
end

procedure damage_p_proc begin
   set_damage_dude
end

procedure destroy_p_proc begin
   if (getQuest(GVAR_GARAGE_QST_BOSS) == qStart) then begin
   set_global_var(GVAR_GARAGE_QST_BOSS,qEnd);
   end
   kill_critter_opt
end

procedure talk_p_proc begin
   dialog_proc(0,0,NodeStartDialog,-1,-1)
end

procedure Node998 begin
   set_enemy_dude;
end

procedure Node999 begin
end

procedure NodeStartDialog begin
   if (getQuest(GVAR_GARAGE_QST_BOSS) == qStart) then begin
      if (obj_critter_a>0) then begin
      call NodeDialog;
      end
      else begin
      call NodeDialog200;
      end
   end
   else begin
      call NodeSndDialog;
   end
end

//--- ОСНОВНОЙ ДИАЛОГ

procedure NodeSndDialog begin
   bitDialogExp(bit_1,0)

   if (getQuest(GVAR_GARAGE_STANOK_DESTROY) >= qEnd) then begin
      Reply(300)
   end
   else if (getQuest(GVAR_GARAGE_QST_LUCS_TRADE) >= qEnd) then begin
      if (global_var(GVAR_GARAGE_STATUS_BOSS) < 0) then begin
      Reply(305)
      end
      else begin
      Reply(302)
      //NAnswer(303,1,Node999) !!!
      end
      NAnswer(304,1,Node999)
   end
   else begin
      Reply(300)
   end

   NAnswer(301,1,Node999)
end

//--- ДИАЛОГ НА ВРЕМЯ СТЫЧКИ С ЛЮДЬМИ ЛУКАСА

procedure NodeDialog begin
   inc_global_var(GVAR_GARAGE_QST_BOSS);
   Reply(200)
   NAnswer(201,1,NodeDialog2)
end

procedure NodeDialog2 begin
   Reply(202)
   NAnswer(203,1,NodeDialog3)
end

procedure NodeDialog3 begin
   Reply(204)
   NAnswer(205,1,NodeDialog4)
end

procedure NodeDialog4 begin
   Reply(206)
   NAnswer(210,1,NodeDialog6)
   NAnswer(207,1,NodeDialog5)
end

procedure NodeDialog5 begin
   set_map_var(MVAR_GARAGE_TRAYVON_ATACK,2);
   Reply(208)
   NAnswer(209,1,Node999)
end

procedure NodeDialog6 begin
   Reply(211)
   NAnswerSkill(213,3,NodeDialog7,SKILL_CONVERSANT,50,213,NodeDialog7b)
   NAnswer(212,1,NodeDialog5)
end

procedure NodeDialog7 begin
   Reply(217)
   NAnswer(219,1,NodeDialog8)
   NAnswer(218,1,NodeDialog5)
end

procedure NodeDialog7b begin
   Reply(214)
   NAnswer(215,1,NodeDialog5)
   NAnswer(216,1,NodeDialog5)
end

procedure NodeDialog8 begin
   set_map_var(MVAR_GARAGE_TRAYVON_ATACK,1);
   set_SLAVA(20)
   Reply(220)
   if (global_var(GVAR_GARAGE_STATUS_BOSS) < 0) then begin
   NAnswer(222,1,Node999)
   end
   else begin
   NAnswer(221,1,Node999)
   end
end

procedure NodeDialog200 begin
   inc_global_var(GVAR_GARAGE_QST_BOSS);
   Reply(230)
   NAnswer(231,1,NodeDialog201)
end

procedure NodeDialog201 begin
   Reply(232)
   NAnswer(203,1,NodeDialog202)
   NAnswer(233,1,Node999)
end

procedure NodeDialog202 begin
   Reply(204)
   NAnswer(233,1,Node999)
end
