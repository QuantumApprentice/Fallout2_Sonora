#include "..\headers\define.h"
//---------------------

#define NAME                            SCRIPT_ZCCrvGrd
#define TOWN_REP_VAR                    -1 // привязка к репутации (-1, если не нужно)

#define NUM_TEAM                        TEAM_CARAVAN          // группа, к которой принадлежит персонаж (-1 если нужно оставить Team, заданный в редакторе)
#define NUM_AI                          AI_CARAVAN_GUARD      // AI персонажа (-1 если нужно оставить AI, заданный в редакторе)
#define KILL_SLAVA                      SLAVA_KILL_GOOD       // слава за убийство этого персонажа (SLAVA_KILL_GOOD, SLAVA_KILL_BAD, SLAVA_KILL_NEITRAL, SLAVA_KILL_CHILD)
#define KILL_TOWN_REP                   REP_KILL_GOOD         // репутация за убийство этого персонажа (REP_KILL_GOOD, REP_KILL_BAD, REP_KILL_NEITRAL, REP_KILL_CRITICAL для враждебности всего поселения, REP_KILL_CHILD)
#define KILL_FRAC_REP                   FRAC_NON              // принадлежность определенной фракции (-1, если не нужно)

#define FLAG_STEAL                      75 // Нижний порог навыка для кражи (для торговцев 200, для животных 300)
#define FLAG_ALCOHOL                    1  // Примет ли алкоголь от ГГ: 0 - принимает, 1 - не принимает
#define FLAG_FOOD                       1  // Примет ли пищу от ГГ: 0 - принимает, 1 - не принимает

//---------------------

procedure start;
procedure map_enter_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;
procedure pickup_p_proc;
procedure combat_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;

//---------------------

//#define LVAR_Flag_A                     (6) // отвечает за ...

import variable obj_critter_a; // указатель на криттера 1

//---------------------



procedure start begin
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
       set_start_home

       if ((cur_map_index == MAP_SB_CARAVAN_A) or (cur_map_index == MAP_SB_CARAVAN_B) or (cur_map_index == MAP_SB_CARAVAN_C)) then begin
          /*
          if (global_var(GVAR_BRAHMIN_QST_CARAVAN_DUDEENEMY) == 0) then begin
             critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_PLAYER);
          end
          */
          if self_is_female then begin
             if (gvar_bit(GVAR_BRAHMIN_QST_CARAVAN_GUARD_DEAD, bit_1) != 0) then begin
             rm_timer_event(self_obj);
             set_obj_visibility(self_obj, 1);
             destroy_object(self_obj);
             end
          end
          else begin
             if ((gvar_bit(GVAR_BRAHMIN_QST_CARAVAN_GUARD_DEAD, bit_2) != 0) and (item_amnt(self_obj,PID_TOOL_FLARE)>0)) then begin
             rm_timer_event(self_obj);
             set_obj_visibility(self_obj, 1);
             destroy_object(self_obj);
             end
             if ((gvar_bit(GVAR_BRAHMIN_QST_CARAVAN_GUARD_DEAD, bit_3) != 0) and (item_amnt(self_obj,PID_TOOL_FLARE)<=0)) then begin
             rm_timer_event(self_obj);
             set_obj_visibility(self_obj, 1);
             destroy_object(self_obj);
             end
          end
       end
   end
end

procedure look_at_p_proc begin
   lookProcMsg
end

procedure description_p_proc begin
   dsrptProcMsg
end

procedure timed_event_p_proc begin
   // Таймер за номером 1 рарезервирован под перемещение персонажа по локации
   // Таймер за номером 2 рарезервирован под всплывающие реплики
   if (fixed_param == 1) then begin
       if ((cur_map_index == MAP_SB_CARAVAN_A) or (cur_map_index == MAP_SB_CARAVAN_B) or (cur_map_index == MAP_SB_CARAVAN_C)) then begin
       timer_move_random
       end
       else begin
       timer_move_to_home
       end
   end
   if (fixed_param == 2) then begin
       //timer_float_reply(110,119)
   end
end

procedure critter_p_proc begin
   if ((cur_map_index == MAP_SB_CARAVAN_A) or (cur_map_index == MAP_SB_CARAVAN_B) or (cur_map_index == MAP_SB_CARAVAN_C)) then begin
      if (random(0,4) == 1) then begin
      obj_critter_a := self_obj;
      end
   end
   if (global_var(GVAR_BRAHMIN_QST_CARAVAN_DUDEENEMY) >0) then begin
      critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,NUM_TEAM);
      set_critter_options(2,0) //условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ
   end
   else begin
      set_critter_options(0,0) //условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ
   end
end

procedure use_skill_on_p_proc begin
   skillUseSteal
   skillUseFAid
end

procedure pickup_p_proc begin
   pickupProcCritter
end

procedure use_obj_on_p_proc begin
   UsedItemToCrit_Proc
end

procedure combat_p_proc begin
   effectAttak(0,0,1,1,1) //отравление min, облучение min, травма ног %, травма рук %, травма глаз %
end

procedure damage_p_proc begin
   set_damage_dude
   if ((cur_map_index == MAP_SB_CARAVAN_A) or (cur_map_index == MAP_SB_CARAVAN_B) or (cur_map_index == MAP_SB_CARAVAN_C)) then begin
       if (source_obj == dude_obj) then begin
       off_enemy_dude;
       end
   end
end

procedure destroy_p_proc begin
   if (obj_critter_a == self_obj) then begin
   obj_critter_a := 0;
   end
   brahminCaravanDudeEnemy
   if ((cur_map_index == MAP_SB_CARAVAN_A) or (cur_map_index == MAP_SB_CARAVAN_B) or (cur_map_index == MAP_SB_CARAVAN_C)) then begin
      if self_is_female then begin
         set_gvar_bit_on(GVAR_BRAHMIN_QST_CARAVAN_GUARD_DEAD, bit_1);
      end
      else begin
         if (gvar_bit(GVAR_BRAHMIN_QST_CARAVAN_GUARD_DEAD, bit_2) == 0) then begin
         set_gvar_bit_on(GVAR_BRAHMIN_QST_CARAVAN_GUARD_DEAD, bit_2);
         end
         else if (gvar_bit(GVAR_BRAHMIN_QST_CARAVAN_GUARD_DEAD, bit_3) == 0) then begin
         set_gvar_bit_on(GVAR_BRAHMIN_QST_CARAVAN_GUARD_DEAD, bit_3);
         end
      end
   end
   kill_critter_opt
end

procedure talk_p_proc begin
   floater(self_obj,random(110,115),COLOR_MSG_NORMAL);
end
