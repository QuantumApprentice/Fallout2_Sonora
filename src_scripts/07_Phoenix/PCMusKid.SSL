#include "..\headers\define.h"
//---------------------

#define NAME                            SCRIPT_PCMusKid
#define TOWN_REP_VAR                    GVAR_TOWN_REP_PHOENIX // привязка к репутации (-1, если не нужно)

#define NUM_TEAM                        TEAM_PHOENIX_CITIZEN  // группа, к которой принадлежит персонаж (-1 если нужно оставить Team, заданный в редакторе)
#define NUM_AI                          AI_CHILD              // AI персонажа (-1 если нужно оставить AI, заданный в редакторе)
#define KILL_SLAVA                      SLAVA_KILL_CHILD      // слава за убийство этого персонажа (SLAVA_KILL_GOOD, SLAVA_KILL_BAD, SLAVA_KILL_NEITRAL, SLAVA_KILL_CHILD)
#define KILL_TOWN_REP                   REP_KILL_CHILD        // репутация за убийство этого персонажа (-100 для враждебности всего поселения)
#define KILL_FRAC_REP                   FRAC_NON              // принадлежность определенной фракции (-1, если не нужно)

#define FLAG_STEAL                      30 // Нижний порог навыка для кражи (для торговцев 200, для животных 300)
#define FLAG_ALCOHOL                    1  // Примет ли алкоголь от ГГ: 0 - принимает, 1 - не принимает
#define FLAG_FOOD                       0  // Примет ли пищу от ГГ: 0 - принимает, 1 - не принимает

//---------------------

procedure start;
procedure map_enter_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;
procedure pickup_p_proc;
procedure combat_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;

procedure Node998;
procedure Node999;
procedure NodeStartDialog;
procedure NodeFstDialog;
procedure NodeFstDialog2;
procedure NodeFstDialog3;
procedure NodeFstDialog4;
procedure NodeFstDialog5;
procedure NodeFstDialog6;
procedure NodeFstDialog7;
procedure NodeFstDialog100;
procedure NodeFstDialog101;
procedure NodeFstDialog102;
procedure NodeFstDialog103;
procedure NodeFstDialog104;
procedure NodeFstDialog105;
procedure NodeFstDialog106;
procedure NodeFstDialog200;
procedure NodeFstDialog201;
procedure NodeFstDialog202;
procedure NodeSndDialog;

//---------------------

#define LVAR_Flag_Status                   (6) // статус: -1 - ГГ отобрал подаяния, 0 - ГГ ничего не предпринял, >=1 - ГГ дал подаяние

variable amntCaps;

//---------------------



procedure start begin
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
       set_start_home
       if (local_var(LVAR_Flag_Status) != 0) then begin
       rm_timer_event(self_obj);
       set_obj_visibility(self_obj, 1);
       destroy_object(self_obj);
       end
   end
end

procedure look_at_p_proc begin
   lookProcMsg
end

procedure description_p_proc begin
   dsrptProcMsg
end

procedure timed_event_p_proc begin
   // Таймер за номером 1 рарезервирован под перемещение персонажа по локации
   // Таймер за номером 2 рарезервирован под всплывающие реплики
   if (fixed_param == 1) then begin
       if (game_time_hour < 0600) then begin
       timer_move_to_tile(0,27713,3)
       end
       else begin
       timer_move_to_home
       end
   end
   if (fixed_param == 2) then begin
       //timer_float_reply(110,111)
   end
end

procedure critter_p_proc begin
   set_critter_options(0,0) //условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ
   if (local_var(LVAR_Flag_Status) < 0) then begin
   Flee_From_Dude
   end
end

procedure use_skill_on_p_proc begin
   skillUseSteal
   skillUseFAid
end

procedure pickup_p_proc begin
   pickupProcCritter
end

procedure use_obj_on_p_proc begin
   UsedItemToCrit_Proc
end

procedure combat_p_proc begin
   effectAttak(0,0,0,0,0) //отравление min, облучение min, травма ног %, травма рук %, травма глаз %
end

procedure damage_p_proc begin
   set_damage_dude
end

procedure destroy_p_proc begin
   kill_critter_opt
end

procedure talk_p_proc begin
   setReaction(1) // Отношение к ГГ(1 - персонаж положительный, -1 отрицательный, 0 нейтральный)
   if (local_var(LVAR_Flag_Status) > 0) then begin
   z_floater(self_obj,102,COLOR_MSG_GREEN);
   end
   else if (local_var(LVAR_Flag_Status) < 0) then begin
   floater(self_obj,103,COLOR_MSG_RED);
   end
   else if (game_time_hour < 0600) then begin
   floater(self_obj,104,COLOR_MSG_LIGHT_GREY);
   end
   else begin
   dialog_proc(0,0,NodeStartDialog,-1,-1)
   end
end

procedure Node998 begin
   set_enemy_dude;
end

procedure Node999 begin
end

procedure NodeStartDialog begin
   amntCaps := 0;
   if (bitDialog(bit_1) == 0) then begin
   call NodeFstDialog;
   end
   else begin
   call NodeSndDialog;
   end
end

//--- ПЕРВЫЙ ДИАЛОГ

procedure NodeFstDialog begin
   Reply(200)
   NAnswer(201,1,NodeFstDialog2)
   NAnswer(202,1,Node999)
end

procedure NodeFstDialog2 begin
   Reply(203)
   NAnswer(204,1,NodeFstDialog3)
end

procedure NodeFstDialog3 begin
   Reply(205)
   NAnswer(206,1,NodeFstDialog4)
end

procedure NodeFstDialog4 begin
   Reply(207)
   BAnswer(216,1,NodeFstDialog6)
   GAnswer(217,1,NodeFstDialog7)
   NAnswer(218,1,NodeFstDialog5)
end

procedure NodeFstDialog5 begin
   bitDialogExp(bit_1,0)
   Reply(209)

   if (item_amnt(dude_obj,PID_BOOTLE_CAPS) >= 1) then begin
   GAnswer(211,1,NodeFstDialog101)
   end
   if (item_amnt(dude_obj,PID_BOOTLE_CAPS) >= 2) then begin
   GAnswer(212,1,NodeFstDialog102)
   end
   if (item_amnt(dude_obj,PID_BOOTLE_CAPS) >= 3) then begin
   GAnswer(213,1,NodeFstDialog103)
   end
   if (item_amnt(dude_obj,PID_BOOTLE_CAPS) >= 4) then begin
   zGAnswer(214,1,NodeFstDialog104)
   end
   if (item_amnt(dude_obj,PID_BOOTLE_CAPS) >= 5) then begin
   GAnswer(215,1,NodeFstDialog105)
   end

   BAnswer(230,1,NodeFstDialog200)
   NAnswer(210,1,Node999)
end

procedure NodeFstDialog6 begin
   setBonusReaction(-20)
   call NodeFstDialog5;
end

procedure NodeFstDialog7 begin
   setBonusReaction(20)
   call NodeFstDialog5;
end

procedure NodeFstDialog100 begin
   if (amntCaps > 0) then begin
   set_local_var(LVAR_Flag_Status,amntCaps);
   set_map_var(MVAR_PNX_MARACAS,1);
   item_add(self_obj,PID_BOOTLE_CAPS,amntCaps)
   item_remove(dude_obj,PID_BOOTLE_CAPS,amntCaps)
   setBonusReaction(10*amntCaps)
   set_SLAVA(2*amntCaps)
   end
   amntCaps := 0;

   zReply(220)
   BAnswer(221,1,NodeFstDialog106)
   NAnswer(222,1,Node999)
end

procedure NodeFstDialog101 begin
   amntCaps := 1;
   call NodeFstDialog100;
end

procedure NodeFstDialog102 begin
   amntCaps := 2;
   call NodeFstDialog100;
end

procedure NodeFstDialog103 begin
   amntCaps := 3;
   call NodeFstDialog100;
end

procedure NodeFstDialog104 begin
   amntCaps := 4;
   call NodeFstDialog100;
end

procedure NodeFstDialog105 begin
   amntCaps := 5;
   set_REP(1)
   call NodeFstDialog100;
end

procedure NodeFstDialog106 begin
   setBonusReaction(-20)
   call Node999;
end

procedure NodeFstDialog200 begin
   setBonusReaction(-10)
   Reply(231)
   NAnswerStat(233,1,NodeFstDialog202,STAT_ag,5,233,NodeFstDialog201)
   NAnswer(232,1,Node999)
end

procedure NodeFstDialog201 begin
   set_local_var(LVAR_Flag_Status,-1);
   set_map_var(MVAR_PNX_MARACAS,-1);
   setBonusReaction(-100)
   set_SLAVA(-10)
   set_REP(-1)

   Reply(234)
   BAnswer(235,1,Node999)
   NAnswer(236,1,Node999)
end

procedure NodeFstDialog202 begin
   set_local_var(LVAR_Flag_Status,-2);
   set_map_var(MVAR_PNX_MARACAS,-1);
   setBonusReaction(-100)
   set_SLAVA(-10)
   set_REP(-1)

   item_add(dude_obj,PID_BOOTLE_CAPS,10)
   item_remove(self_obj,PID_BOOTLE_CAPS,10)

   Reply(237)
   BAnswer(238,1,Node999)
   NAnswer(239,1,Node999)
end

//--- ПОВТОРНЫЙ ДИАЛОГ

procedure NodeSndDialog begin
   if (local_var(LVAR_Flag_Status) == 0) then begin
   call NodeFstDialog5;
   end
   else begin
   Reply(200)
   NAnswer(202,1,Node999)
   end
end
