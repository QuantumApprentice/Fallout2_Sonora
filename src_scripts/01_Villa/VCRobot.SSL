#include "..\headers\define.h"
//---------------------

#define NAME                            SCRIPT_VCRobot
#define TOWN_REP_VAR                    GVAR_TOWN_REP_VILLA   // привязка к репутации (-1, если не нужно)

#define NUM_TEAM                        TEAM_CARAVAN          // группа, к которой принадлежит персонаж (-1 если нужно оставить Team, заданный в редакторе)
#define NUM_AI                          AI_ROBOT_SECURITY     // AI персонажа (-1 если нужно оставить AI, заданный в редакторе)
#define KILL_SLAVA                      SLAVA_KILL_NEITRAL    // слава за убийство этого персонажа (SLAVA_KILL_GOOD, SLAVA_KILL_BAD, SLAVA_KILL_NEITRAL, SLAVA_KILL_CHILD)
#define KILL_TOWN_REP                   REP_KILL_NEITRAL      // репутация за убийство этого персонажа (REP_KILL_GOOD, REP_KILL_NEITRAL, REP_KILL_BAD, REP_KILL_CHILD, REP_KILL_CRITICAL)
#define KILL_FRAC_REP                   FRAC_NON              // принадлежность определенной фракции (-1, если не нужно)

#define FLAG_STEAL                      0  // Нижний порог навыка для кражи (для торговцев 200, для животных 300)
#define FLAG_ALCOHOL                    1  // Примет ли алкоголь от ГГ: 0 - принимает, 1 - отказывается
#define FLAG_FOOD                       1  // Примет ли пищу от ГГ: 0 - принимает, 1 - отказывается

//---------------------

procedure start;
procedure map_enter_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;
procedure pickup_p_proc;
procedure push_p_proc;
procedure combat_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;

procedure Node998;
procedure Node999;
procedure NodeStartDialog;
procedure NodeFstDialog;
procedure NodeFstDialog2;
procedure NodeFstDialog3;
procedure NodeSndDialog;
procedure NodePartyAdd;
procedure NodePartyNon;
procedure NodeOpt100;
procedure NodeOpt101;
procedure NodeAnswer;
procedure NodeAnswer100;
procedure NodeAnswer101;
procedure NodeAnswer102;
procedure NodeAnswer200;
procedure NodeAnswer201;
procedure NodeAnswer202;

//---------------------

#define LVAR_Flag_Sarcasm                   (6) // при 0 говорит как личность Феликса, при !=0 говорит как Мистер Помощник

//---------------------



procedure start begin
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
       set_start_home
   end
end

procedure look_at_p_proc begin
   lookProcMsg
end

procedure description_p_proc begin
   dsrptProcMsg
end

procedure timed_event_p_proc begin
   // Таймер за номером 1 зарезервирован под перемещение персонажа по локации
   // Таймер за номером 2 зарезервирован под всплывающие реплики
   if (fixed_param == 1) then begin
       timer_move_to_home
   end
   if (fixed_param == 2) then begin
       //timer_float_reply(120,121)
   end
end

procedure critter_p_proc begin
   set_critter_options(0,0) //условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ
   if (global_var(GVAR_VILLA_STATUS_ROBOT) > 0) then begin
   partyRunToDude // персонаж бегает вслед за ГГ
   end
end

procedure use_skill_on_p_proc begin
   skillUseSteal
   skillUseFAid
end

procedure pickup_p_proc begin
   pickupProcCritter
end

procedure push_p_proc begin
   if (global_var(GVAR_VILLA_STATUS_ROBOT) == 0) then begin
   script_overrides;
   end
end

procedure use_obj_on_p_proc begin
   UsedItemToCrit_Proc
end

procedure combat_p_proc begin
   effectAttak(0,0,2,2,2) //отравление min, облучение min, травма ног %, травма рук %, травма глаз %
end

procedure damage_p_proc begin
   set_damage_dude
end

procedure destroy_p_proc begin
   set_global_var(GVAR_VILLA_STATUS_ROBOT,-1);
   kill_critter_opt
end

procedure talk_p_proc begin
   setReaction(0) // Отношение к ГГ (1 - персонаж положительный, -1 отрицательный, 0 нейтральный)
   dialog_proc(0,0,NodeStartDialog,-1,-1)
end

procedure Node998 begin
end

procedure Node999 begin
   bitDialogExp(bit_1,0)
end

procedure NodeStartDialog begin
   if (bitDialog(bit_1) == 0) then begin
   call NodeFstDialog;
   end
   else begin
   call NodeSndDialog;
   end
end

//--- ПЕРВЫЙ ДИАЛОГ

procedure NodeFstDialog begin
   Reply(200)
   NAnswer(mstr(201)+dude_name+mstr(202),1,NodeFstDialog2)
end

procedure NodeFstDialog2 begin
   Reply(203)
   NAnswer(204,1,NodeFstDialog3)
end

procedure NodeFstDialog3 begin
   Reply(205)
   NAnswer(206,1,NodeAnswer)
   NAnswer(207,1,Node999)
end

//--- ПОВТОРНЫЙ ДИАЛОГ

procedure NodeSndDialog begin
   if (local_var(LVAR_Flag_Sarcasm) != 0) then begin
   Reply(217)
   end
   else if (cur_map_index == MAP_VAULT_27_INTERIER) then begin
   Reply(218)
   end
   else if (cur_map_index == MAP_PHOENIX_ANAL) then begin
   Reply(219)
   end
   else if ((cur_map_index == MAP_VILLA_CRYPT) and (self_elevation != 0)) then begin
   Reply(211)
   end
   else begin
   Reply(210)
   end

   if (global_var(GVAR_VILLA_STATUS_ROBOT) == 0) then begin
   NAnswer(240,4,NodePartyAdd)
   end
   if (global_var(GVAR_VILLA_STATUS_ROBOT) > 0) then begin
   NAnswer(245,4,NodePartyNon)
   end

   if (local_var(LVAR_Flag_Sarcasm) == 0) then begin
      NAnswer(213,1,NodeAnswer)
      NAnswer(250,1,NodeOpt100)
   end
   else begin
      NAnswer(251,1,NodeOpt100)
   end

   if weapon_invn_self then begin
   NAnswer(256,1,NodeOpt101)
   end

   NAnswer(212,1,Node999)
end

//--- ОПЦИИ НАПАРНИКА

procedure NodePartyAdd begin
   set_global_var(GVAR_VILLA_STATUS_ROBOT,1);
   rm_timer_event(self_obj);
   party_add(self_obj);

   if (local_var(LVAR_Flag_Sarcasm) == 0) then begin
   Reply(241)
   NAnswer(242,1,Node999)
   end
   else begin
   Reply(243)
   NAnswer(244,1,Node999)
   end
end

procedure NodePartyNon begin
   set_global_var(GVAR_VILLA_STATUS_ROBOT,0);
   rm_timer_event(self_obj);
   party_remove(self_obj);
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,NUM_TEAM);

   if (local_var(LVAR_Flag_Sarcasm) == 0) then begin
   Reply(246)
   NAnswer(247,1,Node999)
   end
   else begin
   Reply(248)
   NAnswer(249,1,Node999)
   end
end

procedure NodeOpt100 begin
   if (local_var(LVAR_Flag_Sarcasm) == 0) then begin
   set_local_var(LVAR_Flag_Sarcasm,1);
   Reply(252)
   NAnswer(253,1,NodeSndDialog)
   end
   else begin
   set_local_var(LVAR_Flag_Sarcasm,0);
   Reply(254)
   NAnswer(255,1,NodeSndDialog)
   end
end

procedure NodeOpt101 begin
   inven_unwield(self_obj);
   call NodeSndDialog;
end

//--- РАССПРОСЫ

procedure NodeAnswer begin
   Reply(214)
   NAnswer(220,1,NodeAnswer100)
   NAnswer(227,1,NodeAnswer200)
   NAnswer(230,1,NodeAnswer201)
   NAnswer(233,1,NodeAnswer202)
   NAnswer(215,1,NodeSndDialog)
end

procedure NodeAnswer100 begin
   Reply(221)
   NAnswer(222,1,NodeAnswer101)
end

procedure NodeAnswer101 begin
   Reply(223)
   NAnswer(224,1,NodeAnswer102)
end

procedure NodeAnswer102 begin
   Reply(225)
   NAnswer(216,1,NodeAnswer)
   NAnswer(215,1,NodeSndDialog)
end

procedure NodeAnswer200 begin
   Reply(228)
   NAnswer(229,1,NodeAnswer)
end

procedure NodeAnswer201 begin
   Reply(231)
   NAnswer(232,1,NodeAnswer)
end

procedure NodeAnswer202 begin
   Reply(234)
   NAnswer(235,1,NodeAnswer)
   NAnswer(236,1,NodeAnswer)
   NAnswer(237,1,NodeAnswer)
end
