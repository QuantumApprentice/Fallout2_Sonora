#include "..\headers\define.h"
//---------------------

#define NAME                            SCRIPT_SCLumpy
#define TOWN_REP_VAR                    GVAR_TOWN_REP_PUERTO // привязка к репутации (-1, если не нужно)

#define NUM_TEAM                        TEAM_GHOUL_WILD       // группа, к которой принадлежит персонаж (-1 если нужно оставить Team, заданный в редакторе)
#define NUM_AI                          AI_GHOUL_GENERIC      // AI персонажа (-1 если нужно оставить AI, заданный в редакторе)
#define KILL_SLAVA                      SLAVA_KILL_CHILD      // слава за убийство этого персонажа (SLAVA_KILL_GOOD, SLAVA_KILL_BAD, SLAVA_KILL_NEITRAL, SLAVA_KILL_CHILD)
#define KILL_TOWN_REP                   REP_KILL_NEITRAL      // репутация за убийство этого персонажа (-100 для враждебности всего поселения)
#define KILL_FRAC_REP                   FRAC_NON              // принадлежность определенной фракции (-1, если не нужно)

#define FLAG_STEAL                      25 // Нижний порог навыка для кражи (для торговцев 200, для животных 300)
#define FLAG_ALCOHOL                    1  // Примет ли алкоголь от ГГ: 0 - принимает, 1 - не принимает
#define FLAG_FOOD                       1  // Примет ли пищу от ГГ: 0 - принимает, 1 - не принимает

//---------------------

procedure start;
procedure map_enter_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;
procedure pickup_p_proc;
procedure combat_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;

procedure Node998;
procedure Node999;
procedure NodeStartDialog;
procedure NodeFstDialog;
procedure NodeFstDialog100;
procedure NodeFstDialog101;
procedure NodeFstDialog102;
procedure NodeFstDialog103;
procedure NodeFstDialog104;
procedure NodeFstDialog105;

//---------------------

//#define LVAR_Flag_A                     (6) // отвечает за ...

//---------------------



procedure start begin
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
       set_start_home
       set_obj_visibility(self_obj, 1);

       if (bitDialog(bit_1) != 0) then begin
          rm_timer_event(self_obj);
          destroy_object(self_obj);
       end
       else if ((global_var(GVAR_GAME_END) != 0) and (global_var(GVAR_CAR_MAP_INDEX) == cur_map_index)) then begin
          set_obj_visibility(self_obj, 0);
          floater(self_obj,103,COLOR_MSG_RED);
          reg_anim_clear(self_obj);
          reg_anim_begin();
          reg_anim_animate(self_obj,ANIM_fall_back_sf,-1);
          reg_anim_end();
       end
   end
end

procedure look_at_p_proc begin
   lookProcMsg
end

procedure description_p_proc begin
   dsrptProcMsg
end

procedure timed_event_p_proc begin
   // Таймер за номером 1 рарезервирован под перемещение персонажа по локации
   // Таймер за номером 2 рарезервирован под всплывающие реплики
   if (fixed_param == 1) then begin
       //timer_move_random_home(3)
   end
   if (fixed_param == 2) then begin
       timer_float_reply(110,118)
   end
   if (fixed_param == 3) then begin
       reg_anim_clear(self_obj);
       reg_anim_begin();
       reg_anim_animate(self_obj,ANIM_back_to_standing,-1);
       reg_anim_end();
       add_timer_event(self_obj, 20, 4);
   end
   if (fixed_param == 4) then begin
       bitDialogExp(bit_2,0)
   end
end

procedure critter_p_proc begin
   set_critter_options(0,0) //условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ

   //--- Персонаж уходит с карты
   if (bitDialog(bit_2) != 0) then begin
      if (combat_is_initialized == false) then begin
          if (self_tile == 26902) then begin
          set_obj_visibility(self_obj, 1);
          destroy_object(self_obj);
          end
          else begin
          move_to_tile(0,26902,0)
          end
      end
   end
end

procedure use_skill_on_p_proc begin
   skillUseSteal
   skillUseFAid
end

procedure pickup_p_proc begin
   pickupProcCritter
end

procedure use_obj_on_p_proc begin
   UsedItemToCrit_Proc
end

procedure combat_p_proc begin
   effectAttak(0,0,0,0,0) //отравление min, облучение min, травма ног %, травма рук %, травма глаз %
end

procedure damage_p_proc begin
   set_damage_dude
end

procedure destroy_p_proc begin
   kill_critter_opt
end

procedure talk_p_proc begin
   if (bitDialog(bit_1) == 0) then begin
   setReaction(0) // Отношение к ГГ (1 - персонаж положительный, -1 отрицательный, 0 нейтральный)
   dialog_proc(0,0,NodeStartDialog,-1,-1)
   add_timer_event(self_obj, 5, 3);
   end
   else begin
   z_floater(self_obj,102,COLOR_MSG_RED);
   end
end

procedure Node998 begin
end

procedure Node999 begin
end

procedure NodeStartDialog begin
   call NodeFstDialog;
end

//--- ПЕРВЫЙ ДИАЛОГ

procedure NodeFstDialog begin
   bitDialogExp(bit_1,100)
   Reply(200)
   NAnswer(201,1,NodeFstDialog100)
   BAnswer(212,1,NodeFstDialog104)
end

procedure NodeFstDialog100 begin
   Reply(202)
   NAnswer(203,1,NodeFstDialog101)
end

procedure NodeFstDialog101 begin
   Reply(204)
   NAnswer(205,1,NodeFstDialog102)
   NAnswer(206,1,NodeFstDialog102)
end

procedure NodeFstDialog102 begin
   Reply(207)
   NAnswer(208,1,NodeFstDialog103)
   NAnswer(209,1,NodeFstDialog103)
end

procedure NodeFstDialog103 begin
   Reply(210)
   NAnswer(211,1,Node999)
end

procedure NodeFstDialog104 begin
   Reply(213)
   NAnswer(214,1,NodeFstDialog105)
end

procedure NodeFstDialog105 begin
   Reply(215)
   NAnswer(205,1,NodeFstDialog102)
   NAnswer(206,1,NodeFstDialog102)
end
