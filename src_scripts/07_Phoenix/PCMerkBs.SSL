#include "..\headers\define.h"
//---------------------

#define NAME                            SCRIPT_PCMerkBs
#define TOWN_REP_VAR                    GVAR_TOWN_REP_PHOENIX // привязка к репутации (-1, если не нужно)

#define NUM_TEAM                        TEAM_PHOENIX_MERC     // группа, к которой принадлежит персонаж (-1 если нужно оставить Team, заданный в редакторе)
#define NUM_AI                          AI_GUARDS             // AI персонажа (-1 если нужно оставить AI, заданный в редакторе)
#define KILL_SLAVA                      (SLAVA_KILL_BAD*3)    // слава за убийство этого персонажа (SLAVA_KILL_GOOD, SLAVA_KILL_BAD, SLAVA_KILL_NEITRAL, SLAVA_KILL_CHILD)
#define KILL_TOWN_REP                   REP_KILL_NEITRAL      // репутация за убийство этого персонажа (-100 для враждебности всего поселения)
#define KILL_FRAC_REP                   FRAC_NON              // принадлежность определенной фракции (-1, если не нужно)

#define FLAG_STEAL                      100 // Нижний порог навыка для кражи (для торговцев 200, для животных 300)
#define FLAG_ALCOHOL                    0  // Примет ли алкоголь от ГГ: 0 - принимает, 1 - не принимает
#define FLAG_FOOD                       1  // Примет ли пищу от ГГ: 0 - принимает, 1 - не принимает

//---------------------

procedure start;
procedure map_enter_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;
procedure pickup_p_proc;
procedure combat_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;

procedure Node998;
procedure Node999;
procedure NodeStartDialog;

procedure NodeStopDialog100;
procedure NodeStopDialog101;
procedure NodeStopDialog200;
procedure NodeStopDialog201;
procedure NodeStopDialog202;
procedure NodeStopDialog203;
procedure NodeStopDialog300;
procedure NodeStopDialog301;
procedure NodeStopDialog302;
procedure NodeStopDialog400;
procedure NodeStopDialog401;
procedure NodeStopDialog402;
procedure NodeStopDialog403;
procedure NodeStopDialog500;
procedure NodeStopDialog501;
procedure NodeStopDialog502;
procedure NodeStopDialog503;
procedure NodeStopDialog600;
procedure NodeStopDialog601;
procedure NodeStopDialog602;
procedure NodeStopDialog650;
procedure NodeStopDialog651;
procedure NodeStopDialog652;
procedure NodeStopDialog700;
procedure NodeStopDialog701;
procedure NodeStopDialog702;
procedure NodeStopDialog703;

procedure NodeFstDialog;
procedure NodeFstDialog200;
procedure NodeFstDialog201;
procedure NodeFstDialog202;
procedure NodeSndDialog;

procedure NodeCrook;
procedure NodeCrook2;
procedure NodeCrook3;
procedure NodeCrook4;
procedure NodeDudeSlave;
procedure NodeDudeSlave2;
procedure NodeDudeSlave3;
procedure NodeDudeSlave4;
procedure NodeDudeSlave5;

procedure NodeAnswer;
procedure NodeAnswer2;
procedure NodeAnswer100;
procedure NodeAnswer101;
procedure NodeAnswer102;
procedure NodeAnswer103;
procedure NodeAnswer104;
procedure NodeAnswer105;
procedure NodeAnswer106;
procedure NodeAnswer107;
procedure NodeAnswer108;
procedure NodeAnswer109;
procedure NodeAnswer110;
procedure NodeAnswer111;

procedure NodeWanted;
procedure NodeWanted1;
procedure NodeWanted2;
procedure NodeWanted3;
procedure NodeWanted4;
procedure NodeWanted100;
procedure NodeWanted101;
procedure NodeWanted102;
procedure NodeWanted103;
procedure NodeWanted104;
procedure NodeWanted105;
procedure NodeWanted106;
procedure NodeWanted200_1;
procedure NodeWanted200_2;
procedure NodeWanted201_1;
procedure NodeWanted201_2;
procedure NodeWanted202_1;
procedure NodeWanted202_2;
procedure NodeWanted300;
procedure NodeWanted301;
procedure NodeWanted302;
procedure NodeWanted303;
procedure NodeWanted304;
procedure NodeWanted305;
procedure NodeWanted350;
procedure NodeWanted351;
procedure NodeWanted352;
procedure NodeWanted353;
procedure NodeWanted354;
procedure NodeWanted355;
procedure NodeWanted356;
procedure NodeWanted357;
procedure NodeWanted358;
procedure NodeWanted359;
procedure NodeWanted360;
procedure NodeWanted370;

procedure NodeCardinal100;
procedure NodeCardinal101;
procedure NodeCardinal102;
procedure NodeCardinal103;
procedure NodeCardinal104;
procedure NodeCardinal105;
procedure NodeCardinal106;
procedure NodeCardinal107;
procedure NodeCardinal108;
procedure NodeCardinal109;

//---------------------

#define LVAR_DudeIsSlave_Know             (6) // фиксируется знание шефа о том, что ГГ беглый раб

//---------------------



procedure start begin
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
       set_start_home
       if (map_first_run) then begin
       item_add(self_obj,PID_BOOTLE_CAPS,random(65,80))
       end
   end
end

procedure look_at_p_proc begin
   lookProcMsg
end

procedure description_p_proc begin
   dsrptProcMsg
   //Вы видите крутого парня в отличной броне. От его холодного взгляда становится не по себе.
end

procedure timed_event_p_proc begin
   // Таймер за номером 1 рарезервирован под перемещение персонажа по локации
   // Таймер за номером 2 рарезервирован под всплывающие реплики
   if (fixed_param == 1) then begin
       timer_move_to_home
   end
   if (fixed_param == 2) then begin
       //timer_float_reply(110,111)
   end
end

procedure critter_p_proc begin
   set_critter_options(1,0) //условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ
end

procedure use_skill_on_p_proc begin
   skillUseSteal
   skillUseFAid
end

procedure pickup_p_proc begin
   pickupProcCritter
end

procedure use_obj_on_p_proc begin
   UsedItemToCrit_Proc
end

procedure combat_p_proc begin
   effectAttak(0,0,2,2,2) //отравление min, облучение min, травма ног %, травма рук %, травма глаз %
end

procedure damage_p_proc begin
   set_damage_dude
end

procedure destroy_p_proc begin
   set_global_var(GVAR_PHENIX_STATUS_MERCBOSS,-1);
   if (getQuest(GVAR_PHENIX_QST_MERC_A) == qStart) then begin
   setQuest(GVAR_PHENIX_QST_MERC_A,qZero);
   end
   if (getQuest(GVAR_PHENIX_QST_MERC_B) == qStart) then begin
   setQuest(GVAR_PHENIX_QST_MERC_B,qZero);
   end
   kill_critter_opt
end

procedure talk_p_proc begin
   setReaction(0) // Отношение к ГГ (1 - персонаж положительный, -1 отрицательный, 0 нейтральный)
   fracReaction(GVAR_TITUL_BOS,20) // Поправка к отношению за принадлежность к фракциям
   fracReaction(GVAR_TITUL_BOS_TRAITOR,-20) // Поправка к отношению за принадлежность к фракциям
   fracReaction(GVAR_TITUL_ARIZONA_RANGER,-40) // Поправка к отношению за принадлежность к фракциям
   fracReaction(GVAR_TITUL_PHENIX_CULT,40) // Поправка к отношению за принадлежность к фракциям
   fracReaction(GVAR_TITUL_WARRIOR_ROAD,-20) // Поправка к отношению за принадлежность к фракциям
   fracReaction(GVAR_TITUL_DUDE_IS_SLAVE,-20) // Поправка к отношению за принадлежность к фракциям

   if ((global_var(GVAR_ADDICT_BUFF_OUT) > 0) or (global_var(GVAR_ADDICT_MENTATS) > 0) or (global_var(GVAR_ADDICT_PSYCHO) > 0) or (global_var(GVAR_ADDICT_BUFF_OUT) > 0)) then begin
   floater(self_obj,103,COLOR_MSG_RED);
   end
   else begin
   dialog_proc(0,0,NodeStartDialog,-1,-1)
   end
end

procedure Node998 begin
   set_enemy_dude;
end

procedure Node999 begin
end

procedure NodeStartDialog begin
   if ((bitDialog(bit_2) == 0) and (global_var(GVAR_TITUL_REBIRTH) > 0)) then begin
   call NodeFstDialog200;
   end
   else if (bitDialog(bit_1) == 0) then begin
   call NodeFstDialog;
   end
   else if ((getQuest(GVAR_PHENIX_QST_MERC_A) >= qEnd) and (getQuest(GVAR_PHENIX_QST_MERC_B) == qZero)) then begin
   call NodeWanted300;
   end
   else if ((bitDialog(bit_3) == 0) and (global_var(GVAR_TOWN_REP_PHOENIX) >= 30)) then begin
   call NodeStopDialog100;
   end
   else if ((bitDialog(bit_4) == 0) and (global_var(GVAR_TOWN_REP_PHOENIX) <= -30)) then begin
   call NodeStopDialog200;
   end
   else if ((bitDialog(bit_5) == 0) and (global_var(GVAR_TOWN_REP_PHOENIX_CULT) <= -30)) then begin
   call NodeStopDialog300;
   end
   else if ((bitDialog(bit_6) == 0) and (global_var(GVAR_TOWN_REP_FLAGSTAFF) <= -30)) then begin
   call NodeStopDialog400;
   end
   else if ((bitDialog(bit_6) == 0) and (global_var(GVAR_TOWN_REP_FLAGSTAFF_MERC) <= -30)) then begin
   call NodeStopDialog400;
   end
   else if ((bitDialog(bit_7) == 0) and (global_var(GVAR_TITUL_DUDE_IS_SLAVE) > 0)) then begin
   call NodeStopDialog500;
   end
   else if ((bitDialog(bit_8) == 0) and (global_var(GVAR_TITUL_WARRIOR_ROAD) > 0)) then begin
   call NodeStopDialog600;
   end
   else if ((bitDialog(bit_9) == 0) and (armor_pid_dude_var == PID_ARMOR_ROAD)) then begin
   call NodeStopDialog650;
   end
   else if ((bitDialog(bit_10) == 0) and (global_var(GVAR_TOWN_REP_CASA_GRANDE) <= -30)) then begin
   call NodeStopDialog700;
   end
   else begin
   call NodeSndDialog;
   end
end

//--- СТОП-ДИАЛОГ

procedure NodeStopDialog100 begin
   bitDialogExp(bit_3,0)
   zReply(220)
   NAnswer(221,1,NodeStopDialog101)
end

procedure NodeStopDialog101 begin
   zReply(222)
   NAnswer(237,1,NodeStartDialog)
   BAnswer(238,1,NodeStartDialog)
end

procedure NodeStopDialog200 begin
   bitDialogExp(bit_4,0)
   Reply(223)
   NAnswerSkill(233,3,NodeStopDialog203,SKILL_CONVERSANT,50,232,NodeStopDialog202)
   NAnswer(224,1,NodeStopDialog201)
   BAnswer(228,1,NodeStopDialog202)
end

procedure NodeStopDialog201 begin
   Reply(225)
   NAnswer(226,1,Node998)
   BAnswer(227,1,Node998)
end

procedure NodeStopDialog202 begin
   Reply(229)
   NAnswer(230,1,Node998)
   BAnswer(231,1,Node998)
end

procedure NodeStopDialog203 begin
   Reply(234)
   NAnswer(235,1,Node999)
   BAnswer(236,1,Node998)
end

procedure NodeStopDialog300 begin
   bitDialogExp(bit_5,0)
   setBonusReaction(-40)
   Reply(240)
   NAnswer(241,1,NodeStopDialog301)
end

procedure NodeStopDialog301 begin
   Reply(242)
   NAnswer(243,1,NodeStopDialog302)
end

procedure NodeStopDialog302 begin
   Reply(244)
   NAnswer(245,1,Node999)
   NAnswer(246,1,Node999)
end

procedure NodeStopDialog400 begin
   bitDialogExp(bit_6,0)
   setBonusReaction(-40)
   Reply(250)
   NAnswer(251,1,NodeStopDialog401)
   BAnswer(252,1,NodeStopDialog401)
end

procedure NodeStopDialog401 begin
   Reply(253)
   NAnswer(254,1,NodeStopDialog403)
   NAnswer(258,1,NodeStopDialog402)
end

procedure NodeStopDialog402 begin
   setBonusReaction(-10)
   Reply(259)
   NAnswer(249,1,Node999)
   BAnswer(248,1,Node998)
   BAnswer(247,1,Node999)
end

procedure NodeStopDialog403 begin
   setBonusReaction(5)
   Reply(255)
   NAnswer(256,1,Node999)
   BAnswer(248,1,NodeStopDialog402)
   BAnswer(257,1,Node999)
end

procedure NodeStopDialog500 begin
   bitDialogExp(bit_7,0)
   set_local_var(LVAR_DudeIsSlave_Know,1);
   Reply(260)
   NAnswer(265,1,NodeStopDialog502)
   BAnswer(261,1,NodeStopDialog501)
end

procedure NodeStopDialog501 begin
   Reply(262)
   NAnswer(263,1,Node998)
   BAnswer(264,1,Node998)
end

procedure NodeStopDialog502 begin
   Reply(266)
   NAnswer(267,1,NodeStopDialog503)
   BAnswer(268,1,Node998)
end

procedure NodeStopDialog503 begin
   Reply(269)
   NAnswer(202,1,Node999)
end

procedure NodeStopDialog600 begin
   bitDialogExp(bit_8,0)
   bitDialogExp(bit_9,0)
   Reply(270)
   NAnswer(271,1,NodeStopDialog601)
   BAnswer(275,1,NodeStopDialog602)
end

procedure NodeStopDialog601 begin
   Reply(272)
   NAnswer(273,1,Node999)
   BAnswer(274,1,Node999)
end

procedure NodeStopDialog602 begin
   Reply(276)
   NAnswer(277,1,Node999)
   BAnswer(278,1,Node998)
end

procedure NodeStopDialog650 begin
   bitDialogExp(bit_8,0)
   bitDialogExp(bit_9,0)
   Reply(280)
   NAnswer(281,1,NodeStopDialog651)
   BAnswer(285,1,NodeStopDialog652)
end

procedure NodeStopDialog651 begin
   Reply(282)
   NAnswer(283,1,Node999)
   BAnswer(284,1,Node999)
end

procedure NodeStopDialog652 begin
   Reply(286)
   NAnswer(283,1,Node999)
   BAnswer(284,1,Node999)
end

procedure NodeStopDialog700 begin
   bitDialogExp(bit_10,0)
   setBonusReaction(-20)
   zReply(290)
   BAnswer(291,1,NodeStopDialog701)
   NAnswer(294,1,NodeStopDialog702)
end

procedure NodeStopDialog701 begin
   setBonusReaction(-10)
   Reply(292)
   NAnswer(293,1,NodeStopDialog703)
end

procedure NodeStopDialog702 begin
   Reply(295)
   NAnswer(296,1,NodeStopDialog703)
end

procedure NodeStopDialog703 begin
   Reply(297)
   NAnswer(298,1,Node999)
   BAnswer(299,1,Node999)
end

//--- ПЕРВЫЙ ДИАЛОГ

procedure NodeFstDialog begin
   bitDialogExp(bit_1,0)
   Reply(200)
   NAnswer(201,1,NodeSndDialog)
end

procedure NodeFstDialog200 begin
   bitDialogExp(bit_2,0)
   Reply(210)
   NAnswer(211,1,NodeFstDialog201)
   BAnswer(215,1,NodeFstDialog202)
end

procedure NodeFstDialog201 begin
   Reply(212)
   NAnswer(213,1,NodeStartDialog)
   BAnswer(214,1,NodeStartDialog)
end

procedure NodeFstDialog202 begin
   setBonusReaction(-5)
   Reply(216)
   NAnswer(217,1,NodeStartDialog)
   BAnswer(218,1,NodeStartDialog)
end

//--- ОСНОВНОЙ ДИАЛОГ

procedure NodeSndDialog begin
   if ((armor_pid_dude_var == PID_ARMOR_POWER_A) or (armor_pid_dude_var == PID_ARMOR_POWER_B)) then begin
   Reply(102)
   end
   else if (getReaction>0) then begin
   Reply(300)
   end
   else begin
   Reply(301)
   end

   //--- вопросы:
   NAnswer(350,1,NodeAnswer)

   //--- титул беглого раба:
   if ((local_var(LVAR_DudeIsSlave_Know) > 0) and (global_var(GVAR_TITUL_DUDE_IS_SLAVE) > 0)) then begin
   NAnswer(310,1,NodeDudeSlave)
   end

   //--- розыск:
   if (getQuest(GVAR_PHENIX_QST_MERC_B) < qEnd) then begin
      NAnswer(400,1,NodeWanted)
   end
   if (getQuest(GVAR_PHENIX_QST_MERC_A) < qEnd) then begin
      //- головы Лос Панчос
      if (bitDialog(bit_12) != 0) then begin
      NAnswer(411,1,NodeWanted100)
      end
   end
   else if (getQuest(GVAR_PHENIX_QST_MERC_A) >= qEnd) then begin
      //- голова Гила МакКорда
      if (getQuest(GVAR_PHENIX_QST_MERC_B) == qZero) then begin
      NAnswer(457,1,NodeWanted300)
      end
      if (getQuest(GVAR_PHENIX_QST_MERC_B) == qStart) then begin
      NAnswer(480,1,NodeWanted350)
      end
   end

   //--- ликвидация Крука:
   if (getQuest(GVAR_PHENIX_QST_CULT_D) == qStart) then begin
      if (global_var(GVAR_BRAHMIN_STATUS_COMMANDER)>0) then begin
      NAnswer(390,4,NodeCrook)
      end
   end

   //--- кардинал в тюрьме:
   if (global_var(GVAR_PHENIX_STATUS_CARD_PRISON) == 1) then begin
      NAnswer(330,1,NodeCardinal100)
   end

   //--- магистральщик Чак:
   if (getQuest(GVAR_RAIDER_QST_HAK) == qStart) then begin
      if (global_var(GVAR_RAIDER_STATUS_HAK) == 0) then begin
      NAnswer(302,1,NodeAnswer110)
      end
   end

   NAnswer(309,1,Node999)
end


//--- ДЖОРДЖ КРУК

procedure NodeCrook begin
   Reply(391)
   NAnswer(392,1,NodeCrook2)
end

procedure NodeCrook2 begin
   Reply(393)
   NAnswer(394,1,NodeCrook3)
end

procedure NodeCrook3 begin
   Reply(395)
   if (item_amnt(dude_obj,PID_BOOTLE_CAPS)>=500) then begin
   NAnswer(397,1,NodeCrook4)
   end
   NAnswer(396,1,Node999)
end

procedure NodeCrook4 begin
   set_global_var(GVAR_BRAHMIN_STATUS_COMMANDER,-10);
   item_remove(dude_obj,PID_BOOTLE_CAPS,500)
   Reply(398)
   NAnswer(399,1,Node999)
end

//--- ГГ БЕГЛЫЙ РАБ

procedure NodeDudeSlave begin
   Reply(311)
   NAnswer(312,1,NodeDudeSlave2)
   if (item_amnt(dude_obj,PID_BOOTLE_CAPS) >= 1000) then begin
   NAnswer(315,1,NodeDudeSlave3)
   end
end

procedure NodeDudeSlave2 begin
   Reply(313)
   NAnswer(314,1,Node999)
end

procedure NodeDudeSlave3 begin
   item_remove(dude_obj,PID_BOOTLE_CAPS,1000)
   //item_add(self_obj,PID_BOOTLE_CAPS,1000)
   Reply(316)
   NAnswer(317,1,NodeDudeSlave4)
end

procedure NodeDudeSlave4 begin
   Reply(318)
   NAnswer(319,1,NodeDudeSlave5)
end

procedure NodeDudeSlave5 begin
   set_global_var(GVAR_TITUL_DUDE_IS_SLAVE,0);
   set_local_var(LVAR_DudeIsSlave_Know,0);
   PLAY_FADE
   game_time_advance(ONE_GAME_HOUR);
   Reply(320)
   NAnswer(321,1,Node999)
   BAnswer(322,1,Node999)
end

//--- РАССПРОСЫ

procedure NodeAnswer begin
   bitDialogExp(bit_13,0)
   Reply(351)
   if (bitDialog(bit_11) == 0) then begin
   NAnswer(360,1,NodeAnswer100)
   end
   NAnswer(352,1,NodeAnswer2)
end

procedure NodeAnswer2 begin
   Reply(353)
   NAnswer(354,1,Node999)
end

procedure NodeAnswer100 begin
   Reply(361)
   NAnswer(362,1,NodeAnswer101)
end

procedure NodeAnswer101 begin
   Reply(363)
   if ((getQuest(GVAR_MAIN_QUEST_TWOSUN) < qEnd) or (getQuest(GVAR_MAIN_QUEST_FLAGSTAFF) < qEnd)) then begin
      if (getQuest(GVAR_MAIN_QUEST_FLAGSTAFF) > qZero) then begin
      NAnswer(372,1,NodeAnswer105)
      end else begin
      NAnswer(370,1,NodeAnswer104)
      end
   end
   NAnswer(367,1,NodeAnswer103)
   NAnswer(364,1,NodeAnswer102)
end

procedure NodeAnswer102 begin
   Reply(365)
   NAnswer(366,1,Node999)
end

procedure NodeAnswer103 begin
   Reply(368)
   NAnswer(369,1,Node999)
end

procedure NodeAnswer104 begin
   Reply(371)
   NAnswer(376,1,NodeAnswer106)
   NAnswer(374,1,Node999)
   NAnswer(375,1,Node999)
end

procedure NodeAnswer105 begin
   Reply(373)
   NAnswer(376,1,NodeAnswer106)
   NAnswer(374,1,Node999)
   NAnswer(375,1,Node999)
end

procedure NodeAnswer106 begin
   Reply(377)
   if (item_amnt(dude_obj,PID_BOOTLE_CAPS) >= 100) then begin
   NAnswer(378,1,NodeAnswer107)
   NAnswer(379,1,Node999)
   end
   else begin
   NAnswer(386,1,Node999)
   end
end

procedure NodeAnswer107 begin
   bitDialogExp(bit_11,0)
   if (getQuest(GVAR_MAIN_QUEST_PHENIX) < qStart) then begin
   setQuest(GVAR_MAIN_QUEST_PHENIX,qStart);
   end
   item_remove(dude_obj,PID_BOOTLE_CAPS,100)
   item_add(self_obj,PID_BOOTLE_CAPS,100)
   PLAY_FADE

   if (getQuest(GVAR_PHENIX_CULT_DESTROY) >= qEnd) then begin
   call NodeAnswer109;
   end
   else begin
   call NodeAnswer108;
   end
end

procedure NodeAnswer108 begin
   Reply(380)
   NAnswer(381,1,Node999)
   BAnswer(382,1,Node999)
end

procedure NodeAnswer109 begin
   if (getQuest(GVAR_MAIN_QUEST_FLAGSTAFF) < qStart) then begin
   setQuest(GVAR_MAIN_QUEST_FLAGSTAFF,qStart);
   end
   if (getQuest(GVAR_MAIN_QUEST_TWOSUN) < qStart) then begin
   setQuest(GVAR_MAIN_QUEST_TWOSUN,qStart);
   end
   Reply(383)
   NAnswer(384,1,Node999)
   BAnswer(385,1,Node999)
end

procedure NodeAnswer110 begin
   Reply(303)
   if (item_amnt(dude_obj,PID_BOOTLE_CAPS) >= 250) then begin
   NAnswer(304,1,NodeAnswer111)
   end
   //! убедить красноречием?
   NAnswer(307,1,Node999)
end

procedure NodeAnswer111 begin
   set_global_var(GVAR_RAIDER_STATUS_HAK, 1);
   item_remove(dude_obj,PID_BOOTLE_CAPS,250)
   Reply(305)
   NAnswer(306,1,Node999)
end

//--- ОХОТА ЗА ГОЛОВАМИ

procedure NodeWanted begin
   if (bitDialog(bit_12) == 0) then begin
   call NodeWanted1;
   end
   else begin
   call NodeWanted4;
   end
end

procedure NodeWanted1 begin
   bitDialogExp(bit_12,0)
   zReply(401)
   zNAnswer(402,1,NodeWanted2)
end

procedure NodeWanted2 begin
   if (global_var(GVAR_TITUL_ARENA_MASTER) > 0) then begin
   Reply(403)
   NAnswer(404,1,NodeWanted3)
   end
   else begin
   call NodeWanted3;
   end
end

procedure NodeWanted3 begin
   Reply(407)
   NAnswer(408,1,NodeWanted4)
end

procedure NodeWanted4 begin
   Reply(409)
   NAnswer(410,1,Node999)
end

//- Банда Лос Панчос

procedure NodeWanted100 begin
   Reply(412)

   //--- Атаман 1 Эрнандо
   if (global_var(GVAR_PHENIX_STATUS_LOS_A) == -1) then begin
      if (item_amnt(dude_obj,PID_HEAD_A) > 0) then begin
      NAnswer(420,1,NodeWanted200_1)
      end
      else begin
      NAnswer(427,1,NodeWanted200_2)
      end
   end
   if (global_var(GVAR_PHENIX_STATUS_LOS_A) == -9) then begin
      if (item_amnt(dude_obj,PID_HEAD_A) > 0) then begin
      NAnswer(420,1,NodeWanted200_1)
      end
   end

   //--- Атаман 2 Чучо
   if (global_var(GVAR_PHENIX_STATUS_LOS_B) == -1) then begin
      if (item_amnt(dude_obj,PID_HEAD_B) > 0) then begin
      NAnswer(422,1,NodeWanted201_1)
      end
      else begin
      NAnswer(428,1,NodeWanted201_2)
      end
   end
   if (global_var(GVAR_PHENIX_STATUS_LOS_B) == -9) then begin
      if (item_amnt(dude_obj,PID_HEAD_B) > 0) then begin
      NAnswer(422,1,NodeWanted201_1)
      end
   end

   //--- Атаман 3 Эйди
   if (global_var(GVAR_PHENIX_STATUS_LOS_C) == -1) then begin
      if (item_amnt(dude_obj,PID_HEAD_C) > 0) then begin
      NAnswer(424,1,NodeWanted202_1)
      end
      else begin
      NAnswer(429,1,NodeWanted202_2)
      end
   end
   if (global_var(GVAR_PHENIX_STATUS_LOS_C) == -9) then begin
      if (item_amnt(dude_obj,PID_HEAD_C) > 0) then begin
      NAnswer(424,1,NodeWanted202_1)
      end
   end

   NAnswer(413,1,NodeWanted101)
end

procedure NodeWanted101 begin
   Reply(414)
   NAnswer(415,1,Node999)
end

procedure NodeWanted102 begin
   set_REP(3)
   setBonusReaction(20)
   item_add(dude_obj,PID_BOOTLE_CAPS,500)

   Reply(433)
   if ((global_var(GVAR_PHENIX_STATUS_LOS_A) <= -10) and (global_var(GVAR_PHENIX_STATUS_LOS_B) <= -10) and (global_var(GVAR_PHENIX_STATUS_LOS_C) <= -10)) then begin
   NAnswer(434,1,NodeWanted104)
   NAnswer(435,1,NodeWanted104)
   end
   else begin
   NAnswer(434,1,Node999)
   NAnswer(435,1,Node999)
   end
end

procedure NodeWanted103 begin
   Reply(430)
   NAnswer(431,1,Node999)
   NAnswer(432,1,Node999)
end

procedure NodeWanted104 begin
   Reply(440)
   NAnswer(441,1,NodeWanted105)
end

procedure NodeWanted105 begin
   Reply(442)
   //NAnswer(443,1,NodeWanted106)
   NAnswer(452,1,NodeWanted106)
   NAnswer(453,1,NodeWanted106)
end

procedure NodeWanted106 begin
   setQuest(GVAR_PHENIX_QST_MERC_A,qEnd);
   setBonusReaction(20)
   exp_add(500);
   Reply(454)
   NAnswer(455,1,Node999)
   BAnswer(456,1,Node999)
end

/*
procedure NodeWanted106 begin
   Reply(444)
   NAnswer(448,1,NodeWanted108)
   NAnswer(445,1,NodeWanted107)
end

procedure NodeWanted107 begin
   Reply(446)
   NAnswer(447,1,NodeWanted108)
   NAnswer(448,1,NodeWanted108)
end

procedure NodeWanted108 begin
   setQuest(GVAR_PHENIX_QST_MERC_A,qEnd);
   setBonusReaction(20)
   Reply(449)
   NAnswer(450,1,Node999)
   BAnswer(451,1,Node999)
end
*/

/*
procedure NodeWanted107 begin
   +довоенный викси
   +затухание
   +12 часов
   перемещение в центр района
   разворот ГГ
   сообщение в лог: Некоторое время вы общаетесь с Брюсом Тернерем, обмениваясь отличными историями.

   [Далее]
   Иначе говоря, это отличная новость. И ты заслуживаешь особое почтения после столь опасной операции. Вот, возьми немного виски из моих личных запасов. Настоящий, довоенный. Такой теперь никто не делает.
   Будет весело. Наконец-то я увижу тебя в обычном состоянии, Брюс...
   Это немного не то, чего мне бы хотелось. Но тоже сойдет.


   call Node999;
end
*/


procedure NodeWanted200_1 begin
   set_global_var(GVAR_PHENIX_STATUS_LOS_A,-10);
   item_remove(dude_obj,PID_HEAD_A,1)
   Reply(421)
   NAnswer(426,1,NodeWanted102)
end

procedure NodeWanted200_2 begin
   set_global_var(GVAR_PHENIX_STATUS_LOS_A,-9);
   Reply(421)
   NAnswer(426,1,NodeWanted103)
end

procedure NodeWanted201_1 begin
   set_global_var(GVAR_PHENIX_STATUS_LOS_B,-10);
   item_remove(dude_obj,PID_HEAD_B,1)
   Reply(423)
   NAnswer(426,1,NodeWanted102)
end

procedure NodeWanted201_2 begin
   set_global_var(GVAR_PHENIX_STATUS_LOS_B,-9);
   Reply(421)
   NAnswer(426,1,NodeWanted103)
end

procedure NodeWanted202_1 begin
   set_global_var(GVAR_PHENIX_STATUS_LOS_C,-10);
   item_remove(dude_obj,PID_HEAD_C,1)
   Reply(425)
   NAnswer(426,1,NodeWanted102)
end

procedure NodeWanted202_2 begin
   set_global_var(GVAR_PHENIX_STATUS_LOS_C,-9);
   Reply(421)
   NAnswer(426,1,NodeWanted103)
end

//- Стрелок-предатель

procedure NodeWanted300 begin
   Reply(460)
   NAnswer(461,1,NodeWanted301)
end

procedure NodeWanted301 begin
   Reply(462)
   NAnswer(463,1,NodeWanted302)
end

procedure NodeWanted302 begin
   Reply(464)
   NAnswer(465,1,NodeWanted303)
end

procedure NodeWanted303 begin
   Reply(466)
   NAnswer(467,1,NodeWanted304)
   NAnswer(469,1,NodeWanted305)
end

procedure NodeWanted304 begin
   Reply(468)
   NAnswer(469,1,NodeWanted305)
   NAnswer(470,1,NodeWanted305)
end

procedure NodeWanted305 begin
   setQuest(GVAR_PHENIX_QST_MERC_B,qStart);
   Reply(471)
   NAnswer(472,1,Node999)
   NAnswer(473,1,Node999)
end

procedure NodeWanted350 begin
   Reply(481)
   if (item_amnt(dude_obj,PID_HEAD_D) > 0) then begin
   NAnswer(474,1,NodeWanted370)
   end
   NAnswer(483,1,NodeWanted351)
   if (bitDialog(bit_14) == 0) then begin
   NAnswerReaction(489,4,NodeWanted353,20,486,NodeWanted352)
   end
   NAnswer(482,1,Node999)
end

procedure NodeWanted351 begin
   Reply(484)
   NAnswer(485,1,Node999)
end

procedure NodeWanted352 begin
   Reply(487)
   NAnswer(490,1,NodeWanted351)
   NAnswer(488,1,Node999)
end

procedure NodeWanted353 begin
   bitDialogExp(bit_14,0)
   item_add(dude_obj,PID_BOOTLE_CAPS,100)
   Reply(491)
   NAnswer(492,1,NodeWanted354)
   NAnswer(493,1,NodeWanted355)
   NAnswer(494,1,NodeWanted356)
   NAnswer(495,1,NodeWanted357)
   NAnswer(496,1,NodeWanted358)
   NAnswer(497,1,NodeWanted359)
end

procedure NodeWanted354 begin
   item_add(dude_obj,PID_AMMO_10_MM,24)
   call NodeWanted360;
end
procedure NodeWanted355 begin
   item_add(dude_obj,PID_AMMO_44_MAGNUM,18)
   call NodeWanted360;
end
procedure NodeWanted356 begin
   item_add(dude_obj,PID_AMMO_308,10)
   call NodeWanted360;
end
procedure NodeWanted357 begin
   item_add(dude_obj,PID_AMMO_5_MM,7) // в пачке 5 патронов
   call NodeWanted360;
end
procedure NodeWanted358 begin
   item_add(dude_obj,PID_AMMO_40_MM,4)
   call NodeWanted360;
end
procedure NodeWanted359 begin
   item_add(dude_obj,PID_AMMO_MICRO_FUSION_CELL,25)
   call NodeWanted360;
end

procedure NodeWanted360 begin
   Reply(498)
   NAnswer(499,1,Node999)
end

procedure NodeWanted370 begin
   setQuest(GVAR_PHENIX_QST_MERC_B,qEnd);
   item_remove(dude_obj,PID_HEAD_D,1)
   item_add(dude_obj,PID_BOOTLE_CAPS,750)
   setBonusReaction(50)
   exp_add(500);
   Reply(475)
   NAnswer(476,1,Node999)
   BAnswer(477,1,Node999)
end

//--- КАРДИНАЛ В ТЮРЬМЕ

procedure NodeCardinal100 begin
   Reply(331)
   NAnswer(332,1,NodeCardinal101)
end

procedure NodeCardinal101 begin
   Reply(333)
   NAnswer(335,1,NodeCardinal102)
   NAnswer(339,1,NodeCardinal104)
   NAnswer(334,1,Node999)
end

procedure NodeCardinal102 begin
   Reply(336)
   if (item_amnt(dude_obj,PID_BOOTLE_CAPS) >= 10) then begin
   NAnswer(337,1,NodeCardinal103)
   end
   NAnswer(339,1,NodeCardinal104)
   NAnswer(338,1,Node999)
end

procedure NodeCardinal103 begin
   item_remove(dude_obj,PID_BOOTLE_CAPS,10)
   item_add(self_obj,PID_BOOTLE_CAPS,10)
   call NodeCardinal109;
   call Node999;
end

procedure NodeCardinal104 begin
   Reply(340)
   if (item_amnt(dude_obj,PID_BOOTLE_CAPS) >= 100) then begin
   NAnswer(341,1,NodeCardinal105)
   end
   if (global_var(GVAR_TITUL_PHENIX_CULT)>0) then begin
   NAnswer(346,4,NodeCardinal106)
   end
   NAnswerRep(324,4,NodeCardinal108,30,327,NodeCardinal107)
   NAnswer(343,1,NodeCardinal102)
   NAnswer(342,1,Node999)
end

procedure NodeCardinal105 begin
   item_remove(dude_obj,PID_BOOTLE_CAPS,100)
   item_add(self_obj,PID_BOOTLE_CAPS,100)
   call NodeCardinal109;
   set_global_var(GVAR_PHENIX_STATUS_CARD_PRISON,2);
   Reply(344)
   NAnswer(345,1,Node999)
end

procedure NodeCardinal106 begin
   call NodeCardinal109;
   set_global_var(GVAR_PHENIX_STATUS_CARD_PRISON,2);
   Reply(347)
   NAnswer(348,1,Node999)
   NAnswer(349,1,Node999)
end

procedure NodeCardinal107 begin
   Reply(328)
   NAnswer(329,1,Node999)
   BAnswer(323,1,Node999)
end

procedure NodeCardinal108 begin
   call NodeCardinal109;
   set_global_var(GVAR_PHENIX_STATUS_CARD_PRISON,2);
   Reply(325)
   NAnswer(326,1,Node999)
end

procedure NodeCardinal109 begin
   if (tile_contains_obj_pid(23746, 0, 33555332) == true) then begin
   obj_unlock(tile_contains_pid_obj(23746, 0, 33555332));
   end
end
