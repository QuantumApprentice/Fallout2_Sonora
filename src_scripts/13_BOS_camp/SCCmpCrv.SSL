#include "..\headers\define.h"
//---------------------

#define NAME                            SCRIPT_SCCmpCrv
#define TOWN_REP_VAR                    GVAR_TOWN_REP_PUERTO  // привязка к репутации (-1, если не нужно)

#define NUM_TEAM                        TEAM_CARAVAN          // группа, к которой принадлежит персонаж (-1 если нужно оставить Team, заданный в редакторе)
#define NUM_AI                          AI_CARAVAN_DRIVER     // AI персонажа (-1 если нужно оставить AI, заданный в редакторе)
#define KILL_SLAVA                      SLAVA_KILL_GOOD       // слава за убийство этого персонажа (SLAVA_KILL_GOOD, SLAVA_KILL_BAD, SLAVA_KILL_NEITRAL, SLAVA_KILL_CHILD)
#define KILL_TOWN_REP                   REP_KILL_GOOD         // репутация за убийство этого персонажа (-100 для враждебности всего поселения)
#define KILL_FRAC_REP                   FRAC_NON              // принадлежность определенной фракции (-1, если не нужно)

#define FLAG_STEAL                      0  // Нижний порог навыка для кражи (для торговцев 200, для животных 300)
#define FLAG_ALCOHOL                    1  // Примет ли алкоголь от ГГ: 0 - принимает, 1 - не принимает
#define FLAG_FOOD                       0  // Примет ли пищу от ГГ: 0 - принимает, 1 - не принимает

#define TILE_RUN_FLAG                   22735

//---------------------

procedure start;
procedure map_enter_p_proc;
procedure map_update_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;
procedure uninjure_proc;
procedure pickup_p_proc;
procedure push_p_proc;
procedure combat_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;

procedure Node998;
procedure Node999;
procedure NodeStartDialog;
procedure NodeFstDialog;
procedure NodeFstDialog2;
procedure NodeFstDialog3;
procedure NodeSndDialog;
procedure NodeSndDialog2;
procedure NodeSndDialog3;
procedure NodeParty;
procedure NodeNonParty;
procedure NodeAnswer100;
procedure NodeAnswer101;
procedure NodeAnswer102;

//---------------------

#define LVAR_Flag_Party                     (7) // сопровождение ГГ

//---------------------



procedure start begin
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
       set_start_home
       set_injure_start(1,0,0,0,0) // травмировать (отметить нужное цифрой 1): a - левая рука, b - правая рука, c - левая нога, d - правая нога, e - слепота
       if ((getQuest(GVAR_PUERTO_QST_BANDITS_CMP) != qStart) or (global_var(GVAR_PUERTO_STATUS_CRVN_BNDS) != 0)) then begin
       rm_timer_event(self_obj);
       set_obj_visibility(self_obj, 1);
       end
   end
end

procedure map_update_p_proc begin
   set_injure_update(100,103)
end

procedure look_at_p_proc begin
   lookProcMsg
end

procedure description_p_proc begin
   dsrptProcMsg
end

procedure timed_event_p_proc begin
   // Таймер за номером 1 зарезервирован под перемещение персонажа по локации
   // Таймер за номером 2 зарезервирован под всплывающие реплики
   if (fixed_param == 1) then begin
       /*
       if (local_var(LVAR_Flag_Party) == 0) then begin
       timer_move_to_home
       end
       */
   end
   if (fixed_param == 2) then begin
       //timer_float_reply(120,121)
   end
end

procedure critter_p_proc begin
   set_critter_options(1,0) //условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ
   if (local_var(LVAR_Flag_Party)>0) then begin
   partyRunToDude // персонаж бегает вслед за ГГ
   end
end

procedure use_skill_on_p_proc begin
   skillUseSteal
   skillUseFAid
   skillUseDoctor(40)
end

procedure uninjure_proc begin
   //не удалять
   set_SLAVA(10)
end
procedure pickup_p_proc begin
   pickupProcCritter
end

procedure push_p_proc begin
   push_options(HomeTile,5,102) //Толкать (точка отсчета, макс расстояние, фраза нпс при толкании), удалить если не надо
end

procedure use_obj_on_p_proc begin
   UsedItemToCrit_Proc
end

procedure combat_p_proc begin
   effectAttak(0,0,0,0,0) //отравление min, облучение min, травма ног %, травма рук %, травма глаз %
end

procedure damage_p_proc begin
   set_damage_dude
end

procedure destroy_p_proc begin
   set_global_var(GVAR_PUERTO_STATUS_CRVN_BNDS,-1);
   kill_critter_opt
end

procedure talk_p_proc begin
   if (global_var(GVAR_PUERTO_STATUS_CRVN_BNDS) > 0) then begin
   floater(self_obj,103,COLOR_MSG_NORMAL);
   end
   else if (getQuest(GVAR_PUERTO_QST_BANDITS_PIP) >= qEnd) then begin
   floater(self_obj,103,COLOR_MSG_NORMAL);
   end
   else begin
   setReaction(1) // Отношение к ГГ (1 - персонаж положительный, -1 отрицательный, 0 нейтральный)
   dialog_proc(0,0,NodeStartDialog,-1,-1)
   end
end

procedure Node998 begin
   setNodeCombat;
end

procedure Node999 begin
end

procedure NodeStartDialog begin
   if (bitDialog(bit_1) == 0) then begin
   call NodeFstDialog;
   end
   else begin
   call NodeSndDialog;
   end
end

//--- ПЕРВЫЙ ДИАЛОГ

procedure NodeFstDialog begin
   bitDialogExp(bit_1,0)
   Reply(200)
   BAnswer(201,1,NodeFstDialog2)
   NAnswer(205,1,NodeFstDialog3)
end

procedure NodeFstDialog2 begin
   Reply(202)
   NAnswer(203,1,NodeParty)
   NAnswer(204,1,Node999)
end

procedure NodeFstDialog3 begin
   Reply(206)
   NAnswer(207,1,NodeParty)
   NAnswer(208,1,Node999)
end

procedure NodeSndDialog begin
   Reply(210)
   NAnswer(220,1,NodeAnswer100)
   if ((self_elevation == 0) and (tile_distance(dude_tile, TILE_RUN_FLAG) < 13)) then begin
   NAnswer(211,1,NodeSndDialog2)
   end
   NAnswer(209,1,NodeNonParty)
   NAnswer(207,1,NodeParty)
end

procedure NodeSndDialog2 begin
   Reply(212)
   NAnswer(213,1,NodeSndDialog3)
end

procedure NodeSndDialog3 begin
   set_global_var(GVAR_PUERTO_STATUS_CRVN_BNDS,1);
   move_obj_inven_to_obj(self_obj, dude_obj);
   set_SLAVA(25)
   exp_add(250);
   set_local_var(LVAR_Flag_Party,0);
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,NUM_TEAM);
   set_obj_visibility(self_obj, 1);
   call Node999;
end

procedure NodeParty begin
   set_local_var(LVAR_Flag_Party,1);
   critter_set_flee_state(self_obj, true);
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_PLAYER);
   rm_timer_event(self_obj);
   call Node999;
end

procedure NodeNonParty begin
   set_local_var(LVAR_Flag_Party,0);
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,NUM_TEAM);
   call Node999;
end

procedure NodeAnswer100 begin
   Reply(221)
   NAnswer(222,1,NodeAnswer101)
   NAnswer(224,1,NodeAnswer102)
   NAnswer(226,1,NodeSndDialog)
end

procedure NodeAnswer101 begin
   Reply(223)
   NAnswer(224,1,NodeAnswer102)
   NAnswer(226,1,NodeSndDialog)
end

procedure NodeAnswer102 begin
   Reply(225)
   NAnswer(222,1,NodeAnswer101)
   NAnswer(226,1,NodeSndDialog)
end
