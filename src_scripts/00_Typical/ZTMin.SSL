#include "..\headers\define.h"
#define NAME                    SCRIPT_ZTMin
#define TRAP_ACTIV              PID_TRAP_Aa
#define TRAP_DSTR               PID_TRAP_Ab
#define TRAP_SCRIPT             SCRIPT_ZTMin

procedure start;
procedure map_enter_p_proc;
procedure map_update_p_proc;
procedure spatial_p_proc;
procedure use_p_proc;
procedure use_skill_on_p_proc;
procedure use_obj_on_p_proc;
procedure damage_p_proc;
procedure detected_proc;
procedure step_proc;
procedure trap_destroy;
procedure trap_install;
procedure trap_vis;
procedure trap_invis;

variable critter_source;



procedure start begin
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
      call trap_invis;

      if (cur_map_index == MAP_CASA_NUEVA_CEMENT) then begin
      if (global_var(GVAR_NUEVA_STATUS_EL_GIGANTO) != 0) then begin
         if trapObj(TRAP_DSTR) then begin
         end
         else begin
         call trap_destroy;
         end
         set_obj_visibility(tile_contains_pid_obj(self_tile, self_elevation, TRAP_DSTR), 1);
      end
      end
   end
end

procedure map_update_p_proc begin
   if (cur_map_index == MAP_JACKALS) then begin
      if (map_var(MVAR_JKLS_TRAPS_VSBL) > 0) then begin
      call trap_destroy;
      end
   end
   if (cur_map_index == MAP_FLAGSTAFF_MUSEUM) then begin
      if (map_var(MVAR_FLAGMUS_TRAPS_VSBL) > 0) then begin
      call trap_vis;
      end
   end
   if (cur_map_index == MAP_FLAGSTAFF_WINON) then begin
      if (map_var(MVAR_FLAG_WINON_TRAPS_VSBL) > 0) then begin
      call trap_vis;
      end
   end
   if (cur_map_index == MAP_VILLA_HOME) then begin
      if (map_var(MVAR_VILLA_Home_Traps) > 0) then begin
      call trap_destroy;
      end
   end
   if (cur_map_index == MAP_TWO_SUN_DSTR) then begin
      if (map_var(MVAR_TsnBank_TrapVsbl) > 0) then begin
      call trap_vis;
      end
   end
   if (cur_map_index == MAP_RAIDERS_BASA) then begin
      if (map_var(MVAR_ROADWAR_TRAPS_VSBL) > 0) then begin
      call trap_vis;
      end
   end
   if (cur_map_index == MAP_INFERNO_SHOP) then begin
      if (map_var(MVAR_INF_SHOP_TrapVsbl) > 0) then begin
      call trap_vis;
      end
   end
end

procedure spatial_p_proc begin
   critter_source := source_obj;

   //--- Обнаружение криттера:
   if (source_obj == dude_obj) then begin
      if (trapObj(TRAP_ACTIV) or trapObj(TRAP_DSTR)) then begin
      end
      else begin
      call detected_proc;
      end
   end

   //--- Прохождение криттера по ловушке:
   if (source_tile == self_tile) then begin
      if trapObj(TRAP_DSTR) then begin
      end
      else begin
         if (type_kill_source(KILL_TYPE_floater) or type_kill_source(KILL_TYPE_plant) or (source_PID == PID_ROBOT_EYEBALL)) then begin
         end
         else if ((cur_map_index == MAP_INFERNO_SHOP) and (critter_source != dude_obj)) then begin
         end
         else begin
            if ((critter_source == dude_obj) and DudePerkHas(PERK_light_step)) then begin
            display_mstr(113);
            end
            else begin
            call step_proc;
            end
         end
      end
   end
end

procedure use_p_proc begin
   script_overrides;
   display_mstr(120);
end

procedure use_skill_on_p_proc begin
   if (action_being_used == SKILL_TRAPS) then begin
      script_overrides;

      if trapObj(TRAP_ACTIV) then begin
         if (has_skill(dude_obj, SKILL_TRAPS) >= 60) then begin
         exp_add(75);
         dude_floater_OK;
         display_mstr(116);
         call trap_destroy;
         end
         else begin
         skill_display_mstr(115,SKILL_TRAPS,60)
         end
      end
      else if trapObj(TRAP_DSTR) then begin
         if (has_skill(dude_obj, SKILL_TRAPS) >= 40) then begin
         dude_floater_OK;
         display_mstr(118);
         call trap_install;
         end
         else begin
         skill_display_mstr(117,SKILL_TRAPS,40)
         end
      end
      else begin
         display_mstr(119);
      end
   end
end

procedure use_obj_on_p_proc begin
   stopUseCarGaz

   if ((objUsedPid == PID_W_GRENADE_FRAG) or (objUsedPid == PID_W_GRENADE_PLASMA)) then begin
      script_overrides;

      if trapObj(TRAP_ACTIV) then begin
         display_mstr(115);
      end
      else if trapObj(TRAP_DSTR) then begin
         item_remove(dude_obj, objUsedPid, 1)
         dude_floater_OK;
         display_mstr(118);
         call trap_install;
      end
      else begin
         display_mstr(119);
      end
   end
end

procedure damage_p_proc begin
   if (target_obj != 0) then begin
      call trap_destroy;
   end
end

procedure detected_proc begin
   variable detected_var;

   //--- Просчет вероятности
   detected_var := dude_perception;
   detected_var += (has_skill(dude_obj, SKILL_TRAPS)/5);
   if (global_var(GVAR_LIGHT_MAP_GLOBAL) <= 20) then begin
   detected_var -= 20;
   end
   else if (global_var(GVAR_LIGHT_MAP_GLOBAL) <= 50) then begin
   detected_var -= 10;
   end
   if dude_is_sneaking then begin
   detected_var += 20;
   end
   if DudePerkHas(PERK_light_step) then begin
   detected_var += 100;
   end

   //--- Обнаружение ловушки
   if (random(0,100) <= detected_var) then begin
      call trap_vis;
      floater(critter_source,110,COLOR_MSG_RED);
      display_mstr(111);
      reg_anim_clear(critter_source);
   end
end

procedure step_proc begin
   //--- Обновление ловушки
   call trap_destroy;
   play_sfx("WHO1XXX1");

   //--- Травмирование
   critter_dmg(critter_source, random(40,60), DMG_explosion);

   if (random(0,100) <= (40+(combat_difficulty*2))) then begin
       if (get_critter_stat(critter_source,STAT_current_hp) <5) then begin
       end
       else if (critter_source == dude_obj) then begin
          dam_to_dude_LEG
       end
       else begin
          if (random(0,1) == 0) then begin
          critter_injure(critter_source, DAM_CRIP_LEG_RIGHT);
          end
          else begin
          critter_injure(critter_source, DAM_CRIP_LEG_LEFT);
          end
       end
   end

   //--- Отчет
   display_msg(obj_name(critter_source)+mstr(112));
   destroy_object(self_obj);
end

procedure trap_destroy begin
   if trapObj(TRAP_DSTR) then begin
   end
   else begin
   create_object_sid(TRAP_DSTR,self_tile,self_elevation,-1);
   end
   call trap_invis;
   //destroy_object(self_obj);
end

procedure trap_install begin
   set_obj_visibility(self_obj, 1);
   if trapObj(TRAP_ACTIV) then begin
   end
   else begin
   create_object_sid(TRAP_ACTIV,self_tile,self_elevation,TRAP_SCRIPT);
   end
   destroy_object(self_obj);
end

procedure trap_vis begin
   if (trapObj(TRAP_ACTIV) or trapObj(TRAP_DSTR)) then begin
   end
   else begin
   create_object_sid(TRAP_ACTIV,self_tile,self_elevation,TRAP_SCRIPT);
   end
end

procedure trap_invis begin
   if trapObj(TRAP_ACTIV) then begin
   destroy_object(tile_contains_pid_obj(self_tile, self_elevation, TRAP_ACTIV));
   end
end
