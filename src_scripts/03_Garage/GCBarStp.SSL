#include "..\headers\define.h"
//---------------------

#define NAME                            SCRIPT_GCBarStp
#define TOWN_REP_VAR                    GVAR_TOWN_REP_GARAGE  // привязка к репутации (-1, если не нужно)

#define NUM_TEAM                        TEAM_GARAGE_CITIZEN   // группа, к которой принадлежит персонаж (-1 если нужно оставить Team, заданный в редакторе)
#define NUM_AI                          AI_PERSON_TOUGH       // AI персонажа (-1 если нужно оставить AI, заданный в редакторе)
#define KILL_SLAVA                      SLAVA_KILL_BAD        // слава за убийство этого персонажа (SLAVA_KILL_GOOD, SLAVA_KILL_BAD, SLAVA_KILL_NEITRAL, SLAVA_KILL_CHILD)
#define KILL_TOWN_REP                   REP_KILL_BAD          // репутация за убийство этого персонажа (REP_KILL_GOOD, REP_KILL_BAD, REP_KILL_NEITRAL, REP_KILL_CRITICAL для враждебности всего поселения, REP_KILL_CHILD)
#define KILL_FRAC_REP                   FRAC_NON              // принадлежность определенной фракции (-1, если не нужно)

#define FLAG_STEAL                      20 // Нижний порог навыка для кражи (для торговцев 200, для животных 300)
#define FLAG_ALCOHOL                    0  // Примет ли алкоголь от ГГ: 0 - принимает, 1 - не принимает
#define FLAG_FOOD                       1  // Примет ли пищу от ГГ: 0 - принимает, 1 - не принимает

#define TILE_EXIT_BAR                   20896

//---------------------

procedure start;
procedure map_enter_p_proc;
procedure weapon_wield_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;
procedure pickup_p_proc;
procedure combat_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;

procedure Node998;
procedure Node999;
procedure NodeStartDialog;
procedure NodeStopDialog;
procedure NodeStopDialog2;
procedure NodeStopDialog3;
procedure NodeStopDialog4;
procedure NodeStopDialog5;
procedure NodeFstDialog;
procedure NodeFstDialog2;
procedure NodeFstDialog3;
procedure NodeFstDialog4;
procedure NodeFstDialog5;
procedure NodeFstDialog6;
procedure NodeFstDialog7;
procedure NodeFstDialog8;
procedure NodeSndDialog;
procedure NodeSndDialog2;
procedure NodeSndDialog10;
procedure NodeSndDialog11;
procedure NodeSndDialog12;
procedure NodeSndDialog20;
procedure NodeSndDialog30;

//---------------------

#define LVAR_WeaponUp                     (6) // смена оружия под уровень ГГ

//---------------------



procedure start begin
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
       set_start_home
       if (map_var(MVAR_GARAGE_BAR_COMBAT) >= 3) then begin
       critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_BANDITS);
       end
       call weapon_wield_proc;
       if (global_var(GVAR_GARAGE_STATUS_LARRY) < 0) then begin
       rm_timer_event(self_obj);
       set_obj_visibility(self_obj, 1);
       end
   end
end

procedure weapon_wield_proc begin
   if (dude_level >= 2) then begin
       if (local_var(LVAR_WeaponUp) == 0) then begin
       inven_unwield(self_obj);
       item_add(self_obj,PID_W_KNIFE,1)
       set_local_var(LVAR_WeaponUp,1);
       end
   end
   if (dude_level >= 5) then begin
       if (local_var(LVAR_WeaponUp) == 1) then begin
       inven_unwield(self_obj);
       item_add(self_obj,PID_W_CROWBAR,1)
       set_local_var(LVAR_WeaponUp,2);
       end
   end
end

procedure look_at_p_proc begin
   lookProcMsg
end

procedure description_p_proc begin
   dsrptProcMsg
end

procedure timed_event_p_proc begin
   // Таймер за номером 1 рарезервирован под перемещение персонажа по локации
   // Таймер за номером 2 рарезервирован под всплывающие реплики
   if (fixed_param == 1) then begin
       timer_move_to_home
   end
   if (fixed_param == 2) then begin
       //timer_float_reply(120,121)
   end
end

procedure critter_p_proc begin
   set_critter_options(0,1) //условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ

   if (map_var(MVAR_GARAGE_BAR_COMBAT) == 1) then begin
      if (map_var(MVAR_GARAGE_BAR_PUT) != 0) then begin
         set_map_var(MVAR_GARAGE_BAR_COMBAT,-1);
      end
      else begin
         dialogue_system_enter;
      end
   end

   if (map_var(MVAR_GARAGE_BAR_COMBAT) == 2) then begin
      if (map_var(MVAR_GARAGE_BAR_PUT) != 0) then begin
         set_map_var(MVAR_GARAGE_BAR_COMBAT,-1);
      end
      else begin
         if (tile_distance(dude_tile, TILE_EXIT_BAR) < 2) then begin
         dialogue_system_enter;
         end
      end
   end
end

procedure use_skill_on_p_proc begin
   skillUseSteal
   skillUseFAid
end

procedure pickup_p_proc begin
   pickupProcCritter
end

procedure use_obj_on_p_proc begin
   UsedItemToCrit_Proc
   if (itemTypeUseDude == 2) then begin
       if ((map_var(MVAR_GARAGE_BAR_COMBAT) >= 0) and (map_var(MVAR_GARAGE_BAR_COMBAT) < 3)) then begin
           set_REP(1)
           exp_add(100);
           floater(self_obj,102,COLOR_MSG_GREEN);
           set_map_var(MVAR_GARAGE_BAR_COMBAT,-1);
       end
   end
end

procedure combat_p_proc begin
   effectAttak(0,0,0,0,0) //отравление min, облучение min, травма ног %, травма рук %, травма глаз %
end

procedure damage_p_proc begin
   set_damage_dude
end

procedure destroy_p_proc begin
   set_global_var(GVAR_GARAGE_STATUS_LARRY,-1);
   set_map_var(MVAR_GARAGE_BAR_COMBAT,4);
   kill_critter_opt
end

procedure talk_p_proc begin
   setReaction(-1) // Отношение к ГГ(1 - персонаж положительный, -1 отрицательный, 0 нейтральный)
   fracReaction(GVAR_TITUL_ARIZONA_RANGER,-60) // Поправка к отношению за принадлежность к фракциям
   if (map_var(MVAR_GARAGE_BAR_COMBAT) == 0) then begin
   floater(self_obj,103,COLOR_MSG_RED);
   end
   else begin
   dialog_proc(0,0,NodeStartDialog,-1,-1)
   end
end

procedure Node998 begin
   set_map_var(MVAR_GARAGE_BAR_COMBAT,3);
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_BANDITS);
   set_enemy_dude;
end

procedure Node999 begin
end

procedure NodeStartDialog begin
   if (map_var(MVAR_GARAGE_BAR_COMBAT) == 1) then begin
      call NodeFstDialog;
   end
   else if ((map_var(MVAR_GARAGE_BAR_COMBAT) == 2) and (tile_distance(dude_tile, TILE_EXIT_BAR) < 2)) then begin
      call NodeStopDialog;
   end
   else begin
      call NodeSndDialog;
   end
end

procedure NodeStopDialog begin
   set_map_var(MVAR_GARAGE_BAR_COMBAT,-1);
   Reply(300)
   BAnswer(301,1,NodeStopDialog2)
   NAnswer(304,1,NodeStopDialog3)
   if (item_amnt(dude_obj,PID_BOOTLE_CAPS) >= 10) then begin
   NAnswer(310,1,NodeStopDialog5)
   end
end

procedure NodeStopDialog2 begin
   setBonusReaction(-50)
   Reply(302)
   NAnswer(303,1,Node998)
   NAnswer(304,1,NodeStopDialog3)
   if (item_amnt(dude_obj,PID_BOOTLE_CAPS) >= 10) then begin
   NAnswer(310,1,NodeStopDialog5)
   end
end

procedure NodeStopDialog3 begin
   Reply(305)
   NAnswer(306,1,NodeStopDialog4)
end

procedure NodeStopDialog4 begin
   zReply(307)
   BAnswer(308,1,Node998)
   NAnswer(309,1,Node999)
end

procedure NodeStopDialog5 begin
   item_remove(dude_obj,PID_BOOTLE_CAPS,10)
   item_add(self_obj,PID_BOOTLE_CAPS,10)
   Reply(311)
   BAnswer(312,1,Node998)
   NAnswer(313,1,Node999)
end

//--- ПЕРВЫЙ ДИАЛОГ

procedure NodeFstDialog begin
   //bitDialogExp(bit_1,0)
   set_map_var(MVAR_GARAGE_BAR_COMBAT,2);
   Reply(200)
   NAnswer(201,1,NodeFstDialog2)
   BAnswer(211,1,NodeFstDialog6)
   NAnswer(213,1,NodeFstDialog7)
end

procedure NodeFstDialog2 begin
   zReply(202)
   NAnswer(203,3,NodeFstDialog3)
   NAnswer(207,1,NodeFstDialog4)
   BAnswer(209,1,NodeFstDialog5)
end

procedure NodeFstDialog3 begin
   setBonusReaction(10)
   Reply(204)
   NAnswer(205,1,Node999)
   NAnswer(206,1,Node999)
end

procedure NodeFstDialog4 begin
   setBonusReaction(-20)
   Reply(208)
   NAnswer(205,1,Node999)
   NAnswer(206,1,Node999)
end

procedure NodeFstDialog5 begin
   setBonusReaction(-50)
   Reply(210)
   NAnswer(205,1,Node999)
   NAnswer(206,1,Node999)
end

procedure NodeFstDialog6 begin
   Reply(212)
   NAnswer(203,3,NodeFstDialog3)
   NAnswer(207,1,NodeFstDialog4)
   BAnswer(209,1,NodeFstDialog5)
end

procedure NodeFstDialog7 begin
   Reply(214)
   NAnswer(215,1,NodeFstDialog8)
end

procedure NodeFstDialog8 begin
   Reply(216)
   NAnswer(205,1,Node999)
   NAnswer(206,1,Node999)
end

//--- ПОВТОРНЫЙ ДИАЛОГ

procedure NodeSndDialog begin
   if (map_var(MVAR_GARAGE_BAR_COMBAT) < 0) then begin
   Reply(220)
   end
   else begin
   Reply(220)
   end

   NAnswer(230,1,NodeSndDialog10)
   NAnswer(250,1,NodeSndDialog20)
   NAnswer(256,1,NodeSndDialog30)
   NAnswer(221,1,NodeSndDialog2)
end

procedure NodeSndDialog2 begin
   Reply(222)
   NAnswer(223,1,Node999)
end

procedure NodeSndDialog10 begin
   if (town_rep_is_accepted or (getReaction > 40)) then begin
   Reply(236)
   NAnswer(237,1,NodeSndDialog12)
   end
   else begin
   Reply(231)
   NAnswer(232,1,NodeSndDialog11)
   end
end

procedure NodeSndDialog11 begin
   Reply(233)
   NAnswer(237,1,NodeSndDialog12)
   NAnswer(234,1,NodeSndDialog)
   BAnswer(235,1,Node998)
end

procedure NodeSndDialog12 begin
   if (town_rep_is_accepted or (getReaction > 40)) then begin
      if (global_var(GVAR_GARAGE_STATUS_BOSS) < 0) then begin
      Reply(242)
      end
      else begin
      Reply(241)
      end
      NAnswer(243,1,NodeSndDialog)
   end
   else begin
      Reply(238)
      NAnswer(239,1,NodeSndDialog)
      BAnswer(240,1,Node998)
   end
end

procedure NodeSndDialog20 begin
   if (town_rep_is_accepted or (getReaction > 40)) then begin
   Reply(253)
   end
   else begin
   Reply(251)
   end
   NAnswer(252,1,NodeSndDialog)
end

procedure NodeSndDialog30 begin
   Reply(257)
   NAnswer(258,1,Node999)
   BAnswer(259,1,Node999)
end
