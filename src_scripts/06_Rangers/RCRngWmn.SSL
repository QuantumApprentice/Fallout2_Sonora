#include "..\headers\define.h"
//---------------------

#define NAME                            SCRIPT_RCRngWmn
#define TOWN_REP_VAR                    GVAR_TOWN_REP_RANGERS // привязка к репутации (-1, если не нужно)

#define NUM_TEAM                        TEAM_RANGERS          // группа, к которой принадлежит персонаж (-1 если нужно оставить Team, заданный в редакторе)
#define NUM_AI                          AI_RANGER             // AI персонажа (-1 если нужно оставить AI, заданный в редакторе)
#define KILL_SLAVA                      SLAVA_KILL_GOOD       // слава за убийство этого персонажа (SLAVA_KILL_GOOD, SLAVA_KILL_BAD, SLAVA_KILL_NEITRAL, SLAVA_KILL_CHILD)
#define KILL_TOWN_REP                   REP_KILL_CRITICAL     // репутация за убийство этого персонажа (-100 для враждебности всего поселения)
#define KILL_FRAC_REP                   FRAC_RANGER           // принадлежность определенной фракции (-1, если не нужно)

#define FLAG_STEAL                      80 // Нижний порог навыка для кражи (для торговцев 200, для животных 300)
#define FLAG_ALCOHOL                    1  // Примет ли алкоголь от ГГ: 0 - принимает, 1 - не принимает
#define FLAG_FOOD                       1  // Примет ли пищу от ГГ: 0 - принимает, 1 - не принимает

//---------------------

procedure start;
procedure map_enter_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;
procedure pickup_p_proc;
procedure combat_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;

procedure Node998;
procedure Node999;
procedure NodeStartDialog;

//---------------------

//#define LVAR_Flag_A                     (6) // отвечает за ...

//---------------------



procedure start begin
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
       set_start_home

       if (global_var(GVAR_STATUS_RANGERS_AGATA) > 0) then begin
       set_global_var(GVAR_STATUS_RANGERS_AGATA,0);
       end

       if (map_var(MVAR_RNGRS_Councilium) != 1) then begin
       if ((global_var(GVAR_JACKALS_RANGER_CONTRAKT) == 1) and (getQuest(GVAR_JACKALS_UNION_CITY) == qEnd)) then begin
           if (map_var(MVAR_RNGRS_JclTalk) <= 2) then begin
           set_global_var(GVAR_STATUS_RANGERS_AGATA, 1);
           end
           if (map_var(MVAR_RNGRS_JclTalk) == 0) then begin
           set_map_var(MVAR_RNGRS_JclTalk,1);
           end
           if (map_var(MVAR_RNGRS_JclTalk) == 1) then begin
           set_map_var(MVAR_RNGRS_JclTalkMsg,110);
           add_timer_event(self_obj, 40, 3);
           end
       end
       end

       set_obj_visibility(self_obj, 1);
       if (global_var(GVAR_STATUS_RANGERS_AGATA) > 0) then begin
       set_obj_visibility(self_obj, 0);
       end
   end
end

procedure look_at_p_proc begin
   lookProcMsg
end

procedure description_p_proc begin
   dsrptProcMsg
end

procedure timed_event_p_proc begin
   // Таймер за номером 1 рарезервирован под перемещение персонажа по локации
   // Таймер за номером 2 рарезервирован под всплывающие реплики
   if (fixed_param == 1) then begin
       timer_move_to_home
   end
   if (fixed_param == 2) then begin
       //timer_float_reply(110,119)
   end
   if (fixed_param == 3) then begin
       if (self_elevation == dude_elevation) then begin
           if ((tile_is_visible(self_tile) == TRUE) or (tile_distance_objs(self_obj,dude_obj)<10)) then begin
           floater(self_obj,map_var(MVAR_RNGRS_JclTalkMsg),COLOR_MSG_RED);
           inc_map_var(MVAR_RNGRS_JclTalkMsg);
           end
       end

       if (map_var(MVAR_RNGRS_JclTalkMsg) > 120) then begin
           set_local_var(LVAR_Home_Tile,18307);
           set_local_var(LVAR_Home_Rotat,2);
           set_map_var(MVAR_RNGRS_JclTalk,2);
       end
       else begin
           if ((self_elevation == dude_elevation) and (tile_is_visible(self_tile) == TRUE)) then begin
           add_timer_event(self_obj, 40, 3);
           end
           else begin
           add_timer_event(self_obj, 10, 3);
           end
       end
   end
end

procedure critter_p_proc begin
   set_critter_options(1,0) //условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ
end

procedure use_skill_on_p_proc begin
   skillUseSteal
   skillUseFAid
end

procedure pickup_p_proc begin
   pickupProcCritter
end

procedure use_obj_on_p_proc begin
   UsedItemToCrit_Proc
end

procedure combat_p_proc begin
   effectAttak(0,0,1,1,1) //отравление min, облучение min, травма ног %, травма рук %, травма глаз %
end

procedure damage_p_proc begin
   set_damage_dude
end

procedure destroy_p_proc begin
   set_map_var(MVAR_RNGRS_JclTalk,0);
   set_global_var(GVAR_STATUS_RANGERS_AGATA,-1);
   kill_critter_opt
end

procedure talk_p_proc begin
   if (map_var(MVAR_RNGRS_JclTalk) == 2) then begin
      if (bitDialog(bit_1) == 0) then begin
      dialog_proc(0,0,NodeStartDialog,-1,-1)
      end
      else begin
      floater(self_obj,103,COLOR_MSG_RED);
      end
   end
   else if (map_var(MVAR_RNGRS_JclTalk) > 2) then begin
      display_mstr(104);
   end
   else begin
      display_mstr(102);
   end
end

procedure Node998 begin
end

procedure Node999 begin
   set_global_var(GVAR_STATUS_RANGERS_AGATA, 0);
   set_map_var(MVAR_RNGRS_JclTalk, 10);
end

procedure NodeStartDialog begin
   bitDialogExp(bit_1,0)
   Reply(200)
   NAnswer(201,1,Node999)
   BAnswer(202,1,Node999)
end
