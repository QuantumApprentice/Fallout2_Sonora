#include "..\headers\define.h"
//---------------------

#define NAME                            SCRIPT_VCCitizn
#define TOWN_REP_VAR                    GVAR_TOWN_REP_VILLA   // привязка к репутации (-1, если не нужно)

#define NUM_TEAM                        TEAM_VILLA_PEASAN     // группа, к которой принадлежит персонаж (-1 если нужно оставить Team, заданный в редакторе)
#define NUM_AI                          AI_TRIBLE_VILLA       // AI персонажа (-1 если нужно оставить AI, заданный в редакторе)
#define KILL_SLAVA                      SLAVA_KILL_GOOD       // слава за убийство этого персонажа (SLAVA_KILL_GOOD, SLAVA_KILL_BAD, SLAVA_KILL_NEITRAL, SLAVA_KILL_CHILD)
#define KILL_TOWN_REP                   REP_KILL_CRITICAL     // репутация за убийство этого персонажа (REP_KILL_GOOD, REP_KILL_BAD, REP_KILL_NEITRAL, REP_KILL_CRITICAL для враждебности всего поселения, REP_KILL_CHILD)
#define KILL_FRAC_REP                   FRAC_NON              // принадлежность определенной фракции (-1, если не нужно)

#define FLAG_STEAL                      20 // Нижний порог навыка для кражи (для торговцев 200, для животных 300)
#define FLAG_ALCOHOL                    1  // Примет ли алкоголь от ГГ: 0 - принимает, 1 - не принимает
#define FLAG_FOOD                       0  // Примет ли пищу от ГГ: 0 - принимает, 1 - не принимает

//---------------------

procedure start;
procedure map_enter_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure timed_event_p_proc;
procedure replyfloat_proc;
procedure critter_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;
procedure pickup_p_proc;
procedure push_p_proc;
procedure combat_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;

//---------------------

#define LVAR_Tile_B                     (6) // номер тайла размещения во время Совета
#define LVAR_Rotat_B                    (7) // разворот во время Совета

variable replyFloat;

//---------------------



procedure start begin
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
       set_start_home
       if (getQuest(GVAR_MAIN_QUEST_FLAGSTAFF) < qEnd) then begin
       rm_timer_event(self_obj);
       set_obj_visibility(self_obj, 1);
       end
       else begin
       set_obj_visibility(self_obj, 0);
       end

       if (item_amnt(self_obj,PID_TEST) > 0) then begin
           if (item_amnt(self_obj,PID_TEST) == 1) then begin
           set_local_var(LVAR_Tile_B,14487); // мужик слева
           set_local_var(LVAR_Rotat_B,5); //
           end
           if (item_amnt(self_obj,PID_TEST) == 2) then begin
           set_local_var(LVAR_Tile_B,14084); // женщина справа
           set_local_var(LVAR_Rotat_B,5); //
           end
           if (item_amnt(self_obj,PID_TEST) == 3) then begin
           set_local_var(LVAR_Tile_B,13882); // мужик справа
           set_local_var(LVAR_Rotat_B,4); //
           end
           if (item_amnt(self_obj,PID_TEST) == 4) then begin
           set_local_var(LVAR_Tile_B,13888); // женщина слева
           set_local_var(LVAR_Rotat_B,5); //
           end

           item_remove(self_obj,PID_TEST,999)
       end
   end
end

procedure look_at_p_proc begin
   lookProcMsg
end

procedure description_p_proc begin
   dsrptProcMsg
end

procedure timed_event_p_proc begin
   // Таймер за номером 1 рарезервирован под перемещение персонажа по локации
   // Таймер за номером 2 рарезервирован под всплывающие реплики
   if (fixed_param == 1) then begin
       if ((local_var(LVAR_Tile_B) > 0) and (map_var(MVAR_Plaza_Council) > 0)) then begin
       timer_move_to_tile(0,local_var(LVAR_Tile_B),local_var(LVAR_Rotat_B))
       end
       else begin
       timer_move_random_home(4)
       end
   end
   if (fixed_param == 2) then begin
       call replyfloat_proc;
       timer_float_reply(replyFloat,replyFloat)
   end
end

procedure replyfloat_proc begin
   replyFloat := 0;
   replyFloat := 110+(random(0,7)*3);

       // Реплики про Шакалов
       if (replyFloat == 110) then begin
           if ((getQuest(GVAR_JACKALS_MERC_ATAK) == qEnd) or (global_var(GVAR_JACKALS_DEAD) >= LIMIT_JACKALS_DEAD)) then begin
           replyFloat += 1;
           end
           else if (global_var(GVAR_JACKALS_RANGER_CONTRAKT) >0) then begin
           replyFloat += 2;
           end
       end

       // Реплики про Гараж-Сити
       if (replyFloat == 113) then begin
           if (getQuest(GVAR_GARAGE_STANOK_DESTROY) >= qEnd) then begin
           replyFloat += 1;
           end
           else if ((global_var(GVAR_GARAGE_STATUS_BOSS) < 0) and (global_var(GVAR_GARAGE_STATUS_LUCAS) < 0)) then begin
           replyFloat += 1;
           end
           else if ((getQuest(GVAR_GARAGE_QST_LUCS_TRADE) == 4) or (getQuest(GVAR_GARAGE_QST_BOSS_TRADE) == 4)) then begin
           replyFloat += 2;
           end
       end

       // Реплики про Рейнджеров
       if (replyFloat == 116) then begin
           if (getQuest(GVAR_RANGERS_DUMB_DESTROY) >= qEnd) then begin
           replyFloat += 1;
           end
           else if (getQuest(GVAR_VILLA_ALLIANCE_RANGER) >= qEnd) then begin
           replyFloat += 2;
           end
       end

       // Реплики про Флагпул
       if (replyFloat == 119) then begin
           if (getQuest(GVAR_FLAGSTAFF_MINES_DESTROY) >= qEnd) then begin
           replyFloat += 1;
           end
           else if (getQuest(GVAR_FLAGSTAFF_MERPSWDR) >= qEnd) then begin
           replyFloat += 2;
           end
       end

       // Реплики про Феникс
       if (replyFloat == 122) then begin
           replyFloat += random(0,2);
       end

       // Падре
       if (replyFloat == 125) then begin
           if (global_var(GVAR_VILLA_STATUS_PADRE) < 0) then begin
           replyFloat += 1;
           end
           else if (global_var(GVAR_VILLA_STATUS_PADRE) == 1) then begin
           replyFloat += 2;
           end
       end

       // Мадре
       if (replyFloat == 128) then begin
           if (global_var(GVAR_VILLA_STATUS_MADRE) < 0) then begin
           replyFloat += 1;
           end
           else if (global_var(GVAR_VILLA_STATUS_MADRE) == 1) then begin
           replyFloat += 2;
           end
       end

       // Реплики про ГГ
       if (replyFloat == 131) then begin
           if town_rep_is_accepted then begin
           replyFloat += random(0,2);
           end
           else begin
           replyFloat += random(3,5);
           end
       end

       // Очищение реплик на время Совета
       if (map_var(MVAR_Plaza_Council) > 0) then begin
       replyFloat := 103;
       end
end

procedure critter_p_proc begin
   if (map_var(MVAR_Plaza_Council) > 0) then begin
   if (local_var(LVAR_Tile_B) > 0) then begin
      if (self_visible == true) then begin
      if (self_tile != local_var(LVAR_Tile_B)) then begin
      reg_anim_clear(self_obj);
      move_to(self_obj,local_var(LVAR_Tile_B),0);
      end
      end
   end
   end
   set_critter_options(3,0) //условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ
end

procedure use_skill_on_p_proc begin
   skillUseSteal
   skillUseFAid
end

procedure pickup_p_proc begin
   pickupProcCritter
end

procedure push_p_proc begin
   push_options(HomeTile,5,102) //Толкать (точка отсчета, макс расстояние, фраза нпс при толкании), удалить если не надо
end

procedure use_obj_on_p_proc begin
   UsedItemToCrit_Proc
end

procedure combat_p_proc begin
   effectAttak(0,0,0,0,0) //отравление min, облучение min, травма ног %, травма рук %, травма глаз %
end

procedure damage_p_proc begin
   set_damage_dude
end

procedure destroy_p_proc begin
   kill_critter_opt
end

procedure talk_p_proc begin
   if ((armor_pid_dude_var == PID_ARMOR_POWER_A) or (armor_pid_dude_var == PID_ARMOR_POWER_B)) then begin
   set_map_var(MVAR_Plaza_PwrArmr,1);
   floater(self_obj,random(220,229),COLOR_MSG_RED);
   end
   else if town_rep_is_accepted then begin
   floater(self_obj,random(200,202),COLOR_MSG_NORMAL);
   end
   else begin
   floater(self_obj,random(210,212),COLOR_MSG_NORMAL);
   end
end
