#include "..\headers\define.h"
//---------------------

#define NAME                            SCRIPT_GCCrvBos
#define TOWN_REP_VAR                    GVAR_TOWN_REP_GARAGE   // привязка к репутации (-1, если не нужно)

#define NUM_TEAM                        TEAM_CARAVAN          // группа, к которой принадлежит персонаж (-1 если нужно оставить Team, заданный в редакторе)
#define NUM_AI                          AI_CARAVAN_DRIVER     // AI персонажа (-1 если нужно оставить AI, заданный в редакторе)
#define KILL_SLAVA                      SLAVA_KILL_GOOD       // слава за убийство этого персонажа (SLAVA_KILL_GOOD, SLAVA_KILL_BAD, SLAVA_KILL_NEITRAL, SLAVA_KILL_CHILD)
#define KILL_TOWN_REP                   REP_KILL_GOOD         // репутация за убийство этого персонажа (REP_KILL_GOOD, REP_KILL_BAD, REP_KILL_NEITRAL, REP_KILL_CRITICAL для враждебности всего поселения, REP_KILL_CHILD)
#define KILL_FRAC_REP                   FRAC_NON              // принадлежность определенной фракции (-1, если не нужно)

#define FLAG_STEAL                      70 // Нижний порог навыка для кражи (для торговцев 200, для животных 300)
#define FLAG_ALCOHOL                    1  // Примет ли алкоголь от ГГ: 0 - принимает, 1 - не принимает
#define FLAG_FOOD                       1  // Примет ли пищу от ГГ: 0 - принимает, 1 - не принимает

//---------------------

procedure start;
procedure map_enter_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;
procedure pickup_p_proc;
procedure combat_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;

//---------------------

//#define LVAR_Flag_A                     (6) // отвечает за ...

//---------------------



procedure start begin
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
       set_start_home
       set_map_var(MVAR_GARAGE_TILE_CRVN, tile_num_in_direction(self_tile,3,3)); // изначально 21298, затем 21299

       if (global_var(GVAR_GARAGE_STATUS_CARAVAN) <= 0) then begin
       rm_timer_event(self_obj);
       set_obj_visibility(self_obj, 1);
       end
       else if ((getQuest(GVAR_GARAGE_QST_BOSS_TRADE) == 2) or (getQuest(GVAR_GARAGE_QST_LUCS_TRADE) == 2)) then begin
       set_obj_visibility(self_obj, 0);
       end
       else begin
       rm_timer_event(self_obj);
       set_obj_visibility(self_obj, 1);
       end
   end
end

procedure look_at_p_proc begin
   lookProcMsg
end

procedure description_p_proc begin
   dsrptProcMsg
end

procedure timed_event_p_proc begin
   // Таймер за номером 1 зарезервирован под перемещение персонажа по локации
   // Таймер за номером 2 зарезервирован под всплывающие реплики
   if (fixed_param == 1) then begin
       timer_move_to_home
   end
   if (fixed_param == 2) then begin
       //timer_float_reply(120,121)
   end
end

procedure critter_p_proc begin
   set_critter_options(0,0) //условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ
end

procedure use_skill_on_p_proc begin
   skillUseSteal
   skillUseFAid
end

procedure pickup_p_proc begin
   pickupProcCritter
end

procedure use_obj_on_p_proc begin
   UsedItemToCrit_Proc
end

procedure combat_p_proc begin
   effectAttak(0,0,0,0,0) //отравление min, облучение min, травма ног %, травма рук %, травма глаз %
end

procedure damage_p_proc begin
   set_damage_dude
end

procedure destroy_p_proc begin
   set_global_var(GVAR_GARAGE_STATUS_CARAVAN,-1);
   setQuest(GVAR_GARAGE_QST_BOSS_TRADE,-1);
   setQuest(GVAR_GARAGE_QST_LUCS_TRADE,-1);
   kill_critter_opt
end

procedure talk_p_proc begin
   if (global_var(GVAR_GARAGE_STATUS_CARAVAN) < 0) then begin
   display_mstr(111);
   end
   else if ((getQuest(GVAR_GARAGE_QST_BOSS_TRADE) > 2) or (getQuest(GVAR_GARAGE_QST_LUCS_TRADE) > 2)) then begin
   floater(self_obj,112,COLOR_MSG_NORMAL);
   end
   else if ((global_var(GVAR_GARAGE_STATUS_BOSS) < 0) and (global_var(GVAR_GARAGE_STATUS_LUCAS) < 0)) then begin
   floater(self_obj,113,COLOR_MSG_RED);
   end
   else if ((armor_pid_dude_var == PID_ARMOR_PURPLE_ROBE_A) or (armor_pid_dude_var == PID_ARMOR_PURPLE_ROBE_B) or (global_var(GVAR_TITUL_PHENIX_CULT) > 0)) then begin
   floater(self_obj,114,COLOR_MSG_GREEN);
   end
   else if ((armor_pid_dude_var == PID_ARMOR_POWER_A) or (armor_pid_dude_var == PID_ARMOR_POWER_B)) then begin
   floater(self_obj,115,COLOR_MSG_GREEN);
   end
   else begin
   floater(self_obj,110,COLOR_MSG_NORMAL);
   end
end
