#include "..\headers\define.h"
//---------------------

#define NAME                            SCRIPT_GCCtzB
#define TOWN_REP_VAR                    GVAR_TOWN_REP_GARAGE  // привязка к репутации (-1, если не нужно)

#define NUM_TEAM                        TEAM_GARAGE_CITIZEN   // группа, к которой принадлежит персонаж (-1 если нужно оставить Team, заданный в редакторе)
#define NUM_AI                          AI_PERSON_NORMAL      // AI персонажа (-1 если нужно оставить AI, заданный в редакторе)
#define KILL_SLAVA                      SLAVA_KILL_GOOD       // слава за убийство этого персонажа (SLAVA_KILL_GOOD, SLAVA_KILL_BAD, SLAVA_KILL_NEITRAL, SLAVA_KILL_CHILD)
#define KILL_TOWN_REP                   REP_KILL_GOOD         // репутация за убийство этого персонажа (REP_KILL_GOOD, REP_KILL_NEITRAL, REP_KILL_BAD, REP_KILL_CHILD, REP_KILL_CRITICAL)
#define KILL_FRAC_REP                   FRAC_NON              // принадлежность определенной фракции (-1, если не нужно)

#define FLAG_STEAL                      35 // Нижний порог навыка для кражи (для торговцев 200, для животных 300)
#define FLAG_ALCOHOL                    0  // Примет ли алкоголь от ГГ: 0 - принимает, 1 - не принимает
#define FLAG_FOOD                       0  // Примет ли пищу от ГГ: 0 - принимает, 1 - не принимает

//---------------------

procedure start;
procedure map_enter_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;
procedure pickup_p_proc;
procedure combat_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;

procedure Node998;
procedure Node999;
procedure NodeStartDialog;
procedure NodeFstDialog;
procedure NodeFstDialog2;
procedure NodeSndDialog;
procedure NodeAnswer100;
procedure NodeAnswer101;
procedure NodeAnswer102;
procedure NodeAnswer103;
procedure NodeAnswer104;
procedure NodeAnswer105;
procedure NodeAnswer106;
procedure NodeAnswer107;
procedure NodeAnswer200;
procedure NodeAnswer201;
procedure NodeAnswer202;

//---------------------

//#define LVAR_Flag_Food                   (6) // отвечает за ...

//---------------------



procedure start begin
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
       set_start_home
   end
end

procedure look_at_p_proc begin
   lookProcMsg
end

procedure description_p_proc begin
   dsrptProcMsg
end

procedure timed_event_p_proc begin
   // Таймер за номером 1 зарезервирован под перемещение персонажа по локации
   // Таймер за номером 2 зарезервирован под всплывающие реплики
   if (fixed_param == 1) then begin
       timer_move_random_home(2)
   end
   if (fixed_param == 2) then begin
       //timer_float_reply(120,121)
   end
end

procedure critter_p_proc begin
   set_critter_options(1,0) //условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ
end

procedure use_skill_on_p_proc begin
   skillUseSteal
   skillUseFAid
end

procedure pickup_p_proc begin
   pickupProcCritter
end

procedure use_obj_on_p_proc begin
   UsedItemToCrit_Proc
end

procedure combat_p_proc begin
   effectAttak(0,0,0,1,0) //отравление min, облучение min, травма ног %, травма рук %, травма глаз %
end

procedure damage_p_proc begin
   set_damage_dude
end

procedure destroy_p_proc begin
   if (getQuest(GVAR_GARAGE_QST_DOGS) == qStart) then begin
   setQuest(GVAR_GARAGE_QST_DOGS,qStop);
   end
   kill_critter_opt
end

procedure talk_p_proc begin
   if (getQuest(GVAR_GARAGE_QST_DOGS) >= qEnd) then begin
   floater(self_obj, 102, COLOR_MSG_NORMAL);
   end
   else begin
   dialog_proc(0,0,NodeStartDialog,-1,-1)
   end
end

procedure Node998 begin
   set_enemy_dude;
end

procedure Node999 begin
end

procedure NodeStartDialog begin
   if (bitDialog(bit_1) == 0) then begin
      call NodeFstDialog;
   end
   else begin
      call NodeSndDialog;
   end
end

//--- ПЕРВЫЙ ДИАЛОГ

procedure NodeFstDialog begin
   bitDialogExp(bit_1,0)
   Reply(200)
   NAnswer(201,1,NodeFstDialog2)
end

procedure NodeFstDialog2 begin
   Reply(202)
   NAnswer(203,1,NodeSndDialog)
   BAnswer(204,1,NodeSndDialog)
end

//--- ПОВТОРНЫЙ ДИАЛОГ

procedure NodeSndDialog begin
   if (getReaction>=25) then begin
   Reply(211)
   end
   else begin
   Reply(210)
   end

   NAnswer(250,1,NodeAnswer200)
   NAnswer(220,1,NodeAnswer100)
   if (getQuest(GVAR_GARAGE_QST_DOGS) == qStart) then begin
      NAnswer(232,1,NodeAnswer103)
      if (global_var(GVAR_GARAGE_DOGS_FACTUR)>=7) then begin
      GAnswer(236,1,NodeAnswer104)
      end
   end
   NAnswer(212,1,Node999)
end

//--- РАССПРОС ПРО СОБАК

procedure NodeAnswer100 begin
   Reply(221)
   NAnswer(222,1,NodeAnswer101)
   NAnswer(223,1,NodeSndDialog)
end

procedure NodeAnswer101 begin
   if (getQuest(GVAR_GARAGE_QST_DOGS) == qZero) then begin
   Reply(224)
   NAnswer(225,1,NodeAnswer102)
   NAnswer(230,1,NodeSndDialog)
   end
   else begin
   Reply(229)
   NAnswer(230,1,NodeSndDialog)
   end
end

procedure NodeAnswer102 begin
   setQuest(GVAR_GARAGE_QST_DOGS,qStart);
   Reply(226)
   NAnswer(231,1,NodeSndDialog)
   NAnswer(227,1,Node999)
   NAnswer(228,1,Node999)
end

procedure NodeAnswer103 begin
   Reply(233)
   NAnswer(234,1,NodeSndDialog)
   NAnswer(235,1,Node999)
end

procedure NodeAnswer104 begin
   setQuest(GVAR_GARAGE_QST_DOGS,qEnd);
   setBonusReaction(50)
   set_REP(2)
   exp_add(200);
   set_SLAVA(global_var(GVAR_GARAGE_DOGS_FACTUR))
   item_add(dude_obj,PID_BOOTLE_CAPS,(5*global_var(GVAR_GARAGE_DOGS_FACTUR)))

   Reply(237)
   NAnswer(238,1,NodeAnswer105)
   NAnswer(239,1,Node999)
end

procedure NodeAnswer105 begin
   Reply(240)
   BAnswer(242,7,NodeAnswer106)
   NAnswer(241,1,Node999)
end

procedure NodeAnswer106 begin
   Reply(243)
   NAnswer(245,7,NodeAnswer107)
   NAnswer(244,1,Node999)
end

procedure NodeAnswer107 begin
   bitDialogExp(bit_2,50)
   set_REP(-4)
   set_SLAVA(-5)
   setBonusReaction(-75)
   item_add(dude_obj,PID_BOOTLE_CAPS,100)
   Reply(246)
   NAnswer(247,1,Node999)
end

//--- РАССПРОСЫ ПРО ГАРАЖ И КРЫШКИ

procedure NodeAnswer200 begin
   Reply(251)
   NAnswer(252,1,NodeAnswer201)
   NAnswer(258,1,NodeSndDialog)
end

procedure NodeAnswer201 begin
   bitDialogExp(bit_3,0)
   Reply(253)
   NAnswer(254,1,NodeAnswer202)
end

procedure NodeAnswer202 begin
   Reply(255)
   NAnswer(256,1,NodeSndDialog)
   NAnswer(257,1,Node999)
end
