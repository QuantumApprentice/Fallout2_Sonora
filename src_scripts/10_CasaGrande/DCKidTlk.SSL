#include "..\headers\define.h"
//---------------------

#define NAME                            SCRIPT_DCKidTlk
#define TOWN_REP_VAR                    GVAR_TOWN_REP_CASA_GRANDE // привязка к репутации (-1, если не нужно)

#define NUM_TEAM                        TEAM_CASAGRANDE_PEASAN // группа, к которой принадлежит персонаж (-1 если нужно оставить Team, заданный в редакторе)
#define NUM_AI                          AI_CHILD              // AI персонажа (-1 если нужно оставить AI, заданный в редакторе)
#define KILL_SLAVA                      SLAVA_KILL_CHILD      // слава за убийство этого персонажа (SLAVA_KILL_GOOD, SLAVA_KILL_BAD, SLAVA_KILL_NEITRAL, SLAVA_KILL_CHILD)
#define KILL_TOWN_REP                   REP_KILL_CHILD        // репутация за убийство этого персонажа (REP_KILL_GOOD, REP_KILL_BAD, REP_KILL_NEITRAL, REP_KILL_CRITICAL для враждебности всего поселения, REP_KILL_CHILD)
#define KILL_FRAC_REP                   FRAC_NON              // принадлежность определенной фракции (-1, если не нужно)

#define FLAG_STEAL                      35 // Нижний порог навыка для кражи (для торговцев 200, для животных 300)
#define FLAG_ALCOHOL                    1  // Примет ли алкоголь от ГГ: 0 - принимает, 1 - не принимает
#define FLAG_FOOD                       1  // Примет ли пищу от ГГ: 0 - принимает, 1 - не принимает

//---------------------

procedure start;
procedure map_enter_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;
procedure pickup_p_proc;
procedure combat_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;

procedure Node998;
procedure Node999;
procedure NodeStartDialog;
procedure NodeFstDialog;
procedure NodeFstDialog2;
procedure NodeFstDialog3;
procedure NodeSndDialog;
procedure NodeSndDialog2;
procedure NodeAnswer100;
procedure NodeGranadDelete;
procedure NodeGranad;
procedure NodeGranad100;
procedure NodeGranad101;
procedure NodeGranad102;
procedure NodeGranad103;
procedure NodeGranad104;
procedure NodeGranad200;
procedure NodeGranad201;
procedure NodeGranad202;
procedure NodeGranad203;
procedure NodeGranad204;
procedure NodeGranad205;
procedure NodeGranad206;
procedure NodeGranad300;
procedure NodeGranad301;
procedure NodeGranad302;
procedure NodeGranad400;
procedure NodeGranad401;
procedure NodeGranad402;
procedure NodeGranad403;
procedure NodeGranad404;
procedure NodeMap100;
procedure NodeMap101;
procedure NodeMap102;
procedure NodeMap103;
procedure NodeMap200;
procedure NodeMap201;
procedure NodeMap202;

//---------------------

#define LVAR_GRANADA_STATUS                (6) // статус гранаты: 0 - у мальца, 1 - у ГГ, -1 - ГГ мотивировал вытащить кольцо

//---------------------



procedure start begin
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
       if (local_var(LVAR_GRANADA_STATUS) == 1) then begin
       set_local_var(LVAR_GRANADA_STATUS,2);
       end
       set_start_home
   end
end

procedure look_at_p_proc begin
   lookProcMsg
end

procedure description_p_proc begin
   dsrptProcMsg
end

procedure timed_event_p_proc begin
   // Таймер за номером 1 рарезервирован под перемещение персонажа по локации
   // Таймер за номером 2 рарезервирован под всплывающие реплики
   if (fixed_param == 1) then begin
       if (game_time_hour < 0600) then begin
       timer_move_to_tile(0,23138,3)
       end
       else begin
       timer_move_random_home(3)
       end
   end
   if (fixed_param == 2) then begin
       //timer_float_reply(110,119)
   end
   if (fixed_param == 3) then begin
       dude_teleport_map(1,1,MAP_CASA_GRANDE_SCHOOL,3);
   end
   if (fixed_param == 4) then begin
       item_remove(self_obj,PID_W_GRENADE_NUKE,1)
       item_remove(self_obj,PID_W_GRENADE_PLASMA,1)
       item_remove(self_obj,PID_W_GRENADE_FRAG,1)
       set_SLAVA(-50)
       set_REP(-3)
       exp_add(200);
       play_sfx("WHO1XXX1");
       critter_dmg(self_obj, 50, DMG_normal_dam);
   end
end

procedure critter_p_proc begin
   set_critter_options(1,0) //условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ
end

procedure use_skill_on_p_proc begin
   skillUseSteal
   skillUseFAid
end

procedure pickup_p_proc begin
   pickupProcCritter
end

procedure use_obj_on_p_proc begin
   UsedItemToCrit_Proc
end

procedure combat_p_proc begin
   effectAttak(0,0,0,0,1) //отравление min, облучение min, травма ног %, травма рук %, травма глаз %
end

procedure damage_p_proc begin
   set_damage_dude
end

procedure destroy_p_proc begin
   kill_critter_opt
end

procedure talk_p_proc begin
   if (game_time_hour < 0600) then begin
   z_floater(self_obj,102,COLOR_MSG_LIGHT_GREY);
   end
   else if (local_var(LVAR_GRANADA_STATUS) < 0) then begin
   end
   else begin
   setReaction(0) // Отношение к ГГ (1 - персонаж положительный, -1 отрицательный, 0 нейтральный)
   fracReaction(GVAR_TITUL_WARRIOR_ROAD,-50) // Поправка к отношению за принадлежность к фракциям
   dialog_proc(0,0,NodeStartDialog,-1,-1)
   end
end

procedure Node998 begin
   set_enemy_dude;
end

procedure Node999 begin
end

procedure NodeStartDialog begin
   if (bitDialog(bit_1) == 0) then begin
   call NodeFstDialog;
   end
   else if ((bitDialog(bit_3) == 0) and (armor_pid_dude_var == PID_ARMOR_VAULT)) then begin
   call NodeSndDialog2;
   end
   else if ((bitDialog(bit_3) == 0) and (armor_pid_dude_var <= 0)) then begin
   call NodeSndDialog2;
   end
   else begin
   call NodeSndDialog;
   end
end

//--- ПЕРВЫЙ ДИАЛОГ

procedure NodeFstDialog begin
   Reply(200)
   NAnswer(201,1,NodeFstDialog2)
end

procedure NodeFstDialog2 begin
   Reply(202)
   NAnswer(203,1,NodeFstDialog3)
   BAnswer(204,1,NodeFstDialog3)
end

procedure NodeFstDialog3 begin
   bitDialogExp(bit_1,0)

   item_add(self_obj,PID_W_BROCK,1)

   if (dude_luck >= 9) then begin
   item_add(self_obj,PID_W_GRENADE_NUKE,1)
   end
   else if (dude_luck >= 6) then begin
   item_add(self_obj,PID_W_GRENADE_PLASMA,1)
   end
   else begin
   item_add(self_obj,PID_W_GRENADE_FRAG,1)
   end

   inven_unwield(self_obj);

   Reply(205)
   call NodeGranad;
   NAnswer(206,1,Node999)
end

//--- ПОВТОРНЫЙ ДИАЛОГ

procedure NodeSndDialog begin
   inven_unwield(self_obj);
   if (local_var(LVAR_GRANADA_STATUS) == 0) then begin
   if ((item_amnt(self_obj,PID_W_GRENADE_FRAG) <= 0) and (item_amnt(self_obj,PID_W_GRENADE_PLASMA) <= 0) and (item_amnt(self_obj,PID_W_GRENADE_NUKE) <= 0)) then begin
   set_local_var(LVAR_GRANADA_STATUS,2); // поправка на случай кражи гранаты
   end
   end

   Reply(210)

   if (local_var(LVAR_GRANADA_STATUS) == 0) then begin
   call NodeGranad;
   end

   if (item_amnt(dude_obj,PID_RELICT_MUSIC_BOX) > 0) then begin
   NAnswer(300,1,NodeMap100)
   end

   NAnswer(290,1,NodeAnswer100)
   NAnswer(211,1,Node999)
end

procedure NodeSndDialog2 begin
   bitDialogExp(bit_3,0)
   Reply(212)
   NAnswer(213,1,NodeStartDialog)
   NAnswer(214,1,NodeStartDialog)
end

procedure NodeAnswer100 begin
   Reply(291)
   zNAnswer(292,1,NodeSndDialog)
   BAnswer(293,1,NodeSndDialog)
end

//--- ГРАНАТА

procedure NodeGranadDelete begin
   set_local_var(LVAR_GRANADA_STATUS,1);
   inven_unwield(self_obj);
   if (item_amnt(self_obj,PID_W_GRENADE_FRAG) > 0) then begin
       item_remove(self_obj,PID_W_GRENADE_FRAG,1)
       item_add(dude_obj,PID_W_GRENADE_FRAG,1)
   //drop_obj(obj_carrying_pid_obj(self_obj, PID_W_GRENADE_FRAG));
   end
   if (item_amnt(self_obj,PID_W_GRENADE_PLASMA) > 0) then begin
       item_remove(self_obj,PID_W_GRENADE_PLASMA,1)
       item_add(dude_obj,PID_W_GRENADE_PLASMA,1)
   //drop_obj(obj_carrying_pid_obj(self_obj, PID_W_GRENADE_PLASMA));
   end
   if (item_amnt(self_obj,PID_W_GRENADE_NUKE) > 0) then begin
       item_remove(self_obj,PID_W_GRENADE_NUKE,1)
       item_add(dude_obj,PID_W_GRENADE_NUKE,1)
   //drop_obj(obj_carrying_pid_obj(self_obj, PID_W_GRENADE_NUKE));
   end
end

procedure NodeGranad begin
   if (global_var(GVAR_TITUL_ARIZONA_RANGER) >0) then begin
   NAnswer(220,3,NodeGranad102)
   end
   NAnswer(230,1,NodeGranad100)
   NAnswerSkill(250,3,NodeGranad203,SKILL_CONVERSANT,50,240,NodeGranad200)
   NAnswerSkill(271,3,NodeGranad400,SKILL_GAMBLING,40,275,NodeGranad401)
   NAnswerSlava(260,3,NodeGranad300,-100,264,NodeGranad301)
end

procedure NodeGranad100 begin
   Reply(231)
   NAnswer(232,1,NodeGranad101)
end

procedure NodeGranad101 begin
   //goBarterMod;
   goBarter(0);
   Reply(233)
   NAnswer(234,1,Node999)
end

procedure NodeGranad102 begin
   Reply(221)
   NAnswer(222,1,NodeGranad103)
   NAnswer(223,1,Node999)
end

procedure NodeGranad103 begin
   Reply(224)
   NAnswer(225,1,NodeGranad104)
   NAnswer(226,1,Node999)
end

procedure NodeGranad104 begin
   call NodeGranadDelete;
   Reply(227)
   NAnswer(228,1,Node999)
end

procedure NodeGranad200 begin
   Reply(241)
   NAnswer(243,5,NodeGranad201)
   NAnswer(242,1,Node999)
end

procedure NodeGranad201 begin
   Reply(244)
   NAnswer(246,1,NodeGranad202)
   NAnswer(245,1,Node999)
end

procedure NodeGranad202 begin
   set_local_var(LVAR_GRANADA_STATUS,-1);
   add_timer_event(self_obj,5,4);
   call Node999;
end

procedure NodeGranad203 begin
   Reply(251)
   BAnswer(252,1,NodeGranad204)
end

procedure NodeGranad204 begin
   Reply(253)
   BAnswer(254,1,NodeGranad205)
end

procedure NodeGranad205 begin
   Reply(255)
   BAnswer(256,1,NodeGranad206)
end

procedure NodeGranad206 begin
   set_local_var(LVAR_GRANADA_STATUS,1);
   bitDialogExp(bit_2,150)
   call NodeGranadDelete;
   Reply(257)
   NAnswer(258,1,Node999)
end

procedure NodeGranad300 begin
   Reply(261)
   NAnswer(262,1,Node999)
end

procedure NodeGranad301 begin
   Reply(265)
   BAnswer(266,1,NodeGranad302)
   NAnswer(267,1,Node999)
end

procedure NodeGranad302 begin
   bitDialogExp(bit_2,150)
   call NodeGranadDelete;
   set_REP(-1)
   set_SLAVA(-10)
   Reply(268)
   BAnswer(269,1,Node999)
   NAnswer(270,1,Node999)
end

procedure NodeGranad400 begin
   Reply(272)
   NAnswer(273,1,NodeGranad402)
   NAnswer(274,1,Node999)
end

procedure NodeGranad401 begin
   Reply(276)
   NAnswer(277,1,NodeSndDialog)
   NAnswer(278,1,Node999)
end

procedure NodeGranad402 begin
   if (item_amnt(dude_obj,PID_BOOTLE_CAPS) < 5) then begin
   display_mstr(279);
   call Node999;
   end
   else if (has_skill(dude_obj, SKILL_GAMBLING) >= random(0,120)) then begin
   call NodeGranad403;
   end
   else begin
   call NodeGranad404;
   end
end

procedure NodeGranad403 begin
   set_local_var(LVAR_GRANADA_STATUS,1);
   bitDialogExp(bit_2,150)
   setBonusReaction(-20)
   call NodeGranadDelete;
   Reply(280)
   NAnswer(281,1,Node999)
   BAnswer(282,1,Node999)
end

procedure NodeGranad404 begin
   item_add(self_obj,PID_BOOTLE_CAPS,5)
   item_remove(dude_obj,PID_BOOTLE_CAPS,5)
   Reply(283)
   NAnswer(284,1,Node999)
   BAnswer(285,1,Node999)
end

//--- ТАЙНИК ЗА РЕЛИКВИЮ

procedure NodeMap100 begin
   Reply(301)
   BAnswer(305,1,NodeMap102)
   NAnswer(302,1,NodeMap101)
   BAnswer(314,1,Node999)
end

procedure NodeMap101 begin
   Reply(303)
   GAnswer(304,1,NodeMap200)
   BAnswer(305,1,NodeMap102)
   BAnswer(314,1,Node999)
end

procedure NodeMap102 begin
   setBonusReaction(-5)
   Reply(306)
   BAnswer(307,1,NodeMap103)
   GAnswer(309,1,NodeMap200)
   BAnswer(314,1,Node999)
end

procedure NodeMap103 begin
   setBonusReaction(-20)
   Reply(308)
   GAnswer(309,1,NodeMap200)
   BAnswer(314,1,Node999)
end

procedure NodeMap200 begin
   Reply(310)
   NAnswer(311,1,NodeMap201)
end

procedure NodeMap201 begin
   Reply(312)
   NAnswer(313,1,NodeMap202)
end

procedure NodeMap202 begin
   if (item_amnt(dude_obj,PID_RELICT_MUSIC_BOX) > 0) then begin
   item_remove(dude_obj,PID_RELICT_MUSIC_BOX,1)
   setBonusReaction(25)
   exp_add(500);
   g_display_mstr(491);
   add_timer_event(self_obj, 2, 3);
   end
   call Node999;
end
