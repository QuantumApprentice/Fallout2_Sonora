#include "..\headers\define.h"
//---------------------

#define NAME                            SCRIPT_FCCtznB
#define TOWN_REP_VAR                    GVAR_TOWN_REP_FLAGSTAFF   // привязка к репутации (-1, если не нужно)

#define NUM_TEAM                        TEAM_FLAGSTAFF_CITIZEN // группа, к которой принадлежит персонаж (-1 если нужно оставить Team, заданный в редакторе)
#define NUM_AI                          AI_PERSON_WIMPY       // AI персонажа (-1 если нужно оставить AI, заданный в редакторе)
#define KILL_SLAVA                      SLAVA_KILL_GOOD       // слава за убийство этого персонажа (SLAVA_KILL_GOOD, SLAVA_KILL_BAD, SLAVA_KILL_NEITRAL, SLAVA_KILL_CHILD)
#define KILL_TOWN_REP                   REP_KILL_GOOD         // репутация за убийство этого персонажа (REP_KILL_GOOD, REP_KILL_BAD, REP_KILL_NEITRAL, REP_KILL_CRITICAL для враждебности всего поселения, REP_KILL_CHILD)
#define KILL_FRAC_REP                   FRAC_NON              // принадлежность определенной фракции (-1, если не нужно)

#define FLAG_STEAL                      20 // Нижний порог навыка для кражи (для торговцев 200, для животных 300)
#define FLAG_ALCOHOL                    1  // Примет ли алкоголь от ГГ: 0 - принимает, 1 - не принимает
#define FLAG_FOOD                       1  // Примет ли пищу от ГГ: 0 - принимает, 1 - не принимает

//---------------------

procedure start;
procedure map_enter_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;
procedure pickup_p_proc;
procedure combat_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;

procedure Node998;
procedure Node999;
procedure NodeStartDialog;
procedure NodeStopDialog;
procedure NodeStopDialog2;
procedure NodeDialog;
procedure NodeDialog100;
procedure NodeDialog101;
procedure NodeDialog102;
procedure NodeDialog103;
procedure NodeDialog104;
procedure NodeDialog200;
procedure NodeDialog201;
procedure NodeDialog202;
procedure NodeDialog203;
procedure NodeDialog204;
procedure NodeDialog205;
procedure NodeDialog206;
procedure NodeDialog300;
procedure NodeDialog301;
procedure NodeDialog302;
procedure NodeQstKid;
procedure NodeQstKid1;
procedure NodeQstKid2;
procedure NodeQstKid3;
procedure NodeQstKid4;
procedure NodeQstKid5;

//---------------------

//#define LVAR_Flag_A                     (6) // отвечает за ...

#define TILE_CHORCH                     16483

//---------------------



procedure start begin
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
       set_start_home
   end
end

procedure look_at_p_proc begin
   lookProcMsg
end

procedure description_p_proc begin
   dsrptProcMsg
end

procedure timed_event_p_proc begin
   // Таймер за номером 1 рарезервирован под перемещение персонажа по локации
   // Таймер за номером 2 рарезервирован под всплывающие реплики
   if (fixed_param == 1) then begin
       if ((game_time_hour <= 0300) and (getQuest(GVAR_FLAGSTAFF_CULT_GOHOME) == qStart) and (map_var(MVAR_FLAG_CitiznB) == 0)) then begin
       timer_move_to_tile(0,tile_num_in_direction(TILE_CHORCH, random(0,5), random(1,5)),random(0,5))
       end
       else begin
       timer_move_to_home
       end
   end
   if (fixed_param == 2) then begin
       //timer_float_reply(120,121)
   end
   if (fixed_param == 3) then begin
       KillScorpTail(106) // фраза над жертвой (-1 если не нужна)
   end
end

procedure critter_p_proc begin
   set_critter_options(1,0) //условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ
end

procedure use_skill_on_p_proc begin
   skillUseSteal
   skillUseFAid
end

procedure pickup_p_proc begin
   pickupProcCritter
end

procedure use_obj_on_p_proc begin
   UsedItemToCrit_Proc
   if ((getQuest(GVAR_FLAGSTAFF_CULT_GOHOME) == qStart) and (map_var(MVAR_FLAG_CitiznB) != 0)) then begin
   UsedToCrit_Scorp(105,3) // фраза над жертвой (-1 если не нужна), номер таймера
   end
end

procedure combat_p_proc begin
   effectAttak(0,0,0,0,0) //отравление min, облучение min, травма ног %, травма рук %, травма глаз %
end

procedure damage_p_proc begin
   set_damage_dude
end

procedure destroy_p_proc begin
   kill_critter_opt
   set_map_var(MVAR_FLAG_CitiznB,-1);
   if (getQuest(GVAR_FLAGSTAFF_CULT_GOHOME) == qStart) then begin
       if ((map_var(MVAR_FLAG_CitiznB) < 0) and (global_var(GVAR_STATUS_FLAGSTAFF_PASTOR) < 0)) then begin
       setQuest(GVAR_FLAGSTAFF_CULT_GOHOME,qZero);
       end
   end
end

procedure talk_p_proc begin
   setReaction(0) // Отношение к ГГ(1 - персонаж положительный, -1 отрицательный, 0 нейтральный)
   fracReaction(GVAR_TITUL_PHENIX_CULT,-40) // Поправка к отношению за принадлежность к фракциям
   fracReaction(GVAR_TITUL_DUDE_IS_SLAVE,-20) // Поправка к отношению за принадлежность к фракциям

   ScorpPoisonFlt
   else if ((game_time_hour <= 0300) and (tile_distance(self_tile, TILE_CHORCH) <= 5) and (getQuest(GVAR_FLAGSTAFF_CULT_GOHOME) == qStart) and (map_var(MVAR_FLAG_CitiznB) == 0)) then begin
      dialog_proc(1,0,NodeStopDialog,-1,-1)
   end
   else if (game_time_hour <= 0600) then begin
      floater(self_obj,102,COLOR_MSG_RED);
   end
   else if (getQuest(GVAR_FLAGSTAFF_RAD_KID_STATUS) == -1) then begin
      floater(self_obj,104,COLOR_MSG_RED);
   end
   else begin
      if (map_var(MVAR_FLAG_CitiznB) == 0) then begin
      floater(self_obj,103,COLOR_MSG_NORMAL);
      end
      else begin
      dialog_proc(1,0,NodeStartDialog,-1,-1)
      end
   end
end

procedure Node998 begin
   set_enemy_dude;
end

procedure Node999 begin
end

procedure NodeStartDialog begin
   call NodeDialog;
end

//-----------------------

procedure NodeStopDialog begin
   set_map_var(MVAR_FLAG_CitiznB,1);
   bitDialogExp(bit_2,50)
   Reply(200)
   NAnswer(201,1,NodeStopDialog2)
end

procedure NodeStopDialog2 begin
   Reply(202)
   NAnswer(203,1,Node999)
end

//-----------------------

procedure NodeDialog begin
   bitDialogExp(bit_1,0)

   if (getQuest(GVAR_FLAGSTAFF_RAD_KID_STATUS) > qEnd) then begin
      Reply(226)
   end
   else if (getQuest(GVAR_FLAGSTAFF_RAD_KID_STATUS) == qEnd) then begin
      Reply(224)
   end
   else begin
      if (getReaction>=50) then begin
      Reply(224)
      end
      else if (getReaction>=20) then begin
      Reply(225)
      end
      else begin
      Reply(210)
      end
   end


   if ((map_var(MVAR_FLAG_CitiznB) > 0) and (getQuest(GVAR_FLAGSTAFF_CULT_GOHOME) == qStart)) then begin
      NAnswer(212,1,NodeDialog100)
      NAnswer(250,1,NodeDialog300)
      if (global_var(GVAR_STATUS_FLAGSTAFF_PASTOR) < 0) then begin
      NAnswer(241,1,NodeDialog206)
      end
      else if (map_var(MVAR_FLAG_CitiznB) == 1) then begin
      NAnswerSkill(230,3,NodeDialog200,SKILL_CONVERSANT,20,230,NodeDialog201)
      end
   end
   if (getQuest(GVAR_FLAGSTAFF_RAD_KID_STATUS) >= qStart) then begin
      NAnswer(271,1,NodeQstKid5)
   end
   if (bitDialog(bit_7) == 0) then begin
      if (getQuest(GVAR_FLAGSTAFF_RAD_KID_STATUS) == (qEnd+1)) then begin
      NAnswer(260,1,NodeQstKid4)
      end
      if (getQuest(GVAR_FLAGSTAFF_RAD_KID_STATUS) == qEnd) then begin
      NAnswer(259,1,NodeQstKid1)
      end
   end

   NAnswer(211,1,Node999)
end

//--- Отношение к культу

procedure NodeDialog100 begin
   Reply(213)
   NAnswer(214,1,NodeDialog101)
end

procedure NodeDialog101 begin
   Reply(215)
   NAnswer(216,1,NodeDialog102)
   NAnswer(204,1,NodeDialog)
   NAnswer(217,1,Node999)
end

procedure NodeDialog102 begin
   Reply(218)
   NAnswer(219,1,NodeDialog103)
end

procedure NodeDialog103 begin
   Reply(220)
   NAnswer(221,1,NodeDialog104)
   NAnswer(222,1,Node999)
end

procedure NodeDialog104 begin
   Reply(223)
   NAnswer(222,1,Node999)
end

//--- Завершение квеста на священника

procedure NodeDialog200 begin
   Reply(231)
   NAnswerSkill(232,3,NodeDialog202,SKILL_CONVERSANT,40,232,NodeDialog203)
   if (getQuest(GVAR_FLAGSTAFF_RAD_KID_STATUS) == (qEnd+1)) then begin
   NAnswer(244,1,NodeDialog205)
   end
   NAnswer(233,1,Node999)
end

procedure NodeDialog201 begin
   Reply(231)
   if (getQuest(GVAR_FLAGSTAFF_RAD_KID_STATUS) == (qEnd+1)) then begin
   NAnswer(244,1,NodeDialog205)
   end
   NAnswer(233,1,Node999)
end

procedure NodeDialog202 begin
   bitDialogExp(bit_3,50)
   Reply(234)
   NAnswerSkill(235,3,NodeDialog205,SKILL_CONVERSANT,60,235,NodeDialog204)
   NAnswer(236,1,Node999)
end

procedure NodeDialog203 begin
   Reply(234)
   NAnswer(236,1,Node999)
end

procedure NodeDialog204 begin
   Reply(237)
   NAnswer(238,1,Node999)
end

procedure NodeDialog205 begin
   set_map_var(MVAR_FLAG_CitiznB,2);
   bitDialogExp(bit_4,100)
   Reply(239)
   NAnswer(240,1,Node999)
end

procedure NodeDialog206 begin
   bitDialogExp(bit_5,350)
   setQuest(GVAR_FLAGSTAFF_CULT_GOHOME,qEnd);
   item_add(dude_obj,PID_BOOTLE_CAPS,100)
   Reply(242)
   if (getQuest(GVAR_FLAGSTAFF_RAD_KID_STATUS) == qZero) then begin
   NAnswer(243,1,NodeQstKid)
   end
   else begin
   NAnswer(245,1,Node999)
   end
end

//--- Квест на лечение ребенка

procedure NodeDialog300 begin
   Reply(251)
   NAnswer(253,1,NodeDialog301)
   NAnswer(252,1,NodeDialog)
end

procedure NodeDialog301 begin
   Reply(254)
   NAnswer(255,1,NodeDialog302)
   NAnswer(252,1,NodeDialog)
end

procedure NodeDialog302 begin
   Reply(256)
   if (getQuest(GVAR_FLAGSTAFF_RAD_KID_STATUS) == qZero) then begin
   NAnswer(257,1,NodeQstKid)
   end
   NAnswer(252,1,NodeDialog)
end

procedure NodeQstKid begin
   setQuest(GVAR_FLAGSTAFF_RAD_KID_STATUS,qStart);
   Reply(258)
   NAnswer(268,1,Node999)
end

procedure NodeQstKid1 begin
   bitDialogExp(bit_7,50)
   setBonusReaction(90)
   set_REP(3)
   Reply(261)
   NAnswer(265,1,NodeQstKid3)
   GAnswer(262,1,NodeQstKid2)
end

procedure NodeQstKid2 begin
   setBonusReaction(10)
   set_REP(1)
   set_SLAVA(20)
   Reply(263)
   NAnswer(264,1,Node999)
end

procedure NodeQstKid3 begin
   item_add(dude_obj,PID_BOOTLE_CAPS,100)
   Reply(266)
   NAnswer(267,1,Node999)
end

procedure NodeQstKid4 begin
   bitDialogExp(bit_7,50)
   Reply(269)
   NAnswer(270,1,Node999)
end

procedure NodeQstKid5 begin
   bitDialogExp(bit_6,25)
   Reply(272)
   NAnswer(204,1,NodeDialog)
end
