#include "..\headers\define.h"
//---------------------

#define NAME                            SCRIPT_FCVillB
#define TOWN_REP_VAR                    GVAR_TOWN_REP_VILLA   // привязка к репутации (-1, если не нужно)

#define NUM_TEAM                        TEAM_PLAYER           // группа, к которой принадлежит персонаж (-1 если нужно оставить Team, заданный в редакторе)
#define NUM_AI                          AI_TRIBLE_VILLA       // AI персонажа (-1 если нужно оставить AI, заданный в редакторе)
#define KILL_SLAVA                      SLAVA_KILL_GOOD       // слава за убийство этого персонажа (SLAVA_KILL_GOOD, SLAVA_KILL_BAD, SLAVA_KILL_NEITRAL, SLAVA_KILL_CHILD)
#define KILL_TOWN_REP                   REP_KILL_NEITRAL      // репутация за убийство этого персонажа (REP_KILL_GOOD, REP_KILL_BAD, REP_KILL_NEITRAL, REP_KILL_CRITICAL для враждебности всего поселения, REP_KILL_CHILD)
#define KILL_FRAC_REP                   FRAC_NON              // принадлежность определенной фракции (-1, если не нужно)

#define FLAG_STEAL                      0  // Нижний порог навыка для кражи (для торговцев 200, для животных 300)
#define FLAG_ALCOHOL                    1  // Примет ли алкоголь от ГГ: 0 - принимает, 1 - не принимает
#define FLAG_FOOD                       1  // Примет ли пищу от ГГ: 0 - принимает, 1 - не принимает

//---------------------

procedure start;
procedure map_enter_p_proc;
procedure map_update_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;
procedure pickup_p_proc;
procedure push_p_proc;
procedure combat_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;

procedure Node998;
procedure Node999;
procedure NodeStartDialog;
procedure NodeFstDialog;
procedure NodeFstDialog2;
procedure NodeFstDialog3;
procedure NodeSndDialog;
procedure NodeSndDialog100;
procedure NodeSndDialog101;
procedure NodeSndDialog102;
procedure NodeFinalDialog;

//---------------------

//#define LVAR_Flag_A                     (6) // отвечает за ...

//---------------------

/*
   ВНИМАНИЕ!!! Этот скрипт необходимо синхронизировать с FCVillA
*/



procedure start begin
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
       set_start_home

       if ((global_var(GVAR_VILLA_STATUS_HUGO) >= 2) or (getQuest(GVAR_FLAGSTAFF_SLAVES_MINE) >= qEnd)) then begin
       rm_timer_event(self_obj);
       set_obj_visibility(self_obj, 1);
       destroy_object(self_obj);
       end
   end
end

procedure map_update_p_proc begin
   if (getQuest(GVAR_FLAGSTAFF_CAVEVILLA) >= qEnd) then begin
       if (global_var(GVAR_VILLA_STATUS_HUGO) < 2) then begin
       exp_add(250);
       set_REP(2)
       display_mstr(105);
       set_obj_visibility(self_obj, 1);
       set_global_var(GVAR_VILLA_STATUS_HUGO, 2);
       destroy_object(self_obj);
       end
   end
end

procedure look_at_p_proc begin
   lookProcMsg
end

procedure description_p_proc begin
   dsrptProcMsg
end

procedure timed_event_p_proc begin
   // Таймер за номером 1 рарезервирован под перемещение персонажа по локации
   // Таймер за номером 2 рарезервирован под всплывающие реплики
   if (fixed_param == 1) then begin
       //timer_move_to_home
   end
   if (fixed_param == 2) then begin
       //timer_float_reply(120,121)
   end
end

procedure critter_p_proc begin
   if (getQuest(GVAR_FLAGSTAFF_CAVEVILLA) <= qStop) then begin
   set_critter_options(2,1) //условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ
   end
   else begin
   set_critter_options(0,0) //условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ
   end

   if (getQuest(GVAR_FLAGSTAFF_CAVEVILLA) == qStart) then begin
   partyRunToDude
   end
end

procedure use_skill_on_p_proc begin
   skillUseSteal
   skillUseFAid
end

procedure pickup_p_proc begin
   pickupProcCritter
end

procedure push_p_proc begin
   if (getQuest(GVAR_FLAGSTAFF_CAVEVILLA) <= qZero) then begin
   push_options(HomeTile,3,106) //Толкать (точка отсчета, макс расстояние, фраза нпс при толкании), удалить если не надо
   end
end

procedure use_obj_on_p_proc begin
   UsedItemToCrit_Proc
end

procedure combat_p_proc begin
   effectAttak(0,0,0,0,0) //отравление min, облучение min, травма ног %, травма рук %, травма глаз %
end

procedure damage_p_proc begin
   set_damage_dude
end

procedure destroy_p_proc begin
   kill_critter_opt
   if (cur_map_index == MAP_FLAGSTAFF_CAVE) then begin
   set_map_var(MVAR_FLAG_RatKingVsbl,1);
   end
   set_global_var(GVAR_VILLA_STATUS_HUGO,-1);
   if ((global_var(GVAR_VILLA_STATUS_SANCHO) < 0) and (global_var(GVAR_VILLA_STATUS_HUGO) < 0)) then begin
   setQuest(GVAR_FLAGSTAFF_SLAVES_MINE,qStop);
   end
   display_mstr(104);
   if (source_obj == dude_obj) then begin
   setQuest(GVAR_FLAGSTAFF_CAVEVILLA,qStop);
   floater(dude_obj,102,COLOR_MSG_RED);
   end
end

procedure talk_p_proc begin
   setReaction(1) // Отношение к ГГ(1 - персонаж положительный, -1 отрицательный, 0 нейтральный)
   if (getQuest(GVAR_FLAGSTAFF_CAVEVILLA) <= qStop) then begin
      floater(self_obj,103,COLOR_MSG_RED);
   end
   else if (getQuest(GVAR_FLAGSTAFF_SLAVES_MINE) >= qEnd) then begin
      floater(self_obj,107,COLOR_MSG_NORMAL);
   end
   else begin
      dialog_proc(0,0,NodeStartDialog,-1,-1)
   end
end

procedure Node998 begin
   set_enemy_dude;
end

procedure Node999 begin
end

procedure NodeStartDialog begin
   if (getQuest(GVAR_FLAGSTAFF_CAVEVILLA) != qZero) then begin
   bitDialogExp(bit_1,0)
   end

   if (cur_map_index == MAP_FLAGSTAFF_CAVE) then begin
      if (bitDialog(bit_1) == 0) then begin
      call NodeFstDialog;
      end
      else begin
      call NodeSndDialog;
      end
   end
   else begin
      call NodeFinalDialog;
   end
end

//----- ПЕРВЫЙ ДИАЛОГ

procedure NodeFstDialog begin
   Reply(dude_name+mstr(200))
   NAnswer(201,1,NodeFstDialog2)
end

procedure NodeFstDialog2 begin
   Reply(202)
   NAnswer(203,1,NodeFstDialog3)
end

procedure NodeFstDialog3 begin
   bitDialogExp(bit_1,0)
   Reply(204)
   NAnswer(205,1,Node999)
end

//----- ПОВТОРНЫЙ ДИАЛОГ

procedure NodeSndDialog begin
   if (global_var(GVAR_FLAGSTAFF_RAT_KING_LIFE) == 0) then begin
   Reply(207)
   end
   else begin
   Reply(random(207,208))
   end

   if (getQuest(GVAR_FLAGSTAFF_CAVEVILLA) == qStart) then begin
      if ((dude_elevation == 0) and (tile_distance(dude_tile, 27901) < 13)) then begin
      GAnswer(206,1,NodeFinalDialog)
      end
   end
   if (bitDialog(bit_2) == 0) then begin
      NAnswer(210,4,NodeSndDialog100)
   end
   NAnswer(209,1,Node999)
end

procedure NodeSndDialog100 begin
   Reply(211)
   NAnswer(212,1,NodeSndDialog101)
   NAnswer(218,1,Node999)
end

procedure NodeSndDialog101 begin
   Reply(213)
   BAnswer(214,1,NodeSndDialog102)
   NAnswer(219,1,Node999)
end

procedure NodeSndDialog102 begin
   bitDialogExp(bit_2,50)
   Reply(215)
   BAnswer(216,1,Node999)
   NAnswer(217,1,Node999)
end

//----- ФИНАЛЬНЫЙ ДИАЛОГ

procedure NodeFinalDialog begin
   setQuest(GVAR_FLAGSTAFF_SLAVES_MINE,qEnd);
   setQuest(GVAR_FLAGSTAFF_CAVEVILLA,qEnd);

   Reply(222)
   NAnswer(223,1,Node999)
end
