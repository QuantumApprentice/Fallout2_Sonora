#include "..\headers\define.h"
#include "..\headers\rangers.h"
//---------------------

#define NAME                            SCRIPT_RCWshKid
#define TOWN_REP_VAR                    GVAR_TOWN_REP_RANGERS // привязка к репутации (-1, если не нужно)

#define NUM_TEAM                        TEAM_CARAVAN          // группа, к которой принадлежит персонаж (-1 если нужно оставить Team, заданный в редакторе)
#define NUM_AI                          -1                    // AI персонажа (-1 если нужно оставить AI, заданный в редакторе)
#define KILL_SLAVA                      SLAVA_KILL_CHILD      // слава за убийство этого персонажа (SLAVA_KILL_GOOD, SLAVA_KILL_BAD, SLAVA_KILL_NEITRAL, SLAVA_KILL_CHILD)
#define KILL_TOWN_REP                   REP_KILL_CHILD        // репутация за убийство этого персонажа (REP_KILL_GOOD, REP_KILL_NEITRAL, REP_KILL_BAD, REP_KILL_CHILD, REP_KILL_CRITICAL)
#define KILL_FRAC_REP                   FRAC_NON              // принадлежность определенной фракции (-1, если не нужно)

#define FLAG_STEAL                      10 // Нижний порог навыка для кражи (для торговцев 200, для животных 300)
#define FLAG_ALCOHOL                    0  // Примет ли алкоголь от ГГ: 0 - принимает, 1 - не принимает
#define FLAG_FOOD                       0  // Примет ли пищу от ГГ: 0 - принимает, 1 - не принимает

//---------------------

procedure start;
procedure map_enter_p_proc;
procedure map_update_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;
procedure pickup_p_proc;
procedure combat_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;

procedure Node998;
procedure Node999;
procedure NodeStartDialog;
procedure NodeDialog100;
procedure NodeDialog101;

//---------------------

#define LVAR_Dialog_Stop                (6) // фиксирует факт связи когда ГГ выходит из пещеры после убийства монстра

//---------------------



procedure start begin
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
       set_start_home
       if DUDE_ESCAPED then begin
       /*
       !!! разместить на карте могилу отца и матери, если:
           + ГГ сбежал
           + бандит остался жив и не сбежал из хибары
           + отец и мать не были физически убиты в боевом режиме (иначе их тела останутся на карте вместе с могилами, что не логично)
       */
       set_gvar_bit_on(GVAR_RANGERS_QST_CADET_MASK, bit_3);
       end
       if DEAD_MILTON_CHILD then begin
       rm_timer_event(self_obj);
       move_to(self_obj, 9560, 0);
       set_obj_visibility(self_obj, 1);
       end
   end
end

procedure map_update_p_proc begin
   if (gvar_bit(GVAR_RANGERS_QST_CADET_MASK, bit_12) != 0) then begin
      if (local_var(LVAR_Dialog_Stop) == 0) then begin

         if (MILTON_ENEMY_TO_DUDE or DEAD_MILTON_CHILD or DEAD_MILTON_MISS or DEAD_MILTON_MSTR) then begin
             set_gvar_bit_off(GVAR_RANGERS_QST_CADET_MASK, bit_12);
         end
         else if DUDE_ESCAPED then begin
             set_gvar_bit_off(GVAR_RANGERS_QST_CADET_MASK, bit_12);
         end
         else if (BANDIT_INVISIBLE and BANDIT_LIFE) then begin
             set_local_var(LVAR_Flags,0);
             off_enemy_dude;
             rm_timer_event(self_obj);
             reg_anim_clear(self_obj);
             move_to(self_obj, 12703, 0);
             anim(self_obj, ANIMATE_ROTATION, 5);

             obj_close(tile_contains_pid_obj(22733, 0, 33555338));
             obj_close(tile_contains_pid_obj(20938, 0, 33555340));
             //obj_lock(tile_contains_pid_obj(20938, 0, 33555340));
             if (tile_contains_obj_pid(22342,0,50331764) == true) then begin
             create_object_sid(33555936, 22342, 0, SCRIPT_ZSBlock);
             obj_lock(tile_contains_pid_obj(22342, 0, 33555936));
             destroy_object(tile_contains_pid_obj(22342, 0, 50331764));
             end

             set_gvar_bit_on(GVAR_RANGERS_QST_CADET_MASK, bit_13);
             set_gvar_bit_on(GVAR_RANGERS_QST_CADET_MASK, bit_8);
         end

         set_local_var(LVAR_Dialog_Stop,1);
      end
   end
end

procedure look_at_p_proc begin
   lookProcMsg
end

procedure description_p_proc begin
   dsrptProcMsg
end

procedure timed_event_p_proc begin
   // Таймер за номером 1 зарезервирован под перемещение персонажа по локации
   // Таймер за номером 2 зарезервирован под всплывающие реплики
   if (fixed_param == 1) then begin
       if BANDIT_INVISIBLE then begin
       timer_move_random_home(6)
       end
   end
   if (fixed_param == 2) then begin
       //timer_float_reply(120,121)
   end
end

procedure critter_p_proc begin
   set_critter_options(1,0) //условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ
   if (gvar_bit(GVAR_RANGERS_QST_CADET_MASK, bit_12) != 0) then begin
   dialogue_system_forcing(10)
   end
end

procedure use_skill_on_p_proc begin
   skillUseSteal
   skillUseFAid
end

procedure pickup_p_proc begin
   pickupProcCritter
end

procedure use_obj_on_p_proc begin
   UsedItemToCrit_Proc
end

procedure combat_p_proc begin
   effectAttak(0,0,0,0,0) //отравление min, облучение min, травма ног %, травма рук %, травма глаз %
end

procedure damage_p_proc begin
   set_damage_dude
end

procedure destroy_p_proc begin
   set_gvar_bit_on(GVAR_RANGERS_QST_CADET_MASK, bit_3);
   if (source_obj == dude_obj) then begin
   set_gvar_bit_on(GVAR_RANGERS_QST_CADET_MASK, bit_1);
   end
   kill_critter_opt
end

procedure talk_p_proc begin
   if (gvar_bit(GVAR_RANGERS_QST_CADET_MASK, bit_12) != 0) then begin
      dialog_proc(0,0,NodeStartDialog,-1,-1)
   end
   else if MILTON_ENEMY_TO_DUDE then begin
      floater(self_obj,115,COLOR_MSG_RED);
   end
   else if (BANDIT_DEAD or BANDIT_RUN) then begin
      if DEAD_MILTON_MISS then begin
      floater(self_obj,114,COLOR_MSG_RED);
      end
      else if DEAD_MILTON_MSTR then begin
      floater(self_obj,113,COLOR_MSG_RED);
      end
      else begin
      floater(self_obj,112,COLOR_MSG_GREEN);
      end
   end
   else if BANDIT_VISIBLE then begin
      floater(self_obj,111,COLOR_MSG_RED);
   end
   else if RATKING_DEAD then begin
      floater(self_obj,116,COLOR_MSG_GREEN);
   end
   else begin
      floater(self_obj,110,COLOR_MSG_NORMAL);
   end
end

procedure Node998 begin
   set_enemy_dude;
end

procedure Node999 begin
end

procedure NodeStartDialog begin
   call NodeDialog100;
end

procedure NodeDialog100 begin
   set_gvar_bit_on(GVAR_RANGERS_QST_CADET_MASK, bit_11);
   set_gvar_bit_off(GVAR_RANGERS_QST_CADET_MASK, bit_12);
   Reply(200)
   NAnswer(201,1,NodeDialog101)
   NAnswer(202,1,NodeDialog101)
   NAnswer(206,1,NodeDialog101)
end

procedure NodeDialog101 begin
   Reply(203)
   NAnswer(204,1,Node999)
   NAnswer(205,1,Node999)
end
