#include "..\headers\define.h"
//---------------------

#define NAME                            SCRIPT_ZCDog
#define TOWN_REP_VAR                    -1   // привязка к репутации (-1, если не нужно)

#define NUM_TEAM                        TEAM_DOG              // группа, к которой принадлежит персонаж (-1 если нужно оставить Team, заданный в редакторе)
#define NUM_AI                          -1                    // AI персонажа (-1 если нужно оставить AI, заданный в редакторе)
#define KILL_SLAVA                      SLAVA_KILL_NEITRAL    // слава за убийство этого персонажа (SLAVA_KILL_GOOD, SLAVA_KILL_BAD, SLAVA_KILL_NEITRAL, SLAVA_KILL_CHILD)
#define KILL_TOWN_REP                   REP_KILL_NEITRAL      // репутация за убийство этого персонажа (REP_KILL_GOOD, REP_KILL_BAD, REP_KILL_NEITRAL, REP_KILL_CRITICAL для враждебности всего поселения, REP_KILL_CHILD)
#define KILL_FRAC_REP                   FRAC_NON              // принадлежность определенной фракции (-1, если не нужно)

#define FLAG_STEAL                      300 // Нижний порог навыка для кражи (для торговцев 200, для животных 300)
#define FLAG_ALCOHOL                    1  // Примет ли алкоголь от ГГ: 0 - принимает, 1 - не принимает
#define FLAG_FOOD                       0  // Примет ли пищу от ГГ: 0 - принимает, 1 - не принимает

//---------------------

procedure start;
procedure map_enter_p_proc;
procedure look_at_p_proc;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;
procedure pickup_p_proc;
procedure combat_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;

//---------------------



procedure start begin
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
       set_start_home
       set_obj_visibility(self_obj, 0);

       if (cur_map_index == MAP_CASA_GRANDE_WATER) then begin
       critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_CASAGRANDE_CITYHALL);
       end
       if (cur_map_index == MAP_BOS_CAMP_A) then begin
       critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_BANDITS);
       end
       if (cur_map_index == MAP_BOS_CAMP_B) then begin
       critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_BANDITS);
       end
       if (cur_map_index == MAP_BOS_CAMP_C) then begin
       critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_BANDITS);
       end

       if ((cur_map_index == MAP_PHOENIX_DSTROY) and (getQuest(GVAR_PHENIX_PARTY_QST_A) == qStart)) then begin
          rm_timer_event(self_obj);
          set_obj_visibility(self_obj, 1);
       end
       else if ((cur_map_index == MAP_CASA_GRANDE_BAR) and (getQuest(GVAR_CGRANDE_ATTAK_TO_BAR) == qStart)) then begin
          rm_timer_event(self_obj);
          set_obj_visibility(self_obj, 1);
       end
       else if ((cur_map_index == MAP_RANGERS_MOHAVE) and (getQuest(GVAR_RANGERS_QST_IMMIGRANTS) == qStart)) then begin
          rm_timer_event(self_obj);
          set_obj_visibility(self_obj, 1);
       end
       else if (cur_map_index == MAP_GARAGE_HISTORY) then begin
          if ((getQuest(GVAR_GARAGE_QST_BOSS_TRADE) == 2) or (getQuest(GVAR_GARAGE_QST_LUCS_TRADE) == 2)) then begin
          rm_timer_event(self_obj);
          set_obj_visibility(self_obj, 1);
          end
       end
   end
end

procedure look_at_p_proc begin
   script_overrides;
   if (cur_map_index == MAP_CASA_GRANDE_WATER) then begin
   display_mstr(103);
   end
   else if (self_PID == PID_COYOTE) then begin
   display_mstr(100);
   end
   else if (self_PID == PID_WOLF) then begin
   display_mstr(101);
   end
   else begin
   display_mstr(102);
   end
end

procedure timed_event_p_proc begin
   // Таймер за номером 1 рарезервирован под перемещение персонажа по локации
   // Таймер за номером 2 рарезервирован под всплывающие реплики
   if (fixed_param == 1) then begin
       if (item_amnt(self_obj,PID_AMMO_SHOTGUN_SHELLS) > 0) then begin
       timer_move_to_home
       end
       else if (cur_map_index == MAP_CASA_GRANDE_WATER) then begin
       timer_move_random_home(3)
       end
       else if (cur_map_index == MAP_SPECIAL_VAULT) then begin
       timer_move_random_home(5)
       end
       else if (HomeTile > 0) then begin
       timer_move_random_home(7)
       end
       else begin
       timer_move_random
       end
   end
   if (fixed_param == 2) then begin
       //timer_float_reply(120,121)
   end
end

procedure critter_p_proc begin
   if ((item_amnt(self_obj,PID_AMMO_SHOTGUN_SHELLS) > 0) or (cur_map_index == MAP_PHOENIX_BNDCS) or (cur_map_index == MAP_PHOENIX_SIGN) or (cur_map_index == MAP_CASA_GRANDE_WATER) or (cur_map_index == MAP_SPECIAL_VAULT)) then begin
   set_critter_options(0,0) //условие нападения (0 только личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ
   end
   else begin
   set_critter_options(2,0) //условие нападения (0 только личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ
   end
end

procedure use_skill_on_p_proc begin
   skillUseSteal
   skillUseFAid
end

procedure pickup_p_proc begin
   pickupProcCritter
end

procedure use_obj_on_p_proc begin
   UsedItemToCrit_Proc
end

procedure combat_p_proc begin
   effectAttak(0,0,1,1,0) //отравление min, облучение min, травма ног %, травма рук %, травма глаз %
   effectIntimidationPerk
end

procedure damage_p_proc begin
   set_damage_dude
end

procedure destroy_p_proc begin
   if (cur_map_index == MAP_GARAGE_TRILER) then begin
   inc_global_var(GVAR_GARAGE_DOGS_FACTUR);
   end
   if (item_amnt(self_obj,PID_AMMO_SHOTGUN_SHELLS) > 0) then begin
   item_remove(self_obj,PID_AMMO_SHOTGUN_SHELLS,999)
   end
   kill_critter_opt
end

procedure talk_p_proc begin
   script_overrides;
   floater(self_obj,random(104,105),COLOR_MSG_NORMAL);
end
