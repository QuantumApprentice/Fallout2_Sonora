#include "..\headers\define.h"
//---------------------

#define NAME                            SCRIPT_MCRaider
#define TOWN_REP_VAR                    GVAR_TOWN_REP_RAIDERS // привязка к репутации (-1, если не нужно)

#define NUM_TEAM                        TEAM_WAR_ROADS        // группа, к которой принадлежит персонаж (-1 если нужно оставить Team, заданный в редакторе)
#define NUM_AI                          AI_WAR_ROAD           // AI персонажа (-1 если нужно оставить AI, заданный в редакторе)
#define KILL_SLAVA                      SLAVA_KILL_BAD        // слава за убийство этого персонажа (SLAVA_KILL_GOOD, SLAVA_KILL_BAD, SLAVA_KILL_NEITRAL, SLAVA_KILL_CHILD)
#define KILL_TOWN_REP                   REP_KILL_CRITICAL     // репутация за убийство этого персонажа (REP_KILL_GOOD, REP_KILL_BAD, REP_KILL_NEITRAL, REP_KILL_CRITICAL для враждебности всего поселения, REP_KILL_CHILD)
#define KILL_FRAC_REP                   FRAC_WARROAD          // принадлежность определенной фракции (-1, если не нужно)

#define FLAG_STEAL                      75 // Нижний порог навыка для кражи (для торговцев 200, для животных 300)
#define FLAG_ALCOHOL                    0  // Примет ли алкоголь от ГГ: 0 - принимает, 1 - не принимает
#define FLAG_FOOD                       1  // Примет ли пищу от ГГ: 0 - принимает, 1 - не принимает

//---------------------

procedure start;
procedure map_enter_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;
procedure pickup_p_proc;
procedure combat_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;

//---------------------

#define LVAR_Tile_B                     (6) // номер второго тайла для патрулирования

import variable box_stop_use;

//---------------------



procedure start begin
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
       set_start_home
       if (item_amnt(self_obj,PID_TEST) > 0) then begin
           if (cur_map_index == MAP_RAIDERS_BASA) then begin
              if (item_amnt(self_obj,PID_TEST) == 1) then begin
              set_local_var(LVAR_Tile_B,17509); // патрульный выше барака (ходит вдоль барака)
              end
              if (item_amnt(self_obj,PID_TEST) == 2) then begin
              set_local_var(LVAR_Tile_B,18103); // патрульный у топливного бака (ходить вдоль административного здания)
              end
              if (item_amnt(self_obj,PID_TEST) == 3) then begin
              set_local_var(LVAR_Tile_B,16476); // рейдер в административном здании (ходит из зала к матрасам)
              end
           end
           item_remove(self_obj,PID_TEST,999)
       end
   end
end

procedure look_at_p_proc begin
   lookProcMsg
end

procedure description_p_proc begin
   dsrptProcMsg
end

procedure timed_event_p_proc begin
   // Таймер за номером 1 рарезервирован под перемещение персонажа по локации
   // Таймер за номером 2 рарезервирован под всплывающие реплики
   if (fixed_param == 1) then begin
       if (local_var(LVAR_Tile_B) > 0) then begin
       timer_move_patrol(HomeTile,local_var(LVAR_Tile_B))
       end
       else begin
       timer_move_to_home
       end
   end
   if (fixed_param == 2) then begin
       //timer_float_reply(110,119)
   end
end

procedure critter_p_proc begin
   variable attakParam;
   if (global_var(GVAR_TITUL_WARRIOR_ROAD) < 1) then begin
   set_using_stop
   end

   attakParam := 1;
   if ((armor_pid_dude_var == PID_ARMOR_ROAD) and (tile_distance(dude_tile, self_tile) > 3)) then begin
   attakParam := 0;
   end
   else if ((global_var(GVAR_TITUL_WARRIOR_ROAD) > 0) or (map_var(MVAR_ROADWAR_DOPUSK) > 0)) then begin
   attakParam := 0;
   end
   set_critter_options(attakParam,0) //условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ
end

procedure use_skill_on_p_proc begin
   skillUseSteal
   skillUseFAid
end

procedure pickup_p_proc begin
   pickupProcCritter
end

procedure use_obj_on_p_proc begin
   UsedItemToCrit_Proc
end

procedure combat_p_proc begin
   effectAttak(0,0,1,1,1) //отравление min, облучение min, травма ног %, травма рук %, травма глаз %
end

procedure damage_p_proc begin
   if (source_obj == dude_obj) then begin
   set_map_var(MVAR_ROADWAR_DOPUSK,-1);
   end
   set_damage_dude
end

procedure destroy_p_proc begin
   if (source_obj == dude_obj) then begin
   set_map_var(MVAR_CAR_PRESENT,0);
   set_map_var(MVAR_ROADWAR_DOPUSK,-1);
   end
   kill_critter_opt
end

procedure talk_p_proc begin
   if (armor_pid_dude_var == PID_ARMOR_POWER_A) then begin
   floater(self_obj,random(130,132),COLOR_MSG_RED);
   end
   else if (armor_pid_dude_var == PID_ARMOR_POWER_B) then begin
   floater(self_obj,random(130,132),COLOR_MSG_RED);
   end
   else if (global_var(GVAR_TITUL_WARRIOR_ROAD) > 0) then begin
   floater(self_obj,random(110,116),COLOR_MSG_NORMAL);
   end
   else begin
   floater(self_obj,random(120,126),COLOR_MSG_RED);
   end
end
