#include "..\headers\define.h"
//---------------------

#define NAME                            SCRIPT_PCRngBoy
#define TOWN_REP_VAR                    GVAR_TOWN_REP_PHOENIX // привязка к репутации (-1, если не нужно)

#define NUM_TEAM                        TEAM_PHOENIX_RING     // группа, к которой принадлежит персонаж (-1 если нужно оставить Team, заданный в редакторе)
#define NUM_AI                          AI_PERSON_TOUGH       // AI персонажа (-1 если нужно оставить AI, заданный в редакторе)
#define KILL_SLAVA                      SLAVA_KILL_GOOD       // слава за убийство этого персонажа (SLAVA_KILL_GOOD, SLAVA_KILL_BAD, SLAVA_KILL_NEITRAL, SLAVA_KILL_CHILD)
#define KILL_TOWN_REP                   REP_KILL_GOOD         // репутация за убийство этого персонажа (-100 для враждебности всего поселения)
#define KILL_FRAC_REP                   FRAC_NON              // принадлежность определенной фракции (-1, если не нужно)

#define FLAG_STEAL                      50 // Нижний порог навыка для кражи (для торговцев 200, для животных 300)
#define FLAG_ALCOHOL                    0  // Примет ли алкоголь от ГГ: 0 - принимает, 1 - не принимает
#define FLAG_FOOD                       0  // Примет ли пищу от ГГ: 0 - принимает, 1 - не принимает

//---------------------

procedure start;
procedure map_enter_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;
procedure pickup_p_proc;
procedure combat_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;

//---------------------

#define LVAR_Flag_Stavka                     (6) // ставка: 0 - на ГГ, 1 - против ГГ

//---------------------



procedure start begin
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
       set_start_home
       set_local_var(LVAR_Flag_Stavka,random(0,1));
       if (global_var(GVAR_PHENIX_STATUS_ARENABOSS) < 0) then begin
       rm_timer_event(self_obj);
       set_obj_visibility(self_obj, 1);
       end
   end
end

procedure look_at_p_proc begin
   lookProcMsg
end

procedure description_p_proc begin
   //dsrptProcMsg
   script_overrides;
   if (map_var(MVAR_PNX_RING_START) == 0) then begin
   display_mstr(101);
   end
   else begin
   display_mstr(102);
   end
end

procedure timed_event_p_proc begin
   // Таймер за номером 1 рарезервирован под перемещение персонажа по локации
   // Таймер за номером 2 рарезервирован под всплывающие реплики
   if (fixed_param == 1) then begin
       timer_move_to_home
   end
   if (fixed_param == 2) then begin
      //timer_float_reply(170,179)
      if (map_var(MVAR_PNX_RING_START) == 0) then begin
         floater(self_obj, random(170,179), COLOR_MSG_NORMAL);
         add_timer_event(self_obj, random(50,100), 2);
      end
      else begin
         if (local_var(LVAR_Flag_Stavka) == 0) then begin
            if (random(0,1) == 1) then begin
            float_msg(self_obj, mstr(random(195,199))+g_mstr(global_var(GVAR_PHENIX_TITUL_ARENA_NAME))+mstr(194), COLOR_MSG_GREEN);
            end
            else begin
            floater(self_obj, random(181,193), COLOR_MSG_NORMAL);
            end
         end
         else begin
            floater(self_obj, random(181,193), COLOR_MSG_NORMAL);
         end
         add_timer_event(self_obj, random(40,60), 2);
      end

   end
end

procedure critter_p_proc begin
   set_critter_options(0,0) //условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ
end

procedure use_skill_on_p_proc begin
   skillUseSteal
   skillUseFAid
end

procedure pickup_p_proc begin
   pickupProcCritter
end

procedure use_obj_on_p_proc begin
   UsedItemToCrit_Proc
end

procedure combat_p_proc begin
   effectAttak(0,0,0,0,0) //отравление min, облучение min, травма ног %, травма рук %, травма глаз %
end

procedure damage_p_proc begin
   set_damage_dude
end

procedure destroy_p_proc begin
   //set_map_var(MVAR_PNX_RING_BODY, self_obj);
   kill_critter_opt
end

procedure talk_p_proc begin
   if (global_var(GVAR_TITUL_ARENA_MASTER) > 0) then begin
      if (local_var(LVAR_Flag_Stavka) == 0) then begin
      floater(self_obj,random(150,157),COLOR_MSG_NORMAL);
      end
      else begin
      floater(self_obj,random(160,167),COLOR_MSG_NORMAL);
      end
   end
   else begin
      floater(self_obj,random(110,117),COLOR_MSG_NORMAL);
   end
end
