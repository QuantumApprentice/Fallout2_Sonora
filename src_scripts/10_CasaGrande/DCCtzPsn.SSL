#include "..\headers\define.h"
//---------------------

#define NAME                            SCRIPT_DCCtzPsn
#define TOWN_REP_VAR                    GVAR_TOWN_REP_CASA_GRANDE // привязка к репутации (-1, если не нужно)

#define NUM_TEAM                        TEAM_CASAGRANDE_PEASAN // группа, к которой принадлежит персонаж (-1 если нужно оставить Team, заданный в редакторе)
#define NUM_AI                          AI_PERSON_TOUGH       // AI персонажа (-1 если нужно оставить AI, заданный в редакторе)
#define KILL_SLAVA                      SLAVA_KILL_GOOD       // слава за убийство этого персонажа (SLAVA_KILL_GOOD, SLAVA_KILL_BAD, SLAVA_KILL_NEITRAL, SLAVA_KILL_CHILD)
#define KILL_TOWN_REP                   REP_KILL_GOOD         // репутация за убийство этого персонажа (REP_KILL_GOOD, REP_KILL_BAD, REP_KILL_NEITRAL, REP_KILL_CRITICAL для враждебности всего поселения, REP_KILL_CHILD)
#define KILL_FRAC_REP                   FRAC_NON              // принадлежность определенной фракции (-1, если не нужно)

#define FLAG_STEAL                      0  // Нижний порог навыка для кражи (для торговцев 200, для животных 300)
#define FLAG_ALCOHOL                    0  // Примет ли алкоголь от ГГ: 0 - принимает, 1 - не принимает
#define FLAG_FOOD                       0  // Примет ли пищу от ГГ: 0 - принимает, 1 - не принимает

//---------------------

procedure start;
procedure map_enter_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;
procedure pickup_p_proc;
procedure combat_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure uninjure_proc;
procedure talk_p_proc;

//---------------------

//#define LVAR_Flag_A                     (6) // отвечает за ...

//---------------------



procedure start begin
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
       set_start_home
       if (get_Medic_Quest == 0) then begin
       reg_anim_clear(self_obj);
       reg_anim_begin();
       reg_anim_animate(self_obj,ANIM_fall_back_sf,-1);
       reg_anim_end();
       critter_injure(self_obj, DAM_KNOCKED_DOWN);
       end
   end
end

procedure look_at_p_proc begin
   lookProcMsg
end

procedure description_p_proc begin
   script_overrides;
   if (get_Medic_Quest == 0) then begin
   display_mstr(101);
   end
   else begin
   display_mstr(102);
   end
end

procedure timed_event_p_proc begin
   // Таймер за номером 1 зарезервирован под перемещение персонажа по локации
   // Таймер за номером 2 зарезервирован под всплывающие реплики
   if (fixed_param == 1) then begin
       //timer_move_to_home
   end
   if (fixed_param == 2) then begin
       if (get_Medic_Quest == 0) then begin
       timer_float_reply(120,125)
       end
   end
   if (fixed_param == 3) then begin
       reg_anim_clear(self_obj);
       reg_anim_begin();
       reg_anim_animate(self_obj,ANIM_back_to_standing,-1);
       reg_anim_end();
   end
end

procedure critter_p_proc begin
   set_critter_options(1,0) //условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ
end

procedure use_skill_on_p_proc begin
   skillUseSteal
   skillUseFAid
   if (action_being_used == SKILL_DOCTOR) then begin
      if (get_Medic_Quest == 0) then begin
         script_overrides;
         if (has_skill(dude_obj, SKILL_DOCTOR) < 50) then begin
         skill_display_mstr(104,SKILL_DOCTOR,50)
         end
         else begin
         call uninjure_proc;
         end
      end
   end
end

procedure pickup_p_proc begin
   pickupProcCritter
end

procedure use_obj_on_p_proc begin
   UsedItemToCrit_Proc
   if (objUsedPid == PID_M_ANTIDOTE) then begin
      if (get_Medic_Quest == 0) then begin
      //script_overrides;
      call uninjure_proc;
      end
   end
end

procedure combat_p_proc begin
   effectAttak(0,0,0,0,0) //отравление min, облучение min, травма ног %, травма рук %, травма глаз %
end

procedure damage_p_proc begin
   set_damage_dude
end

procedure destroy_p_proc begin
   kill_critter_opt
end

procedure uninjure_proc begin
   PLAY_FADE
   set_REP(2)
   exp_add(200);
   display_mstr(103);
   set_Medic_Quest;
   add_timer_event(self_obj, 10, 3);
end

procedure talk_p_proc begin
   if (get_Medic_Quest == 0) then begin
   floater(self_obj,random(110,112),COLOR_MSG_NORMAL);
   end
   else if (lvar_bit(LVAR_Self_Flags,bit_12) == 0) then begin
   item_add(dude_obj,PID_AMMO_SHOTGUN_SHELLS,10)
   floater(self_obj,118,COLOR_MSG_GREEN);
   set_lvar_bit_on(LVAR_Self_Flags, bit_12);
   end
   else begin
   floater(self_obj,random(115,117),COLOR_MSG_NORMAL);
   end
end
