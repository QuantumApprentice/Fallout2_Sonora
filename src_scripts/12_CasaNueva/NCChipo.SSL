#include "..\headers\define.h"
//---------------------

#define NAME                            SCRIPT_NCChipo
#define TOWN_REP_VAR                    GVAR_TOWN_REP_CASA_NUEVA // привязка к репутации (-1, если не нужно)

#define NUM_TEAM                        TEAM_CASANUEVA_PEASAN // группа, к которой принадлежит персонаж (-1 если нужно оставить Team, заданный в редакторе)
#define NUM_AI                          AI_CHILD              // AI персонажа (-1 если нужно оставить AI, заданный в редакторе)
#define KILL_SLAVA                      SLAVA_KILL_CHILD      // слава за убийство этого персонажа (SLAVA_KILL_GOOD, SLAVA_KILL_BAD, SLAVA_KILL_NEITRAL, SLAVA_KILL_CHILD)
#define KILL_TOWN_REP                   REP_KILL_CRITICAL     // репутация за убийство этого персонажа (REP_KILL_GOOD, REP_KILL_NEITRAL, REP_KILL_BAD, REP_KILL_CHILD, REP_KILL_CRITICAL)
#define KILL_FRAC_REP                   FRAC_NON              // принадлежность определенной фракции (-1, если не нужно)

#define FLAG_STEAL                      25 // Нижний порог навыка для кражи (для торговцев 200, для животных 300)
#define FLAG_ALCOHOL                    1  // Примет ли алкоголь от ГГ: 0 - принимает, 1 - не принимает
#define FLAG_FOOD                       1  // Примет ли пищу от ГГ: 0 - принимает, 1 - не принимает

//---------------------

procedure start;
procedure map_enter_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;
procedure pickup_p_proc;
procedure combat_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;

procedure Node998;
procedure Node999;
procedure NodeStartDialog;
procedure NodeFstDialog;
procedure NodeFstDialog2;
procedure NodeFstDialog3;
procedure NodeSndDialog;
procedure NodeAnswer100;
procedure NodeAnswer101;
procedure NodeAnswer102;
procedure NodeAnswer103;
procedure NodeAnswer104;
procedure NodeAnswer105;

//---------------------

//#define LVAR_Flag_A                     (6) // отвечает за ...

//---------------------



procedure start begin
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
       set_start_home
   end
end

procedure look_at_p_proc begin
   lookProcMsg
end

procedure description_p_proc begin
   dsrptProcMsg
end

procedure timed_event_p_proc begin
   // Таймер за номером 1 зарезервирован под перемещение персонажа по локации
   // Таймер за номером 2 зарезервирован под всплывающие реплики
   if (fixed_param == 1) then begin
       timer_move_to_home
   end
   if (fixed_param == 2) then begin
       if (global_var(GVAR_NUEVA_STATUS_PINKI) == 1) then begin
       timer_float_reply(110,124)
       end
       else if (global_var(GVAR_NUEVA_STATUS_PINKI) == 0) then begin
       timer_float_reply(130,139)
       end
   end
end

procedure critter_p_proc begin
   set_critter_options(1,0) //условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ
end

procedure use_skill_on_p_proc begin
   skillUseSteal
   skillUseFAid
end

procedure pickup_p_proc begin
   pickupProcCritter
end

procedure use_obj_on_p_proc begin
   UsedItemToCrit_Proc
end

procedure combat_p_proc begin
   effectAttak(0,0,0,0,0) //отравление min, облучение min, травма ног %, травма рук %, травма глаз %
end

procedure damage_p_proc begin
   set_damage_dude
end

procedure destroy_p_proc begin
   if (getQuest(GVAR_NUEVA_QST_PINKI) < qEnd) then begin
   setQuest(GVAR_NUEVA_QST_PINKI,qStop);
   end
   kill_critter_opt
end

procedure talk_p_proc begin
   if (global_var(GVAR_NUEVA_STATUS_PINKI)<=-99) then begin
   z_floater(self_obj,106,COLOR_MSG_RED);
   end
   else if (global_var(GVAR_NUEVA_STATUS_PINKI)<0) then begin
   floater(self_obj,105,COLOR_MSG_RED);
   end
   else if (game_time_hour < 0600) then begin
   floater(self_obj,104,COLOR_MSG_RED);
   end
   else begin
   setReaction(1) // Отношение к ГГ (1 - персонаж положительный, -1 отрицательный, 0 нейтральный)
   dialog_proc(102,0,NodeStartDialog,-1,-1)
   end
end

procedure Node998 begin
   set_enemy_dude;
end

procedure Node999 begin
end

procedure NodeStartDialog begin
   if (bitDialog(bit_1) == 0) then begin
   call NodeFstDialog;
   end
   else begin
   call NodeSndDialog;
   end
end

//--- ПЕРВЫЙ ДИАЛОГ

procedure NodeFstDialog begin
   bitDialogExp(bit_1,0)
   Reply(200)
   NAnswer(201,1,NodeFstDialog2)
end

procedure NodeFstDialog2 begin
   Reply(202)
   if (global_var(GVAR_NUEVA_STATUS_PINKI) == 0) then begin
   NAnswer(204,1,NodeFstDialog3)
   end
   else begin
   NAnswer(203,1,NodeSndDialog)
   end
end

procedure NodeFstDialog3 begin
   Reply(205)
   NAnswer(206,1,NodeSndDialog)
end

//--- ВТОРОЙ ДИАЛОГ

procedure NodeSndDialog begin
   if (global_var(GVAR_NUEVA_STATUS_PINKI) > 1) then begin
   Reply(214)
   end
   else if (global_var(GVAR_NUEVA_STATUS_PINKI) == 1) then begin
   Reply(213)
   end
   else if ((armor_pid_dude_var == PID_ARMOR_POWER_A) or (armor_pid_dude_var == PID_ARMOR_POWER_B)) then begin
   Reply(103)
   end
   else if town_rep_is_liked then begin
   Reply(212)
   end
   else if town_rep_is_hated then begin
   Reply(211)
   end
   else begin
   Reply(210)
   end

   if (getQuest(GVAR_NUEVA_QST_PINKI) == qZero) then begin
   NAnswer(230,3,NodeAnswer101)
   end
   else if (getQuest(GVAR_NUEVA_QST_PINKI) < qEnd) then begin
   if (global_var(GVAR_NUEVA_STATUS_PINKI) == 1) then begin
   GAnswer(239,3,NodeAnswer104)
   end
   end
   if (getQuest(GVAR_NUEVA_QST_PINKI) >= qEnd) then begin
   if (global_var(GVAR_NUEVA_STATUS_PINKI) == 1) then begin
   NAnswer(243,3,NodeAnswer105)
   end
   end

   if (global_var(GVAR_NUEVA_STATUS_MER)<=-999) then begin
   NAnswer(220,1,NodeAnswer100)
   end
   NAnswer(219,1,Node999)
end

procedure NodeAnswer100 begin
   Reply(221)
   NAnswer(222,1,Node999)
end

procedure NodeAnswer101 begin
   Reply(231)
   NAnswer(232,1,NodeAnswer102)
end

procedure NodeAnswer102 begin
   Reply(233)
   NAnswer(234,1,NodeAnswer103)
   NAnswer(242,1,NodeAnswer103)
end

procedure NodeAnswer103 begin
   setQuest(GVAR_NUEVA_QST_PINKI,qStart);
   Reply(235)
   if (global_var(GVAR_NUEVA_STATUS_PINKI) == 1) then begin
   GAnswer(239,3,NodeAnswer104)
   end
   BAnswer(236,1,Node999)
   NAnswer(237,1,Node999)
   GAnswer(238,1,Node999)
end

procedure NodeAnswer104 begin
   setQuest(GVAR_NUEVA_QST_PINKI,qEnd+1);
   //? смена дислокации
   set_SLAVA(25)
   exp_add(250);
   Reply(240)
   zNAnswer(241,1,Node999)
end

procedure NodeAnswer105 begin
   Reply(244)
   NAnswer(245,1,Node999)
end
